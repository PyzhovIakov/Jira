
#Область СлужебныеПроцедурыИФункции

Функция СведенияПоддержкиПоОбращению(Обращение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СведенияПоддержки = Новый Структура;
	СведенияПоддержки.Вставить("ЕстьОтвет",			Ложь);
	СведенияПоддержки.Вставить("ДатаОтвета",		'0001-01-01');
	СведенияПоддержки.Вставить("ДокументОтвета",	Неопределено);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СведенияПоддержки.ЕстьОтвет КАК ЕстьОтвет,
	|	СведенияПоддержки.ДатаОтвета КАК ДатаОтвета,
	|	СведенияПоддержки.ДокументОтвета КАК ДокументОтвета
	|ИЗ
	|	РегистрСведений.CRM_СведенияПоддержки КАК СведенияПоддержки
	|ГДЕ
	|	СведенияПоддержки.Обращение = &Обращение");
	
	Запрос.Параметры.Вставить("Обращение", Обращение);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СведенияПоддержки, Выборка);
	КонецЕсли;
	
	Возврат СведенияПоддержки;
	
КонецФункции

Функция ЕстьОтветПоОбращению(Обращение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЕстьОтвет = Ложь;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СведенияПоддержки.ЕстьОтвет КАК ЕстьОтвет
	|ИЗ
	|	РегистрСведений.CRM_СведенияПоддержки КАК СведенияПоддержки
	|ГДЕ
	|	СведенияПоддержки.Обращение = &Обращение");
	
	Запрос.Параметры.Вставить("Обращение", Обращение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЕстьОтвет = Выборка.ЕстьОтвет;
	КонецЕсли;
	
	Возврат ЕстьОтвет;
	
КонецФункции

Процедура ЗарегистрироватьОтветПоОбращению(Источник, НаборЗаписейЖурнала) Экспорт
	
	Если Источник.ДополнительныеСвойства.Свойство("АвтоматическийОтвет") Тогда
		Возврат;
	КонецЕсли;
	
	РазрешенныеТипыИсточников = Новый Массив;
	РазрешенныеТипыИсточников.Добавить(Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее"));
	РазрешенныеТипыИсточников.Добавить(Тип("ДокументСсылка.CRM_СообщениеМессенджера"));
	РазрешенныеТипыИсточников.Добавить(Тип("ДокументСсылка.СообщениеSMS"));
	РазрешенныеТипыИсточников.Добавить(Тип("ДокументСсылка.ТелефонныйЗвонок"));
	
	ТипИсточника = ТипЗнч(Источник.Ссылка);
	Если РазрешенныеТипыИсточников.Найти(ТипИсточника) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТипИсточника = Тип("ДокументСсылка.CRM_СообщениеМессенджера")
		И Источник.ВидСообщения = Перечисления.CRM_ВидыСообщенияМессенджера.Входящее Тогда
		Возврат;
	КонецЕсли;
	Если ТипИсточника = Тип("ДокументСсылка.ТелефонныйЗвонок")
		И Источник.Входящий Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипИсточника = Тип("ДокументСсылка.CRM_СообщениеМессенджера") Тогда
		// Сообщение не привязано к интересу в журнале документов. К интерему привязан Диалог этого Сообщения
		Диалог = Источник.Диалог;
		СвязанноеОбращение = CRM_КлиентыСервер.ПолучитьГлавнуюЗаписьПоОбъекту(Диалог).CRM_Интерес;
	Иначе
		СвязанноеОбращение = НаборЗаписейЖурнала[0].CRM_Интерес;
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(СвязанноеОбращение) Тогда
		Возврат;
	КонецЕсли;
	Если СвязанноеОбращение.ТипОбращения = Справочники.CRM_ТипыОбращений.Интерес Тогда
		Возврат;
	КонецЕсли;
	
	Если Не РегистрыСведений.CRM_СведенияПоддержки.ЕстьОтветПоОбращению(СвязанноеОбращение) Тогда
		ЗаписатьОтветПоОбращению(СвязанноеОбращение, Источник.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьСледующееДелоПоОбращению(Обращение, ДанныеСледующегоДела) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.CRM_СведенияПоддержки.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Обращение = Обращение;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		Если ДанныеСледующегоДела <> Неопределено Тогда
			МенеджерЗаписи.Обращение = Обращение;
			МенеджерЗаписи.СледующееДело			= ДанныеСледующегоДела.ПланируемаяАктивность;
			МенеджерЗаписи.СледующееДелоСрок		= ДанныеСледующегоДела.ПланируемаяДата;
			МенеджерЗаписи.СледующееДелоИсполнитель	= ДанныеСледующегоДела.Ответственный;
		Иначе
			МенеджерЗаписи.Обращение = Обращение;
			МенеджерЗаписи.СледующееДело			= Неопределено;
			МенеджерЗаписи.СледующееДелоСрок		= '0001-01-01';
			МенеджерЗаписи.СледующееДелоИсполнитель	= Неопределено;
		КонецЕсли;
	Иначе
		Если ДанныеСледующегоДела <> Неопределено Тогда
			МенеджерЗаписи.Обращение = Обращение;
			МенеджерЗаписи.СледующееДело			= ДанныеСледующегоДела.ПланируемаяАктивность;
			МенеджерЗаписи.СледующееДелоСрок		= ДанныеСледующегоДела.ПланируемаяДата;
			МенеджерЗаписи.СледующееДелоИсполнитель	= ДанныеСледующегоДела.Ответственный;
		КонецЕсли;
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура ЗаписатьОтветПоОбращению(Обращение, ДокументОтвета) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.CRM_СведенияПоддержки.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Обращение = Обращение;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Обращение = Обращение;
	МенеджерЗаписи.ЕстьОтвет = Истина;
	МенеджерЗаписи.ДатаОтвета = ДокументОтвета.Дата;
	МенеджерЗаписи.ДокументОтвета = ДокументОтвета;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти