#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  ИдентификаторПакета - см. РегистрСведений.СоставПакетовДокументовЭДО.ИдентификаторПакета
//  ЭлектронныйДокумент - см. РегистрСведений.СоставПакетовДокументовЭДО.ЭлектронныйДокумент
//  ДополнительныеПараметры - СтрокаТаблицыЗначений,Структура:
//  * ИдентификаторОрганизации - Строка
//  * ИдентификаторДокумента - Строка
Процедура Записать(ИдентификаторПакета, ЭлектронныйДокумент, ДополнительныеПараметры = Неопределено) Экспорт
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ЭлектронныйДокумент.Установить(ЭлектронныйДокумент);
	ЗаписьНабора = НаборЗаписей.Добавить();
	ЗаписьНабора.ЭлектронныйДокумент = ЭлектронныйДокумент;
	ЗаписьНабора.ИдентификаторПакета = ИдентификаторПакета;
	ДополнитьСвойствамиДокумента(НаборЗаписей, ДополнительныеПараметры);
	НаборЗаписей.Записать();
КонецПроцедуры

// Параметры:
//  ЭлектронныйДокумент - см. РегистрСведений.СоставПакетовДокументовЭДО.ЭлектронныйДокумент
//  ДополнительныеПараметры - Структура:
//  * ИдентификаторОрганизации - Строка
//  * ИдентификаторДокумента - Строка
Процедура Удалить(ЭлектронныйДокумент, ДополнительныеПараметры = Неопределено) Экспорт
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ЭлектронныйДокумент.Установить(ЭлектронныйДокумент);
	ДополнитьСвойствамиДокумента(НаборЗаписей, ДополнительныеПараметры);
	НаборЗаписей.Записать();
КонецПроцедуры

// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей.СоставПакетовДокументовЭДО
//  ДополнительныеПараметры - СтрокаТаблицыЗначений,Структура:
//  * ИдентификаторОрганизации - Строка
//  * ИдентификаторДокумента - Строка
Процедура ДополнитьСвойствамиДокумента(НаборЗаписей, ДополнительныеПараметры)
	
	Если ДополнительныеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваДокумента = Новый Структура("ИдентификаторОрганизации, ИдентификаторДокумента",
		ДополнительныеПараметры.ИдентификаторОрганизации, ДополнительныеПараметры.ИдентификаторДокумента);
	НаборЗаписей.ДополнительныеСвойства.Вставить("СвойстваДокумента", СвойстваДокумента);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли