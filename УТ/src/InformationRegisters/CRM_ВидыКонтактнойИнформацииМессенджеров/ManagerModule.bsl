#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает вид контактной информации мессенджера
//
// Параметры:
//  ТипМессенджера	 - Строка	 - Тип месенджера
//  Контакт			 - СпраочникСсылка	 - Контакт
// 
// Возвращаемое значение:
//  Справочники.ВидыКонтактнойИнформации - Вид контактной информации
//
Функция ВидКонтактнойИнформацииМессенджера(ТипМессенджера, Контакт) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	CRM_ВидыКонтактнойИнформацииМессенджеров.ВидКИ_Клиент КАК ВидКИ_Клиент,
	               |	CRM_ВидыКонтактнойИнформацииМессенджеров.ВидКИ_КЛ КАК ВидКИ_КЛ,
	               |	CRM_ВидыКонтактнойИнформацииМессенджеров.ВидКИ_ПК КАК ВидКИ_ПК,
	               |	CRM_ВидыКонтактнойИнформацииМессенджеров.ВидКИ_Пользователь КАК ВидКИ_Пользователь
	               |ИЗ
	               |	РегистрСведений.CRM_ВидыКонтактнойИнформацииМессенджеров КАК CRM_ВидыКонтактнойИнформацииМессенджеров
	               |ГДЕ
	               |	CRM_ВидыКонтактнойИнформацииМессенджеров.ТипМессенджера = &ТипМессенджера";
	
	Запрос.УстановитьПараметр("ТипМессенджера", ТипМессенджера);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда

		Если ТипЗнч(Контакт) = Тип("СправочникСсылка.Партнеры") Тогда
			Возврат Выборка.ВидКИ_Клиент;
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
			Возврат Выборка.ВидКИ_КЛ;
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.CRM_ПотенциальныеКлиенты") Тогда
			Возврат Выборка.ВидКИ_ПК;
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.Пользователи") Тогда
			Возврат Выборка.ВидКИ_Пользователь;
		Иначе
			Возврат Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
		КонецЕсли;
		
	Иначе
		Возврат Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
	КонецЕсли;

КонецФункции

// Записывает виды контактной иформации
//
// Параметры:
//  ТипМессенджера	 - Строка	 - Тип месенджера
//
Процедура ЗаписатьВидыКИ(ТипМессенджера) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ТипМессенджера = ТипМессенджера;
	
	МенеджерЗаписи.ВидКИ_Клиент = CRM_РаботаСМессенджерамиСерверПовтИсп.ПолучитьВидКИМессенджера(ТипМессенджера,
		 Справочники.Партнеры.ПустаяСсылка());
	МенеджерЗаписи.ВидКИ_КЛ = CRM_РаботаСМессенджерамиСерверПовтИсп.ПолучитьВидКИМессенджера(ТипМессенджера,
		 Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка());
	МенеджерЗаписи.ВидКИ_ПК = CRM_РаботаСМессенджерамиСерверПовтИсп.ПолучитьВидКИМессенджера(ТипМессенджера,
		 Справочники.CRM_ПотенциальныеКлиенты.ПустаяСсылка());
	МенеджерЗаписи.ВидКИ_Пользователь = CRM_РаботаСМессенджерамиСерверПовтИсп.ПолучитьВидКИМессенджера(ТипМессенджера,
		 Справочники.Пользователи.ПустаяСсылка());
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                         
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Определяет, является ли переданный вид контактной информации видом контактной информации мессенджера
//
// Параметры:
//  ВидКИ	 - СправочникСсылка.ВидыКонтактнойИнформации - вид контактной информации
// 
// Возвращаемое значение:
//  Булево - результат функции
//
Функция ЭтоВидКИМессенджера(ВидКИ) Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.ТипМессенджера КАК ТипМессенджера
	|ИЗ
	|	РегистрСведений.CRM_ВидыКонтактнойИнформацииМессенджеров КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.ВидКИ_Клиент = &ВидКИ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.ТипМессенджера
	|ИЗ
	|	РегистрСведений.CRM_ВидыКонтактнойИнформацииМессенджеров КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.ВидКИ_КЛ = &ВидКИ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.ТипМессенджера
	|ИЗ
	|	РегистрСведений.CRM_ВидыКонтактнойИнформацииМессенджеров КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.ВидКИ_ПК = &ВидКИ");
	
	Запрос.УстановитьПараметр("ВидКИ", ВидКИ);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

#Область ПереходНаНовуюВерсию

// Процедура обновляет вид контактной информации пользователя.
//
Процедура ОбновитьВидКИПользователя() Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	CRM_УчетныеЗаписиМессенджеров.ТипМессенджера КАК ТипМессенджера
	                      |ИЗ
	                      |	Справочник.CRM_УчетныеЗаписиМессенджеров КАК CRM_УчетныеЗаписиМессенджеров
	                      |ГДЕ
	                      |	НЕ CRM_УчетныеЗаписиМессенджеров.ПометкаУдаления");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ТипМессенджера) Тогда
			ЗаписатьВидыКИ(Выборка.ТипМессенджера);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
