#Область СлужебныйПрограммныйИнтерфейс

// Сохраняет текущие данные исходящего письма при включенном автосохранении
//
// Параметры:
//  Параметры  - Структура:
//    * Ссылка     - ДокументСсылка.ЭлектронноеПисьмоИсходящее - ссылка на письмо, данные которого сохраняются
//    * Пользователь     - СправочникСсылка.Пользователи - пользователь, данные для которого сохраняются
//    * ДобавляемыеДанные     - ТаблицаЗначений
//    * УдаляемыеДанные     - ТаблицаЗначений
//  АдресХранилища - Строка - результат выполнения процедуры
//
Процедура ВыполнитьАвтосохранениеЭлектронногоПисьма(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	ХешиТекущихКартинок = Новый Массив;
	
	Для Каждого Строка Из ПараметрыПроцедуры.УдаляемыеДанные Цикл
		МенеджерЗаписи = РегистрыСведений.CRM_ДанныеАвтосохранения.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = ПараметрыПроцедуры.Объект;
		МенеджерЗаписи.Пользователь = ПараметрыПроцедуры.Пользователь;
		МенеджерЗаписи.ХешСумма = Строка.ХешСумма;
		МенеджерЗаписи.ВидДанных = "Вложение";
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЦикла;
	
	// Сначала получаем текст письма, так как затем в нем будут меняться пути к картинкам.
	ТекстПисьмаСтрокаТЗ = ПараметрыПроцедуры.ДобавляемыеДанные.Найти("ТекстПисьма", "ВидДанных");
	Если ТекстПисьмаСтрокаТЗ = Неопределено Тогда
		ТекстПисьма = Неопределено;
	Иначе
		ТекстПисьма = ТекстПисьмаСтрокаТЗ.ОписаниеДанных;
	КонецЕсли;
	
	Для Каждого Строка Из ПараметрыПроцедуры.ДобавляемыеДанные Цикл
		МенеджерЗаписи = РегистрыСведений.CRM_ДанныеАвтосохранения.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Строка);
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыПроцедуры);
		Если Строка.ВидДанных = "ТекстПисьма" Тогда
			Продолжить;
		ИначеЕсли Строка.ВидДанных = "Вложение" Тогда
			Данные = Новый ДвоичныеДанные(Строка.ИмяФайлаНаКомпьютере);
			МенеджерЗаписи.Данные = Новый ХранилищеЗначения(Данные, Новый СжатиеДанных(9));
		ИначеЕсли Строка.ВидДанных = "Картинка" Тогда
			Если ЭтоАдресВременногоХранилища(Строка.ИмяФайлаНаКомпьютере) Тогда
				Данные = ПолучитьИзВременногоХранилища(Строка.ИмяФайлаНаКомпьютере);
			Иначе
				Данные = Новый ДвоичныеДанные(Строка.ИмяФайлаНаКомпьютере);
			КонецЕсли;
			ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
			Если Данные = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ХешированиеДанных.Добавить(Данные);
			ХешСумма = СтрЗаменить(ХешированиеДанных.ХешСумма, " ", "");
			ХешиТекущихКартинок.Добавить(ХешСумма);
			// В тексте письма нужно заменить пути к картинкам на новые,
			// новые пути будут создаваться при восстановлении данных на форме,
			// место замены определяться по хешу.
			Если ЭтоАдресВременногоХранилища(Строка.ИмяФайлаНаКомпьютере) Тогда
				ТекстПисьма = СтрЗаменить(ТекстПисьма, Строка.ИмяФайлаНаКомпьютере, ХешСумма);
			Иначе
				Путь = СтрЗаменить(Строка.ИмяФайлаНаКомпьютере, "/", "\");
				ТекстПисьма = СтрЗаменить(ТекстПисьма, "<img src=""file://" + Путь, 
					"<img src=""file://" + ХешСумма);
			КонецЕсли;
			Если Не КартинкаСохранена(ПараметрыПроцедуры.Объект, ПараметрыПроцедуры.Пользователь, ХешСумма) Тогда
				МенеджерЗаписи.ХешСумма = ХешСумма;
				МенеджерЗаписи.Данные = Новый ХранилищеЗначения(Данные, Новый СжатиеДанных(9));
				МенеджерЗаписи.Записать();
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
	// В регистре могли остаться записи по картинкам, которые были удалены из письма.
	УдалитьУдаленныеКартинки(ПараметрыПроцедуры.Объект, ПараметрыПроцедуры.Пользователь, 
		ХешиТекущихКартинок, ТекстПисьма);
		
	// Записывается текст с измененными путями к картинкам.
	Если ТекстПисьмаСтрокаТЗ <> Неопределено Тогда
		МенеджерЗаписи = РегистрыСведений.CRM_ДанныеАвтосохранения.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = ПараметрыПроцедуры.Объект;
		МенеджерЗаписи.Пользователь = ПараметрыПроцедуры.Пользователь;
		МенеджерЗаписи.ВидДанных = "ТекстПисьма";
		МенеджерЗаписи.Данные = Новый ХранилищеЗначения(ТекстПисьма, Новый СжатиеДанных(9));
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Данные сохранены успешно.'");
	
	ПоместитьВоВременноеХранилище(ТекстСообщения, АдресРезультата);
	
КонецПроцедуры

// Удаляет неактуальные данные о картинках исходящего письма
//
// Параметры:
//  Ссылка     - ДокументСсылка.ЭлектронноеПисьмоИсходящее - ссылка на письмо
//  Пользователь     - СправочникСсылка.Пользователи - пользователь
//  ХешиТекущихКартинок     - Массив
//  ТекстПисьма     - Строка
//
Процедура УдалитьУдаленныеКартинки(Ссылка, Пользователь, ХешиТекущихКартинок, ТекстПисьма)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	CRM_ДанныеАвтосохранения.ХешСумма КАК ХешСумма
		|ИЗ
		|	РегистрСведений.CRM_ДанныеАвтосохранения КАК CRM_ДанныеАвтосохранения
		|ГДЕ
		|	CRM_ДанныеАвтосохранения.Объект = &Объект
		|	И CRM_ДанныеАвтосохранения.Пользователь = &Пользователь
		|	И НЕ CRM_ДанныеАвтосохранения.ХешСумма В (&ХешиТекущихКартинок)
		|	И CRM_ДанныеАвтосохранения.ВидДанных = ""Картинка""";
	
	Запрос.УстановитьПараметр("Объект", Ссылка);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("ХешиТекущихКартинок", ХешиТекущихКартинок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.CRM_ДанныеАвтосохранения.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = Ссылка;
		МенеджерЗаписи.Пользователь = Пользователь;
		МенеджерЗаписи.ХешСумма = Выборка.ХешСумма;
		МенеджерЗаписи.ВидДанных = "Картинка";
		МенеджерЗаписи.Прочитать();
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
КонецПроцедуры

Функция КартинкаСохранена(Ссылка, Пользователь, ХешСумма)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	CRM_ДанныеАвтосохранения.Объект КАК Объект,
		|	CRM_ДанныеАвтосохранения.Пользователь КАК Пользователь,
		|	CRM_ДанныеАвтосохранения.ХешСумма КАК ХешСумма
		|ИЗ
		|	РегистрСведений.CRM_ДанныеАвтосохранения КАК CRM_ДанныеАвтосохранения
		|ГДЕ
		|	CRM_ДанныеАвтосохранения.Объект = &Объект
		|	И CRM_ДанныеАвтосохранения.Пользователь = &Пользователь
		|	И CRM_ДанныеАвтосохранения.ХешСумма = &ХешСумма";
	
	Запрос.УстановитьПараметр("Объект", Ссылка);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("ХешСумма", ХешСумма);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти