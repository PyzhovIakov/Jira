
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивОтбораТипаМессенджера = Новый Массив;
	
	Если Параметры.Свойство("ТипМессенджера") Тогда
		// Отбор по типу мессенджера.
		Если СтрНачинаетсяС(Параметры.ТипМессенджера, "Telegram") Тогда
			МассивОтбораТипаМессенджера.Добавить("Telegram");
			МассивОтбораТипаМессенджера.Добавить("TelegramBot");
		Иначе
			МассивОтбораТипаМессенджера.Добавить(Параметры.ТипМессенджера);
		КонецЕсли;
		
		// Пустые значения определяемого типа.
		ПустыеЗначения = Новый Массив;
		ПустыеЗначения.Добавить(Неопределено);
		
		Для Каждого ТипЗначения Из Метаданные.ОпределяемыеТипы.КонтактВзаимодействия.Тип.Типы() Цикл
			ПустыеЗначения.Добавить(Новый (ТипЗначения));
		КонецЦикла;
		
		// Параметры динамического списка.
		ОбработкаМессенджера = CRM_РаботаСМессенджерамиСерверПовтИсп.МенеджерМессенджера(Параметры.ТипМессенджера);
		
		СписокБлокировок.Параметры.УстановитьЗначениеПараметра("ТипКИМессенджера", ОбработкаМессенджера.ТипКИМессенджера());
		СписокОтправителей.Параметры.УстановитьЗначениеПараметра("ТипКИМессенджера", ОбработкаМессенджера.ТипКИМессенджера());
		
		СписокБлокировок.Параметры.УстановитьЗначениеПараметра("ТипМессенджера", МассивОтбораТипаМессенджера);
		СписокОтправителей.Параметры.УстановитьЗначениеПараметра("ТипМессенджера", МассивОтбораТипаМессенджера);
		
		СписокБлокировок.Параметры.УстановитьЗначениеПараметра("ПустыеЗначенияКонтакта", ПустыеЗначения);
		СписокОтправителей.Параметры.УстановитьЗначениеПараметра("ПустыеЗначенияКонтакта", ПустыеЗначения);

	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ФлагИзмененияЗаблокированныхОтправителей Тогда
		Оповестить("ИзмененаБлокировкаОтправителей");
	КонецЕсли;
	
КонецПроцедуры // ПриЗакрытии()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьБлокировкуОтправителя(Команда)
	
	ИзменитьБлокировкуОтправителей(Элементы.СписокОтправителей, Истина);
	
КонецПроцедуры // ДобавитьБлокировкуОтправителя()

&НаКлиенте
Процедура УбратьБлокировкуОтправителя(Команда)
	
	ИзменитьБлокировкуОтправителей(Элементы.СписокБлокировок, Ложь);
	
КонецПроцедуры // УбратьБлокировкуОтправителя()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокБлокировок

&НаКлиенте
Процедура СписокБлокировокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПриВыбореСтрокиСпискаФормы(Элемент, Поле);
	
КонецПроцедуры // СписокБлокировокВыбор()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОтправителей

&НаКлиенте
Процедура СписокОтправителейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПриВыбореСтрокиСпискаФормы(Элемент, Поле);
	
КонецПроцедуры // СписокОтправителейВыбор()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьБлокировкуОтправителей(ЭлементСписка, ПризнакБлокировки)
	
	ВыделенныеСтрокиСписка = ЭлементСписка.ВыделенныеСтроки;
	
	Если ВыделенныеСтрокиСписка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивИдентификаторовСписка = Новый Массив;
	Для Каждого ВыделеннаяСтрокаСписка Из ВыделенныеСтрокиСписка Цикл
		ДанныеСтрокиСписка = ЭлементСписка.ДанныеСтроки(ВыделеннаяСтрокаСписка);
		
		Если Не ЗначениеЗаполнено(ДанныеСтрокиСписка.ИдентификаторБлокировки) Тогда
			Продолжить;
		КонецЕсли;
		
		МассивИдентификаторовСписка.Добавить(ДанныеСтрокиСписка.ИдентификаторБлокировки);
	КонецЦикла;
	
	ИзменитьБлокировкуОтправителейНаСервере(МассивИдентификаторовСписка, ПризнакБлокировки);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьБлокировкуОтправителейНаСервере(НаборИдентификаторов, ПризнакБлокировки)
	
	Если ПризнакБлокировки Тогда
		НаборЗаписейРегистра = РегистрыСведений.CRM_ЗаблокированныеОтправителиМессенджера.СоздатьНаборЗаписей();
		
		Для Каждого ИдентификаторПользователя Из НаборИдентификаторов Цикл
			НоваяСтрока = НаборЗаписейРегистра.Добавить();
			НоваяСтрока.Идентификатор = ИдентификаторПользователя;
		КонецЦикла;
		
		НаборЗаписейРегистра.Записать(Ложь);
	Иначе
		Для Каждого ИдентификаторПользователя Из НаборИдентификаторов Цикл
			CRM_РаботаСМессенджерамиСервер.ИзменитьБлокировкуОтправителя(
				ИдентификаторПользователя, Ложь);
		КонецЦикла;
	КонецЕсли;
	
	Элементы.СписокБлокировок.Обновить();
	Элементы.СписокОтправителей.Обновить();
	
	ФлагИзмененияЗаблокированныхОтправителей = Истина;
	
КонецПроцедуры // ИзменитьБлокировкуОтправителейНаСервере()

&НаКлиенте
Процедура ПриВыбореСтрокиСпискаФормы(Элемент, Поле)
	
	ТекущиеДанныеСтроки = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанныеСтроки <> Неопределено Тогда
		Если СтрНайти(Поле.Имя, "Диалог") > 0 Тогда
			ПолеСтроки = "Диалог";
		Иначе
			ПолеСтроки = "Контакт";
		КонецЕсли;
		
		ПоказатьЗначение(, ТекущиеДанныеСтроки[ПолеСтроки]);
	КонецЕсли;
	
КонецПроцедуры // ПриВыбореСтрокиСпискаФормы()

#КонецОбласти
