// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  ИдентификаторЭДО - См. РегистрСведений.НастройкиОблачногоЭДО.ИдентификаторЭДО
// 
// Возвращаемое значение:
//  См. РегистрСведений.НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО
Функция УчетнаяЗаписьОблачногоЭДО(ИдентификаторЭДО) Экспорт
	
	ИдентификаторыЭДО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторЭДО);
	УчетныеЗаписиОблачногоЭДО = УчетныеЗаписиОблачногоЭДО(ИдентификаторыЭДО);
	Если ЗначениеЗаполнено(УчетныеЗаписиОблачногоЭДО) Тогда
		Возврат УчетныеЗаписиОблачногоЭДО[0];
	Иначе
		Возврат Справочники.УчетныеЗаписиОблачногоЭДО.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Неопределено
//                    - Массив из см. РегистрСведений.НастройкиОблачногоЭДО.ИдентификаторЭДО
// 
// Возвращаемое значение:
//  Массив из см. РегистрСведений.НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО
Функция УчетныеЗаписиОблачногоЭДО(ИдентификаторыЭДО = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
		|	НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО
		|ИЗ
		|	РегистрСведений.НастройкиОблачногоЭДО КАК НастройкиОблачногоЭДО";
		
	Если ИдентификаторыЭДО <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
			|ГДЕ
			|	НастройкиОблачногоЭДО.ИдентификаторЭДО В (&ИдентификаторыЭДО)";
		Запрос.УстановитьПараметр("ИдентификаторыЭДО", ИдентификаторыЭДО);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("УчетнаяЗаписьОблачногоЭДО");
	
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Массив из см. РегистрСведений.НастройкиОблачногоЭДО.ИдентификаторЭДО
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - См. РегистрСведений.НастройкиОблачногоЭДО.ИдентификаторЭДО
//  * Значение - См. РегистрСведений.НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО
Функция УчетныеЗаписиПоИдентификаторамЭДО(ИдентификаторыЭДО) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НастройкиОблачногоЭДО.ИдентификаторЭДО,
		|	НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО
		|ИЗ
		|	РегистрСведений.НастройкиОблачногоЭДО КАК НастройкиОблачногоЭДО
		|ГДЕ
		|	НастройкиОблачногоЭДО.ИдентификаторЭДО В (&ИдентификаторыЭДО)";
	Запрос.УстановитьПараметр("ИдентификаторыЭДО", ИдентификаторыЭДО);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.ИдентификаторЭДО, Выборка.УчетнаяЗаписьОблачногоЭДО);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - См. РегистрСведений.НастройкиОблачногоЭДО.ИдентификаторЭДО
//  УчетнаяЗаписьОблачногоЭДО - См. РегистрСведений.НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО
Процедура Записать(ИдентификаторЭДО, УчетнаяЗаписьОблачногоЭДО) Экспорт
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторЭДО.Установить(ИдентификаторЭДО);
	ЗаписьНабора = НаборЗаписей.Добавить();
	ЗаписьНабора.ИдентификаторЭДО = ИдентификаторЭДО;
	ЗаписьНабора.УчетнаяЗаписьОблачногоЭДО = УчетнаяЗаписьОблачногоЭДО;
	НаборЗаписей.Записать();
КонецПроцедуры

// Параметры:
//  УчетнаяЗаписьОблачногоЭДО - См. РегистрСведений.НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО
// 
// Возвращаемое значение:
//  Массив из Строка
Функция ИдентификаторыЭДОУчетнойЗаписи(УчетнаяЗаписьОблачногоЭДО) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ 
		|	НастройкиОблачногоЭДО.ИдентификаторЭДО
		|ИЗ
		|	РегистрСведений.НастройкиОблачногоЭДО КАК НастройкиОблачногоЭДО
		|ГДЕ
		|	НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО = &УчетнаяЗаписьОблачногоЭДО";
	Запрос.УстановитьПараметр("УчетнаяЗаписьОблачногоЭДО", УчетнаяЗаписьОблачногоЭДО);
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ИдентификаторЭДО");
КонецФункции

// Параметры:
//  УчетныеЗаписиОблачногоЭДО - Неопределено
//                            - Массив из СправочникСсылка.УчетныеЗаписиОблачногоЭДО.
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  * Значение - Массив из Строка
Функция ИдентификаторыЭДОПоУчетнымЗаписям(УчетныеЗаписиОблачногоЭДО = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НастройкиОблачногоЭДО.ИдентификаторЭДО,
		|	НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО
		|ИЗ
		|	РегистрСведений.НастройкиОблачногоЭДО КАК НастройкиОблачногоЭДО";
	
	Если УчетныеЗаписиОблачногоЭДО <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
			|ГДЕ
			|	НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО В (&УчетныеЗаписиОблачногоЭДО)";
		Запрос.УстановитьПараметр("УчетныеЗаписиОблачногоЭДО", УчетныеЗаписиОблачногоЭДО);
	КонецЕсли;
	
	Результат = Новый Соответствие;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИдентификаторыЭДО = Результат[Выборка.УчетнаяЗаписьОблачногоЭДО];
		Если ИдентификаторыЭДО = Неопределено Тогда
			ИдентификаторыЭДО = Новый Массив; // Массив из Строка
			Результат.Вставить(Выборка.УчетнаяЗаписьОблачногоЭДО, ИдентификаторыЭДО);
		КонецЕсли;
		ИдентификаторыЭДО.Добавить(Выборка.ИдентификаторЭДО);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - См. РегистрСведений.НастройкиОблачногоЭДО.ИдентификаторЭДО
Процедура Удалить(ИдентификаторЭДО) Экспорт
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторЭДО.Установить(ИдентификаторЭДО);
	НаборЗаписей.Записать();
КонецПроцедуры

// Параметры:
//  ИдентификаторыЭДО - Массив из см. РегистрСведений.НастройкиОблачногоЭДО.ИдентификаторЭДО
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  * Значение - Массив из Строка
Функция СгруппироватьПоУчетнымЗаписям(ИдентификаторыЭДО) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НастройкиОблачногоЭДО.ИдентификаторЭДО,
		|	НастройкиОблачногоЭДО.УчетнаяЗаписьОблачногоЭДО
		|ИЗ
		|	РегистрСведений.НастройкиОблачногоЭДО КАК НастройкиОблачногоЭДО
		|ГДЕ
		|	НастройкиОблачногоЭДО.ИдентификаторЭДО В (&ИдентификаторыЭДО)";
	Запрос.УстановитьПараметр("ИдентификаторыЭДО", ИдентификаторыЭДО);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ИдентификаторыЭДОПоУчетнойЗаписиСервиса = Результат[Выборка.УчетнаяЗаписьОблачногоЭДО]; // Массив из Строка
		Если ИдентификаторыЭДОПоУчетнойЗаписиСервиса = Неопределено Тогда
			ИдентификаторыЭДОПоУчетнойЗаписиСервиса = Новый Массив; // Массив из Строка
			Результат.Вставить(Выборка.УчетнаяЗаписьОблачногоЭДО, ИдентификаторыЭДОПоУчетнойЗаписиСервиса);
		КонецЕсли;
		ИдентификаторыЭДОПоУчетнойЗаписиСервиса.Добавить(Выборка.ИдентификаторЭДО);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
