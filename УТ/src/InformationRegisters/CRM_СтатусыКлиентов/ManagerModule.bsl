#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьСтатусКлиента(Клиент, СтатусРаботы) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Менеджер = СоздатьМенеджерЗаписи();
	Менеджер.Клиент = Клиент;
	Менеджер.Статус = СтатусРаботы;
	Менеджер.Период = ТекущаяДатаСеанса();
	Менеджер.Автор = Пользователи.ТекущийПользователь();
	Менеджер.Записать(Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПереходНаНовуюВерсию

Процедура ЗаполнитьДанныеПриПереходеНаВерсию(Параметры) Экспорт
	
	ДатаРегистрации = Дата(1980, 1, 1);
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 5000
	                      |	Партнеры.Ссылка КАК Клиент,
	                      |	Партнеры.CRM_СтатусРаботы КАК СтатусРаботы
	                      |ИЗ
	                      |	Справочник.Партнеры КАК Партнеры
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_СтатусыКлиентов.СрезПоследних КАК CRM_СтатусыКлиентовСрезПоследних
	                      |		ПО Партнеры.Ссылка = CRM_СтатусыКлиентовСрезПоследних.Клиент
	                      |ГДЕ
	                      |	Партнеры.CRM_СтатусРаботы <> ЗНАЧЕНИЕ(Справочник.CRM_СтатусыРаботыСКлиентом.ПустаяСсылка)
	                      |	И CRM_СтатусыКлиентовСрезПоследних.Статус ЕСТЬ NULL");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Менеджер = СоздатьМенеджерЗаписи();
		Менеджер.Клиент = Выборка.Клиент;
		Менеджер.Статус = Выборка.СтатусРаботы;
		Менеджер.Период = ДатаРегистрации;
		Менеджер.Записать(Истина);
	КонецЦикла;

	Если Выборка.Количество() > 0 Тогда
		ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Клиент);
	КонецЕсли;
	Параметры.ОбработкаЗавершена = (Выборка.Количество() < 5000);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
