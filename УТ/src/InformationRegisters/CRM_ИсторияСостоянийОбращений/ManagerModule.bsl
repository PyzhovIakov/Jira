
#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьИсториюСостояний(ОбращениеОбъект) Экспорт
	
	ПредыдущееСостояние = Неопределено;
	ОбращениеОбъект.ДополнительныеСвойства.Свойство("ПредыдущееСостояниеИнтереса", ПредыдущееСостояние);
	
	НовоеСостояние = ОбращениеОбъект.СостояниеИнтереса;
	
	Если НовоеСостояние = ПредыдущееСостояние Тогда
		Возврат; // Нет перехода
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбращениеСсылка = ОбращениеОбъект.Ссылка;
	ТекущаяДата = ТекущаяДатаСеанса();
	
	//БлокировкаДанных = Новый БлокировкаДанных;
	//ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.CRM_ИсторияСостоянийОбращений");
	//ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	//ЭлементБлокировки.УстановитьЗначение("Обращение", ОбращениеСсылка);
	//БлокировкаДанных.Заблокировать();
	
	Если ЗначениеЗаполнено(ПредыдущееСостояние) Тогда // Финализация предыдущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ИсторияСрезПоследних.Период КАК Период
		|ИЗ
		|	РегистрСведений.CRM_ИсторияСостоянийОбращений.СрезПоследних(, Обращение = &ОбращениеСсылка) КАК ИсторияСрезПоследних");
		
		Запрос.Параметры.Вставить("ОбращениеСсылка", ОбращениеСсылка);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			
			МенеджерЗаписи = СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период = Выборка.Период;
			МенеджерЗаписи.Обращение = ОбращениеСсылка;
			
			МенеджерЗаписи.Прочитать();
			МенеджерЗаписи.ДатаЗавершения = ТекущаяДата;
			МенеджерЗаписи.Записать();
			
		КонецЕсли;
		
	КонецЕсли;
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = ТекущаяДата;
	МенеджерЗаписи.Обращение = ОбращениеСсылка;
	МенеджерЗаписи.Состояние = НовоеСостояние;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти
