
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СхемаКомпоновки = Справочники.CRM_ПапкиДиалогов.ПолучитьСхемуКомпоновки();
	АдресСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновки, ЭтотОбъект.УникальныйИдентификатор);
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы);
	
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновки.НастройкиПоУмолчанию);
	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	
	//////////////////////////////////////
	
	ПараметрВыбораПоПользователю = Новый ПараметрВыбора("Отбор.Пользователь", Пользователи.ТекущийПользователь());
	
	ПараметрыВыбора = Новый Массив();
	ПараметрыВыбора.Добавить(ПараметрВыбораПоПользователю);
	
	Элементы.ПапкаДиалога.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Если КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Отбор для группировки диалогов пустой. Необходимо заполнить отбор.'"), , "Отбор");
		Возврат;
	КонецЕсли;
	
	Результаты = Новый Структура;
	Результаты.Вставить("Отбор", КомпоновщикНастроек.Настройки.Отбор);
	Результаты.Вставить("Замещать", ЗамещатьСуществующиеДиалогиВПапке);
	Результаты.Вставить("ПапкаДиалога", ПапкаДиалога);
	
	Закрыть(Результаты);
	
КонецПроцедуры // Применить()

#КонецОбласти
