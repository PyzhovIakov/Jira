#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
         Возврат;
    КонецЕсли;
	
	ОтборДиалог = Отбор.Найти("Диалог");
	Если ОтборДиалог <> Неопределено Тогда
		ТекущийДиалог = ОтборДиалог.Значение;
		Если ЗначениеЗаполнено(ТекущийДиалог) Тогда
			
			Для Каждого ЗаписьНЗ Из ЭтотОбъект Цикл
				Если ЗаписьНЗ.ВидСообщения = Перечисления.CRM_ВидыСообщенияМессенджера.Входящее Тогда
					
					Если ТекущийДиалог.Статус = Перечисления.CRM_СтатусыДиалогов.Новый Тогда
						ЗаписьНЗ.ДатаАРМ = ЗаписьНЗ.Дата;
					Иначе
						ПоследнееСообщение = CRM_РаботаСМессенджерамиСервер.ПоследнееСообщениеДиалога(ТекущийДиалог);
						
						Если ПоследнееСообщение = Неопределено Тогда
							ЗаписьНЗ.ДатаАРМ = ЗаписьНЗ.Дата;
							Продолжить;
						КонецЕсли;
						
						Если ПоследнееСообщение.ВидСообщения = Перечисления.CRM_ВидыСообщенияМессенджера.Исходящее Тогда
							ЗаписьНЗ.ДатаАРМ = ЗаписьНЗ.Дата;
						Иначе // Если это повторное входящее сообщение НЕ нового диалога, тогда дата для сортировки не должна измениться:
							Если ТипЗнч(ПоследнееСообщение) = Тип("Структура") И ЗначениеЗаполнено(ПоследнееСообщение.ДатаАРМ) Тогда
								ЗаписьНЗ.ДатаАРМ = ПоследнееСообщение.ДатаАРМ;
							Иначе
								ЗаписьНЗ.ДатаАРМ = ЗаписьНЗ.Дата;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
