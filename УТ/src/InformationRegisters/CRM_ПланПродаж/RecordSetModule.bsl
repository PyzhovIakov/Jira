#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ОтменитьСогласованиеВверх") Тогда
		СнятьУтверждениеСВерхнихУровней(ЭтотОбъект[0]);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СнятьУтверждениеСВерхнихУровней(Запись)
	
	Если Не ЗначениеЗаполнено(Запись.ИдентификаторСтрокиРодителя) Тогда
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	CRM_ПланПродаж.ПланПродаж КАК ПланПродаж,
	|	CRM_ПланПродаж.ПериодПлана КАК ПериодПлана,
	|	CRM_ПланПродаж.АналитикаУчетаПланаПродаж КАК АналитикаУчетаПланаПродаж,
	|	CRM_ПланПродаж.УровеньИерархии КАК УровеньИерархии,
	|	CRM_ПланПродаж.Периодичность КАК Периодичность
	|ИЗ
	|	РегистрСведений.CRM_ПланПродаж КАК CRM_ПланПродаж
	|ГДЕ
	|	CRM_ПланПродаж.ИдентификаторСтроки = &ИдентификаторСтроки");
	Запрос.УстановитьПараметр("ИдентификаторСтроки", Запись.ИдентификаторСтрокиРодителя);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаписьРодителя = РегистрыСведений.CRM_ПланПродаж.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(ЗаписьРодителя, Выборка);
		ЗаписьРодителя.Прочитать();
		Если ЗаписьРодителя.Выбран() Тогда
			ЗаписьРодителя.Согласовано = Ложь;
			ЗаписьРодителя.Записать();
		КонецЕсли;
	КонецЦикла;
	СнятьУтверждениеСВерхнихУровней(ЗаписьРодителя);
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли