#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ОбъектыИнтегрированныеС1СДокументооборотом.ДополнительныеДанные КАК ДополнительныеДанные
		|ИЗ
		|	РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом КАК ОбъектыИнтегрированныеС1СДокументооборотом
		|ГДЕ
		|	ОбъектыИнтегрированныеС1СДокументооборотом.Объект = &Объект
		|	И ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО = &ТипОбъектаДО
		|	И ОбъектыИнтегрированныеС1СДокументооборотом.ИдентификаторОбъектаДО = &ИдентификаторОбъектаДО");
	Запрос.УстановитьПараметр("ИдентификаторОбъектаДО", Запись.ИдентификаторОбъектаДО);
	Запрос.УстановитьПараметр("Объект", Запись.Объект);
	Запрос.УстановитьПараметр("ТипОбъектаДО", Запись.ТипОбъектаДО);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если ТипЗнч(Выборка.ДополнительныеДанные) = Тип("ХранилищеЗначения") Тогда
			СтруктураДанных = Выборка.ДополнительныеДанные.Получить();
			Если ТипЗнч(СтруктураДанных) = Тип("Структура") Тогда
				МассивСтрок = Новый Массив;
				Для Каждого Элемент Из СтруктураДанных Цикл
					МассивСтрок.Добавить(СтрШаблон("%1:   %2", Элемент.Ключ, СокрЛП(Элемент.Значение)));
				КонецЦикла;
				ДополнительныеДанные = СтрСоединить(МассивСтрок, Символы.ПС);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ДополнительныеДанные.Видимость = ЗначениеЗаполнено(ДополнительныеДанные);
	
КонецПроцедуры

#КонецОбласти