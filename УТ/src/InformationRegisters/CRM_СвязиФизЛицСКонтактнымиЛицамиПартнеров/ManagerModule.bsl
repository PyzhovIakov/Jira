#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция - Получить физ лицо
//  Функция возвращает ссылку на физическое лицо партнера по его контактному лицу.
//
// Параметры:
//  КонтактноеЛицо	 - Ссылка, СправочникСсылка.КонтактныеЛицаПартнеров	 - Контактное лицо партнера.
// 
// Возвращаемое значение:
//  Ссылка - Ссылка на партнера.
//
Функция ПолучитьФизЛицо(КонтактноеЛицо) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если	ТипЗнч(КонтактноеЛицо) <> Тип("СправочникСсылка.КонтактныеЛицаПартнеров")
		ИЛИ	Не ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		//
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ФизЛицо КАК ФизЛицо
	|ИЗ
	|	РегистрСведений.CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров
	|ГДЕ
	|	КонтактноеЛицо = &КонтактноеЛицо
	|");
	Запрос.УстановитьПараметр("КонтактноеЛицо", КонтактноеЛицо);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ФизЛицо;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

// Функция - Получить контактные лица
//  Функция возвращает ссылку на контактное лицо партнера по физическому лицу.
//
// Параметры:
//  ФизЛицо	 - СправочникСсылка.Партнеры - Физическое лицо, партнера.
// 
// Возвращаемое значение:
//  Массив - Массив ссылок на контактные лица партнеров.
//
Функция ПолучитьКонтактныеЛица(ФизЛицо) Экспорт
	Если	ТипЗнч(ФизЛицо) <> Тип("СправочникСсылка.Партнеры")
		ИЛИ	Не ЗначениеЗаполнено(ФизЛицо) Тогда
		//
		Возврат Новый Массив();
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	КонтактноеЛицо КАК КонтактноеЛицо
	|ИЗ
	|	РегистрСведений.CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров
	|ГДЕ
	|	ФизЛицо = &ФизЛицо
	|");
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("КонтактноеЛицо");
КонецФункции

// Процедура - Записать физ лицо
//  Процедура создает запись регистра CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров.
//
// Параметры:
//  КонтактноеЛицо	 - Ссылка, СправочникСсылка.КонтактныеЛицаПартнеров	 - Контактное лицо партнера.
//  ФизЛицо			 - Ссылка, СправочникСсылка.Партнеры				 - Физическое лицо, партнера.
//
Процедура ЗаписатьФизЛицо(КонтактноеЛицо, ФизЛицо) Экспорт
	Если	ТипЗнч(КонтактноеЛицо) <> Тип("СправочникСсылка.КонтактныеЛицаПартнеров")
		ИЛИ	ТипЗнч(ФизЛицо) <> Тип("СправочникСсылка.Партнеры") Тогда
		//
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КонтактноеЛицо) Или Не ЗначениеЗаполнено(ФизЛицо) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.КонтактноеЛицо = КонтактноеЛицо;
	МенеджерЗаписи.ФизЛицо = ФизЛицо;
	МенеджерЗаписи.Записать();
КонецПроцедуры

// Функция проверки является ли клиент ключевым физ. лицом.
//
// Параметры:
//  Клиент	 - Ссылка, СправочникСсылка.Партнеры - Партнер.
//  КонтактноеЛицо	 - Ссылка, СправочникСсылка.КонтактныеЛицаПартнеров	 - Контактное лицо партнера.
// 
// Возвращаемое значение:
//  Булево - Является ли клиент ключевым физ. лицом.
//
Функция ЭтоКлючевоеЛицо(Клиент, КонтактноеЛицо) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров.ФизЛицо КАК ФизЛицо
	                      |ИЗ
	                      |	РегистрСведений.CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров КАК CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров
	                      |ГДЕ
	                      |	CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров.ФизЛицо = &ФизЛицо");
	
	Запрос.УстановитьПараметр("ФизЛицо", Клиент);
	Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		Запрос.УстановитьПараметр("КонтактноеЛицо", КонтактноеЛицо);
		Запрос.Текст = Запрос.Текст + "
	                      |	И CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров.КонтактноеЛицо = &КонтактноеЛицо";
	КонецЕсли;
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли