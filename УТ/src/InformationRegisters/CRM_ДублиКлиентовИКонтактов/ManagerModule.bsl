#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает дубль.
//
// Параметры:
//	Оригинал - СправочникСсылка.Партнеры, СправочникСсылка.КонтактныеЛицаПартнеров - оригинал.
//	Дубль - СправочникСсылка.Партнеры, СправочникСсылка.КонтактныеЛицаПартнеров - дубль.
//	Заменить - Булево - заменить.
//	Комментарий - Строка - комментарий.
//
Процедура ЗаписатьДубль(Оригинал, Дубль, Заменить = Ложь, Комментарий = "") Экспорт
	
	Если Оригинал = Дубль Тогда
		Возврат;
	КонецЕсли;
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.Контакт = Дубль;
	Запись.Прочитать();
	Если НЕ Запись.Выбран() Тогда
		Запись.Контакт = Дубль;
		Запись.Дата = ТекущаяДатаСеанса();
		Запись.Автор = Пользователи.ТекущийПользователь();
	КонецЕсли;
	Запись.Оригинал = Оригинал;
	Запись.Заменить = Заменить;
	Если ЗначениеЗаполнено(Комментарий) Тогда
		Запись.Комментарий = Комментарий;
	КонецЕсли;
	Запись.Записать();
	
	ДублиКлиента = ПолучитьДубли(Дубль);
	Для Каждого СтрДубля Из ДублиКлиента Цикл
		ЗаписатьДубль(Оригинал, СтрДубля.Контакт, СтрДубля.Заменить, СтрДубля.Комментарий);
	КонецЦикла;
	
КонецПроцедуры

// Удаляет дубль.
//
// Параметры:
//	Дубль - СправочникСсылка.Партнеры, СправочникСсылка.КонтактныеЛицаПартнеров - дубль.
//
Процедура УдалитьДубль(Дубль) Экспорт
	Запись = СоздатьМенеджерЗаписи();
	Запись.Контакт = Дубль;
	Запись.Прочитать();
	Если Запись.Выбран() Тогда
		Запись.Удалить();
	КонецЕсли;
КонецПроцедуры

// Отмечает, как оригинал.
//
// Параметры:
//	Оригинал - СправочникСсылка.Партнеры, СправочникСсылка.КонтактныеЛицаПартнеров - оригинал.
//
Процедура ОтметитьКакОригинал(Оригинал) Экспорт
	Запись = СоздатьМенеджерЗаписи();
	Запись.Контакт = Оригинал;
	Запись.Прочитать();
	Если Запись.Выбран() Тогда
		Дубль = Запись.Оригинал;
		Заменить = Запись.Заменить;
		Комментарий = Запись.Комментарий;
		Запись.Удалить();
		ЗаписатьДубль(Оригинал, Дубль, Заменить, Комментарий);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДубли(Оригинал)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	CRM_ДублиКлиентовИКонтактов.Контакт КАК Контакт,
	                      |	CRM_ДублиКлиентовИКонтактов.Заменить КАК Заменить,
	                      |	CRM_ДублиКлиентовИКонтактов.Комментарий КАК Комментарий,
	                      |	CRM_ДублиКлиентовИКонтактов.Дата КАК Дата,
	                      |	CRM_ДублиКлиентовИКонтактов.Автор КАК Автор
	                      |ИЗ
	                      |	РегистрСведений.CRM_ДублиКлиентовИКонтактов КАК CRM_ДублиКлиентовИКонтактов
	                      |ГДЕ
	                      |	CRM_ДублиКлиентовИКонтактов.Оригинал = &Оригинал");
	Запрос.УстановитьПараметр("Оригинал", Оригинал);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

#КонецОбласти

#КонецЕсли
