// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  Идентификаторы - Массив из см. РегистрСведений.ИдентификаторыСинхронизируемыхОбъектовОблачногоЭДО.Идентификатор
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - см. РегистрСведений.ИдентификаторыСинхронизируемыхОбъектовОблачногоЭДО.Идентификатор
//  * Значение - см. РегистрСведений.ИдентификаторыСинхронизируемыхОбъектовОблачногоЭДО.Ссылка
Функция СсылкиПоИдентификаторам(Идентификаторы) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИдентификаторыСинхронизируемыхОбъектовОблачногоЭДО.Идентификатор,
		|	ИдентификаторыСинхронизируемыхОбъектовОблачногоЭДО.Ссылка
		|ИЗ
		|	РегистрСведений.ИдентификаторыСинхронизируемыхОбъектовОблачногоЭДО КАК
		|		ИдентификаторыСинхронизируемыхОбъектовОблачногоЭДО
		|ГДЕ
		|	ИдентификаторыСинхронизируемыхОбъектовОблачногоЭДО.Идентификатор В (&Идентификаторы)";
	
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.Идентификатор, Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  Идентификатор - см. РегистрСведений.ИдентификаторыСинхронизируемыхОбъектовОблачногоЭДО.Идентификатор
//  Ссылка - см. РегистрСведений.ИдентификаторыСинхронизируемыхОбъектовОблачногоЭДО.Ссылка
Процедура Записать(Идентификатор, Ссылка) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Идентификатор.Установить(Идентификатор);
	НаборЗаписей.Отбор.Ссылка.Установить(Ссылка);
	
	ЗаписьНабора = НаборЗаписей.Добавить();
	ЗаписьНабора.Идентификатор = Идентификатор;
	ЗаписьНабора.Ссылка = Ссылка;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
