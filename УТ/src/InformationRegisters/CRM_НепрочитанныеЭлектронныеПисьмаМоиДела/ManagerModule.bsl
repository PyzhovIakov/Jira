#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - Установить / снять пометку о прочтении
//
// Параметры:
//  ВыделенныеПисьма	 - ДокументСсылка.ЭлектронноеПисьмоВходящее	
//							Или ДокументСсылка.ЭлектронноеПисьмоИсходящее	- Выделенные письма.
//  Пользователь		 - СправочникСсылка.Пользователи - Текущий пользователь почты.
//  ПометкаНовая		 - Булево - Новое значение пометки.  
//
Процедура УстановитьСнятьПометкуОПрочтении(ДанныеПисем, Пользователь, ПометкаНовая) Экспорт
	
	ПисьмаКОбработке = Новый Массив;
	
	Если ТипЗнч(ДанныеПисем) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее")
		Или ТипЗнч(ДанныеПисем) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее") Тогда
		ПисьмаКОбработке.Добавить(ДанныеПисем);
	ИначеЕсли ТипЗнч(ДанныеПисем) = Тип("Массив") Тогда
		Для Каждого Письмо Из ДанныеПисем Цикл
			Если ТипЗнч(Письмо) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее")
			Или ТипЗнч(ДанныеПисем) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее") Тогда
				ПисьмаКОбработке.Добавить(Письмо);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Возврат;
	КонецЕсли;
	
	Для Каждого Письмо Из ПисьмаКОбработке Цикл
		
		МенеджерЗаписи = РегистрыСведений.CRM_НепрочитанныеЭлектронныеПисьмаМоиДела.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Письмо = Письмо;
		МенеджерЗаписи.Пользователь = Пользователь;
		МенеджерЗаписи.Прочитать();
		ПометкаУстановленная = (Не МенеджерЗаписи.Выбран());
		
		Если ПометкаУстановленная = ПометкаНовая Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПометкаНовая Тогда
			
			МенеджерЗаписи.Письмо = Письмо;
			МенеджерЗаписи.Пользователь = Пользователь;
			МенеджерЗаписи.Записать();
			
		ИначеЕсли Не ПометкаУстановленная И ПометкаНовая Тогда
			Попытка
				МенеджерЗаписи.Удалить();
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура - Установить пометку о прочтении для всех пользователей
//
// Параметры:
//  Письмо	 - ДокументСсылка.ЭлектронноеПисьмоВходящее Или ДокументСсылка.ЭлектронноеПисьмоИсходящее
//
Процедура УстановитьПометкуОПрочтенииВсемПользователям(Письмо) Экспорт
	
	НаборЗаписей = РегистрыСведений.CRM_НепрочитанныеЭлектронныеПисьмаМоиДела.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Письмо.Установить(Письмо);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
