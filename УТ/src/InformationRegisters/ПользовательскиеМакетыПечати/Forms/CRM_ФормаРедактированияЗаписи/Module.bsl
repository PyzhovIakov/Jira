
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗакрыватьПриВыборе = Ложь;
	
	ПолныйПутьКМакету = Параметры.ИмяОбъектаМетаданныхМакета;
	
	ЧастиПути = СтрЗаменить(ПолныйПутьКМакету, ".", Символы.ПС);
	
	Если Параметры.Свойство("МакетСсылка") Тогда
		МакетОбъектПоСсылке = Параметры.МакетСсылка.ПолучитьОбъект();
		ЗначениеВРеквизитФормы(МакетОбъектПоСсылке,"МакетОбъект");
		МакетИспользуетсяСтарый = МакетОбъект.Используется;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьМакет(Команда)
	
	ЗаписатьМакетНаСервере();
	
	Если Команда.Имя = "ЗаписатьМакетИЗакрыть" Тогда
		
		Если МакетОбъект.CRM_ПоставляемыйМакет Тогда
			ИмяОбъектаМетаданныхМакета = МакетОбъект.CRM_ИмяМакета;
		Иначе
			ИмяОбъектаМетаданныхМакета = "ПФ_" + Строка(МакетОбъект.Идентификатор);
		КонецЕсли;
		
		Результат = Новый Структура;
		Результат.Вставить("ИмяОбъектаМетаданныхМакета", ИмяОбъектаМетаданныхМакета);
		Результат.Вставить("Представление",              МакетОбъект.Наименование);
		Результат.Вставить("Владелец",                   МакетОбъект.ИсточникДанных);
		Результат.Вставить("ТипМакета",                  МакетОбъект.ТипМакета);
		Результат.Вставить("ИсточникиДанных",            ПолучитьИсточникиДанныхМакета());
		Результат.Вставить("CRM_МакетИспользуется",      МакетОбъект.Используется);
		Результат.Вставить("CRM_ВнешнийМакет",           Не МакетОбъект.CRM_ПоставляемыйМакет);
		Результат.Вставить("CRM_ПоставляемыйМакет",      МакетОбъект.CRM_ПоставляемыйМакет);
		Результат.Вставить("CRM_ИмяМакета",              МакетОбъект.CRM_ИмяМакета);
		
		Закрыть(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьИсточникиДанныхМакета()
	Возврат МакетОбъект.ИсточникиДанных.Выгрузить().ВыгрузитьКолонку("ИсточникДанных");
КонецФункции

&НаСервере
Процедура ЗаписатьМакетНаСервере()
	
	Объект = МакетОбъект.Ссылка.ПолучитьОбъект();
	
	Объект.Наименование = МакетОбъект.Наименование;
	Объект.Используется = МакетОбъект.Используется;
	
	Объект.Записать();
	
	Для Каждого ТекСтрока Из МакетОбъект.ИсточникиДанных Цикл
		ЗаписьМенеджер = РегистрыСведений.ПользовательскиеМакетыПечати.СоздатьМенеджерЗаписи();
		ЗаписьМенеджер.Объект = ТекСтрока.ИсточникДанных.ПолноеИмя;
		ЗаписьМенеджер.ИмяМакета = МакетОбъект.CRM_ИмяМакета;
		ЗаписьМенеджер.Прочитать();
		Если ЗаписьМенеджер.Выбран() Тогда
			ЗаписьМенеджер.Использование = МакетОбъект.Используется;
			ЗаписьМенеджер.CRM_Представление = МакетОбъект.Наименование;
			ЗаписьМенеджер.Записать();
		КонецЕсли;
	КонецЦикла;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура МакетИспользуетсяПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Модифицированность Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ОбработчикВопроса_ПередЗакрытием", ЭтотОбъект);
		ТекстПредупреждения = НСтр("ru = 'Данные макета были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстПредупреждения, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВопроса_ПередЗакрытием(Результат, ДополнительныеПаараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Команда = Команды.Найти("ЗаписатьМакетИЗакрыть");
		ЗаписатьМакет(Команда);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти