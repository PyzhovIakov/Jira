
#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события формы "ПриСозданииНаСервере".
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("СписокМакетов") Тогда
		СписокМакетов = Параметры.СписокМакетов;
	Иначе
		СписокМакетов = Новый СписокЗначений;
	КонецЕсли;	
	Если Параметры.Свойство("ПутьКФайлуМакета") Тогда
		ПутьКФайлуМакета = Параметры.ПутьКФайлуМакета;
		Элементы.ПутьКФайлуМакета.ТолькоПросмотр = Истина;
		Элементы.ВариантМакета.Видимость = Ложь;
		Элементы.ГруппаПараметрыHTMLМакета.Видимость = Ложь;
	КонецЕсли;
	Если Параметры.Свойство("Представление") Тогда
		ПредставлениеМакета = Параметры.Представление;
	КонецЕсли;
	Если Параметры.Свойство("ТипМакета") Тогда
		ТипМакета = Параметры.ТипМакета;
	КонецЕсли;
	Если Параметры.Свойство("Владелец") Тогда
		Владелец = Параметры.Владелец;
		ВладелецМакета = Владелец.ПолноеИмя;
		ПредставлениеВладельца = Владелец.Синоним;
	КонецЕсли;
	
	Если Параметры.Свойство("ИмяОбъектаМетаданныхМакета") Тогда
		ИмяОбъектаМетаданныхМакета = Параметры.ИмяОбъектаМетаданныхМакета;
	КонецЕсли;
	Если ПустаяСтрока(ВладелецМакета) Тогда
		ВладелецМакета = "Документ.КоммерческоеПредложениеКлиенту";
		ПредставлениеВладельца = "Коммерческое предложение клиенту";
		Владелец = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ВладелецМакета, Ложь);
	КонецЕсли;
	
	МакетИспользуется = Истина;
	
	ВариантМакета = 1;
	
	Элементы.ФорматА4.Видимость = Ложь;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" поля формы "ПутьКФайлуМакета".
//
Процедура ПутьКФайлуМакетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОписаниеОповещения = Новый ОписаниеОповещения("ПутьКФайлуМакетаНачалоВыбораПродолжение", ЭтотОбъект);
	НачатьПодключениеРасширенияРаботыСФайлами(ОписаниеОповещения);
КонецПроцедуры // ПутьКФайлуМакетаНачалоВыбора()

&НаКлиенте
Процедура ПутьКФайлуМакетаНачалоВыбораПродолжение(Подключено, ДополнительныеПараметры) Экспорт
	Если Подключено Тогда
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		#Если ВебКлиент Тогда
			ДиалогОткрытияФайла.Фильтр             = "Файл макета (*.htm,*.html)|*.htm;*.html";
		#Иначе
			Если ВариантМакета = 1 Тогда
				ДиалогОткрытияФайла.Фильтр             = "Файл макета (*.htm,*.html)|*.htm;*.html";
			ИначеЕсли ВариантМакета = 3 Тогда
				ДиалогОткрытияФайла.Фильтр             = "Файл макета (*.doc)|*.doc";
			ИначеЕсли ВариантМакета = 4 Тогда
				ДиалогОткрытияФайла.Фильтр             = "Файл макета (*.odt)|*.odt";
			КонецЕсли;
		#КонецЕсли
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru='Выберите путь к файлу макета';en='Select path to file of template'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПутьКФайлуМакетаНачалоВыбораЗавершение", ЭтотОбъект);
		ДиалогОткрытияФайла.Показать(ОписаниеОповещения);
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения("ПутьКФайлуМакетаНачалоВыбораПродолжение", ЭтотОбъект);
		НачатьУстановкуРасширенияРаботыСФайлами(ОписаниеОповещения);
	КонецЕсли;
КонецПроцедуры // ПутьКФайлуМакетаНачалоВыбора()

&НаКлиенте
Процедура ПутьКФайлуМакетаНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы <> Неопределено Тогда
		ПутьКФайлуМакета	= ВыбранныеФайлы[0];
		ТипМакета			= ВРег(ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ПутьКФайлуМакета));
		СформироватьИмяОбъектаМетаданныхМакета();
	КонецЕсли;
КонецПроцедуры // ПутьКФайлуМакетаНачалоВыбора()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Процедура - обработчик команды формы "ОК".
//
Процедура ОК(Команда)
	Если ПроверитьЗаполнениеПолей() Тогда
		
		ЧастиИмени = СтрРазделить(ИмяОбъектаМетаданныхМакета, ".");
		
		МассивИсточникиДанных = Новый Массив;
		МассивИсточникиДанных.Добавить(Владелец);
		
		Результат = Новый Структура;
		Результат.Вставить("ВладелецМакета",             ВладелецМакета);
		Результат.Вставить("ИмяОбъектаМетаданныхМакета", ИмяОбъектаМетаданныхМакета);
		Результат.Вставить("Представление",              ПредставлениеМакета);
		Результат.Вставить("ПредставлениеВладельца",     ПредставлениеВладельца);
		Результат.Вставить("Владелец",                   Владелец);
		Результат.Вставить("ТипМакета",                  ТипМакета);
		Результат.Вставить("ПутьКФайлуМакета",           ПутьКФайлуМакета);
		Результат.Вставить("ЭтоНовый",                   СоздатьПустойМакетHTML);
		Результат.Вставить("ИмяМакета",                  ЧастиИмени[ЧастиИмени.ВГраница()]);
		Результат.Вставить("ИсточникиДанных",            МассивИсточникиДанных);
		Результат.Вставить("CRM_МакетИспользуется",      МакетИспользуется);
		Результат.Вставить("CRM_ВнешнийМакет",           Истина);
		Результат.Вставить("CRM_ПоставляемыйМакет",      Ложь);
		Результат.Вставить("CRM_ИмяМакета",              CRM_ИмяМакета);
		Закрыть(Результат);
		
	КонецЕсли;
КонецПроцедуры // ОК()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Функция проверяет заполнение полей.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Строка	- Сообщение об ошибке.
//
Функция ПроверитьЗаполнениеПолей()
	Рез = Истина;
	Если НЕ ВариантМакета = 2 И НЕ ЗначениеЗаполнено(ПутьКФайлуМакета) Тогда
		ТекстСообщения = НСтр("ru='Не выбран файл макета';en='No template file selected'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПутьКФайлуМакета");
		Рез = Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ВладелецМакета) Тогда
		ТекстСообщения = НСтр("ru='Не выбран документ/справочник для которого создаем макет';
			|en='Document/catalog not selected for which template been created'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВладелецМакета");
		Рез = Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПредставлениеМакета) Тогда
		ТекстСообщения = НСтр("ru='Не указано наименование макета';en='The name of template not specified'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПредставлениеМакета");
		Рез = Ложь;
	КонецЕсли;
	
	ИндексМакета = СписокМакетов.Количество() + 1;
	
	CRM_ИмяМакета = ВладелецМакета + "." + "ПФ_" + ТипМакета + "_Макет" + Строка(ИндексМакета);
	
	НайденныйМакет = СписокМакетов.НайтиПоЗначению(CRM_ИмяМакета);
	
	Если НЕ (НайденныйМакет = Неопределено) Тогда
		Ном = 1;
		Пока НЕ (НайденныйМакет = Неопределено) Цикл
			CRM_ИмяМакета = ВладелецМакета + "." + "ПФ_" + ТипМакета + "_Макет" + Строка(ИндексМакета + Ном);
			НайденныйМакет             = СписокМакетов.НайтиПоЗначению(CRM_ИмяМакета);
			Ном = Ном + 1;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции // ПроверитьЗаполнениеПолей()

&НаКлиенте
// Процедура выполняет заполнение полей "ПредставлениеМакета" и "ИмяОбъектаМетаданныхМакета".
//
// Парамеры:
//	Нет.
//
Процедура СформироватьИмяОбъектаМетаданныхМакета()
	Если ТипМакета = "DOC" Тогда
		ПредставлениеМакета = ПредставлениеВладельца + " (Microsoft Word)";
	ИначеЕсли ТипМакета = "ODT" Тогда
		ПредставлениеМакета = ПредставлениеВладельца + " (OpenOffice.org Writer)";
	ИначеЕсли ТипМакета = "HTM" Или ТипМакета = "HTML" Тогда
		ПредставлениеМакета = ПредставлениеВладельца + " (HTML)";
	ИначеЕсли ТипМакета = "MXL" Тогда
		ПредставлениеМакета = ПредставлениеВладельца + " (Табличный документ)";
	Иначе
		ПредставлениеМакета = "";
	КонецЕсли;
КонецПроцедуры // СформироватьИмяОбъектаМетаданныхМакета()

&НаКлиенте
Процедура СоздатьПустойМакетHTMLПриИзменении(Элемент)
	Элементы.ПутьКФайлуМакета.ТолькоПросмотр = СоздатьПустойМакетHTML;
	Если СоздатьПустойМакетHTML Тогда
		#Если ВебКлиент Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ПутьКПустомуФайлуМакетаПродолжение", ЭтотОбъект);
			НачатьПодключениеРасширенияРаботыСФайлами(ОписаниеОповещения);
		#Иначе
			ПутьКПустомуФайлуМакетаПродолжение(Истина, "");
		#КонецЕсли
		
	Иначе	
		ТипМакета = "";
		ПутьКФайлуМакета = "";
	КонецЕсли;
	
	Элементы.ФорматА4.Видимость = СоздатьПустойМакетHTML;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКПустомуФайлуМакетаПродолжение(Подключено, ДополнительныеПараметры) Экспорт
	Если Подключено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПолучениеКаталогаЗавершение", ЭтотОбъект);
		НачатьПолучениеКаталогаВременныхФайлов(ОписаниеОповещения);
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения("ПутьКПустомуФайлуМакетаПродолжение", ЭтотОбъект);
		НачатьУстановкуРасширенияРаботыСФайлами(ОписаниеОповещения);
		
	КонецЕсли;
КонецПроцедуры // ПутьКФайлуМакетаНачалоВыбора()

&НаКлиенте
Процедура ПолучениеКаталогаЗавершение(Путь, ДопПараметры) Экспорт
	Каталог = Путь;
	ТипМакета = "HTML";
	ИндексМакета = СписокМакетов.Количество() + 1;
	CRM_ИмяМакета = ВладелецМакета + "." + "ПФ_" + ТипМакета + "_Макет" + Строка(ИндексМакета);
	
	#Если ВебКлиент Тогда
		УИН = Новый УникальныйИдентификатор;
	#Иначе
		Если НЕ ЗначениеЗаполнено(УИН) Тогда
			УИН = Новый УникальныйИдентификатор;
		КонецЕсли;
	#КонецЕсли
	
	ИмяФайла = Строка(УИН) + ".html";
	ПутьКФайлуМакета = Каталог + "\" + ИмяФайла;

	ТекДок = Новый ТекстовыйДокумент;
	Если ФорматА4 Тогда
		ТекДок.ДобавитьСтроку("<html>
		|<head>
		|<STYLE TYPE=""text/css"">
		|body {
		|  background: rgb(240,240,240); 
		|}
		|page {
		|  background: white;
		|  display: block;
		|  margin: 0 auto;
		|  margin-bottom: 0.5cm;
		|  box-shadow: 0 0 0.5cm rgba(0,0,0,0.5);
		|}
		|page[size=""A4""] {  
		|  width: 21cm; /* ширина */
		|  padding: 15mm 15mm 15mm 20mm; /* внутренние отступы - верх, право, низ, лево */
		|}
		|@media print {
		|  body, page {
		|    margin: 0;
		|    box-shadow: 0;
		|  }
		|}
		|</STYLE>
		|</head>
		|<body><page  size=""A4"">
		|<div>
		|&nbsp
		|</div>
		|</page>
		|</body>
		|<html>");
		
	Иначе
		ТекДок.ДобавитьСтроку("<html>
		|<head></head>
		|<body></body>
		|</html>");
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияБезДействия", CRM_ОбщегоНазначенияКлиент);	
	ТекДок.НачатьЗапись(ОписаниеОповещения, ПутьКФайлуМакета);
КонецПроцедуры // ПутьКФайлуМакетаНачалоВыбора(

&НаКлиенте
Процедура ВариантМакетаПриИзменении(Элемент)
	
	Элементы.ГруппаПараметрыHTMLМакета.Видимость = (ВариантМакета = 1);
	Элементы.ПутьКФайлуМакета.Видимость          = Не (ВариантМакета = 2);
	
	Если ВариантМакета <> 1 Тогда
		ТипМакета = "";
		ПутьКФайлуМакета = "";
		СоздатьПустойМакетHTML = Ложь;
		ФорматА4 = Ложь;
		Элементы.ФорматА4.Видимость = Ложь;
		Элементы.ПутьКФайлуМакета.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	Если ВариантМакета = 2 Тогда
		ТипМакета = "MXL";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
