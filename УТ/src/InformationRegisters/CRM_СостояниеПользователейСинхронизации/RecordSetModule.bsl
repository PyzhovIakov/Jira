#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если РаботаВМоделиСервиса.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокУзлов = Новый СписокЗначений;
	Для Каждого СтрокаЗаписи Из ЭтотОбъект Цикл
		Если СтрокаЗаписи.Идентификатор = "" Тогда
			СтрокаЗаписи.Идентификатор = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;	
		Если СтрокаЗаписи.Пользователь.CRM_ЕмейлДляСинхронизации <> СтрокаЗаписи.ЭлектроннаяПочта Тогда
			ПользовательОбъект = СтрокаЗаписи.Пользователь.ПолучитьОбъект();
			ПользовательОбъект.CRM_ЕмейлДляСинхронизации = СтрокаЗаписи.ЭлектроннаяПочта;
			ПользовательОбъект.ОбменДанными.Загрузка = Истина;
			ПользовательОбъект.ДополнительныеСвойства.Вставить("ДатаИзменения", ТекущаяДатаСеанса());
			ПользовательОбъект.Записать();
		КонецЕсли;
		
		КодУзла = СтрокаЗаписи.Идентификатор;
		УзелОбмена = ПланыОбмена.CRM_СинхронизацияСМобильнымПриложениемНовый.НайтиПоКоду(КодУзла);
		Если УзелОбмена.Пустая() Тогда
			УзелОбмена = ПланыОбмена.CRM_СинхронизацияСМобильнымПриложениемНовый.СоздатьУзел();
			УзелОбмена.Код = КодУзла;
			УзелОбмена.Наименование = "Пользователь: " + СтрокаЗаписи.Пользователь + ", устройство: " + СтрокаЗаписи.IMEI;
			УзелОбмена.Пользователь = СтрокаЗаписи.Пользователь;
			ИмяФайлаПравил = ПолучитьИмяВременногоФайла("xml");
			Если CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
				
				МакетПравил = ПланыОбмена.CRM_СинхронизацияСМобильнымПриложениемНовый.ПолучитьМакет("CRM_ПравилаОбмена");
				
			Иначе 
				
				МакетПравил = ПланыОбмена.CRM_СинхронизацияСМобильнымПриложениемНовый.ПолучитьМакет("CRM_Модуль_ПравилаОбмена");
				
			КонецЕсли;
			МакетПравил.Записать(ИмяФайлаПравил);
			ДДПравил = Новый ДвоичныеДанные(ИмяФайлаПравил);
			УзелОбмена.ПравилаОбмена = Новый ХранилищеЗначения(ДДПравил);
			УзелОбмена.Записать();
			
			МассивПараметров = Новый Массив;
			МассивПараметров.Добавить(УзелОбмена.Ссылка);

			МенеджерЗаписи = РегистрыСведений.CRM_ДатыОбращенийiCRM.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.УзелОбмена = УзелОбмена.Ссылка;
			МенеджерЗаписи.ДатаПоследнегоОбращения = ТекущаяДатаСеанса();
			МенеджерЗаписи.Записать(Истина); 
			
			ФоновоеЗадание = ФоновыеЗадания.Выполнить("CRM_СинхронизацияСiCRMНовый.ЗарегистрироватьНовыйУзелОбмена",
				 МассивПараметров, КодУзла, "Регистрация изменений для узла " 
				+ КодУзла);
		КонецЕсли;	
		СписокУзлов.Добавить(УзелОбмена.Ссылка);
		
		Если СтрокаЗаписи.Состояние = Перечисления.CRM_СостоянияСинхронизацииПользователя.Отключен Тогда
			РегистрыСведений.CRM_СостояниеПользователейСинхронизации.ОтключитьПользователя(УзелОбмена.Ссылка);
		КонецЕсли;
	КонецЦикла;	
	
	Выборка = ПланыОбмена.CRM_СинхронизацияСМобильнымПриложениемНовый.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтотУзел И Выборка.Код = "" Тогда
			Об = Выборка.ПолучитьОбъект();
			Об.Код = "CRM";
			Об.Наименование = "CRM";
			Об.Записать();
		КонецЕсли;	
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли
