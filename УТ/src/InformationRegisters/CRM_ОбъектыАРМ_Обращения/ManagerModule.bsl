#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ПереходНаНовуюВерсию

Процедура ЗаполнитьДанныеПриПереходеНаВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 5000
	|	CRM_СостоянияЛидов.Лид КАК Лид,
	|	CRM_СостоянияЛидов.ВидКанала КАК ВидКанала,
	|	CRM_СостоянияЛидов.КаналПолучения КАК КаналПолучения,
	|	CRM_СостоянияЛидов.Состояние КАК Состояние
	|ИЗ
	|	РегистрСведений.CRM_СостоянияЛидов КАК CRM_СостоянияЛидов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ОбъектыАРМ_Обращения КАК CRM_ОбъектыАРМ_Обращения
	|		ПО CRM_СостоянияЛидов.Лид = CRM_ОбъектыАРМ_Обращения.Объект
	|ГДЕ
	|	CRM_СостоянияЛидов.Состояние В (ЗНАЧЕНИЕ(Справочник.CRM_СостоянияЛидов.Новый),
	|		CRM_СостоянияЛидов.Состояние = ЗНАЧЕНИЕ(Справочник.CRM_СостоянияЛидов.Повторный))
	|	И НЕ CRM_СостоянияЛидов.Лид ССЫЛКА Документ.CRM_СообщениеМессенджера
	|	И CRM_ОбъектыАРМ_Обращения.Объект ЕСТЬ NULL
	|	И НЕ CRM_СостоянияЛидов.Лид.CRM_СкрытьВАРМ
	|	И НЕ CRM_СостоянияЛидов.Лид.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	CRM_СостоянияЛидов.Лид.ДатаНачала УБЫВ,
	|	CRM_СостоянияЛидов.Лид.Дата УБЫВ");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗарегистрироватьЛидВАРМ(Выборка.Лид, Выборка.Состояние);
	КонецЦикла;
	
	Выборка.Сбросить();
	
	Если Выборка.Следующий() Тогда
		ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Лид); 
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = (Выборка.Количество() < 5000);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ЛидЗарегистрированВАРМ(Объект) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Прочитать();
	
	Возврат НаборЗаписей.Количество() > 0;
	
КонецФункции

Процедура ЗарегистрироватьЛидВАРМ(Источник, Знач Состояние) Экспорт
	
	Если ТипЗнч(Источник) = Тип("ДокументСсылка.CRM_СообщениеМессенджера") Тогда
		Объект = Источник.Диалог;
		СостояниеЛида = РегистрыСведений.CRM_СостоянияЛидов.ПолучитьТекущееСостояниеЛида(Источник.Диалог);
		Если СостояниеЛида <> Неопределено Тогда
			Состояние = СостояниеЛида.Состояние;
		КонецЕсли;
	Иначе
		Объект = Источник;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Записать();
	
	Если Объект.CRM_СкрытьВАРМ Или Объект.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Если Состояние = Справочники.CRM_СостоянияЛидов.Новый
		Или Состояние = Справочники.CRM_СостоянияЛидов.Повторный Тогда
		
		Заголовок = "";
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = Объект;
		Если ТипЗнч(Объект) = Тип("ДокументСсылка.CRM_Заявка") Тогда
			МенеджерЗаписи.ВидКанала = Перечисления.CRM_ВидыКаналовПоступленияОбращений.Заявка;
			МенеджерЗаписи.КаналПолучения = Объект.ИсточникПолучения;
			Заголовок = Объект.Тема;
		ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.ТелефонныйЗвонок") Тогда
			МенеджерЗаписи.ВидКанала = Перечисления.CRM_ВидыКаналовПоступленияОбращений.ТелефонныйЗвонок;
			МенеджерЗаписи.Избранный = (Объект.Важность = Перечисления.ВариантыВажностиВзаимодействия.Высокая);
			МенеджерЗаписи.Звонок_Состояние = Объект.сфпСостояниеЗвонка;
			Заголовок = Объект.Тема;
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.CRM_Диалоги") Тогда
			МенеджерЗаписи.ВидКанала = Перечисления.CRM_ВидыКаналовПоступленияОбращений.Диалоги;
			МенеджерЗаписи.КаналПолучения = Объект.УчетнаяЗапись;
		ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее") Тогда
			МенеджерЗаписи.ВидКанала = Перечисления.CRM_ВидыКаналовПоступленияОбращений.ЭлектроннаяПочта;
			МенеджерЗаписи.КаналПолучения = Объект.УчетнаяЗапись;
			МенеджерЗаписи.Избранный = Объект.CRM_Избранный;
			МенеджерЗаписи.СмещениеДаты = Объект.CRM_СмещениеДатыОтправления;
			МенеджерЗаписи.СмещениеДатыЗаполнено = Объект.CRM_СмещениеДатыОтправленияЗаполнено;
			МенеджерЗаписи.Письмо_Папка = CRM_Взаимодействия.ПапкаПисьма(Объект);
			Заголовок = Объект.Тема;
		ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее") Тогда
			МенеджерЗаписи.ВидКанала = Перечисления.CRM_ВидыКаналовПоступленияОбращений.ЭлектроннаяПочта;
			МенеджерЗаписи.КаналПолучения = Объект.УчетнаяЗапись;
			МенеджерЗаписи.Избранный = Объект.CRM_Избранный;
			Заголовок = Объект.Тема;
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Ответственный) Тогда
			МенеджерЗаписи.Ответственный = Объект.Ответственный;
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "CRM_РольОтветственного")
			И ЗначениеЗаполнено(Объект.CRM_РольОтветственного) Тогда
			МенеджерЗаписи.Ответственный = Объект.CRM_РольОтветственного;
		КонецЕсли;

		Если ТипЗнч(Источник) = Тип("СправочникСсылка.CRM_Диалоги")
			Или ТипЗнч(Источник) = Тип("ДокументСсылка.CRM_СообщениеМессенджера") Тогда
			
			ПоследнееСообщение = CRM_РаботаСМессенджерамиСервер.ПоследнееСообщениеДиалога(Объект);
			
			Если ТипЗнч(Источник) = Тип("СправочникСсылка.CRM_Диалоги") Тогда
				ТемпДата = Источник.ДатаНачала;
				ТемпЗаголовок = "";
			Иначе
				ТемпДата = Источник.Дата;
				ТемпЗаголовок = СокрЛП(Источник.ТекстСообщения);
			КонецЕсли;
			Если ПоследнееСообщение = Неопределено Тогда
				МенеджерЗаписи.Дата = ТемпДата;
				Заголовок = ТемпЗаголовок;
			Иначе
				Если ПоследнееСообщение.Дата > ТемпДата Тогда
					МенеджерЗаписи.Дата = ПоследнееСообщение.Дата;
					Заголовок = ПоследнееСообщение.ТекстСокр;
				Иначе
					МенеджерЗаписи.Дата = ТемпДата;
					Заголовок = ТемпЗаголовок;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			МенеджерЗаписи.Дата = Источник.Дата;
		КонецЕсли;
		
		МенеджерЗаписи.Заголовок = Заголовок;
		
		МенеджерЗаписи.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьПапкуПисьма(Письмо) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Письмо);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда
		Для Каждого Запись Из НаборЗаписей Цикл
			Запись.Письмо_Папка = Неопределено;		
		КонецЦикла;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли