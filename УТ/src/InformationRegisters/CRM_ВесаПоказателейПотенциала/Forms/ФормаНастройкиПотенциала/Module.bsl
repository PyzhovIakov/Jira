
#Область ОписаниеПеременных

&НаКлиенте
Перем ВводЧисла; // Признак ввода числа
&НаКлиенте
Перем ЕстьТочка; // Признак того, что при вводе числа уже поставлен десятичный разделитель.

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события формы "ПриСозданииНаСервере".
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ CRM_ЛицензированиеЭкспортныеМетоды.ПодсистемаCRMИспользуется() Тогда
		CRM_ЛицензированиеЭкспортныеМетоды.ВывестиУведомлениеОНедоступности(НСтр("ru = 'форму настройки Показателей'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	CRM_ЛицензированиеЭкспортныеМетоды.ПолучитьЗащищеннуюОбработку().ПриСозданииНаСервере(ЭтотОбъект,
		 Отказ,
		 СтандартнаяОбработка);
	
	Если Параметры.Свойство("Подразделение") Тогда
		Подразделение = Параметры.Подразделение;
	КонецЕсли;
	
	Если Параметры.Свойство("РежимВыбора") Тогда
		РежимВыбора = Параметры.РежимВыбора;
	КонецЕсли;
	
	Если Параметры.Свойство("Формула") Тогда
		Формула = Параметры.Формула;
	Иначе
		Формула = Подразделение.CRM_ФормулаРасчетаПотенциала;
	КонецЕсли;
	
	Если Параметры.Свойство("ПериодРасчета") Тогда
		ПериодРасчета = Параметры.ПериодРасчета;
	Иначе
		ПериодРасчета = Подразделение.CRM_ПериодРасчетаПотенциала;
	КонецЕсли;
	
	ФормулаНачальная = Формула;
	
	Если Не ЗначениеЗаполнено(Подразделение) Тогда
		ВызватьИсключение НСтр("ru='Форма может быть открыта только для подразделения!';
			|en='The form can be opened only for the unit!'");
	КонецЕсли;
	
	ЗаполнитьТаблицуПоказателей();
	
	ЗаполнитьДеревоПоказателейВерхнейИерархиейНаСервере(Элементы.КомандаФильтрПоФормуле.Пометка);
	
	// Используемые показатели
	ЗаполнитьИспользуемыеПоказатели();
	
	// Клиенты
	ЗагрузитьНастройкиФормы();
	ЗаполнитьПотенциалыКлиентов();
	
	// Заголовок
	Если ПериодРасчета.Пустая() Тогда
		Элементы.ГруппаЛеваяЧасть.Заголовок = "Формула расчета потенциала";
	Иначе
		Элементы.ГруппаЛеваяЧасть.Заголовок = "Формула расчета потенциала (период расчета: " + НРег(ПериодРасчета) + ")";
	КонецЕсли;
	
	// Кнопка свернуть/развернуть дерево показателей.
	Элементы.КомандаСвернуть.Пометка = Истина;
	Элементы.КомандаСвернуть.Заголовок = "Развернуть";
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события формы "ПриОткрытии".
//
Процедура ПриОткрытии(Отказ)
	// Интерфейс
	Заголовок = "Мастер установки показателей для <" + СокрЛП(Подразделение) + ">";
	
	// Формула
	ВводЧисла = Ложь;
	ЕстьТочка = Ложь;
	Если (СтрДлина(Формула) > 0) И (Лев(Формула, 1) <> " ") Тогда
		Формула = " " + Формула;
	КонецЕсли;
	ОпределитьТекущийРежимВвода();
	//ПодключитьОбработчикОжидания("ФокусНаВвод", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события формы "ПриЗакрытии".
//
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	СохранитьНастройкиФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

////////////////////////////////////////////////////////////////////////////////
// Калькулятор

// Обработчик нажатия цифр
//
&НаКлиенте
Процедура КнопкаЦифраНажатие(Элемент)
	Если НЕ ВводЧисла Тогда
		ВводЧисла = Истина;
		ЕстьТочка = Ложь;
		Формула = Формула + " ";
	КонецЕсли;
	Формула = Формула + ТекущийЭлемент.Заголовок;
КонецПроцедуры

// Обработчик нажатия знаков
//
&НаКлиенте
Процедура КнопкаЗнакНажатие(Элемент)
	ВводЧисла = Ложь;
	ЕстьТочка = Ложь;
	Если (СтрДлина(Формула) > 0) И (Прав(Формула, 1) = ",") Тогда
		Формула = Формула + "0";
	КонецЕсли;
	Формула = Формула + " " + ТекущийЭлемент.Заголовок;
КонецПроцедуры

// Обработчик нажатия точки
//
&НаКлиенте
Процедура КнопкаТочкаНажатие(Элемент)
	Если ЕстьТочка ИЛИ (НЕ ВводЧисла) Тогда
		Возврат;
	КонецЕсли;
	ЕстьТочка = Истина;
	Формула = Формула + ",";
КонецПроцедуры

// Обработчик кнопки Забой
//
&НаКлиенте
Процедура КнопкаЗабойНажатие(Элемент)
	Если ПустаяСтрока(Формула) Тогда
		Возврат;
	КонецЕсли;
	// Выясним, что мы вводили до этого.
	ОпределитьТекущийРежимВвода();
	Если ВводЧисла Тогда
		// Для числа удалим символ
		ЕстьТочка = НЕ Прав(Формула, 1) = ",";
		Формула = Сред(Формула, 1, СтрДлина(Формула) - 1);
		// Может, мы удалили уже всё число.
		Если Прав(Формула, 1) = " " Тогда
			Формула = Сред(Формула, 1, СтрДлина(Формула) - 1);
			ВводЧисла = Ложь;
			ЕстьТочка = Ложь;
		КонецЕсли;
		Возврат;
	ИначеЕсли Прав(Формула, 1) = "]" Тогда
		// Удаляем Показатель
		Счетчик = СтрДлина(Формула) - 1;
		КодПоказатель = "";
		Пока Сред(Формула, Счетчик, 1) <> "[" Цикл
			Счетчик = Счетчик - 1;
			КодПоказатель = КодПоказатель + Сред(Формула, Счетчик + 1, 1);
		КонецЦикла;
		// инвертируем КодПоказатель
		Длина = СтрДлина(КодПоказатель);
		ИмяПоказателя = "";
		Пока Длина > 0 Цикл			
			ИмяПоказателя = ИмяПоказателя + Сред(КодПоказатель, Длина, 1);
			Длина = Длина - 1;
		КонецЦикла;	
		
		// Удалим из списка задействованных показателей.
		
		ПоказательНаименование = ПолучитьПоказательПоНаименованию(ИмяПоказателя);
		Показатель = ИспользуемыеПоказатели.НайтиПоЗначению(ПоказательНаименование);
		Если НЕ Показатель = Неопределено Тогда
			ИспользуемыеПоказатели.Удалить(Показатель);
		КонецЕсли;
		
		// Очищаем Показатель (вместе с лидирующим пробелом).
		Формула = Сред(Формула, 1, Счетчик - 2);
		
		Возврат;
	ИначеЕсли СтрНайти("+-*/", Прав(Формула, 1)) > 0 Тогда
		// Знак операции
		// Удалим вместе с пробелом
		Формула = Сред(Формула, 1, СтрДлина(Формула) - 2);
	ИначеЕсли СтрНайти("(", Прав(Формула, 1)) > 0 Тогда
		// Скобка открывающая
		// Удалим вместе с пробелом
		Формула = Сред(Формула, 1, СтрДлина(Формула) - 2);
	ИначеЕсли СтрНайти(")", Прав(Формула, 1)) > 0 Тогда
		// Скобка закрывающая
		// Удалим 
		Формула = Сред(Формула, 1, СтрДлина(Формула) - 1);
	КонецЕсли;
	ОпределитьТекущийРежимВвода();
КонецПроцедуры

// Обработчик кнопки стереть
//
&НаКлиенте
Процедура КнопкаСтеретьНажатие(Элемент)
	Формула = "";
	ВводЧисла = Ложь;
	ЕстьТочка = Ложь;
	ИспользуемыеПоказатели.Очистить();
КонецПроцедуры

// Обработчик кнопки скобка
//
&НаКлиенте
Процедура КнопкаОткрСкобкаНажатие(Элемент)
	ВводЧисла = Ложь;
	ЕстьТочка = Ложь;
	Если (СтрДлина(Формула) > 0) И (Прав(Формула, 1) = ",") Тогда
		Формула = Формула + "0";
	КонецЕсли;
	Формула = Формула + " (";
КонецПроцедуры

// Обработчик кнопки скобка
//
&НаКлиенте
Процедура КнопкаЗакрСкобкаНажатие(Элемент)
	ВводЧисла = Ложь;
	ЕстьТочка = Ложь;
	Если (СтрДлина(Формула) > 0) И (Прав(Формула, 1) = ",") Тогда
		Формула = Формула + "0";
	КонецЕсли;
	Формула = Формула + ")";
КонецПроцедуры

// Определяет режим ввода формулы (вводится ли число или что-то другое).
&НаКлиенте
Процедура ОпределитьТекущийРежимВвода()
	ВводЧисла = Ложь;
	ЕстьТочка = Ложь;
	Если ПустаяСтрока(Формула) Тогда
		Возврат;
	КонецЕсли;
	ПослСимвол = Прав(Формула, 1);
	// Число заканчивается на цифру или (что нежелательно) на разделитель.
	Если СтрНайти("0123456789,", ПослСимвол) > 0 Тогда
		ВводЧисла = Истина;
		// Теперь узнаем, есть ли разделитель в числе.
		Счетчик = СтрДлина(Формула);
		Пока Сред(Формула, Счетчик, 1) <> " " Цикл
			Если Сред(Формула, Счетчик, 1) = "," Тогда
				ЕстьТочка = Истина;
				Возврат;
			КонецЕсли;
			Счетчик = Счетчик - 1;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФормулаОчистка(Элемент, СтандартнаяОбработка)
	ИспользуемыеПоказатели.Очистить();
КонецПроцедуры

// Калькулятор
////////////////////////////////////////////////////////////////////////////////

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаПоказатели

&НаКлиенте
Процедура ТаблицаПоказателиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекДанные = Элементы.ТаблицаПоказатели.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВводЧисла = Ложь;
	ЕстьТочка = Ложь;
	Если (СтрДлина(Формула) > 0) И (Прав(Формула, 1) = ",") Тогда
		Формула = Формула + "0";
	КонецЕсли;
	
	Формула = Формула + " [" + ТекДанные.ПоказательПредставление + "]";
	
	ИспользуемыеПоказатели.Добавить(ТекДанные.Показатель);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаКлиенты

&НаКлиенте
Процедура ТаблицаКлиентыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФорму("Справочник.Партнеры.ФормаВыбора", , Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКлиентыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Партнеры") И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		НайденныеСтроки = ТаблицаКлиенты.НайтиСтроки(Новый Структура("Клиент", ВыбранноеЗначение));
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = ТаблицаКлиенты.Добавить();
			НоваяСтрока.Клиент = ВыбранноеЗначение;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоПоказатели

&НаКлиенте
Процедура ДеревоПоказателиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ДеревоПоказателиВес" И Элемент.ТекущиеДанные.ТолькоПросмотр
		 И Элемент.ТекущиеДанные.ПолучитьЭлементы().Количество() = 0 Тогда
		ОчиститьСообщения();
		ТекстСообщения =
			НСтр("ru = 'Реквизит с типом ""число"" сам является ""весом"". Устанавливается в карточке клиента вручную.';
			|
		                      |en = 'An attribute with type ""number"" is itself a ""weight"". It needs to be set in the client''s card manually.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПоказателиПередРазворачиванием(Элемент, Строка, Отказ)
	
	СтрокаРаскрытияДерева = ДеревоПоказатели.НайтиПоИдентификатору(Строка);
	
	Если СтрокаРаскрытияДерева.ПолучитьРодителя() = Неопределено Тогда
		Если Не ПустаяСтрока(СтрокаРаскрытияДерева.УзелИнициализации) Тогда
			СтрокаРаскрытияДерева.ПолучитьЭлементы().Очистить();
			ЗаполнитьВеткуДереваПотенциала(Строка);
			СтрокаРаскрытияДерева.УзелИнициализации = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДеревоПоказателиПередРазворачиванием()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоПоказателиВес

&НаКлиенте
Процедура ДеревоПоказателиВесПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.ДеревоПоказатели.ТекущиеДанные;
	ТекущаяСтрока.Установлено = Истина;
	
	// Здесь рекурсивно выводим для подчиненных строк.
	
	ИзменитьВесПоказателейВнизПоИерархии(ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РассчитатьПотенциалНовый(Команда)
	ФормулаВведенаКорректно = ПроверитьКорректностьФормулы();
	Если НЕ ФормулаВведенаКорректно Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьВесаПоказателей();
	
	Для Каждого ТекСтрока Из ТаблицаКлиенты Цикл
		ТекСтрока.ПотенциалНовый = CRM_РасчетПотенциалаСервер.РассчитатьПотенциалКлиента(ТекСтрока.Клиент,
			 Подразделение,
			 Формула);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьПотенциалы(Команда)
	//	Сохраняем
	Применить(Команда);
	
	Если Не ЗначениеЗаполнено(Формула) Тогда
		Возврат;
	КонецЕсли;
		
	// Пересчитываем данные в регистре по текущему подразделению.
	
	ДлительнаяОперация =  CRM_РасчетПотенциалаСервер.ЗаполнитьПотенциалыДляПодразделенияВФоне(Подразделение, Формула);
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		//ПересчитатьПотенциалыЗавершение(ДлительнаяОперация);
	ИначеЕсли ДлительнаяОперация.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется расчет потенциалов.';
			| en = 'Potentials are being calculated.'");
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПересчитатьПотенциалыЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
	// Выводим значения текущих потенциалов.
	ЗаполнитьПотенциалыКлиентов();
	
	// Выводим значения новых потенциалов (должны совпать с текущими - проверка).
	РассчитатьПотенциалНовый(Команда);
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьПотенциалыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Настройки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	УдалитьИзВременногоХранилища(Результат.АдресРезультата);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСвернуть(Команда)
	Элементы.КомандаСвернуть.Пометка = НЕ Элементы.КомандаСвернуть.Пометка;
	
	Если Элементы.КомандаСвернуть.Пометка Тогда
		Элементы.КомандаСвернуть.Заголовок = "Развернуть";
	Иначе
		Элементы.КомандаСвернуть.Заголовок = "Свернуть";
	КонецЕсли;
	
	СвернутьРазвернутьДерево();
КонецПроцедуры

&НаКлиенте
Процедура КомандаФильтрПоФормуле(Команда)
	Элементы.КомандаФильтрПоФормуле.Пометка = НЕ Элементы.КомандаФильтрПоФормуле.Пометка;
	
	Если Элементы.КомандаФильтрПоФормуле.Пометка Тогда
		Элементы.КомандаФильтрПоФормуле.Заголовок = "Снять фильтр по формуле";
	Иначе
		Элементы.КомандаФильтрПоФормуле.Заголовок = "Фильтр по формуле";
	КонецЕсли;
	
	ЗаписатьВесаПоказателей();
	ЗаполнитьДеревоПоказателейВерхнейИерархиейНаСервере(Элементы.КомандаФильтрПоФормуле.Пометка);
	СвернутьРазвернутьДерево();
КонецПроцедуры

&НаКлиенте
Процедура КомандаСравнитьС(Команда)
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("ПодразделениеСравнение", ПодразделениеСравнение);
	
	ОповещениеИзменения = Новый ОписаниеОповещения("ОповещениеИзмененияПодразделения", ЭтотОбъект);
	
	ОткрытьФорму("РегистрСведений.CRM_ВесаПоказателейПотенциала.Форма.ФормаВводаПодразделения",
		 СтруктураПараметров, ЭтотОбъект, Новый УникальныйИдентификатор(), , , ОповещениеИзменения,
		 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	ФормулаВведенаКорректно = ПроверитьКорректностьФормулы();
	Если ФормулаВведенаКорректно Тогда
		Если РежимВыбора Тогда
			ЗакрыватьПриВыборе = Ложь;
			ОповеститьОВыборе(Формула);
		Иначе
			СохранитьФормулуПодразделения();
		КонецЕсли;
	КонецЕсли;
	СохранитьВесаПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Применить(Команда);
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьВеткуДереваПотенциала(СтрокаДерева)
	
	ТекущаяСтрокаДерева		= ДеревоПоказатели.НайтиПоИдентификатору(СтрокаДерева);
	ИмяПоказателя			= ТекущаяСтрокаДерева.УзелИнициализации;
	МетаданныеПоказателя	= CRM_РасчетПотенциалаСервер.ПолучитьМетаданныеПоказателя(ИмяПоказателя);
	
	Если МетаданныеПоказателя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Метаданные.Справочники.ЗначенияСвойствОбъектов = МетаданныеПоказателя Тогда
		ДобавитьЭлементыДополнительныеЗначения(ТекущаяСтрокаДерева, ИмяПоказателя);
	ИначеЕсли Метаданные.Справочники.Содержит(МетаданныеПоказателя) Тогда
		Если МетаданныеПоказателя.Иерархический Тогда
			ДобавитьЭлементыИерархическогоСправочника(ТекущаяСтрокаДерева, МетаданныеПоказателя);
		Иначе
			ДобавитьЭлементыЛинейногоСправочника(ТекущаяСтрокаДерева, МетаданныеПоказателя);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьВеткуДереваПотенциала()

&НаСервере
Процедура ЗаполнитьТаблицуПоказателей()
	ТаблицаПоказатели.Очистить();
	Показатели = CRM_РасчетПотенциалаСервер.ПолучитьИменаИНаименованияПоказателей();
	
	Для Каждого ТекПоказатель Из Показатели Цикл
		НоваяСтрока = ТаблицаПоказатели.Добавить();
		НоваяСтрока.Показатель = ТекПоказатель.Имя;
		НоваяСтрока.ПоказательПредставление = ТекПоказатель.Наименование;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПоказателейВерхнейИерархиейНаСервере(СФильтром = Ложь)
	
	ДеревоПоказатели.ПолучитьЭлементы().Очистить();
	
	Показатели = CRM_РасчетПотенциалаСервер.ПолучитьИменаИНаименованияПоказателей();
	
	Для Каждого ТекПоказатель Из Показатели Цикл
		Если СФильтром И ИспользуемыеПоказатели.НайтиПоЗначению(ТекПоказатель.Имя) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ВеткаПоказателя = ДеревоПоказатели.ПолучитьЭлементы().Добавить();
		ВеткаПоказателя.Показатель = ТекПоказатель.Наименование;
		ВеткаПоказателя.ТолькоПросмотр = Истина;
		
		МетаданныеПоказателя = CRM_РасчетПотенциалаСервер.ПолучитьМетаданныеПоказателя(ТекПоказатель.Имя);
		
		Если МетаданныеПоказателя = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если МетаданныеПоказателя = "Булево" Тогда
			ВеткаЗначения = ВеткаПоказателя.ПолучитьЭлементы().Добавить();
			ВеткаЗначения.Показатель = Ложь;
			ВеткаЗначения = ВеткаПоказателя.ПолучитьЭлементы().Добавить();
			ВеткаЗначения.Показатель = Истина;
		ИначеЕсли Метаданные.Перечисления.Содержит(МетаданныеПоказателя) Тогда
			Для Каждого Значение Из МетаданныеПоказателя.ЗначенияПеречисления Цикл
				ВеткаЗначения = ВеткаПоказателя.ПолучитьЭлементы().Добавить();
				ВеткаЗначения.Показатель = Значение;
			КонецЦикла;
		Иначе
			СуществуетЭлементМетаданныхПоказателя = Ложь;
			
			Если Метаданные.Справочники.ЗначенияСвойствОбъектов = МетаданныеПоказателя Тогда
				СуществуетЭлементМетаданныхПоказателя = СуществуетЭлементДополнительныеЗначения(ВеткаПоказателя, ТекПоказатель.Имя);
			ИначеЕсли Метаданные.Справочники.Содержит(МетаданныеПоказателя) Тогда
				Если МетаданныеПоказателя.Иерархический Тогда
					СуществуетЭлементМетаданныхПоказателя = СуществуетЭлементыИерархическогоСправочника(ВеткаПоказателя,
						 МетаданныеПоказателя);
				Иначе
					СуществуетЭлементМетаданныхПоказателя = СуществуетЭлементыЛинейногоСправочника(ВеткаПоказателя,
						 МетаданныеПоказателя);
				КонецЕсли;
			КонецЕсли;
			
			Если СуществуетЭлементМетаданныхПоказателя Тогда
				// Добавим пустой элемент, чтобы можно его было открыть, воспользовавшись ленивой инициализацией.
				ВеткаПоказателя.ПолучитьЭлементы().Добавить();
				ВеткаПоказателя.УзелИнициализации = ТекПоказатель.Имя;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Элементы.ДеревоПоказателиВес.Видимость Тогда
		ЗаполнитьВес("Вес", "Установлено", Подразделение);
	КонецЕсли;
	
	Если Элементы.ДеревоПоказателиВесСравнение.Видимость Тогда
		ЗаполнитьВес("ВесСравнение", "УстановленоСравнение", ПодразделениеСравнение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыИерархическогоСправочника(Знач ВеткаПоказателя,
	 МетаданныеПоказателя = Неопределено,
	 Родитель = Неопределено)
	
	ИмяМетаданных = МетаданныеПоказателя.Имя;
	
	Если Родитель = Неопределено Тогда
		
		Запрос = Новый Запрос();
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	" + ИмяМетаданных + ".Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник." + ИмяМетаданных + " КАК " + ИмяМетаданных + "
		               |ГДЕ
		               |	НЕ " + ИмяМетаданных + ".ПометкаУдаления
					   |И
					   |	" + ИмяМетаданных + ".Родитель = &Родитель
		               |УПОРЯДОЧИТЬ ПО
		               |	" + ИмяМетаданных + ".Наименование
					   |";
		Запрос.УстановитьПараметр("Родитель", Справочники[ИмяМетаданных].ПустаяСсылка());
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяВетка = ВеткаПоказателя.ПолучитьЭлементы().Добавить();
			НоваяВетка.Показатель = Выборка.Ссылка;
			
			ДобавитьЭлементыИерархическогоСправочника(НоваяВетка, МетаданныеПоказателя, Выборка.Ссылка);
			
		КонецЦикла;
		
	Иначе
		
		Запрос = Новый Запрос();
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	" + ИмяМетаданных + ".Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник." + ИмяМетаданных + " КАК " + ИмяМетаданных + "
		               |ГДЕ
		               |	НЕ " + ИмяМетаданных + ".ПометкаУдаления
					   |И
					   |	" + ИмяМетаданных + ".Родитель = &Родитель
		               |УПОРЯДОЧИТЬ ПО
		               |	" + ИмяМетаданных + ".Наименование
					   |";
		Запрос.УстановитьПараметр("Родитель", Родитель);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяВетка = ВеткаПоказателя.ПолучитьЭлементы().Добавить();
			НоваяВетка.Показатель = Выборка.Ссылка;
			
			ДобавитьЭлементыИерархическогоСправочника(НоваяВетка, МетаданныеПоказателя, Выборка.Ссылка);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Функция СуществуетЭлементыИерархическогоСправочника(Знач ВеткаПоказателя,
	 МетаданныеПоказателя = Неопределено,
	 Родитель = Неопределено)
	
	ИмяМетаданных = МетаданныеПоказателя.Имя;
	
	Запрос = Новый Запрос();
	
	Если Родитель = Неопределено Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	" + ИмяМетаданных + ".Ссылка КАК Ссылка
		|ИЗ
		|	Справочник." + ИмяМетаданных + " КАК " + ИмяМетаданных + "
		|ГДЕ
		|	НЕ " + ИмяМетаданных + ".ПометкаУдаления
		|И
		|	" + ИмяМетаданных + ".Родитель = &Родитель
		|УПОРЯДОЧИТЬ ПО
		|	" + ИмяМетаданных + ".Наименование
		|";
		
		Запрос.УстановитьПараметр("Родитель", Справочники[ИмяМетаданных].ПустаяСсылка());
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	" + ИмяМетаданных + ".Ссылка КАК Ссылка
		|ИЗ
		|	Справочник." + ИмяМетаданных + " КАК " + ИмяМетаданных + "
		|ГДЕ
		|	НЕ " + ИмяМетаданных + ".ПометкаУдаления
		|И
		|	" + ИмяМетаданных + ".Родитель = &Родитель
		|УПОРЯДОЧИТЬ ПО
		|	" + ИмяМетаданных + ".Наименование
		|";
		
		Запрос.УстановитьПараметр("Родитель", Родитель);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Следующий();
	
КонецФункции // СуществуетЭлементыИерархическогоСправочника()

&НаСервере
Процедура ДобавитьЭлементыЛинейногоСправочника(ВеткаПоказателя, МетаданныеПоказателя)
	ИмяМетаданных = МетаданныеПоказателя.Имя;
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	" + ИмяМетаданных + ".Ссылка
	               |ИЗ
	               |	Справочник." + ИмяМетаданных + " КАК " + ИмяМетаданных + "
	               |ГДЕ
	               |	НЕ " + ИмяМетаданных + ".ПометкаУдаления
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	" + ИмяМетаданных + ".Наименование";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяВетка = ВеткаПоказателя.ПолучитьЭлементы().Добавить();
		НоваяВетка.Показатель = Выборка.Ссылка;
	КонецЦикла;
	
КонецПроцедуры	

&НаСервере
Функция СуществуетЭлементыЛинейногоСправочника(ВеткаПоказателя, МетаданныеПоказателя)
	
	ИмяМетаданных = МетаданныеПоказателя.Имя;
	
	Запрос = Новый Запрос();
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	" + ИмяМетаданных + ".Ссылка
	|ИЗ
	|	Справочник." + ИмяМетаданных + " КАК " + ИмяМетаданных + "
	|ГДЕ
	|	НЕ " + ИмяМетаданных + ".ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	" + ИмяМетаданных + ".Наименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Следующий();
	
КонецФункции // СуществуетЭлементыЛинейногоСправочника()

&НаСервере
Процедура ДобавитьЭлементыДополнительныеЗначения(ВеткаПоказателя, Свойство)
	
	ВладелецДополнительныхЗначений = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Свойство, "ВладелецДополнительныхЗначений");
	
	Если НЕ ЗначениеЗаполнено(ВладелецДополнительныхЗначений) Тогда
		ВладелецДополнительныхЗначений = Свойство;
	КонецЕсли;
		
	Выборка = Справочники.ЗначенияСвойствОбъектов.Выбрать(, ВладелецДополнительныхЗначений);
	
	Пока Выборка.Следующий() Цикл
		НоваяВетка = ВеткаПоказателя.ПолучитьЭлементы().Добавить();
		НоваяВетка.Показатель = Выборка.Ссылка;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СуществуетЭлементДополнительныеЗначения(ВеткаПоказателя, Свойство)
	
	ВладелецДополнительныхЗначений = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Свойство, "ВладелецДополнительныхЗначений"
	);
	
	Если НЕ ЗначениеЗаполнено(ВладелецДополнительныхЗначений) Тогда
		ВладелецДополнительныхЗначений = Свойство;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗначенияСвойствОбъектов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|ГДЕ
	|	ЗначенияСвойствОбъектов.Владелец = &Владелец";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Владелец", ВладелецДополнительныхЗначений);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Следующий();
	
КонецФункции // СуществуетЭлементДополнительныеЗначения()

&НаСервере
Процедура ЗаполнитьВсеНезаполненныеВеткиДереваПотенциалов()
	
	Для Каждого СтрокаДерева Из ДеревоПоказатели.ПолучитьЭлементы() Цикл
		Если Не ПустаяСтрока(СтрокаДерева.УзелИнициализации) Тогда
			СтрокаДерева.ПолучитьЭлементы().Очистить();
			ЗаполнитьВеткуДереваПотенциала(СтрокаДерева.ПолучитьИдентификатор());
			СтрокаДерева.УзелИнициализации = "";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьВсеНезаполненныеВеткиДереваПотенциалов()

&НаКлиенте
Процедура РазвернутьВсеУровниДерева()
	
	ЗаполнитьВсеНезаполненныеВеткиДереваПотенциалов();
	
	Для Каждого СтрокаДерева Из ДеревоПоказатели.ПолучитьЭлементы() Цикл
		Элементы.ДеревоПоказатели.Развернуть(СтрокаДерева.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьВсеУровниДерева()
	
	Для Каждого СтрокаДерева Из ДеревоПоказатели.ПолучитьЭлементы() Цикл
		Элементы.ДеревоПоказатели.Свернуть(СтрокаДерева.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьДерево()
	
	Если Элементы.КомандаСвернуть.Пометка Тогда
		СвернутьВсеУровниДерева();
	Иначе
		РазвернутьВсеУровниДерева();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИспользуемыеПоказатели()
	
	ИспользуемыеПоказатели.Очистить();
	
	СтрокаФормулы = Формула;
	
	Если ПустаяСтрока(СтрокаФормулы) Тогда
		Возврат;
	КонецЕсли;
	
	Пока СтрНайти(СтрокаФормулы, "[") > 0 Цикл
		
		НачальнаяПозиция = СтрНайти(СтрокаФормулы, "[");
		КонечнаяПозиция  = СтрНайти(СтрокаФормулы, "]");
		
		НаименованиеПоказателя = Сред(СтрокаФормулы, НачальнаяПозиция + 1, КонечнаяПозиция - НачальнаяПозиция - 1);
		
		Показатель = ПолучитьПоказательПоНаименованию(НаименованиеПоказателя);
		
		ИспользуемыеПоказатели.Добавить(Показатель);
		
		СтрокаФормулы = Сред(СтрокаФормулы, КонечнаяПозиция + 1);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьКорректностьФормулы()
	СообщенияОбОшибке = "";
	Результат = (ПустаяСтрока(Формула))
		 ИЛИ CRM_РасчетПотенциалаСервер.СинтаксическийКонтрольФормулыПоказателя(Формула, СообщенияОбОшибке);
	Если НЕ Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщенияОбОшибке);
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаСервере
Процедура СохранитьФормулуПодразделения()
	ОбъектПодразделения = Подразделение.ПолучитьОбъект();
	ОбъектПодразделения.CRM_ФормулаРасчетаПотенциала = Формула;
	Попытка
		ОбъектПодразделения.Записать();
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось записать формулу для подразделения!");
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура СохранитьВесаПоказателей()
	ЗаписатьВесаПоказателей();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВес(ИмяКолонкиВеса, ИмяКолонкиОтметки, ЗаполняемоеПодразделение)
	
	Для Каждого ТекВеткаПоказателя Из ДеревоПоказатели.ПолучитьЭлементы() Цикл
		
		ИмяПоказателя = ПолучитьПоказательПоНаименованию(ТекВеткаПоказателя.Показатель);
		
		МенеджерЗаписи = РегистрыСведений.CRM_ВесаПоказателейПотенциала.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Подразделение      = ЗаполняемоеПодразделение;
		МенеджерЗаписи.ИмяПоказателя      = ИмяПоказателя;
		МенеджерЗаписи.ЗначениеПоказателя = Неопределено;
		
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.Выбран() Тогда
			ТекВеткаПоказателя[ИмяКолонкиВеса] = МенеджерЗаписи.ВесПоказателя;
		Иначе
			ТекВеткаПоказателя[ИмяКолонкиВеса] = 0;
		КонецЕсли;
		
		ТекВеткаПоказателя[ИмяКолонкиОтметки] = Истина;
		
		ЗаполнитьВесРекурсивно(ИмяКолонкиВеса, ИмяКолонкиОтметки, ЗаполняемоеПодразделение, ИмяПоказателя,
			 ТекВеткаПоказателя);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВесРекурсивно(ИмяКолонкиВеса, ИмяКолонкиОтметки, ЗаполняемоеПодразделение,
	 ИмяПоказателя,
	 ТекущаяВетка)
	
	Для Каждого ТекВеткаПоказателя Из ТекущаяВетка.ПолучитьЭлементы() Цикл
		
		ЗначениеПоказателя = ТекВеткаПоказателя.Показатель;
		
		МенеджерЗаписи = РегистрыСведений.CRM_ВесаПоказателейПотенциала.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Подразделение      = ЗаполняемоеПодразделение;
		МенеджерЗаписи.ИмяПоказателя      = ИмяПоказателя;
		МенеджерЗаписи.ЗначениеПоказателя = ЗначениеПоказателя;
		
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.Выбран() Тогда
			ТекВеткаПоказателя[ИмяКолонкиВеса] = МенеджерЗаписи.ВесПоказателя;
			ТекВеткаПоказателя[ИмяКолонкиОтметки] = Истина;
		Иначе
			ТекВеткаПоказателя[ИмяКолонкиВеса] = ТекущаяВетка[ИмяКолонкиВеса];
			ТекВеткаПоказателя[ИмяКолонкиОтметки] = Ложь;
		КонецЕсли;
		
		ЗаполнитьВесРекурсивно(ИмяКолонкиВеса, ИмяКолонкиОтметки, ЗаполняемоеПодразделение, ИмяПоказателя,
			 ТекВеткаПоказателя);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьВесаПоказателей()
	
	НачатьТранзакцию();
	
	Попытка
		Для Каждого ТекВеткаПоказателя Из ДеревоПоказатели.ПолучитьЭлементы() Цикл
			
			ИмяПоказателя = ПолучитьПоказательПоНаименованию(ТекВеткаПоказателя.Показатель);
			
			НаборЗаписей = РегистрыСведений.CRM_ВесаПоказателейПотенциала.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Подразделение.Установить(Подразделение);
			НаборЗаписей.Отбор.ИмяПоказателя.Установить(ИмяПоказателя);
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
		Для Каждого ТекВеткаПоказателя Из ДеревоПоказатели.ПолучитьЭлементы() Цикл
			
			ИмяПоказателя = ПолучитьПоказательПоНаименованию(ТекВеткаПоказателя.Показатель);
			
			Если ТекВеткаПоказателя.Вес <> 0 Тогда
				МенеджерЗаписи = РегистрыСведений.CRM_ВесаПоказателейПотенциала.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.Подразделение      = Подразделение;
				МенеджерЗаписи.ИмяПоказателя      = ИмяПоказателя;
				МенеджерЗаписи.ЗначениеПоказателя = Неопределено;
				МенеджерЗаписи.ВесПоказателя = ТекВеткаПоказателя.Вес;
				
				МенеджерЗаписи.Записать(Истина);
			КонецЕсли;
			
			ЗаписатьВесаПоказателейРекурсивно(ИмяПоказателя, ТекВеткаПоказателя);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для показателя <" + ИмяПоказателя 
			+ "> запись произвести не удалось!");
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьВесаПоказателейРекурсивно(ИмяПоказателя, ТекущаяВетка)
	
	Для Каждого ТекВеткаПоказателя Из ТекущаяВетка.ПолучитьЭлементы() Цикл
		
		ЗначениеПоказателя = ТекВеткаПоказателя.Показатель;
		
		Если ТекВеткаПоказателя.Установлено Тогда
			МенеджерЗаписи = РегистрыСведений.CRM_ВесаПоказателейПотенциала.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Подразделение      = Подразделение;
			МенеджерЗаписи.ИмяПоказателя      = ИмяПоказателя;
			МенеджерЗаписи.ЗначениеПоказателя = ЗначениеПоказателя;
			МенеджерЗаписи.ВесПоказателя      = ТекВеткаПоказателя.Вес;
			
			МенеджерЗаписи.Записать(Истина);
		КонецЕсли;
		
		ЗаписатьВесаПоказателейРекурсивно(ИмяПоказателя, ТекВеткаПоказателя);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВесПоказателейВнизПоИерархии(ВеткаДерева)
	
	Для Каждого ТекСтрока Из ВеткаДерева.ПолучитьЭлементы() Цикл
		
		ТекСтрока.Вес = ВеткаДерева.Вес;
		ТекСтрока.УСтановлено = Ложь;
		ИзменитьВесПоказателейВнизПоИерархии(ТекСтрока);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеИзмененияПодразделения(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатЗакрытия = Подразделение Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для сравнения выбраны два одинаковых подразделения!");
		Элементы.ДеревоПоказателиВесСравнение.Видимость = Ложь;
		ПодразделениеСравнение = Неопределено;
		Возврат;
	КонецЕсли;
		
	Если РезультатЗакрытия.Пустая() Тогда
		Элементы.ДеревоПоказателиВесСравнение.Видимость = Ложь;
		ПодразделениеСравнение = Неопределено;
		Возврат;
	КонецЕсли;
	
	ПодразделениеСравнение = РезультатЗакрытия;
	
	Элементы.ДеревоПоказателиВесСравнение.Видимость = Истина;
	Элементы.ДеревоПоказателиВесСравнение.Заголовок = "Вес (" + СокрЛП(ПодразделениеСравнение) + ")";
	
	ЗаполнитьВес("ВесСравнение", "УстановленоСравнение", ПодразделениеСравнение);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПотенциалыКлиентов()
	// Заполнение ведем из регистра "Потенциалы".
	
	Для Каждого ТекСтрока Из ТаблицаКлиенты Цикл
		ТекСтрока.ПотенциалТекущий = CRM_РасчетПотенциалаСервер.ПолучитьПотенциалКлиента(ТекСтрока.Клиент, Подразделение);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиФормы()
	МассивКлиентов = Новый Массив();
	
	Для Каждого ТекСтрока Из ТаблицаКлиенты Цикл
		МассивКлиентов.Добавить(ТекСтрока.Клиент);
	КонецЦикла;
	
	ХранилищеНастроекДанныхФорм.Сохранить("__СписокКлиентовВНастройкахВесовПотенциалов", "Клиенты", МассивКлиентов);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	МассивКлиентов = ХранилищеНастроекДанныхФорм.Загрузить("__СписокКлиентовВНастройкахВесовПотенциалов", "Клиенты");
	Если МассивКлиентов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаКлиенты.Очистить();
	
	Для Каждого ТекКлиент Из МассивКлиентов Цикл
		НоваяСтрока = ТаблицаКлиенты.Добавить();
		НоваяСтрока.Клиент = ТекКлиент;
	КонецЦикла;
КонецПроцедуры

// Получить показатель по наименованию
//
// Параметры:
//  Наименование - Строка - Наименование показателя.
// 
// Возвращаемое значение:
//  Строка - Показатель.
//
Функция ПолучитьПоказательПоНаименованию(Наименование)
	
	Строки = ТаблицаПоказатели.НайтиСтроки(Новый Структура("ПоказательПредставление", Наименование));
	Если Строки.Количество() > 0 Тогда
		Возврат Строки[0].Показатель;
	КонецЕсли;
	
	Возврат "<показатель не найден>";
	
КонецФункции

&НаКлиенте
Процедура ВводЧиселИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	Формула = Формула + ПолучитьЦифры(Текст);
	Текст = "";
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ФокусНаВвод()
	Если ТекущийЭлемент = Элементы.Формула Тогда
		ТекущийЭлемент = Элементы.ВводЧисел;
		Элементы.Формула.ЦветРамки = WebЦвета.Золотистый;
	ИначеЕсли ТекущийЭлемент <> Элементы.ВводЧисел
		 И Элементы.Формула.ЦветРамки <> Элементы.ТаблицаПоказателиПоказательПредставление.ЦветРамки	Тогда
		Элементы.Формула.ЦветРамки = Элементы.ТаблицаПоказателиПоказательПредставление.ЦветРамки;
	КонецЕсли;
	ПодключитьОбработчикОжидания("ФокусНаВвод", 0.1, Истина);
КонецПроцедуры

Функция ПолучитьЦифры(Знач Текст)
	
	// Создаем переменную, в которую поместим все допустимые символы
	ДопустимыеСимволы = "0123456789-+*/";
	ДлинаДС = СтрДлина(ДопустимыеСимволы);
	Символ = СтрДлина(Текст);
	
	// В переменной Текст находится строка, из которой нужно убрать недопустимые символы
	// Обойдем каждый символ этой строки, начиная с конца
	Пока Символ > 0 Цикл
		Найдено = Ложь;
		
		// Проверим, есть ли этот символ в перечне допустимых.
		// Здесь вместо цикла можно было бы использовать еще функцию СтрНайти()
		Для ДопустимыйСимвол = 1 По ДлинаДС Цикл
			Если Сред(Текст, Символ, 1) = Сред(ДопустимыеСимволы, ДопустимыйСимвол, 1) Тогда
				Найдено = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		// Если символ не найден, удаляем его.
		Если Не Найдено Тогда
			Текст = СтрЗаменить(Текст, Сред(Текст, Символ, 1), "");
		КонецЕсли;
		Символ = Символ - 1;
	КонецЦикла;
	
	Возврат Текст;
	
КонецФункции

#КонецОбласти
