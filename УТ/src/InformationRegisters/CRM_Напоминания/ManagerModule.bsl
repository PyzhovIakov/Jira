#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ПереходНаНовуюВерсию

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаВерсию_3_1_26_17(Параметры) Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	CRM_Напоминания.Пользователь КАК Пользователь,
		|	CRM_Напоминания.Предмет КАК Предмет,
		|	CRM_Напоминания.ДатаНачала КАК ДатаНачала,
		|	CRM_Напоминания.ДатаОповещения КАК ДатаОповещения
		|ИЗ
		|	РегистрСведений.CRM_Напоминания КАК CRM_Напоминания";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	РезультатЗапросаТаблица = Запрос.Выполнить().Выгрузить();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.CRM_Напоминания";
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, РезультатЗапросаТаблица, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаВерсию_3_1_26_17(Параметры) Экспорт
	
	МетаданныеРегистра    = Метаданные.РегистрыСведений.CRM_Напоминания;
	ПолноеИмяРегистра     = МетаданныеРегистра.ПолноеИмя();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ИмяВременнойТаблицы = "ВТДанныеДляОбработки";

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	РезультатСозданияТаблицы 
		= ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь, ПолноеИмяРегистра, МенеджерВременныхТаблиц, ДополнительныеПараметры);
	Если Не РезультатСозданияТаблицы.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Не РезультатСозданияТаблицы.ЕстьДанныеДляОбработки;
		Возврат;
	КонецЕсли;
	Если Не РезультатСозданияТаблицы.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТДанныеДляОбработки.Пользователь КАК Пользователь,
	|	ВТДанныеДляОбработки.Предмет КАК Предмет,
	|	ВТДанныеДляОбработки.ДатаНачала КАК ДатаНачала,
	|	ВТДанныеДляОбработки.ДатаОповещения КАК ДатаОповещения
	|ИЗ
	|	ВТДанныеДляОбработки КАК ВТДанныеДляОбработки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	СекундВСутках = 24 * 60 * 60;
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписейДляИзменения = РегистрыСведений.CRM_Напоминания.СоздатьНаборЗаписей();
		НаборЗаписейДляИзменения.Отбор.Пользователь.Установить(Выборка.Пользователь);
		НаборЗаписейДляИзменения.Отбор.Предмет.Установить(Выборка.Предмет);
		НаборЗаписейДляИзменения.Отбор.ДатаНачала.Установить(Выборка.ДатаНачала);
		НаборЗаписейДляИзменения.Отбор.ДатаОповещения.Установить(Выборка.ДатаОповещения);
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
		ЭлементБлокировки.УстановитьЗначение("Пользователь", Выборка.Пользователь);
		ЭлементБлокировки.УстановитьЗначение("Предмет", Выборка.Предмет);
		ЭлементБлокировки.УстановитьЗначение("ДатаНачала", Выборка.ДатаНачала);
		ЭлементБлокировки.УстановитьЗначение("ДатаОповещения", Выборка.ДатаОповещения);
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка.Заблокировать();
			НаборЗаписейДляИзменения.Прочитать();
			
			Для Каждого Запись Из НаборЗаписейДляИзменения Цикл
				Если Не ЗначениеЗаполнено(Запись.ДатаАктуальности) Тогда
					Запись.ДатаАктуальности = ТекущаяДатаСеанса() + СекундВСутках * 7;
				КонецЕсли;
				Запись.Идентификатор = Новый УникальныйИдентификатор();
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписейДляИзменения);
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписейДляИзменения);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени("РегистрСведений.CRM_Напоминания");
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать записи в %ИмяОбъекта%. По причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяОбъекта%", "РегистрСведений.CRM_Напоминания");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%",    ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта, , 
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь,
		 "РегистрСведений.CRM_Напоминания");
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Процедуре РегистрСведений.CRM_Напоминания.ОбработатьДанныеДляПереходаНаВерсию_3_1_26_17 " 
			+ "не удалось обработать некоторые объекты (пропущены): %1'"), ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			 УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.CRM_Напоминания, ,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Процедура РегистрСведений." 
			+ "CRM_Напоминания.ОбработатьДанныеДляПереходаНаВерсию_3_1_26_17 обработала очередную порцию объектов: %1'"),
			ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
