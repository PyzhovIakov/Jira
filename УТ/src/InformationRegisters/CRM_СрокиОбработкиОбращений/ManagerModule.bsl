#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ПереходНаНовуюВерсию

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗарегистрироватьСрокОбработкиОбращения(Знач Обращение, Знач Состояние) Экспорт
	
	Если НЕ (Состояние = Справочники.CRM_СостоянияЛидов.Новый
		Или Состояние = Справочники.CRM_СостоянияЛидов.Повторный) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОбращения = ТипЗнч(Обращение);
	ОбращениеСсылка = Обращение.Ссылка;
	Если ТипОбращения = Тип("ДокументСсылка.ТелефонныйЗвонок") Тогда
		НастройкиСрока = Константы.CRM_СрокОбработкиЗвонков.Получить().Получить();
		Если НастройкиСрока = Неопределено Тогда
			НастройкиСрока = Новый Структура;
			НастройкиСрока.Вставить("CRM_СрокОбработкиДней", 0);
			НастройкиСрока.Вставить("CRM_СрокОбработкиЧасов", 8);
			НастройкиСрока.Вставить("CRM_СрокОбработкиМинут", 0);
		КонецЕсли; 
	ИначеЕсли ТипОбращения = Тип("ДокументСсылка.CRM_Заявка") Тогда
		НастройкиСрока = Обращение.ИсточникПолучения;
	ИначеЕсли ТипОбращения = Тип("ДокументСсылка.CRM_СообщениеМессенджера") Тогда
		НастройкиСрока = Обращение.Диалог.УчетнаяЗапись;
		ОбращениеСсылка = Обращение.Диалог;
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Обращение, "УчетнаяЗапись") Тогда
		НастройкиСрока = Обращение.УчетнаяЗапись;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ТипОбращения = Тип("СправочникСсылка.CRM_Диалоги") Тогда
		ОбращениеДата = Обращение.ДатаНачала;
	Иначе
		ОбращениеДата = Обращение.Дата;
	КонецЕсли;
	
	Если ТипЗнч(НастройкиСрока) <> Тип("Структура") Тогда
		Если Не НастройкиСрока.CRM_ИсточникЛидов Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.Обращение = ОбращениеСсылка;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ОбращениеСсылка, "CRM_СкрытьВАРМ")
		И ОбращениеСсылка["CRM_СкрытьВАРМ"] Тогда
		Запись.Прочитать();
		Если Запись.Выбран() Тогда
			Запись.Удалить();
		КонецЕсли;
	Иначе
		СтруктураСрока = Новый Структура();
		СтруктураСрока.Вставить("Дней", НастройкиСрока.CRM_СрокОбработкиДней);
		СтруктураСрока.Вставить("Часов", НастройкиСрока.CRM_СрокОбработкиЧасов);
		СтруктураСрока.Вставить("Минут", НастройкиСрока.CRM_СрокОбработкиМинут);
		
		Если СтруктураСрока.Дней = 0 И СтруктураСрока.Часов = 0 И СтруктураСрока.Минут = 0 Тогда
			СтруктураСрока.Часов = 8;
		КонецЕсли;
		
		Запись.СрокОбработки = CRM_ОбщегоНазначенияСервер.ПолучитьДатуПоКалендарю(ОбращениеДата, СтруктураСрока);
		Запись.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли