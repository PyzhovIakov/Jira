#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записать информацию об обмене.
// 
// Параметры:
//  ИнформацияОРесурсе - Структура - из:
//   * Метод - Строка -
//   * РесурсСервиса - Строка - 
//   * Заголовки - Строка -  
//  КодСостояния - Строка - 
//  ОтветСервиса - Строка - 
//  ЕстьОшибка - Булево - Есть ошибка
//  Исходящее - Булево - Исходящее
Процедура ЗаписатьИнформациюОбОбмене(ИнформацияОРесурсе, КодСостояния, ОтветСервиса, ЕстьОшибка = Ложь, Исходящее = Истина) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ЖурналСервисаПрогнозирования");
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЖурналСервисаПрогнозирования.НомерСообщения КАК НомерСообщения
		|ИЗ
		|	РегистрСведений.ЖурналСервисаПрогнозирования КАК ЖурналСервисаПрогнозирования
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСообщения УБЫВ";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() > 0 Тогда
			Выборка.Следующий();
			НомерСообщенияОбмена = Выборка.НомерСообщения + 1;
		Иначе
			НомерСообщенияОбмена = 0;
		КонецЕсли;
		
		ТекущаяДата = ТекущаяДатаСеанса();
		
		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Период.Установить(ТекущаяДата);
		Набор.Отбор.НомерСообщения.Установить(НомерСообщенияОбмена);
		Набор.Отбор.Исходящее.Установить(Исходящее);
		Набор.Отбор.ЕстьОшибка.Установить(ЕстьОшибка);
		
		Запись = Набор.Добавить();
		Запись.Период = ТекущаяДата;
		Запись.НомерСообщения = НомерСообщенияОбмена;
		Запись.ТекстСообщения = ОтветСервиса;
		Запись.КодСостояния = КодСостояния;
		Запись.ЕстьОшибка = ЕстьОшибка;
		Запись.Исходящее = Исходящее;
		Запись.Метод = ИнформацияОРесурсе.Метод;
		Запись.РесурсСервиса = ИнформацияОРесурсе.РесурсСервиса;
		Запись.Заголовки = ИнформацияОРесурсе.Заголовки;
		
		Набор.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыСведений.ЖурналСервисаПрогнозирования,
			,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

// Проверяет количество записей в регистре, и при превышении порога очищает его.
Процедура ПроверитьОчиститьЖурналСервисаПрогнозирования() Экспорт
	
	НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	Если Не НастройкиСервиса.АвтоматическиОчищатьЖурналСервисаПрогнозирования Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ЖурналСервисаПрогнозирования");
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(ЖурналСервисаПрогнозирования.НомерСообщения) КАК КоличествоЗаписей
		|ИЗ
		|	РегистрСведений.ЖурналСервисаПрогнозирования КАК ЖурналСервисаПрогнозирования";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() > 0 Тогда
			Выборка.Следующий();
			Если Выборка.КоличествоЗаписей >= НастройкиСервиса.ПорогОчисткиЖурналаСервисаПрогнозирования Тогда
				Набор = СоздатьНаборЗаписей();
				Набор.Записать();
			КонецЕсли;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыСведений.ЖурналСервисаПрогнозирования,
			,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

// Полная информация о последних записях.
// 
// Параметры:
//  ЛимитВыборки - Число - Лимит выборки
//  ТолькоОшибки - Булево - Только ошибки
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Полная информация о последних записях, колонки:
//   * ПериодНомер - Дата - 
//   * Исходящее - Булево - 
//   * ЕстьОшибка - Булево - 
//   * КодСостояния - Строка - 
//   * Запрос - Строка - 
//   * ТекстСообщения - Строка - 
Функция ПолнаяИнформацияОПоследнихЗаписях(ЛимитВыборки = 10, ТолькоОшибки = Ложь) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 10
	|	СТРОКА(ЖурналСервисаПрогнозирования.Период)
	|		+ &РазделительПолей + СТРОКА(ЖурналСервисаПрогнозирования.НомерСообщения) КАК ПериодНомер,
	|	ЖурналСервисаПрогнозирования.Исходящее КАК Исходящее,
	|	ЖурналСервисаПрогнозирования.ЕстьОшибка КАК ЕстьОшибка,
	|	ЖурналСервисаПрогнозирования.КодСостояния КАК КодСостояния,
	|	ЖурналСервисаПрогнозирования.Метод + &РазделительПолей + ЖурналСервисаПрогнозирования.РесурсСервиса КАК Запрос,
	|	ЖурналСервисаПрогнозирования.ТекстСообщения КАК ТекстСообщения
	|ИЗ
	|	РегистрСведений.ЖурналСервисаПрогнозирования КАК ЖурналСервисаПрогнозирования
	|ГДЕ
	|	ЖурналСервисаПрогнозирования.ЕстьОшибка = &ТолькоОшибки
	|	И НЕ ЖурналСервисаПрогнозирования.РесурсСервиса ПОДОБНО &ИсключаемыйЗапрос
	|	И НЕ ВЫРАЗИТЬ(ЖурналСервисаПрогнозирования.ТекстСообщения КАК СТРОКА(1)) = """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";
	
	Запрос.УстановитьПараметр("ТолькоОшибки",      ТолькоОшибки);
	Запрос.УстановитьПараметр("ИсключаемыйЗапрос", "%auth/info");
	Запрос.УстановитьПараметр("РазделительПолей",  " - ");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "10", ЛимитВыборки);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли