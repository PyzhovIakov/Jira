#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура устанавливает дату касания.
//
// Параметры:
//  Контакт	 - СправочникСсылка.Партнеры, СправочникСсылка.КонтактныеЛицаПартнеров	 - Контакт.
//  Дата	 - Дата	 - Дата касания.
//
Процедура УстановитьДатуКасания(Контакт, Дата) Экспорт
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаКасания = ПолучитьДатуКасания(Контакт);
	Если ДатаКасания >= Дата Тогда
		Возврат;
	КонецЕсли;
	
	Запись = РегистрыСведений.CRM_ДатыКасанийКонтактов.СоздатьМенеджерЗаписи();
	Запись.Контакт = Контакт;
	Запись.СвязанноеСвойство = Справочники.CRM_СвязанныеСвойства.ДатаПоследнегоВзаимодействия;
	Запись.Дата = Дата;
	Запись.Записать();
	
КонецПроцедуры

// Функция возвращает дату последнего касания.
//
// Параметры:
//  Контакт	 - СправочникСсылка.Партнеры, СправочникСсылка.КонтактныеЛицаПартнеров	 - Контакт.
// 
// Возвращаемое значение:
//   - Дата
//
Функция ПолучитьДатуКасания(Контакт) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДатыКасанийКонтактов.Дата КАК Дата
	|ИЗ
	|	РегистрСведений.CRM_ДатыКасанийКонтактов КАК ДатыКасанийКонтактов
	|ГДЕ
	|	ДатыКасанийКонтактов.Контакт = &Контакт
	|	И ДатыКасанийКонтактов.СвязанноеСвойство = ЗНАЧЕНИЕ(Справочник.CRM_СвязанныеСвойства.ДатаПоследнегоВзаимодействия)");
	
	Запрос.Параметры.Вставить("Контакт", Контакт);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат '00010101';
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Дата;
	
КонецФункции

#КонецОбласти
	
#КонецЕсли
