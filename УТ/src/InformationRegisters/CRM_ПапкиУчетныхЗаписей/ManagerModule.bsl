#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет запись.
//
// Параметры:
//  УчетнаяЗапись	 - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты	 - Учетная запись.
//  ВидПапки		 - ПеречислениеСсылка.CRM_ВидыПапокЭлектроннойПочты	 - Вид папки.
//  Папка			 - СправочникСсылка.ПапкиЭлектронныхПисем			 - Папка.
//
Процедура УстановитьПапку(УчетнаяЗапись, ВидПапки, Папка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ВызватьИсключение НСтр("ru = 'Не выбрана учетная запись'");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ВидПапки) Тогда
		ВызватьИсключение НСтр("ru = 'Не выбран вид папки'");
	КонецЕсли;
	
	Запись = РегистрыСведений.CRM_ПапкиУчетныхЗаписей.СоздатьМенеджерЗаписи();
	Запись.УчетнаяЗапись = УчетнаяЗапись;
	Запись.ВидПапки = ВидПапки;
	Запись.Прочитать();
	
	Если Запись.Выбран() И Не ЗначениеЗаполнено(Папка) Тогда
		Запись.Удалить();
		Возврат;
	КонецЕсли;
	
	Запись.УчетнаяЗапись = УчетнаяЗапись;
	Запись.ВидПапки = ВидПапки;
	Запись.Папка = Папка;
	Запись.Записать(Истина);
	
КонецПроцедуры

// Возвращает папку необходимого вида для учетной записи. Если не удалось найти, возвращает Неопределено.
//
// Параметры:
//  УчетнаяЗапись	 - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты	 - Учетная запись.
//  ВидПапки		 - ПеречислениеСсылка.CRM_ВидыПапокЭлектроннойПочты	 - Вид папки.
// 
// Возвращаемое значение:
//   - СправочникСсылка.ПапкиЭлектронныхПисем, Неопределено
//
Функция ПолучитьПапку(УчетнаяЗапись, ВидПапки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ВызватьИсключение НСтр("ru = 'Не выбрана учетная запись'");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ВидПапки) Тогда
		ВызватьИсключение НСтр("ru = 'Не выбран вид папки'");
	КонецЕсли;
	
	Запись = РегистрыСведений.CRM_ПапкиУчетныхЗаписей.СоздатьМенеджерЗаписи();
	Запись.УчетнаяЗапись = УчетнаяЗапись;
	Запись.ВидПапки = ВидПапки;
	Запись.Прочитать();
	
	Если Не Запись.Выбран() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Запись.Папка;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоУмолчанию() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПапкиУчетныхЗаписей.УчетнаяЗапись КАК УчетнаяЗапись
	|ПОМЕСТИТЬ НастроенныеУчетныеЗаписи
	|ИЗ
	|	РегистрСведений.CRM_ПапкиУчетныхЗаписей КАК ПапкиУчетныхЗаписей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПапкиЭлектронныхПисем.Ссылка КАК Папка,
	|	ПапкиЭлектронныхПисем.Владелец КАК УчетнаяЗапись,
	|	ПапкиЭлектронныхПисем.CRM_Вид КАК ВидПапки,
	|	ПапкиЭлектронныхПисем.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ПапкиЭлектронныхПисем КАК ПапкиЭлектронныхПисем
	|ГДЕ
	|	ПапкиЭлектронныхПисем.ПредопределеннаяПапка
	|	И НЕ ПапкиЭлектронныхПисем.Владелец В
	|				(ВЫБРАТЬ
	|					НастроенныеУчетныеЗаписи.УчетнаяЗапись КАК УчетнаяЗапись
	|				ИЗ
	|					НастроенныеУчетныеЗаписи)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УчетнаяЗапись");
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ИменаПредопределенныхПапок = CRM_УправлениеЭлектроннойПочтой.ИменаПредопределенныхПапок();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ВидПапки = Выборка.ВидПапки;
		Если Не ЗначениеЗаполнено(ВидПапки) Тогда
			ВидПапки = CRM_УправлениеЭлектроннойПочтой.ВидПапкиПоНаименованию(Выборка.Наименование, ИменаПредопределенныхПапок);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ВидПапки) Тогда
			Продолжить;
		КонецЕсли;
		МенеджерЗаписи = РегистрыСведений.CRM_ПапкиУчетныхЗаписей.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.УчетнаяЗапись	= Выборка.УчетнаяЗапись;
		МенеджерЗаписи.ВидПапки			= ВидПапки;
		МенеджерЗаписи.Папка			= Выборка.Папка;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
