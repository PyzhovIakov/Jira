#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет тип записи взаиморасчетов по операции взаиморасчетов
// 
// Параметры:
//  ОперацияВзаиморасчетов - ПеречислениеСсылка.ОперацииВзаиморасчетов - Операция взаиморасчетов
//  ЭтоПредоплата - Булево - Предоплата или долг записи
//  ВидДвижения - ВидДвиженияНакопления - Вид движения записи
//  Сторно - Булево - Это запись сторно
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыЗаписейВзаиморасчетов - Тип записи по операции
Функция ТипЗаписиПоОперации(ОперацияВзаиморасчетов, ЭтоПредоплата, ВидДвижения, Сторно) Экспорт
	
	Если ВидДвижения = ВидДвиженияНакопления.Приход И Не Сторно
		Или ВидДвижения = ВидДвиженияНакопления.Расход И Сторно Тогда
		Если ЭтоПредоплата Тогда
			Если ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.Оплата Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ОплатаУвеличениеПредоплаты;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаНакладной Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеПредоплаты;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.ПереносВзаиморасчетов Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеПредоплаты;
			Иначе
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты;
			КонецЕсли;
		Иначе
			Если ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.Отгрузка Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ОтгрузкаУвеличениеДолга;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаНакладной Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеДолга;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.ВозвратДенежныхСредств Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУвеличениеДолга;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.ПереносВзаиморасчетов Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеДолга;
			Иначе
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЭтоПредоплата Тогда
			Если ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.Отгрузка Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеПредоплаты;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаНакладной Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУменьшениеПредоплаты;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.ВозвратДенежныхСредств Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУменьшениеПредоплаты;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.ПереносВзаиморасчетов Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУменьшениеПредоплаты;
			Иначе
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеПредоплаты;
			КонецЕсли;
		Иначе
			Если ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.Оплата Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ОплатаУменьшениеДолга;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.Отгрузка Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаНакладной Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУменьшениеДолга;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.ВозвратДенежныхСредств Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУменьшениеДолга;
			ИначеЕсли ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.ПереносВзаиморасчетов Тогда
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУменьшениеДолга;
			Иначе
				Возврат Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеДолга;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
