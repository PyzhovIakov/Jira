#Область ОбработчикиСобытий

Функция pingGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаПроверкаСоединения(СтруктураОтвета);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если ИспользоватьСерверЛояльности Тогда
		
		СтруктураОтвета.Connection = Истина;
		
	Иначе
		
		СтруктураОтвета.Error = Истина;
		СтруктураОтвета.ErrorMessage = НСтр("ru = 'Использование сервера лояльности отключено в настройках программы.'");
		
	КонецЕсли;
	
	СтруктураОтветаJSON = Новый ЗаписьJSON;
	СтруктураОтветаJSON.УстановитьСтроку();
	ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
	
	Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьБонусыКлиентаGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаБонусыКлиента(СтруктураОтвета);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		
		Возврат Ответ;
		
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда
			
			СтруктураОтвета.ClientNotFound = Ложь;
			БонусыЗаблокированы = СерверЛояльностиПоставщикДанных.БонусыЗаблокированы(КартаКлиента);
			
			Если ПараметрыМетода.ТребуетсяБлокировкаБонусов
				И Не БонусыЗаблокированы Тогда
				СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьДанныеОБонусах(КартаКлиента, СтруктураОтвета);
				Если СтруктураОтвета.BonusCount > 0 Тогда
					СерверЛояльностиПоставщикДанных.ЗаблокироватьБонусы(КартаКлиента, ТекущаяДатаСеанса());
				КонецЕсли;
			КонецЕсли;
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьСтруктуруОтветаНомерамиКарты(КартаКлиента, СтруктураОтвета);
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПолучитьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция РазблокироватьБонусыКлиентаPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Попытка
		
		ИспользоватьСерверЛояльности = Ложь;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
		Если Не ИспользоватьСерверЛояльности Тогда
			Возврат Ответ;
		КонецЕсли;
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.РазблокироватьБонусы(КартаКлиента);
			ТекстСообщения = НСтр("ru = 'Бонусы разблокированы'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Карта клиента не найдена'");
		КонецЕсли;
		
		Ответ.УстановитьТелоИзСтроки(ТекстСообщения);
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "РазблокироватьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция СписатьБонусыКлиентаPOST(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда 
			Если ЗначениеЗаполнено(ПараметрыМетода.КоличествоБонусныхБаллов) Тогда
				СерверЛояльностиПоставщикДанных.СписатьБонусы(КартаКлиента, ПараметрыМетода.КоличествоБонусныхБаллов);
				ТекстСообщения = НСтр("ru = 'Бонусы успешно списаны'");
			Иначе
				ТекстСообщения = НСтр("ru = 'Количество баллов не указано'");
			КонецЕсли;
		Иначе 
			ТекстСообщения = НСтр("ru = 'Карта клиента не найдена'");
		КонецЕсли;
		
		Ответ.УстановитьТелоИзСтроки(ТекстСообщения);
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "СписатьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция ВыдатьКартуКлиентуPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаКартаКлиента(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанных.ДополнитьСтруктураПараметрамиМетодаВыдатьКарту(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПроверитьДанныеКартыКлиента(ПараметрыМетода, СтруктураОтвета);
		
		Если СтруктураОтвета.Error = Ложь Тогда
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
			КартаСуществует = ЗначениеЗаполнено(КартаКлиента);
			ДругойКлиент = Неопределено;
			
			Если Не КартаСуществует Тогда
				КартаКлиента = Неопределено;
				ОписаниеОшибки = "";
				СерверЛояльностиПоставщикДанныхПереопределяемый.СоздатьКартуКлиента(ПараметрыМетода, КартаКлиента, ДругойКлиент, ОписаниеОшибки);
			КонецЕсли;

			Если ЗначениеЗаполнено(КартаКлиента) Тогда
				СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьСтруктуруОтветаДаннымиКарты(КартаКлиента, СтруктураОтвета, КартаСуществует);
			ИначеЕсли ЗначениеЗаполнено(ДругойКлиент) Тогда
				СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьСтруктуруОтветаДаннымиКлиента(ДругойКлиент, СтруктураОтвета);
			Иначе
				СтруктураОтвета.Error = Истина;
				Если ПустаяСтрока(ОписаниеОшибки) Тогда
					СтруктураОтвета.ErrorMessage = НСтр("ru = 'Не удалось создать карту клиента'");
				Иначе
					СтруктураОтвета.ErrorMessage = ОписаниеОшибки;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ВыдатьКартуКлиенту");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ИнформацияОЗапретахПродажGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаИнформацияОЗапретеПродаж(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	ПараметрыМетода = Неопределено;
	СерверЛояльностиПоставщикДанных.ПараметрыМетодаЗапретыПродаж(Запрос, ПараметрыМетода);
	
	Попытка
		
		СтруктураОтвета = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ДанныеОЗапретахПродаж(ПараметрыМетода, СтруктураОтвета);
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ИнформацияОЗапретахПродаж");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьДанныеПодарочногоСертификатаGET(Запрос)
	
	Попытка
		
		Ответ = Новый HTTPСервисОтвет(200);
		
		ИспользоватьСерверЛояльности = Ложь;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
		
		СтруктураОтвета 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаДанныеПодарочногоСертификата(СтруктураОтвета);
		
		Если Не ИспользоватьСерверЛояльности Тогда
			СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
			Возврат Ответ;
		КонецЕсли;
		
		ПараметрыМетода 	= Неопределено;
		ДанныеСертификата 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
	
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьБалансПодарочногоСертификата(ДанныеСертификата, СтруктураОтвета);
			
		Иначе
			
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = НСтр("ru = 'Подарочный сертификат не найден'");
		КонецЕсли;
		
		// Возвращаем структуру ответа 
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки(), "ПолучитьДанныеПодарочногоСертификата");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьДанныеПодарочногоСертификатаPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
		
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаДанныеПодарочногоСертификата(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	
	ВходящиеДанные = Запрос.ПолучитьТелоКакСтроку();
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ЕстьОшибки", Ложь);
	ПараметрыМетода.Вставить("ОписаниеОшибки", "");
	
	Попытка
		
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПараметрыМетодаИзЗапросаПолучитьДанныеПодарочногоСертификата(ВходящиеДанные, ПараметрыМетода);
		
		Если ПараметрыМетода.ЕстьОшибки Тогда
			
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = ПараметрыМетода.ОписаниеОшибки;
			
		Иначе
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьБалансПодарочногоСертификата(ПараметрыМетода, СтруктураОтвета);
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПараметрыМетодаИзЗапросаПолучитьДанныеПодарочногоСертификата");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьДанныеПромокодаGET(Запрос)
	
	Попытка
		
		Ответ = Новый HTTPСервисОтвет(200);
		
		ИспользоватьСерверЛояльности = Ложь;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
		
		СтруктураОтвета 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаДанныеПромокода(СтруктураОтвета);
		
		Если Не ИспользоватьСерверЛояльности Тогда
			СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
			Возврат Ответ;
		КонецЕсли;
		
		ПараметрыМетода 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаПромокода(Запрос, ПараметрыМетода);
		
		Если ПараметрыМетода.Промокод <> Неопределено Тогда
			СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьСостояниеПромокода(ПараметрыМетода, СтруктураОтвета, Истина);
		КонецЕсли;
		
		// Возвращаем структуру ответа 
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПолучитьДанныеПромокода");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция РазблокироватьПодарочныйСертификатPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		ДанныеСертификата = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.РазблокироватьПодарочныйСертификат(ДанныеСертификата);
			ТекстСообщения = НСтр("ru = 'Подарочный сертификат разблокирован'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Подарочный сертификат не найден'");
		КонецЕсли;
		
		Ответ.УстановитьТелоИзСтроки(ТекстСообщения);
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "РазблокироватьПодарочныйСертификат");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции 

Функция РазблокироватьПромокодPOST(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		
		СерверЛояльностиПоставщикДанных.СнятьРезервПримененияПромокода(ПараметрыМетода.Промокод);
		ТекстСообщения = НСтр("ru = 'Промокод разблокирован'");
			
		Ответ.УстановитьТелоИзСтроки(ТекстСообщения);
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "РазблокироватьПромокод");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция СписатьПодарочныйСертификатPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		ДанныеСертификата = Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.СписатьБалансПодарочногоСертификата(ДанныеСертификата, ПараметрыМетода.СуммаСписания);
			ТекстСообщения = НСтр("ru = 'Подарочный сертификат успешно списан на указанную сумму'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Подарочный сертификат не найден'");
		КонецЕсли;
		Ответ.УстановитьТелоИзСтроки(ТекстСообщения);
	
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "СписатьПодарочныйСертификат");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ИспользоватьАвтоматическиеСкидкиGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаИспользоватьАвтоматическиеСкидки(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьАвтоматическиеСкидки(СтруктураОтвета.UseAutomaticDiscounts);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьБонусыКонтрагентов(СтруктураОтвета.UseCounterpartyBonuses);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
	
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
	
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ЗапросИспользованияАвтоматическихСкидок");
		
	КонецПопытки;
		
	Возврат Ответ;
	
КонецФункции

Функция РассчитатьАвтоматическиеСкидкиGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаРасчетСкидок(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	ИспользоватьАвтоматическиеСкидки = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьАвтоматическиеСкидки(ИспользоватьАвтоматическиеСкидки);
	
	Если Не ИспользоватьАвтоматическиеСкидки Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветАвтоматическиеСкидки(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	ВходящиеДанные		= Запрос.ПолучитьТелоКакСтроку();
	ПараметрыРасчета 	= Неопределено;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ДесериализоватьПараметрыРасчета(ВходящиеДанные, ПараметрыРасчета);
	
	Попытка
	
		Если ПараметрыРасчета.ЕстьОшибки Тогда
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = ПараметрыРасчета.ОписаниеОшибки;
		Иначе
			СерверЛояльностиПоставщикДанныхПереопределяемый.РассчитатьАвтоматическиеСкидки(ПараметрыРасчета, СтруктураОтвета);
			
			Если ПараметрыРасчета.ВернутьДанныеКарты И ПараметрыРасчета.БлокироватьКарту = "1"
					И Не СтруктураОтвета.LoyaltyCardDescription.CardNotFound Тогда
					
				ДанныеКарты = СтруктураОтвета.LoyaltyCardDescription.LoyaltyCard;
				Если ДанныеКарты.BonusData.BonusCount > 0 И Не ДанныеКарты.IsBlocked Тогда
					СерверЛояльностиПоставщикДанных.ЗаблокироватьБонусы(ПараметрыРасчета.ДисконтнаяКарта, ТекущаяДатаСеанса());
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
	
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "РассчитатьАвтоматическиеСкидки");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция РассчитатьАвтоматическиеСкидкиPOST(Запрос)
	
	Ответ = РассчитатьАвтоматическиеСкидкиGET(Запрос);
	
	Возврат Ответ;
	
КонецФункции

Функция ПроверитьПодарочныйСертификатGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаПроверкаПодарочногоСертификат(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПроверитьПодарочныйСертификат(ПараметрыМетода, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
	
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПроверкаПодарочногоСертификата");
		
	КонецПопытки;
		
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьСписокУправляемыхСкидокGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыРасчета 	= Неопределено;
		СтруктураОтвета = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаУправляемыеСкидки(Запрос, ПараметрыРасчета);
		
		СерверЛояльностиПоставщикДанных.СписокУправляемыхСкидок(СтруктураОтвета, ПараметрыРасчета);
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "СписокУправляемыхСкидок");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьСписокСкидокПоВидамОплатыGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		СтруктураОтвета = Неопределено;
		СерверЛояльностиПоставщикДанных.СписокСкидокПоВидамОплаты(СтруктураОтвета);
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "СписокСкидокПоВидамОплаты");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьОстаткиGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаОстатки(СтруктураОтвета);
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ВходящиеДанные = Запрос.ПолучитьТелоКакСтроку();
		ПараметрыПолученияОстатков = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПараметрыИзЗапросаПолучитьОстатки(ВходящиеДанные, ПараметрыПолученияОстатков);
		
		
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьОстатки(ПараметрыПолученияОстатков, СтруктураОтвета);
		
		// Возвращаем структуру ответа 
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПолучитьОстатки");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьОстаткиPOST(Запрос)
	
	Ответ = ПолучитьОстаткиGET(Запрос);
	
	Возврат Ответ;
	
КонецФункции

Функция НайтиКартуКлиентаGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаСписокКартЛояльности(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента = Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыМетодаИзЗапросаНайтиКартуКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартыЛояльности(ПараметрыМетода, СтруктураОтвета);
		
		Если ПараметрыМетода.БлокироватьКарту = "1" И СтруктураОтвета.LoyaltyCardsList.Количество() = 1 
				И СтруктураОтвета.LoyaltyCardsList[0].BonusData.BonusCount > 0
				И Не СтруктураОтвета.LoyaltyCardsList[0].IsBlocked Тогда
			
			ГУИДКартыСтрокой = СтруктураОтвета.LoyaltyCardsList[0].CardGUID;
			СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьКартуКлиентаПоУникальномуИдентификатору(ГУИДКартыСтрокой,
				КартаКлиента);
			СерверЛояльностиПоставщикДанных.ЗаблокироватьБонусы(КартаКлиента, ТекущаяДатаСеанса());
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "НайтиКартуКлиента");
		
	КонецПопытки;
		
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьВидыКартGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
		
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		СтруктураОтвета = Неопределено;
		
		ПараметрыРасчета 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаВидыДисконтныхКарт(Запрос, ПараметрыРасчета);
		
		СерверЛояльностиПоставщикДанных.СписокВидовДисконтныхКарт(СтруктураОтвета, ПараметрыРасчета);
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПолучитьВидыКарт");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьОборотыПродажGET(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаОборотыПродаж(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда
			
			СтруктураОтвета.ClientNotFound = Ложь;
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьДанныеОбОборотахПродаж(КартаКлиента, СтруктураОтвета);
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьСтруктуруОтветаНомерамиКарты(КартаКлиента, СтруктураОтвета);
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПолучитьОборотыПродаж");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция ПолучитьСписокПродавцовGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	СтруктураОтвета = Неопределено;
	
	Попытка
		
		ПараметрыРасчета 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаСпискаПродавцов(Запрос, ПараметрыРасчета);
		СерверЛояльностиПоставщикДанных.СписокСотрудников(ПараметрыРасчета, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "СписокПродавцов");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьСписокСерийПоОтборуGET(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	
	СтруктураОтвета = Неопределено;

	Попытка
		
		ПараметрыРасчета 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаСпискаСерий(Запрос, ПараметрыРасчета);
		СерверЛояльностиПоставщикДанных.ЗаполнитьСписокСерий(ПараметрыРасчета, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПолучитьСписокСерийПоТовару");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция ПолучитьДанныеСерииGET(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаДанныеСерии(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаДанныеСерии(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьДанныеСерии(ПараметрыМетода, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
	
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПолучитьДанныеСерии");
		
	КонецПопытки;
		
	Возврат Ответ;
КонецФункции

// ++ Локализация

Функция ОбработатьКодМаркировкиPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ВходящиеДанные = Запрос.ПолучитьТелоКакСтроку();
		ПараметрыОбработкиКодаМаркировки = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПараметрыИзЗапросаОбработатьКодМаркировки(ВходящиеДанные, ПараметрыОбработкиКодаМаркировки);
		
		РезультатОтвета = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ОбработатьКодМаркировки(ПараметрыОбработкиКодаМаркировки, РезультатОтвета);
		
		// Возвращаем структуру ответа 
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, РезультатОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ОбработатьКодМаркировки");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьТокенАвторизацииГосИСPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ВходящиеДанные = Запрос.ПолучитьТелоКакСтроку();
		ПараметрыПолученияТокенаАвторизации = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПараметрыИзЗапросаПолучитьТокенАвторизацииГосИС(ВходящиеДанные, ПараметрыПолученияТокенаАвторизации);
		
		РезультатОтвета = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьТокенАвторизацииГосИС(ПараметрыПолученияТокенаАвторизации, РезультатОтвета);
		
		// Возвращаем структуру ответа 
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, РезультатОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПолучитьТокенАвторизацииГосИС");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

// -- Локализация

Функция ПолучитьАктуальнуюЦенуGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаЗапросаЦены(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаАктуальнойЦены(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьАктуальнуюЦену(ПараметрыМетода, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ПолучитьАктуальнуюЦену");
		
	КонецПопытки;
		
	Возврат Ответ;
	
КонецФункции

Функция ОбъединитьБонусныеКартыPOST(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	ВходящиеДанные = Запрос.ПолучитьТелоКакСтроку();
	ПараметрыМетода = Неопределено;
	
	Попытка
		СтруктураОтвета = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПараметрыМетодаИзЗапросаОбъединитьБонусныеКарты(ВходящиеДанные, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ОбъединитьБонусныеКарты(ПараметрыМетода, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "ОбъединитьБонусныеКарты");
		
	КонецПопытки;

	
	Возврат Ответ;
КонецФункции

Функция РазблокироватьБонусыКлиентаНовыйPOST(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	Попытка
		
		ИспользоватьСерверЛояльности = Ложь;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
		
		СтруктураОтвета = Неопределено;
		СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаТекстовоеСообщение(СтруктураОтвета);
		
		Если Не ИспользоватьСерверЛояльности Тогда
			СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
			Возврат Ответ;
		КонецЕсли;
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.РазблокироватьБонусы(КартаКлиента);
			ТекстСообщения = НСтр("ru = 'Бонусы разблокированы'");
			СтруктураОтвета.TextMessage = ТекстСообщения;
		Иначе
			ТекстСообщения = НСтр("ru = 'Карта клиента не найдена'");
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = ТекстСообщения;
		КонецЕсли;
		
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "РазблокироватьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция СписатьБонусыКлиентаНовыйPOST(Запрос)
		
	Ответ = Новый HTTPСервисОтвет(200);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаТекстовоеСообщение(СтруктураОтвета);
		
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанных.ДополнитьСтруктураПараметрамиМетодаСписатьБонусы(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда 
			Если ЗначениеЗаполнено(ПараметрыМетода.КоличествоБонусныхБаллов) Тогда
				СерверЛояльностиПоставщикДанных.СписатьБонусы(КартаКлиента, ПараметрыМетода.КоличествоБонусныхБаллов);
				ТекстСообщения = НСтр("ru = 'Бонусы успешно списаны'");
				СтруктураОтвета.TextMessage = ТекстСообщения;
			Иначе
				ТекстСообщения = НСтр("ru = 'Количество баллов не указано'");
				СтруктураОтвета.Error = Истина;
				СтруктураОтвета.ErrorMessage = ТекстСообщения;
			КонецЕсли;
		Иначе
			ТекстСообщения = НСтр("ru = 'Карта клиента не найдена'");
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = ТекстСообщения;
		КонецЕсли;
		
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "СписатьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция РазблокироватьПодарочныйСертификатНовыйPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаТекстовоеСообщение(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		ДанныеСертификата = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.РазблокироватьПодарочныйСертификат(ДанныеСертификата);
			СтруктураОтвета.TextMessage = НСтр("ru = 'Подарочный сертификат разблокирован.'");
		Иначе
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = НСтр("ru = 'Подарочный сертификат не найден.'");
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "РазблокироватьПодарочныйСертификат");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция СписатьПодарочныйСертификатНовыйPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаТекстовоеСообщение(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		ДанныеСертификата = Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанных.ДополнитьСтруктураПараметрамиМетодаСписатьПодарочныйСертификат(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.СписатьБалансПодарочногоСертификата(ДанныеСертификата, ПараметрыМетода.СуммаСписания);
			СтруктураОтвета.TextMessage = НСтр("ru = 'Подарочный сертификат успешно списан на указанную сумму'");
		Иначе
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = НСтр("ru = 'Подарочный сертификат не найден.'");
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
	
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "СписатьПодарочныйСертификат");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция РазблокироватьПромокодНовыйPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьСтруктуруОтветаТекстовоеСообщение(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаПромокода(Запрос, ПараметрыМетода);
		
		СерверЛояльностиПоставщикДанных.СнятьРезервПримененияПромокода(ПараметрыМетода.Промокод);
		
		СтруктураОтвета.TextMessage = НСтр("ru = 'Промокод разблокирован.'");
			
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, "РазблокироватьПромокод");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти
