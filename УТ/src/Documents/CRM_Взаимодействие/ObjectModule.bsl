#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Описание в комментарии к одноименной процедуре в модуле УправлениеДоступом.
// 
// Параметры:
//  Таблица - ТаблицаЗначений, Неопределено	- Возвращает в этом параметре подготовленные наборы значений
//            доступа. Описание колонок см. в функции УправлениеДоступом.ТаблицаНаборыЗначенийДоступа.
//            Если передано Неопределено, то будет создана и заполнена новая таблица наборов значений доступа.
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("CRM_Модуль_УправлениеДоступом");
	Если МодульУправлениеДоступом <> Неопределено Тогда
		МодульУправлениеДоступом.ЗаполнитьНаборыЗначенийДоступа(ЭтотОбъект, Таблица);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура - обработчик события "ОбработкаЗаполнения".
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	CRM_ОбщегоНазначенияСервер.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.CRM_Интерес") Тогда
		
		ДокументОснование	= ДанныеЗаполнения.Ссылка;
		КонтактноеЛицо		= ДанныеЗаполнения.КонтактноеЛицо;
		ОжидаемаяВыручка	= ДанныеЗаполнения.ОжидаемаяВыручка;
		Ответственный		= ДанныеЗаполнения.Ответственный;
		Партнер				= ДанныеЗаполнения.Партнер;
		Подразделение		= ДанныеЗаполнения.Подразделение;
		ПотенциальныйКлиент	= ДанныеЗаполнения.ПотенциальныйКлиент;
		Организация			= ДанныеЗаполнения.Организация;
		СостояниеИнтереса	= ДанныеЗаполнения.СостояниеИнтереса;
		
		Если Не ЗначениеЗаполнено(КонтактноеЛицо) Тогда
			КонтактноеЛицо = Партнер.CRM_ОсновноеКонтактноеЛицо;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
	КонецЕсли;
	
	// Дозаполнение по умолчанию.
	Если Не ЗначениеЗаполнено(ПлановаяДата) Тогда
		ПлановаяДата = CRM_ОбщегоНазначенияКлиентСервер.ОкруглитьДатуДоМинут(ТекущаяДатаСеанса(), 30);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПлановаяДатаЗавершение) Тогда
		ПлановаяДатаЗавершение = ПлановаяДата + 1800;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Автор) Тогда
		Автор = Пользователи.ТекущийПользователь();
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(ДатаРегистрации) Тогда
		ДатаРегистрации = CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса(); ;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Автор;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Подразделение) Тогда
		Подразделение = Ответственный.Подразделение;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = CRM_ОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Автор, "ОсновнаяОрганизация");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтатусВзаимодействия) Тогда
		СтатусВзаимодействия = Справочники.CRM_СостоянияСобытий.Запланировано;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидВзаимодействия) Тогда
		ВидВзаимодействия = Константы.CRM_ВидВзаимодействияПоУмолчанию.Получить();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ВидВзаимодействия) Тогда
		ВидВзаимодействия = Справочники.CRM_ВидыВзаимодействий.Встреча;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Помещение) Тогда
		Место = Строка(Помещение);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидВзаимодействия.ДлительностьВстречиПоУмолчанию) Тогда
		ПлановаяДатаЗавершенияТМП = ПлановаяДата + ВидВзаимодействия.ДлительностьВстречиПоУмолчанию * 60;
		Если ПлановаяДатаЗавершение < ПлановаяДатаЗавершенияТМП Тогда
			ПлановаяДатаЗавершение = ПлановаяДатаЗавершенияТМП;
		КонецЕсли;	
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ВидВзаимодействия.ШаблонЧекЛиста) Тогда
		ШаблонЧекЛиста = ВидВзаимодействия.ШаблонЧекЛиста;
		ЧекЛист.Очистить();
		Для Каждого Строка Из ШаблонЧекЛиста.ЧекЛист Цикл
			Стр = ЧекЛист.Добавить();
			ЗаполнитьЗначенияСвойств(Стр, Строка);
			Стр.ЗаполненоИзШаблона = Истина;
		КонецЦикла;	
	КонецЕсли;
	
	ИндексЦвета	= Константы.CRM_ЦветСобытияВКалендареПоУмолчанию.Получить();
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЗаписьНовогоВзаимодействия", ЭтоНовый());
	ДополнительныеСвойства.Вставить("СтатусВзаимодействияПередЗаписью", Ссылка.СтатусВзаимодействия);
	Если Не ЭтоНовый() И (ПлановаяДата <> Ссылка.ПлановаяДата) Тогда
		ДополнительныеСвойства.Вставить("СтароеЗначениеПлановойДаты", Ссылка.ПлановаяДата);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		МетаданныеДокументаОснования = ДокументОснование.Метаданные();
		
		Если НЕ ЗначениеЗаполнено(КонтактноеЛицо)
			И CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("КонтактноеЛицо", МетаданныеДокументаОснования)
			Тогда
			КонтактноеЛицо = ДокументОснование.КонтактноеЛицо;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Партнер) И НЕ ЗначениеЗаполнено(ПотенциальныйКлиент)
			 И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.CRM_Интерес") Тогда
			Партнер = ДокументОснование.Партнер;
			Если CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("ПотенциальныйКлиент", МетаданныеДокументаОснования) Тогда
				ПотенциальныйКлиент = ДокументОснование.ПотенциальныйКлиент;
			КонецЕсли;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Если НЕ ЗначениеЗаполнено(ПлановаяДатаЗавершение) ИЛИ
			ПлановаяДатаЗавершение < ПлановаяДата Тогда
			ПлановаяДатаЗавершение = ПлановаяДата + 60 * 15;
		КонецЕсли;
		
	КонецЕсли;
	
	СтарыйОтветственный = Ссылка.Ответственный;
	Если ЗначениеЗаполнено(Ответственный) И Не Ответственный = СтарыйОтветственный Тогда
		
		ПользователейВсего = 0;
		НоваяСтрока = "";
		Для Каждого Строка Из СвоиЛица Цикл
			Если ТипЗнч(Строка.Лицо) = Тип("СправочникСсылка.Пользователи") Тогда
				ПользователейВсего = ПользователейВсего + 1;
			КонецЕсли;
		КонецЦикла;
		Если ПользователейВсего = 1 Тогда
			НоваяСтрока = СвоиЛица[0];
		Иначе
			МассивСтрок = СвоиЛица.НайтиСтроки(Новый Структура("Лицо", Ответственный));
			Если МассивСтрок.Количество() = 0 Тогда
				НоваяСтрока = СвоиЛица.Вставить(0);
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(НоваяСтрока) Тогда
			НоваяСтрока.Лицо	= Ответственный;
			КонтактнаяИнформация	= CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюПоТипуКИ(Ответственный,
				 Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			Если КонтактнаяИнформация.Количество() > 0 Тогда
				НоваяСтрока.Адрес	= КонтактнаяИнформация[0].Представление;
			КонецЕсли;
			НоваяСтрока.ПосетитМероприятие	= 1;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		МассивСтрок	= СторонниеЛица.НайтиСтроки(Новый Структура("КонтактноеЛицо", КонтактноеЛицо));
		Если МассивСтрок.Количество() = 0 Тогда
			НоваяСтрока	= СторонниеЛица.Вставить(0);
			НоваяСтрока.КонтактноеЛицо	= КонтактноеЛицо;
			НоваяСтрока.Партнер	= КонтактноеЛицо.Владелец;
			КонтактнаяИнформация	= CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюПоТипуКИ(КонтактноеЛицо,
				 Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			Если КонтактнаяИнформация.Количество() > 0 Тогда
				НоваяСтрока.Адрес	= КонтактнаяИнформация[0].Представление;
			КонецЕсли;
			НоваяСтрока.ПосетитМероприятие	= 1;
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(Партнер) Тогда
		МассивСтрок	= СторонниеЛица.НайтиСтроки(Новый Структура("Партнер", Партнер));
		Если МассивСтрок.Количество() = 0 Тогда
			НоваяСтрока	= СторонниеЛица.Вставить(0);
			НоваяСтрока.Партнер	= Партнер;
			КонтактнаяИнформация	= CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюПоТипуКИ(Партнер,
				 Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			Если КонтактнаяИнформация.Количество() > 0 Тогда
				НоваяСтрока.Адрес	= КонтактнаяИнформация[0].Представление;
			КонецЕсли;
			НоваяСтрока.ПосетитМероприятие	= 1;
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(ПотенциальныйКлиент) Тогда
		МассивСтрок	= СторонниеЛица.НайтиСтроки(Новый Структура("ПотенциальныйКлиент", ПотенциальныйКлиент));
		Если МассивСтрок.Количество() = 0 Тогда
			НоваяСтрока	= СторонниеЛица.Вставить(0);
			НоваяСтрока.ПотенциальныйКлиент	= ПотенциальныйКлиент;
			КонтактнаяИнформация	= CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюПоТипуКИ(ПотенциальныйКлиент,
				 Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
			Если КонтактнаяИнформация.Количество() > 0 Тогда
				НоваяСтрока.Адрес	= КонтактнаяИнформация[0].Представление;
			КонецЕсли;
			НоваяСтрока.ПосетитМероприятие	= 1;
		КонецЕсли;
	КонецЕсли;
	
	СписокКлиентов = "";
	Для Каждого Стр Из СторонниеЛица Цикл
		Если ЗначениеЗаполнено(Стр.КонтактноеЛицо) Тогда
			СписокКлиентов = СписокКлиентов + ?(СписокКлиентов = "", "", "; ") + Строка(Стр.КонтактноеЛицо);
		ИначеЕсли ЗначениеЗаполнено(Стр.Партнер) Тогда
			СписокКлиентов = СписокКлиентов + ?(СписокКлиентов = "", "", "; ") + Строка(Стр.Партнер);
		ИначеЕсли ЗначениеЗаполнено(Стр.ПотенциальныйКлиент) Тогда
			СписокКлиентов = СписокКлиентов + ?(СписокКлиентов = "", "", "; ") + Строка(Стр.ПотенциальныйКлиент);
		КонецЕсли;	
	КонецЦикла;
	СписокУчастников = "";
	Для Каждого Стр Из СвоиЛица Цикл
		СписокУчастников = СписокУчастников + ?(СписокУчастников = "", "", "; ") + Строка(Стр.Лицо);
	КонецЦикла;
	
	Если ПометкаУдаления И НЕ Отказ Тогда
		СтатусВзаимодействия = Справочники.CRM_СостоянияСобытий.Отменено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОснование) И (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.CRM_Интерес")) Тогда
		ТаблицаСвязей = Новый ТаблицаЗначений;
		ТаблицаСвязей.Колонки.Добавить("Ссылка");
		ТаблицаСвязей.Колонки.Добавить("СостояниеИнтереса");
		ТаблицаСвязей.Колонки.Добавить("ВведенНаОсновании");
		ТаблицаСвязей.Колонки.Добавить("ОсновнойИнтерес");
		НоваяСтрока = ТаблицаСвязей.Добавить();
		НоваяСтрока.Ссылка 				= ДокументОснование;
		НоваяСтрока.СостояниеИнтереса	= СостояниеИнтереса;
		НоваяСтрока.ВведенНаОсновании	= Ложь;
		НоваяСтрока.ОсновнойИнтерес		= Истина;
		ДополнительныеСвойства.Вставить("CRM_ТаблицаИнтересов", ТаблицаСвязей);
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ДатаРегистрации) Тогда
		ДатаРегистрации = CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса(); ;
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ИмпортИзВнешнегоКалендаря") Тогда
		Возврат;
	КонецЕсли;
	
	// +Календари
	ЗарегистрироватьИзменениеВПланеОбмена();
	// -Календари
	
	МассивСвоихУчастников = СвоиЛица.ВыгрузитьКолонку("Лицо");
	ЭлементАвтор = МассивСвоихУчастников.Найти(Автор);
	Если ЭлементАвтор <> Неопределено Тогда
		МассивСвоихУчастников.Удалить(ЭлементАвтор);
	КонецЕсли;
	
	Если МассивСвоихУчастников.Количество() > 0 Тогда
		ЭтоНовый = Ложь;
		Если (ДополнительныеСвойства.Свойство("ЗаписьНовогоВзаимодействия", ЭтоНовый) И ЭтоНовый) 
			Или ДополнительныеСвойства.Свойство("СтароеЗначениеПлановойДаты") Тогда
			ПараметрыОповещения = CRM_ОповещенияСервер.ПолучитьПараметрыОповещения(МассивСвоихУчастников,
				Справочники.CRM_ВидыОповещений.ОповещатьОНовыхВзаимодействиях, Ссылка);
			Если ПараметрыОповещения <> Неопределено Тогда
				Если ДополнительныеСвойства.Свойство("СтароеЗначениеПлановойДаты") Тогда
					Для Каждого Элемент Из ПараметрыОповещения Цикл
						Элемент.Вставить("СтароеЗначениеПлановойДаты", ДополнительныеСвойства.СтароеЗначениеПлановойДаты);
					КонецЦикла;
				КонецЕсли;
				CRM_ОповещенияСервер.ДобавитьОповещение(ПараметрыОповещения);
			КонецЕсли;
			КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ВидВзаимодействия.ВидДела = Справочники.CRM_ВидыДелВзаимодействий.Документ_CRM_Интерес 
		ИЛИ ВидВзаимодействия.ВидДела = Справочники.CRM_ВидыДелВзаимодействий.Задача_ЗадачаИсполнителя Тогда
		ПроверяемыеРеквизиты.Добавить("Подразделение");
	ИначеЕсли ВидВзаимодействия.ВидДела <> Справочники.CRM_ВидыДелВзаимодействий.ПрочиеДокументы Тогда
		ПроверяемыеРеквизиты.Добавить("ДокументОснование");
	КонецЕсли;

	//Если СтатусВзаимодействия = Справочники.CRM_СостоянияСобытий.Завершено Тогда
	//	ПроверяемыеРеквизиты.Добавить("ЗавершившийПользователь");
	//КонецЕсли;	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Периодическое = Ложь;
	РядСобытий = Справочники.CRM_РядыСобытий.ПустаяСсылка();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// +Календари

Процедура ЗарегистрироватьИзменениеВПланеОбмена()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДополнительныеСвойства.Свойство("ЗаписьНовогоВзаимодействия") Тогда
		ЗаписьНовогоВзаимодействия = ДополнительныеСвойства.ЗаписьНовогоВзаимодействия;
	Иначе
		ЗаписьНовогоВзаимодействия = Ложь;
	КонецЕсли;
	Если ЗаписьНовогоВзаимодействия И
		СтатусВзаимодействия = Справочники.CRM_СостоянияСобытий.Завершено Тогда
		Возврат;
	КонецЕсли;
	
	Календарь = CRM_ОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Ответственный,
		 "КалендарьДляСинхронизации");
	Если Не ЗначениеЗаполнено(Календарь) Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачалаСинхронизации = Календарь.СервисКалендарей.ДатаНачалаСинхронизации;
	Если Дата < ДатаНачалаСинхронизации Тогда
		Возврат;
	КонецЕсли;
	
	УзелДляКалендаря = ПланыОбмена.CRM_СинхронизацияКалендарей.УзелДляКалендаря(Календарь);
	ОбменДанными.Получатели.Добавить(УзелДляКалендаря);
	УдалитьДублированиеРегистрацииИзменений(УзелДляКалендаря);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Удаляет регистрацию изменений в случае, когда у объекта был изменен календарь перед выгрузкой
Процедура УдалитьДублированиеРегистрацииИзменений(УзелДляКалендаря)
	
	// Если ЗначениеЗаполнено(Идентификатор) Тогда
	//	Возврат;
	//КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВзаимодействияИзменения.Узел КАК Узел
	|ИЗ
	|	Документ.CRM_Взаимодействие.Изменения КАК ВзаимодействияИзменения
	|ГДЕ
	|	ВзаимодействияИзменения.Ссылка = &Ссылка
	|	И ВзаимодействияИзменения.Узел <> &Узел");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Узел", УзелДляКалендаря);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ПланыОбмена.УдалитьРегистрациюИзменений(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку(0), Ссылка);
	
КонецПроцедуры

Процедура ОтразитьВКалендаре() Экспорт
	
	// Запись взаимодействия в календарь ответственного.
	НаборЗаписей = РегистрыСведений.CRM_СобытияКалендаря.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Ссылка);
	
	Идентификатор		= "";
	ETag				= "";
	ЦветИндекса			= "";
	Гиперссылка			= "";
	
	ДополнительныеСвойства.Свойство("Идентификатор",		Идентификатор);
	ДополнительныеСвойства.Свойство("ETag",					ETag);
	ДополнительныеСвойства.Свойство("ИндексЦвета",			ЦветИндекса);
	ДополнительныеСвойства.Свойство("Гиперссылка",			Гиперссылка);
	Если ПустаяСтрока(ЦветИндекса) Тогда
		ЦветИндекса		= Строка(ИндексЦвета);
	КонецЕсли;	
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		НаборЗаписей.Прочитать();
		Для Каждого СохраненнаяЗапись Из НаборЗаписей Цикл
			Если ЗначениеЗаполнено(СохраненнаяЗапись.Идентификатор) Тогда
				Идентификатор		= СохраненнаяЗапись.Идентификатор;
				ETag				= СохраненнаяЗапись.ETag;
				ЦветИндекса			= СохраненнаяЗапись.ИндексЦвета;
				Гиперссылка			= СохраненнаяЗапись.Гиперссылка;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		НаборЗаписей.Очистить();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Идентификатор) Тогда
		Ключ = CRM_СинхронизацияКалендарей.КлючИзИдентификатора(Идентификатор, 
			"Документы.CRM_Взаимодействие.ОтразитьВКалендаре()");
	Иначе
		Ключ = CRM_СинхронизацияКалендарей.КлючИзИдентификатора(СтрЗаменить(Ссылка.УникальныйИдентификатор(), "-", ""), 
			"Документы.CRM_Взаимодействие.ОтразитьВКалендаре()");
	КонецЕсли;
	
	// Запись в календарь ответственного.
	ЗаписьСобытия = НаборЗаписей.Добавить();
	ЗаписьСобытия.Идентификатор			= Идентификатор;
	ЗаписьСобытия.Ключ					= Ключ;
	ЗаписьСобытия.ETag					= ETag;
	ЗаписьСобытия.ИндексЦвета			= ЦветИндекса;
	ЗаписьСобытия.Гиперссылка			= Гиперссылка;
	ЗаполнитьСобытиеКалендаря(ЗаписьСобытия, Ответственный);
	
	// Запись в календари участников.
	Для Каждого ДанныеУчастника Из СвоиЛица Цикл
		
		Если ТипЗнч(ДанныеУчастника.Лицо) <> Тип("СправочникСсылка.Пользователи") Или
			Не ЗначениеЗаполнено(ДанныеУчастника.Лицо) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаписьСобытия = НаборЗаписей.Добавить();
		ЗаписьСобытия.Идентификатор			= Идентификатор;
		ЗаписьСобытия.Ключ					= Ключ;
		ЗаписьСобытия.ETag					= ETag;
		ЗаписьСобытия.ИндексЦвета			= ЦветИндекса;
		ЗаписьСобытия.Гиперссылка			= Гиперссылка;
		ЗаполнитьСобытиеКалендаря(ЗаписьСобытия, ДанныеУчастника.Лицо);
		
		Если (ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.CRM_Интерес") И ДокументОснование.Избранный) 
			ИЛИ (ТипЗнч(ДокументОснование) = Тип("ЗадачаСсылка.ЗадачаИсполнителя")
				 И ДокументОснование.Важность = Перечисления.ВариантыВажностиЗадачи.Высокая) Тогда
			ЗаписьСобытия.Важность = Перечисления.ВариантыВажностиВзаимодействия.Высокая;
		КонецЕсли;
	
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ЗаполнитьСобытиеКалендаря(ЗаписьСобытия, Пользователь)
	
	ЗаписьСобытия.ПериодНачало				= ПлановаяДата;
	ЗаписьСобытия.ПериодОкончание			= ПлановаяДатаЗавершение;
	
	ЗаписьСобытия.Пользователь				= Пользователь;
	ЗаписьСобытия.Объект					= Ссылка;
	ЗаписьСобытия.Подразделение				= Подразделение;
	ЗаписьСобытия.ПомещениеСобытия			= Помещение;
	
	ЗаписьСобытия.Тема 						= Тема;
	ЗаписьСобытия.ВходящееИсходящееСобытие	= ВидВзаимодействия.Направление;
	ЗаписьСобытия.ВидСобытия				= ВидВзаимодействия.ВидСобытия;
	ЗаписьСобытия.ОсновнаяКатегорияСобытия	= ОсновнаяКатегория;
	ЗаписьСобытия.ВидВзаимодействия			= ВидВзаимодействия;
	ЗаписьСобытия.ВидДела					= ВидВзаимодействия.ВидДела;
	ЗаписьСобытия.НаВесьДень				= НаВесьДень;
	ЗаписьСобытия.СостояниеСобытия			= СтатусВзаимодействия;
	
	ЗаписьСобытия.Завершено					= СтатусВзаимодействия = Справочники.CRM_СостоянияСобытий.Завершено;
	
	ЗаписьСобытия.Партнер					= ?(ЗначениеЗаполнено(Партнер), Партнер, ПотенциальныйКлиент);
	ЗаписьСобытия.Балл						= Баллы;
	
КонецПроцедуры

// -Календари

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли
