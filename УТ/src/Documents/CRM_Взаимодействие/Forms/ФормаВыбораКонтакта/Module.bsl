
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТипКлиента", ТипКлиента);
	
	CRM_ИспользоватьПотенциальныхКлиентов = ПолучитьФункциональнуюОпцию("CRM_ИспользоватьПотенциальныхКлиентов");
	
	Если Не CRM_ИспользоватьПотенциальныхКлиентов
		Или Не ЗначениеЗаполнено(ТипКлиента) Тогда
		
		ТипКлиента = "Клиент";
	КонецЕсли;
	
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Партнер) Тогда
		КонтактноеЛицо = CRM_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Партнер, "CRM_ОсновноеКонтактноеЛицо");
	Иначе
		КонтактноеЛицо = ПредопределенноеЗначение("Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоПриИзменении(Элемент)
	Партнер	= CRM_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(КонтактноеЛицо, "Владелец");
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Партнер) Тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка	= Ложь;
	СтруктураОтбора			= Новый Структура("Владелец", Партнер);
	ПараметрыФормы			= Новый Структура("Отбор", СтруктураОтбора);
	ОткрытьФорму("Справочник.КонтактныеЛицаПартнеров.ФормаВыбора", ПараметрыФормы, Элемент, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПотенциальныйКлиентПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Партнер) И ТипЗнч(Партнер) = Тип("СправочникСсылка.CRM_ПотенциальныеКлиенты") Тогда
		КонтактноеЛицо = CRM_ОбщегоНазначенияСервер.ПолучитьЗначениеРеквизита(Партнер, "КонтактноеЛицо");
	Иначе
		КонтактноеЛицо = ПредопределенноеЗначение("Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТипКлиентаПриИзменении(Элемент)
	
	Если ТипКлиента = "Клиент" Тогда
		ПотенциальныйКлиент = ПредопределенноеЗначение("Справочник.CRM_ПотенциальныеКлиенты.ПустаяСсылка");
		Элементы.Партнер.Видимость = Истина;
		Элементы.КонтактноеЛицо.Видимость = Истина;
		Элементы.ПотенциальныйКлиент.Видимость = Ложь;
	Иначе
		Партнер = ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка");
		Элементы.Партнер.Видимость = Ложь;
		Элементы.КонтактноеЛицо.Видимость = Ложь;
		Элементы.ПотенциальныйКлиент.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		Закрыть(КонтактноеЛицо);
	ИначеЕсли ТипКлиента = "Клиент" И ЗначениеЗаполнено(Партнер) Тогда
		Закрыть(Партнер);
	ИначеЕсли ТипКлиента = "ПотенциальныйКлиент" И ЗначениеЗаполнено(ПотенциальныйКлиент) Тогда
		Закрыть(ПотенциальныйКлиент);
	Иначе
		Закрыть(Неопределено);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Элементы.ТипКлиента.Видимость = CRM_ИспользоватьПотенциальныхКлиентов;
	Элементы.ДекорацияКлиент.Видимость = Не CRM_ИспользоватьПотенциальныхКлиентов;
	
	Если ТипКлиента = "Клиент" Тогда
		Элементы.Партнер.Видимость = Истина;
		Элементы.ПотенциальныйКлиент.Видимость = Ложь;
	Иначе
		Элементы.Партнер.Видимость = Ложь;
		Элементы.ПотенциальныйКлиент.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
