#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ОБЪЕКТА

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	СуммаЧасов = СтрокиОтчета.Итог("Часов");
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	CRM_ОбщегоНазначенияСервер.ЗаполнитьШапкуДокумента(ЭтотОбъект, ДанныеЗаполнения);
	
	ТекДата = CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса();
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекДата;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Проекты") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	CRM_ЭтапыПроектов.Ссылка КАК Пакет,
		               |	ВЫБОР
		               |		КОГДА CRM_ЭтапыПроектов.СуммарнаяДатаНачалаФакт = ДАТАВРЕМЯ(1, 1, 1)
		               |			ТОГДА CRM_ЭтапыПроектов.СуммарнаяДатаНачалаПлановая
		               |		ИНАЧЕ CRM_ЭтапыПроектов.СуммарнаяДатаНачалаФакт
		               |	КОНЕЦ КАК ПакетСуммарнаяДатаНачала,
		               |	ВЫБОР
		               |		КОГДА CRM_ЭтапыПроектов.СуммарнаяДатаОкончанияФакт = ДАТАВРЕМЯ(1, 1, 1)
		               |			ТОГДА CRM_ЭтапыПроектов.СуммарнаяДатаОкончанияПлановая
		               |		ИНАЧЕ CRM_ЭтапыПроектов.СуммарнаяДатаОкончанияФакт
		               |	КОНЕЦ КАК ПакетСуммарнаяДатаОкончания,
		               |	ВЫБОР
		               |		КОГДА CRM_ЭтапыПроектов.СуммарнаяДлительностьФакт = 0
		               |			ТОГДА CRM_ЭтапыПроектов.СуммарнаяДлительностьПлановая
		               |		ИНАЧЕ CRM_ЭтапыПроектов.СуммарнаяДлительностьФакт
		               |	КОНЕЦ КАК ПакетСуммарнаяДлительность
		               |ИЗ
		               |	Справочник.CRM_ЭтапыПроектов КАК CRM_ЭтапыПроектов
		               |ГДЕ
		               |	НЕ ЕСТЬNULL(CRM_ЭтапыПроектов.ПометкаУдаления, ЛОЖЬ)
		               |	И CRM_ЭтапыПроектов.Владелец = &Проект
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ПакетСуммарнаяДатаНачала,
		               |	ПакетСуммарнаяДатаОкончания";
		
		Запрос.УстановитьПараметр("Проект", ДанныеЗаполнения.Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		СтрокиОтчета.Очистить();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = СтрокиОтчета.Добавить();
			НоваяСтрока.Проект             = ДанныеЗаполнения.Ссылка;
			НоваяСтрока.Этап               = Выборка.Пакет;
			НоваяСтрока.ДатаР              = Выборка.ПакетСуммарнаяДатаНачала;
			НоваяСтрока.Часов              = Выборка.ПакетСуммарнаяДлительность;
			НоваяСтрока.ПодразделениеРабот = Подразделение;
			НоваяСтрока.Комментарий        = "";
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// Инициализация дополнительных свойств для проведения документа.
	CRM_ОбщегоНазначенияСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.CRM_ОтчетОРаботе.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	CRM_ОбщегоНазначенияСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	CRM_ОбщегоНазначенияСервер.ОтразитьРабочееВремя(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей.
	CRM_ОбщегоНазначенияСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Автор = CRM_ОбщегоНазначенияСервер.ТекущийПользователь();

КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли