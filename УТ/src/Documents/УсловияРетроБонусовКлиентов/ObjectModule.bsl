//@strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.УсловияРетроБонусовКлиентов") Тогда
		
		СсылкаНаДокумент = ДанныеЗаполнения; // ДокументСсылка.УсловияРетроБонусовКлиентов
		ЗаполнитьПоУсловиюРетроБонусовКлиентов(СсылкаНаДокумент);
		
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(НачалоДействия)
	   И ЗначениеЗаполнено(ОкончаниеДействия)
	   И ОкончаниеДействия < НачалоДействия Тогда
		
		ТекстСообщения = НСтр("ru = 'Дата окончания действия должна быть не меньше даты начала действия'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОкончаниеДействия",, Отказ);
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив; // Массив Из Строка
	
	Если ВидРетроБонуса.Пустая() Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ОтборУчастников");
		МассивНепроверяемыхРеквизитов.Добавить("ОтборТоваров");
		МассивНепроверяемыхРеквизитов.Добавить("ПериодичностьУсловий");
		МассивНепроверяемыхРеквизитов.Добавить("ПериодичностьНачислений");
		МассивНепроверяемыхРеквизитов.Добавить("СуммаПлан");
		МассивНепроверяемыхРеквизитов.Добавить("БонусПроцент");
		МассивНепроверяемыхРеквизитов.Добавить("Валюта");
		
		МассивНепроверяемыхРеквизитов.Добавить("Контрагенты");
		МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентов");
		МассивНепроверяемыхРеквизитов.Добавить("СегментыПартнеров");
		МассивНепроверяемыхРеквизитов.Добавить("Товары");
		МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорыСоглашения");
		
	Иначе
		
		Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Все Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Контрагенты");
			МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентов");
			МассивНепроверяемыхРеквизитов.Добавить("СегментыПартнеров");
			МассивНепроверяемыхРеквизитов.Добавить("ДоговорыСоглашения");
			
		КонецЕсли;
		
		Если ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Все Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Товары");
			МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
			
		КонецЕсли;
		
		РеквизитыВида = ДанныеВидаДокумента();
		
		ЗапретПревышенияПоСумме =
			(РеквизитыВида.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
			 И ЗапретНачисленияСверхПлана);
		Если РеквизитыВида.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.Номенклатура
		   И ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Выбранные
		   И НЕ ЗапретПревышенияПоСумме Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("БонусПроцент");
			
		КонецЕсли;
		
		Если БезРасчета
		 ИЛИ (РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.НеИспользуется
			  И РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма) Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("БонусПроцент");
			
		КонецЕсли;
		
		ПроверитьЗаполнениеПериодовШапки(РеквизитыВида, Отказ);
		
		Если БезРасчета Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("ОтборТоваров");
			МассивНепроверяемыхРеквизитов.Добавить("Товары");
			МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
			МассивНепроверяемыхРеквизитов.Добавить("ПериодичностьУсловий");
			
		Иначе
			
			МассивНепроверяемыхРеквизитов.Добавить("Контрагенты.СуммаБонус");
			
		КонецЕсли;
		
		Если РеквизитыВида.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.НеИспользуется Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("ПериодичностьУсловий");
			
		КонецЕсли;
		
		Если РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("СуммаПлан");
			
		КонецЕсли;
		
		Если РеквизитыВида.БазаРасчетаПродаж <> Перечисления.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Товары.БазоваяЦена");
			
		КонецЕсли;
		
		Если НЕ БезРасчета
		   И РеквизитыВида.БазаРасчетаПродаж = Перечисления.БазыРасчетаРетроБонусов.ВыручкаВзаиморасчеты Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Валюта");
			
		КонецЕсли;
		
		ПроверитьПоСоставуУчастниковКонтрагенты(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		ПроверитьПоСоставуУчастниковПартнеры(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		ПроверитьПоСоставуУчастниковИНН(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		ПроверитьПоСоставуУчастниковСегментыПартнеров(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		ПроверитьПоСоставуУчастниковДоговора(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		ПроверитьПоСоставуУчастниковСоглашения(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		
		ПроверитьПоСоставуТоваровНоменклатура(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		ПроверитьПоСоставуТоваровСегментыНоменклатуры(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		
		ПроверитьОграниченияПоНачислениям(РеквизитыВида, Отказ);
		
	КонецЕсли;
	
	ИсправлениеДокументов.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		
		ТекстСообщения = НСтр("ru = 'Запрещено изменение документа в подчиненном узле обмена'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	РетроБонусыСервер.ПроверитьЦепочкуДокументов(
		ИсправляемыйДокумент, СторнируемыйДокумент, ИдентификаторЦепочки, РежимЗаписи);
	
	ПропуститьПроверкуИзмененияПоСтатусу = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеСвойства,
		"ПропуститьПроверкуИзмененияПоСтатусу",
		Ложь);
	
	РазрешеноИзменение = Истина;
	
	Если НЕ Статус.Пустая()
	   И НЕ ПропуститьПроверкуИзмененияПоСтатусу Тогда
		
		РазрешеноИзменение = Документы.УсловияРетроБонусовКлиентов.РазрешеноИзменениеПоСтатусу(Статус);
		Если НЕ РазрешеноИзменение Тогда
			
			ШаблонОшибки = НСтр("ru = 'Изменение документа в статусе ""%1"" запрещено'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, Строка(Статус));
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Статус",, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ВидРетроБонуса.Пустая() Тогда
		
		РеквизитыВида = ДанныеВидаДокумента();
		
		Если БезРасчета
		 ИЛИ (РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество
			  И РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма) Тогда
			
			ЗапретНачисленияСверхПлана = Ложь;
			
		КонецЕсли;
		
		Если РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
		 ИЛИ БезРасчета Тогда
			
			СуммаПлан = 0;
			
		КонецЕсли;
		
		ЗапретПревышенияПоСумме =
			(РеквизитыВида.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
			 И ЗапретНачисленияСверхПлана);
		Если БезРасчета
		 ИЛИ (ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Выбранные
			  И РеквизитыВида.СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
			  И НЕ ЗапретПревышенияПоСумме) Тогда
			
			БонусПроцент = 0;
			
		КонецЕсли;
		
		ПараметрыУчастников = Документы.УсловияРетроБонусовКлиентов.ПараметрыОчисткиРеквизитовУчастников();
		ПараметрыУчастников.СоставУчастников = РеквизитыВида.СоставУчастников;
		ПараметрыУчастников.ОтборУчастников = ОтборУчастников;
		Документы.УсловияРетроБонусовКлиентов.ОчиститьНеиспользуемыеРеквизитыУчастников(
			ЭтотОбъект, ПараметрыУчастников);
		Документы.УсловияРетроБонусовКлиентов.УстановитьПризнакиРасчетаПланаУчастников(
			ЭтотОбъект, ПараметрыУчастников);
		
		ПараметрыТоваров = Документы.УсловияРетроБонусовКлиентов.ПараметрыОчисткиРеквизитовТоваров();
		ПараметрыТоваров.СоставТоваров = РеквизитыВида.СоставТоваров;
		ПараметрыТоваров.ОтборТоваров = ОтборТоваров;
		ПараметрыТоваров.ПоказательТоваров = РеквизитыВида.ПоказательТоваров;
		ПараметрыТоваров.БазаРасчета = РеквизитыВида.БазаРасчетаПродаж;
		Документы.УсловияРетроБонусовКлиентов.ОчиститьНеиспользуемыеРеквизитыТоваров(ЭтотОбъект, ПараметрыТоваров);
		
	КонецЕсли;
	
	Если Исправление Тогда
		
		РеквизитыУсловий = Документы.УсловияРетроБонусовКлиентов.АктуальныеДанные(ИсправляемыйДокумент);
		Если РеквизитыУсловий.СегментыЗафиксированы
		   И РазрешеноИзменение Тогда
			
			ТекстУведомления = НСтр("ru = 'Сегменты зафиксированы, ввод исправлений невозможен'");
			ОбщегоНазначения.СообщитьПользователю(ТекстУведомления, ЭтотОбъект,,, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если РазрешеноИзменение Тогда
		
		ПроверитьОграниченияПоНачислениям(РеквизитыВида, Отказ, Истина);
		
	КонецЕсли;
	
	ИсправлениеДокументов.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИсправлениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
	Статус = Перечисления.СтатусыДокументовРетроБонусов.Черновик;
	
	ИменаОчищаемыхПолей = Новый Массив; // Массив из Строка
	ИменаОчищаемыхПолей.Добавить("СуммаПланДоИзменений");
	ИменаОчищаемыхПолей.Добавить("ПланНеУказываетсяДоИзменений");
	ИменаОчищаемыхПолей.Добавить("НачалоДействияДоИзменений");
	ИменаОчищаемыхПолей.Добавить("ОкончаниеДействияДоИзменений");
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "Контрагенты", ИменаОчищаемыхПолей);
	
	ИменаОчищаемыхПолей = Новый Массив; // Массив из Строка
	ИменаОчищаемыхПолей.Добавить("СуммаПланДоИзменений");
	ИменаОчищаемыхПолей.Добавить("НачалоДействияДоИзменений");
	ИменаОчищаемыхПолей.Добавить("ОкончаниеДействияДоИзменений");
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "ИННКонтрагентов", ИменаОчищаемыхПолей);
	
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "СегментыПартнеров");
	
	ИменаОчищаемыхПолей = Новый Массив; // Массив из Строка
	ИменаОчищаемыхПолей.Добавить("СуммаПланДоИзменений");
	ИменаОчищаемыхПолей.Добавить("НачалоДействияДоИзменений");
	ИменаОчищаемыхПолей.Добавить("ОкончаниеДействияДоИзменений");
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "ДоговорыСоглашения");
	
	ИменаОчищаемыхПолей = Новый Массив; // Массив из Строка
	ИменаОчищаемыхПолей.Добавить("НачалоДействияДоИзменений");
	ИменаОчищаемыхПолей.Добавить("ОкончаниеДействияДоИзменений");
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "Товары", ИменаОчищаемыхПолей);
	
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "СегментыТоваров");
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Префикс = ?(Исправление, "К", "0");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Данные вида документа.
// 
// Возвращаемое значение:
//  Структура:
//	* БезРасчета - Булево
//	* НачалоДействия - Дата
//	* ОкончаниеДействия - Дата 
//	* ПериодичностьУсловий - ПеречислениеСсылка.ПериодичностиРетроБонусов
//	* ПериодичностьНачислений - ПеречислениеСсылка.ПериодичностиРетроБонусов
//	* ДетализацияРасчетаУчастников - ПеречислениеСсылка.ДетализацияРасчетаУчастниковРетроБонусов
//	* ОтборТоваров - ПеречислениеСсылка.СоставыСписковРетроБонусов
//	* ОтборУчастников - ПеречислениеСсылка.СоставыСписковРетроБонусов
//	* ПоказательТоваров - ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
//	* БазаРасчетаПродаж - ПеречислениеСсылка.БазыРасчетаРетроБонусов
//	* СоставТоваров - ПеречислениеСсылка.СоставыТоваровРетроБонусов
//	* СоставУчастников - ПеречислениеСсылка.СоставыУчастниковРетроБонусов
//
Функция ДанныеВидаДокумента()
	
	МассивРеквизитов = Новый Массив; // Массив Из Строка
	МассивРеквизитов.Добавить("НачалоДействия");
	МассивРеквизитов.Добавить("ОкончаниеДействия");
	МассивРеквизитов.Добавить("ПериодичностьУсловий");
	МассивРеквизитов.Добавить("ПериодичностьНачислений");
	МассивРеквизитов.Добавить("ДетализацияРасчетаУчастников");
	МассивРеквизитов.Добавить("ОтборТоваров");
	МассивРеквизитов.Добавить("ОтборУчастников");
	МассивРеквизитов.Добавить("БезРасчета");
	МассивРеквизитов.Добавить("ПоказательТоваров");
	МассивРеквизитов.Добавить("БазаРасчетаПродаж");
	МассивРеквизитов.Добавить("СоставТоваров");
	МассивРеквизитов.Добавить("СоставУчастников");
	РеквизитыВида = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидРетроБонуса, МассивРеквизитов);
	
	Возврат РеквизитыВида;
	
КонецФункции

// Заполнить по условию ретро бонусов клиентов.
// 
// Параметры:
//  ДанныеЗаполнения - ДокументСсылка.УсловияРетроБонусовКлиентов - Данные заполнения
//
Процедура ЗаполнитьПоУсловиюРетроБонусовКлиентов(ДанныеЗаполнения)
	
	ДокументЗаполнения = ДанныеЗаполнения; // ДокументСсылка.УсловияРетроБонусовКлиентов
	ПоследнееИсправление = ИсправлениеДокументов.ПоследнийИсправительныйДокумент(ДокументЗаполнения);
	
	ИменаРеквизитов = Новый Структура;
	ИменаРеквизитов.Вставить("НачалоДействия");
	ИменаРеквизитов.Вставить("ОкончаниеДействия");
	ИменаРеквизитов.Вставить("СуммаПлан");
	ИменаРеквизитов.Вставить("ПоказательТоваров", "ВидРетроБонуса.ПоказательТоваров");
	ИменаРеквизитов.Вставить("СоставУчастников", "ВидРетроБонуса.СоставУчастников");
	
	Если ПоследнееИсправление = Неопределено Тогда
		
		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ДокументЗаполнения);
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументЗаполнения, ИменаРеквизитов);
		
	Иначе
		
		ПоследнееИсправлениеСсылка = ПоследнееИсправление.Ссылка; // ДокументСсылка.УсловияРетроБонусовКлиентов
		
		ПоследнееИсправлениеСтатус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ПоследнееИсправлениеСсылка, "Статус");
		Если ПоследнееИсправлениеСтатус <> Перечисления.СтатусыДокументовРетроБонусов.Согласован Тогда
			
			ТекстУведомления = НСтр("ru = 'Последний в цепочке исправлений документ не согласован, ввод исправлений невозможен'");
			ВызватьИсключение ТекстУведомления;
			
		КонецЕсли;
		
		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ПоследнееИсправление.Ссылка);
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПоследнееИсправление.Ссылка, ИменаРеквизитов);
		
	КонецЕсли;
	
	Если ПериодичностьНачислений = Перечисления.ПериодичностиРетроБонусов.Однократно Тогда
		
		МаксимальнаяДата = РетроБонусыСервер.ГраницаНачисленийКлиентов(ИсправляемыйДокумент);
		Если МаксимальнаяДата <> Дата(1, 1, 1) Тогда
			
			ТекстУведомления = НСтр("ru = 'Зарегистрировано начисление ретро-бонусов, ввод исправлений невозможен'");
			ВызватьИсключение ТекстУведомления;
			
		КонецЕсли;
		
	КонецЕсли;
	
	РеквизитыУсловий = Документы.УсловияРетроБонусовКлиентов.АктуальныеДанные(ИсправляемыйДокумент);
	Если РеквизитыУсловий.СегментыЗафиксированы Тогда
		
		ТекстУведомления = НСтр("ru = 'Сегменты зафиксированы, ввод исправлений невозможен'");
		ВызватьИсключение ТекстУведомления;
		
	КонецЕсли;
	
	Статус = Перечисления.СтатусыДокументовРетроБонусов.Черновик;
	
	ДетализацияРасчета = Перечисления.ДетализацияРасчетаУчастниковРетроБонусов;
	
	ДетализацияКонтрагентСоставКлиент =
		(ДетализацияРасчетаУчастников = ДетализацияРасчета.ПоКонтрагенту
		 И РеквизитыОснования.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Клиенты);
	
	ДетализацияКлиентСоставКонтрагент =
		(ДетализацияРасчетаУчастников = ДетализацияРасчета.ПоКлиенту
		 И РеквизитыОснования.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты);
	
	ПланУказывается = НЕ (ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные
						  И (ДетализацияКонтрагентСоставКлиент
							 ИЛИ ДетализацияКлиентСоставКонтрагент));
	
	ЗаполнятьПлан = (ОтборУчастников <> Перечисления.СоставыСписковРетроБонусов.КромеВыбранных
					 И ПланУказывается);
	
	#Область ТЧ_Контрагенты
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Контрагенты
	
	Для Каждого СтрокаТЧ Из Контрагенты Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		СтрокаТЧ.ПланНеУказываетсяДоИзменений = СтрокаТЧ.ПланНеУказывается;
		Если ЗаполнятьПлан Тогда
			
			Если СтрокаТЧ.СуммаПлан = 0 Тогда
				
				СтрокаТЧ.СуммаПланДоИзменений = РеквизитыОснования.СуммаПлан;
				
			Иначе
				
				СтрокаТЧ.СуммаПланДоИзменений = СтрокаТЧ.СуммаПлан;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
			
			ЗаполнитьДанныеПериодовПоОснованию(СтрокаТЧ, РеквизитыОснования);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		Контрагенты.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
	#Область ТЧ_ИННКонтрагентов
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ИННКонтрагентов
	
	Для Каждого СтрокаТЧ Из ИННКонтрагентов Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		Если ЗаполнятьПлан Тогда
			
			Если СтрокаТЧ.СуммаПлан = 0 Тогда
				
				СтрокаТЧ.СуммаПланДоИзменений = РеквизитыОснования.СуммаПлан;
				
			Иначе
				
				СтрокаТЧ.СуммаПланДоИзменений = СтрокаТЧ.СуммаПлан;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
			
			ЗаполнитьДанныеПериодовПоОснованию(СтрокаТЧ, РеквизитыОснования);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		ИННКонтрагентов.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти

	#Область ТЧ_ДоговорыСоглашения
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ДоговорыСоглашения
	
	Для Каждого СтрокаТЧ Из ДоговорыСоглашения Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		Если ЗаполнятьПлан Тогда
			
			Если СтрокаТЧ.СуммаПлан = 0 Тогда
				
				СтрокаТЧ.СуммаПланДоИзменений = РеквизитыОснования.СуммаПлан;
				
			Иначе
				
				СтрокаТЧ.СуммаПланДоИзменений = СтрокаТЧ.СуммаПлан;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ЗаполнитьДанныеПериодовПоОснованию(СтрокаТЧ, РеквизитыОснования);
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		ДоговорыСоглашения.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
	#Область ТЧ_Товары
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Товары
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		
		Если РетроБонусыКлиентСервер.ИспользуетсяПериодУсловийТоваров(РеквизитыОснования.ПоказательТоваров) Тогда
			
			ЗаполнитьДанныеПериодовПоОснованию(СтрокаТЧ, РеквизитыОснования);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		Товары.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
	#Область ТЧ_СегментыПартнеров
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.СегментыПартнеров
	
	Для Каждого СтрокаТЧ Из СегментыПартнеров Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		СегментыПартнеров.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
	#Область ТЧ_СегментыТоваров
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.СегментыТоваров
	
	Для Каждого СтрокаТЧ Из СегментыТоваров Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		СегментыТоваров.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
КонецПроцедуры

// Параметры:
//  СтрокаДанных - СтрокаТабличнойЧасти:
//  * НачалоДействия - Дата
//  * НачалоДействияДоИзменений - Дата
//  * ОкончаниеДействия - Дата
//  * ОкончаниеДействияДоИзменений - Дата
//	ДанныеОснования - Структура:
//  * НачалоДействия - Дата
//  * ОкончаниеДействия - Дата
//
Процедура ЗаполнитьДанныеПериодовПоОснованию(СтрокаДанных, Знач ДанныеОснования)
	
	ПустаяДата = Дата(1, 1, 1);
	
	Если СтрокаДанных.НачалоДействия = ПустаяДата Тогда
				
		СтрокаДанных.НачалоДействияДоИзменений = ДанныеОснования.НачалоДействия;
		
	Иначе
		
		СтрокаДанных.НачалоДействияДоИзменений = СтрокаДанных.НачалоДействия;
		
	КонецЕсли;
	
	Если СтрокаДанных.ОкончаниеДействия = ПустаяДата Тогда
		
		СтрокаДанных.ОкончаниеДействияДоИзменений = ДанныеОснования.ОкончаниеДействия;
		
	Иначе
		
		СтрокаДанных.ОкончаниеДействияДоИзменений = СтрокаДанных.ОкончаниеДействия;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  Отказ - Булево - Отказ
//
Процедура ПроверитьЗаполнениеПериодовШапки(РеквизитыВида, Отказ)
	
	ПустаяДата = Дата(1, 1, 1);
	МаксимальнаяДата = Дата(2999, 12, 31);
	
	НачалоДействияВида = РеквизитыВида.НачалоДействия;
	ОкончаниеДействияВида = РеквизитыВида.ОкончаниеДействия;
	Если ОкончаниеДействияВида = ПустаяДата Тогда
		ОкончаниеДействияВида = МаксимальнаяДата;
	КонецЕсли;
	
	Если НачалоДействия <> ПустаяДата
	   И НачалоДействия < НачалоДействияВида Тогда
		
		ТекстСообщения = НСтр("ru = 'Начало периода действия документа раньше, чем начало действия вида ретро-бонуса'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "НачалоДействия",, Отказ);
		
	КонецЕсли;
	
	Если ОкончаниеДействия <> ПустаяДата
	   И ОкончаниеДействия > ОкончаниеДействияВида Тогда
		
		ТекстСообщения = НСтр("ru = 'Окончание периода действия документа больше, чем окончание действия вида ретро-бонуса'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОкончаниеДействия",, Отказ);
		
	КонецЕсли;
	
	Если НачалоДействия <> ПустаяДата
	   И ОкончаниеДействия <> ПустаяДата Тогда
		
		РезультатПроверки = РетроБонусыСервер.ПроверитьПериодыПоПериодичности(
			ПериодичностьУсловий, 
			НачалоДействия,
			ОкончаниеДействия);
		Если РезультатПроверки.ЕстьНарушениеНачала Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
				РезультатПроверки.ТекстОшибкиНачала, ЭтотОбъект, "НачалоДействия",, Отказ);
			
		КонецЕсли;
		
		Если РезультатПроверки.ЕстьНарушениеОкончания Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
				РезультатПроверки.ТекстОшибкиОкончания, ЭтотОбъект, "ОкончаниеДействия",, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуУчастниковКонтрагенты(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.Контрагенты
	 ИЛИ ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Все Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
		
		ПредставлениеТаблицы = НСтр("ru = 'Выбранные контрагенты'");
		РетроБонусыСервер.ПроверитьПериодыТаблицы(ЭтотОбъект, "Контрагенты", ПредставлениеТаблицы, Отказ);
		
	Иначе
		
		Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.КромеВыбранных Тогда
			
			ПредставлениеТаблицы = НСтр("ru = 'Кроме контрагентов'");
			
			ПроверитьУказаниеПустыхЗначенийУчастников(Истина, Отказ, ПредставлениеТаблицы);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, "Контрагенты", "Контрагент,Партнер", Отказ, ПредставлениеТаблицы,, ОтборСтрок);
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, "Контрагенты", ПредставлениеТаблицы, Отказ);
	
	Если НЕ НачислитьСразу Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагенты.Партнер");
	КонецЕсли;
	МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентов");
	МассивНепроверяемыхРеквизитов.Добавить("СегментыПартнеров");
	МассивНепроверяемыхРеквизитов.Добавить("ДоговорыСоглашения");
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуУчастниковПартнеры(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.Клиенты
	 ИЛИ ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Все Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
		
		ПредставлениеТаблицы = НСтр("ru = 'Выбранные клиенты'");
		РетроБонусыСервер.ПроверитьПериодыТаблицы(ЭтотОбъект, "Контрагенты", ПредставлениеТаблицы, Отказ);
		
	Иначе
		
		Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.КромеВыбранных Тогда
			
			ПредставлениеТаблицы = НСтр("ru = 'Кроме клиентов'");
			
			ПроверитьУказаниеПустыхЗначенийУчастников(Ложь, Отказ, ПредставлениеТаблицы);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, "Контрагенты", "Партнер,Контрагент", Отказ, ПредставлениеТаблицы,, ОтборСтрок);
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, "Контрагенты", ПредставлениеТаблицы, Отказ);
	
	Если НЕ НачислитьСразу Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагенты.Контрагент");
	КонецЕсли;
	МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентов");
	МассивНепроверяемыхРеквизитов.Добавить("СегментыПартнеров");
	МассивНепроверяемыхРеквизитов.Добавить("ДоговорыСоглашения");
	
КонецПроцедуры

Процедура ПроверитьУказаниеПустыхЗначенийУчастников(ПроверкаПартнера, Отказ, ПредставлениеТаблицы)
	
	Если ПроверкаПартнера Тогда
		
		КлючевоеПоле = "Контрагент";
		ПредставлениеКлючевогоПоля = НСтр("ru = 'Контрагент'");
		ВспомогательноеПоле = "Партнер";
		ПредставлениеВспомогательногоПоля = НСтр("ru = 'Клиент'");
		ПустоеВспомогательноеПоле = Справочники.Партнеры.ПустаяСсылка(); // СправочникСсылка.Партнеры, СправочникСсылка.Контрагенты
		
	Иначе
		
		КлючевоеПоле = "Партнер";
		ПредставлениеКлючевогоПоля = НСтр("ru = 'Клиент'");
		ВспомогательноеПоле = "Контрагент";
		ПредставлениеВспомогательногоПоля = НСтр("ru = 'Контрагент'");
		ПустоеВспомогательноеПоле = Справочники.Контрагенты.ПустаяСсылка();
		
	КонецЕсли;
	
	Выборка = ВыборкаПустыхИЗаполненныхУчастников(КлючевоеПоле, ВспомогательноеПоле, ПустоеВспомогательноеПоле);
	
	Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.КромеВыбранных Тогда
		
		ШаблонСообщения = НСтр("ru = 'В списке ""%1"" по ключевому полю ""%2"" (%3) в разных строках одновременно указано и не указано значение в поле ""%4""'");
		
	Иначе
		
		ШаблонСообщения = НСтр("ru = 'В списке ""%1"" по ключевому полю ""%2"" (%3) в разных строках для поля ""%4"" одновременно не указано значение и указано'");
		
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			"Контрагенты", Выборка.НомерСтроки, "НомерСтроки");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения,
			ПредставлениеТаблицы,
			ПредставлениеКлючевогоПоля,
			Строка(Выборка.КлючевоеПоле),
			ПредставлениеВспомогательногоПоля);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, Поле,, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  КлючевоеПоле - Строка - Ключевое поле
//  ВспомогательноеПоле - Строка - Вспомогательное поле
//  ПустоеВспомогательноеПоле - СправочникСсылка.Контрагенты, СправочникСсылка.Партнеры - Пустая ссылка типа
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//	* НомерСтроки - Число -
//	* КлючевоеПоле - СправочникСсылка.Партнеры, СправочникСсылка.Контрагенты -
//
Функция ВыборкаПустыхИЗаполненныхУчастников(КлючевоеПоле, ВспомогательноеПоле, ПустоеВспомогательноеПоле)
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПроверки.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПроверки.Партнер КАК Партнер,
	|	ТаблицаПроверки.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ ТаблицаПроверки
	|ИЗ
	|	&ТаблицаПроверки КАК ТаблицаПроверки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаПроверки.НомерСтроки) КАК НомерСтроки,
	|	&КлючевоеПоле КАК КлючевоеПоле
	|ИЗ
	|	ТаблицаПроверки КАК ТаблицаПроверки
	|
	|СГРУППИРОВАТЬ ПО
	|	&КлючевоеПоле
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|		КОГДА &ВспомогательноеПоле = &ПустоеВспомогательноеПоле
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ) > 1";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КлючевоеПоле", КлючевоеПоле);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВспомогательноеПоле", ВспомогательноеПоле);
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ПустоеВспомогательноеПоле", ПустоеВспомогательноеПоле);
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	ТаблицаПроверки = Контрагенты.Выгрузить(ОтборСтрок, "НомерСтроки, Контрагент, Партнер");
	Запрос.УстановитьПараметр("ТаблицаПроверки", ТаблицаПроверки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат Выборка;
	
КонецФункции

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуУчастниковИНН(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.ИНН
	 ИЛИ ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Все Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицы = "ИННКонтрагентов";
	ПредставлениеТаблицы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(
		РеквизитыВида.СоставУчастников, ОтборУчастников);
	Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
		
		РетроБонусыСервер.ПроверитьПериодыТаблицы(ЭтотОбъект, ИмяТаблицы, ПредставлениеТаблицы, Отказ);
		
	КонецЕсли;
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, ИмяТаблицы, "ИНН", Отказ, ПредставлениеТаблицы,, ОтборСтрок);
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, ИмяТаблицы, ПредставлениеТаблицы, Отказ);
	
	РетроБонусыСервер.ПроверитьЗаполнениеИНН(ЭтотОбъект, ИмяТаблицы, ПредставлениеТаблицы, Отказ);
	МассивНепроверяемыхРеквизитов.Добавить("Контрагенты");
	МассивНепроверяемыхРеквизитов.Добавить("СегментыПартнеров");
	МассивНепроверяемыхРеквизитов.Добавить("ДоговорыСоглашения");
			
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуУчастниковСегментыПартнеров(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.СегментыПартнеров Тогда
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Контрагенты");
	МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентов");
	МассивНепроверяемыхРеквизитов.Добавить("ДоговорыСоглашения");
	
	Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.КромеВыбранных Тогда
		
		ПредставлениеТаблицы = НСтр("ru = 'Кроме сегментов клиентов'");
		
	Иначе
		
		ПредставлениеТаблицы = НСтр("ru = 'Выбранные сегменты клиентов'");
		
	КонецЕсли;
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, "СегментыПартнеров", ПредставлениеТаблицы, Отказ);
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, "СегментыПартнеров", "Сегмент", Отказ, ПредставлениеТаблицы,, ОтборСтрок);
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуУчастниковДоговора(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.Договоры Тогда
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Контрагенты");
	МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентов");
	МассивНепроверяемыхРеквизитов.Добавить("СегментыПартнеров");
	
	МассивНепроверяемыхРеквизитов.Добавить("ДоговорыСоглашения.Соглашение");
	
	ПредставлениеТаблицы = НСтр("ru = 'Выбранные договоры'");
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, "ДоговорыСоглашения", ПредставлениеТаблицы, Отказ);
	РетроБонусыСервер.ПроверитьПериодыТаблицы(ЭтотОбъект, "ДоговорыСоглашения", ПредставлениеТаблицы, Отказ);
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, "ДоговорыСоглашения", "Договор", Отказ, ПредставлениеТаблицы,, ОтборСтрок);
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуУчастниковСоглашения(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.Соглашения Тогда
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Контрагенты");
	МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентов");
	МассивНепроверяемыхРеквизитов.Добавить("СегментыПартнеров");
	
	МассивНепроверяемыхРеквизитов.Добавить("ДоговорыСоглашения.Контрагент");
	МассивНепроверяемыхРеквизитов.Добавить("ДоговорыСоглашения.Договор");
	
	ПредставлениеТаблицы = НСтр("ru = 'Выбранные соглашения'");
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, "ДоговорыСоглашения", ПредставлениеТаблицы, Отказ);
	РетроБонусыСервер.ПроверитьПериодыТаблицы(ЭтотОбъект, "ДоговорыСоглашения", ПредставлениеТаблицы, Отказ);
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, "ДоговорыСоглашения", "Соглашение", Отказ, ПредставлениеТаблицы,, ОтборСтрок);
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуТоваровНоменклатура(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.Номенклатура
	 ИЛИ ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Все Тогда
		Возврат;
	КонецЕсли;
	
	ПоказателиПродаж = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		
		КлючевыеПоля = "Номенклатура,Характеристика";
		
	Иначе
		
		КлючевыеПоля = "Номенклатура";
		
	КонецЕсли;
	
	Если ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
		
		ПериодыУсловияЗаполнены = (ЗначениеЗаполнено(НачалоДействия)
								   И ЗначениеЗаполнено(ОкончаниеДействия));
		
		ПредставлениеТаблицы = НСтр("ru = 'Выбранные товары'");
		РетроБонусыСервер.ПроверитьПериодыТаблицы(ЭтотОбъект, "Товары", ПредставлениеТаблицы, Отказ);
		
		Если РеквизитыВида.ПоказательТоваров = ПоказателиПродаж.КоличествоСовокупно
		 ИЛИ РеквизитыВида.ПоказательТоваров = ПоказателиПродаж.ПакетноеПредложение Тогда
				
			ОтборСтрок = Новый Структура;
			ОтборСтрок.Вставить("Отменено", Ложь);
			РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
				ЭтотОбъект, "Товары", КлючевыеПоля, Отказ, ПредставлениеТаблицы,, ОтборСтрок);
			
		КонецЕсли;
		
		Если ПериодыУсловияЗаполнены Тогда
			
			Если РеквизитыВида.ПоказательТоваров = ПоказателиПродаж.Сумма
			 ИЛИ РеквизитыВида.ПоказательТоваров = ПоказателиПродаж.НеИспользуется
			 ИЛИ РеквизитыВида.ПоказательТоваров = ПоказателиПродаж.Количество Тогда
				
				ПроверитьПериодыТоваровПоКлючам(РеквизитыВида, ПредставлениеТаблицы, Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если РеквизитыВида.ПоказательТоваров = ПоказателиПродаж.Сумма
		  И ЗапретНачисленияСверхПлана Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Процент");
			
		КонецЕсли;
		
	Иначе
		
		ПредставлениеТаблицы = НСтр("ru = 'Кроме товаров'");
		
		ОтборСтрок = Новый Структура;
		ОтборСтрок.Вставить("Отменено", Ложь);
		РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
			ЭтотОбъект, "Товары", КлючевыеПоля, Отказ, ПредставлениеТаблицы,, ОтборСтрок);
			
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Процент");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.НачалоДействия");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ОкончаниеДействия");
		
	КонецЕсли;
	
	Если (РеквизитыВида.ПоказательТоваров <> ПоказателиПродаж.Количество
		  И РеквизитыВида.ПоказательТоваров <> ПоказателиПродаж.КоличествоСовокупно
		  И РеквизитыВида.ПоказательТоваров <> ПоказателиПродаж.Количество) Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоПлан");
		
	КонецЕсли;
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, "Товары", ПредставлениеТаблицы, Отказ);
	
КонецПроцедуры

Процедура ПроверитьПериодыТоваровПоКлючам(РеквизитыВида, ПредставлениеТаблицы, Отказ)
	
	Данные = ДанныеПериодовТоваровПоКлючам(РеквизитыВида);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		
		ШаблонСообщенияПериоды = НСтр("ru = 'В списке ""%1"" пересекаются периоды действия по ключевым полям ""Номенклатура, Характеристика"" (%2)'");
		ШаблонСообщенияДубли = НСтр("ru = 'В списке ""%1"" дублируются показатели ""Кол-во, план и/или Бонус, %"" по ключевым полям ""Номенклатура, Характеристика"" (%2)'");
		
	Иначе
		
		ШаблонСообщенияПериоды = НСтр("ru = 'В списке ""%1"" пересекаются периоды действия по ключевому полю ""Номенклатура"" (%2)'");
		ШаблонСообщенияДубли = НСтр("ru = 'В списке ""%1"" дублируются показатели ""Кол-во, план и/или Бонус, %"" по ключевому полю ""Номенклатура"" (%2)'");
		
	КонецЕсли;
	
	Пока Данные.ПересеченияПериодов.Следующий() Цикл
		
		ПредставлениеНоменклатуры = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			Данные.ПересеченияПериодов.Номенклатура,
			Данные.ПересеченияПериодов.Характеристика);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщенияПериоды,
			ПредставлениеТаблицы,
			ПредставлениеНоменклатуры);
			
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			"Товары", Данные.ПересеченияПериодов.ПерваяСтрока, "НомерСтроки");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, ПутьКПолю,, Отказ);
		
	КонецЦикла;
	
	Пока Данные.ДублиПланаПроцента.Следующий() Цикл
		
		ПредставлениеНоменклатуры = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			Данные.ДублиПланаПроцента.Номенклатура,
			Данные.ДублиПланаПроцента.Характеристика);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщенияДубли,
			ПредставлениеТаблицы,
			ПредставлениеНоменклатуры);
			
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			"Товары", Данные.ДублиПланаПроцента.ПерваяСтрока, "НомерСтроки");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, ПутьКПолю,, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
// 
// Возвращаемое значение:
//  Структура - Данные периодов товаров по ключам:
// * ПересеченияПериодов - ВыборкаИзРезультатаЗапроса:
//	** ПерваяСтрока - Число -
// 	** Номенклатура - СправочникСсылка.Номенклатура -
// 	** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры -
// * ДублиПланаПроцента - ВыборкаИзРезультатаЗапроса:
//	** ПерваяСтрока - Число -
// 	** Номенклатура - СправочникСсылка.Номенклатура -
// 	** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры -
// 
Функция ДанныеПериодовТоваровПоКлючам(РеквизитыВида)
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.КоличествоПлан КАК КоличествоПлан,
	|	ТаблицаТоваров.Процент КАК Процент,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.НачалоДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &НачалоДействия
	|		ИНАЧЕ ТаблицаТоваров.НачалоДействия
	|	КОНЕЦ КАК НачалоДействия,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.ОкончаниеДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ОкончаниеДействия
	|		ИНАЧЕ ТаблицаТоваров.ОкончаниеДействия
	|	КОНЕЦ КАК ОкончаниеДействия
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	МИНИМУМ(ТаблицаТоваров.НомерСтроки) КАК ПерваяСтрока
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ТаблицаТоваровЗеркало
	|		ПО ТаблицаТоваров.НомерСтроки <> ТаблицаТоваровЗеркало.НомерСтроки
	|			И ТаблицаТоваров.Номенклатура = ТаблицаТоваровЗеркало.Номенклатура
	|			И ТаблицаТоваров.Характеристика = ТаблицаТоваровЗеркало.Характеристика
	|			И (ТаблицаТоваров.НачалоДействия МЕЖДУ ТаблицаТоваровЗеркало.НачалоДействия И ТаблицаТоваровЗеркало.ОкончаниеДействия
	|				ИЛИ ТаблицаТоваров.ОкончаниеДействия МЕЖДУ ТаблицаТоваровЗеркало.НачалоДействия И ТаблицаТоваровЗеркало.ОкончаниеДействия)
	|ГДЕ
	|	НЕ &ЭтоПрогрессивнаяШкала
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	МИНИМУМ(ТаблицаТоваров.НомерСтроки)
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ТаблицаТоваровЗеркало
	|		ПО ТаблицаТоваров.НомерСтроки <> ТаблицаТоваровЗеркало.НомерСтроки
	|			И ТаблицаТоваров.Номенклатура = ТаблицаТоваровЗеркало.Номенклатура
	|			И ТаблицаТоваров.Характеристика = ТаблицаТоваровЗеркало.Характеристика
	|			И (ТаблицаТоваров.НачалоДействия МЕЖДУ ТаблицаТоваровЗеркало.НачалоДействия И ТаблицаТоваровЗеркало.ОкончаниеДействия
	|				ИЛИ ТаблицаТоваров.ОкончаниеДействия МЕЖДУ ТаблицаТоваровЗеркало.НачалоДействия И ТаблицаТоваровЗеркало.ОкончаниеДействия
	|				ИЛИ &ВесьПериод)
	|			И (ТаблицаТоваров.НачалоДействия <> ТаблицаТоваровЗеркало.НачалоДействия
	|				ИЛИ ТаблицаТоваров.ОкончаниеДействия <> ТаблицаТоваровЗеркало.ОкончаниеДействия)
	|ГДЕ
	|	&ЭтоПрогрессивнаяШкала
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	МИНИМУМ(ТаблицаТоваров.НомерСтроки) КАК ПерваяСтрока
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ТаблицаТоваровЗеркало
	|		ПО ТаблицаТоваров.НомерСтроки <> ТаблицаТоваровЗеркало.НомерСтроки
	|			И ТаблицаТоваров.Номенклатура = ТаблицаТоваровЗеркало.Номенклатура
	|			И ТаблицаТоваров.Характеристика = ТаблицаТоваровЗеркало.Характеристика
	|			И (ТаблицаТоваров.НачалоДействия МЕЖДУ ТаблицаТоваровЗеркало.НачалоДействия И ТаблицаТоваровЗеркало.ОкончаниеДействия
	|				ИЛИ ТаблицаТоваров.ОкончаниеДействия МЕЖДУ ТаблицаТоваровЗеркало.НачалоДействия И ТаблицаТоваровЗеркало.ОкончаниеДействия
	|				ИЛИ &ВесьПериод)
	|			И (ТаблицаТоваров.КоличествоПлан = ТаблицаТоваровЗеркало.КоличествоПлан
	|				ИЛИ ТаблицаТоваров.Процент = ТаблицаТоваровЗеркало.Процент)
	|ГДЕ
	|	&ЭтоПрогрессивнаяШкала
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика";
	
	Запрос.Текст = ТекстЗапроса;
	
	ПараметрыОтбора = Новый Структура("Отменено", Ложь);
	КолонкиВыгрузки = "НомерСтроки, Номенклатура, Характеристика, КоличествоПлан, Процент, НачалоДействия, ОкончаниеДействия";
	ПодготовленныеТовары = Товары.Выгрузить(ПараметрыОтбора, КолонкиВыгрузки);
	Запрос.УстановитьПараметр("ТаблицаТоваров", ПодготовленныеТовары);
	
	Запрос.УстановитьПараметр("НачалоДействия", НачалоДействия);
	Запрос.УстановитьПараметр("ОкончаниеДействия", ОкончаниеДействия);
	
	ЭтоПрогрессивнаяШкала = 
		(РеквизитыВида.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество);
	Запрос.УстановитьПараметр("ЭтоПрогрессивнаяШкала", ЭтоПрогрессивнаяШкала);
	
	ВесьПериод = (ЭтоПрогрессивнаяШкала
				  И ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ВесьПериод);
	Запрос.УстановитьПараметр("ВесьПериод", ВесьПериод);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет(); // РезультатЗапроса
	ВсегоРезультатов = РезультатЗапроса.ВГраница();
	
	ВыборкаПересеченийПериодов = РезультатЗапроса[ВсегоРезультатов - 1].Выбрать();
	ВыборкаДублейПланаПроцента = РезультатЗапроса[ВсегоРезультатов].Выбрать();
	
	Результат = Новый Структура;
	Результат.Вставить("ПересеченияПериодов", ВыборкаПересеченийПериодов);
	Результат.Вставить("ДублиПланаПроцента", ВыборкаДублейПланаПроцента);
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуТоваровСегментыНоменклатуры(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары");
	
	Если ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.КромеВыбранных Тогда
		
		ПредставлениеТаблицы = НСтр("ru = 'Кроме сегментов товаров'");
		
	Иначе
		
		ПредставлениеТаблицы = НСтр("ru = 'Выбранные сегменты товаров'");
		
	КонецЕсли;
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, "СегментыТоваров", "Сегмент", Отказ, ПредставлениеТаблицы,, ОтборСтрок);
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, "СегментыТоваров", ПредставлениеТаблицы, Отказ);
	
КонецПроцедуры

Процедура ИнициализироватьДокумент()
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  Отказ - Булево -
//  ВыполнятьСБлокировкой - Булево -
//
Процедура ПроверитьОграниченияПоНачислениям(РеквизитыВида, Отказ, ВыполнятьСБлокировкой = Ложь)
	
	ПропуститьПроверкуНаОграничениеПоНачислениямПриОтсутствииИзменений = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеСвойства,
		"ПропуститьПроверкуНаОграничениеПоНачислениямПриОтсутствииИзменений",
		Истина);
	
	Если ПропуститьПроверкуНаОграничениеПоНачислениямПриОтсутствииИзменений
	   И Проведен Тогда
		
		ИзмененияДокумента = ОбщегоНазначенияУТ.ИзмененияДокумента(ЭтотОбъект);
		Если ИзмененияДокумента.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = ДанныеДляПроверки();
	
	Если НЕ ЗначениеЗаполнено(Результат.ДокументПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПроверки = Результат.ДокументПроверки;
	ИзменениеСогласованногоДокумента = Результат.ИзменениеСогласованногоДокумента;
	
	Если ВыполнятьСБлокировкой Тогда
		
		Попытка
		
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РетроБонусыКлиентов");
			ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", ДокументПроверки);
			
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Блокировка.Заблокировать();
			
		Исключение
			
			ШаблонОшибки = НСтр("ru = 'Не удалось выполнить проверку на наличие уже введенных начислений по документу условий по причине:
								|%1'");
			ОписаниеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибки, 
				ОписаниеОшибки);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецПопытки;
		
	КонецЕсли;
	
	МаксимальнаяДата = РетроБонусыСервер.ГраницаНачисленийКлиентов(ДокументПроверки);
	
	Если МаксимальнаяДата <> Дата(1, 1, 1) Тогда
		
		Если ИзменениеСогласованногоДокумента Тогда
			
			ТекстСообщения = НСтр("ru = 'Зарегистрировано начисление ретро-бонусов. Изменение документа запрещено.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,, "Объект", Отказ);
			
		ИначеЕсли ПериодичностьНачислений = Перечисления.ПериодичностиРетроБонусов.Однократно Тогда
			
			ТекстСообщения = НСтр("ru = 'Зарегистрировано начисление ретро-бонусов, ввод исправлений невозможен'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
		Иначе
			
			ЦелевыеПоказателиПоТоварам = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
			ЦелевыеПоказателиБезПериода = Новый Массив;  // Массив из ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
			ЦелевыеПоказателиБезПериода.Добавить(ЦелевыеПоказателиПоТоварам.ПакетноеПредложение);
			ЦелевыеПоказателиБезПериода.Добавить(ЦелевыеПоказателиПоТоварам.КоличествоСовокупно);
			
			МаксимальнаяДатаСтрокой = Формат(МаксимальнаяДата, "ДЛФ=D;");
			ВыполнятьПроверкуПериодовВТаблицах = Истина;
			
			Если ОкончаниеДействия < МаксимальнаяДата Тогда
				
				Шаблон = НСтр("ru = 'Дата окончания действия не может быть меньше %1 - даты окончания периода зарегистрированного начисления'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Шаблон, МаксимальнаяДатаСтрокой);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ОкончаниеДействия", "Объект", Отказ);
				ВыполнятьПроверкуПериодовВТаблицах = Ложь;
				
			КонецЕсли;
			
			ИмяПредставлениеТаблицы = РетроБонусыСервер.ИмяПредставлениеТаблицыПоРеквизитамУсловийКлиентов(
				РеквизитыВида.СоставУчастников, ОтборУчастников);
			
			Если ИмяПредставлениеТаблицы.ИмяТаблицы <> "" Тогда
				
				ИмяТаблицы = ИмяПредставлениеТаблицы.ИмяТаблицы;
				ПредставлениеТаблицы = ИмяПредставлениеТаблицы.ПредставлениеТаблицы;
				ПроверитьИзмененияУсловийВТаблице(
					МаксимальнаяДата,
					Истина,
					ОтборУчастников,
					ИмяТаблицы,
					ПредставлениеТаблицы,
					ВыполнятьПроверкуПериодовВТаблицах,
					Отказ);
					
			КонецЕсли;
			
			ИмяПредставлениеТаблицы = РетроБонусыСервер.ИмяПредставлениеТаблицыПоРеквизитамУсловийКлиентов(
				РеквизитыВида.СоставТоваров, ОтборТоваров);
			
			Если ИмяПредставлениеТаблицы.ИмяТаблицы <> "" Тогда
				
				ИмяТаблицы = ИмяПредставлениеТаблицы.ИмяТаблицы;
				ПредставлениеТаблицы = ИмяПредставлениеТаблицы.ПредставлениеТаблицы;
				
				Если ЦелевыеПоказателиБезПериода.Найти(РеквизитыВида.ПоказательТоваров) = Неопределено Тогда
					
					ПроверитьИзмененияУсловийВТаблице(
						МаксимальнаяДата,
						Ложь,
						ОтборТоваров,
						ИмяТаблицы,
						ПредставлениеТаблицы,
						ВыполнятьПроверкуПериодовВТаблицах,
						Отказ); 
						
				Иначе
					
					ПроверитьИзмененияУсловийВТаблицеБезПериодовДействия(
						ОтборТоваров,
						ПредставлениеТаблицы,
						Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает документ требующий проверки и признак изменения статуса.
// 
// Возвращаемое значение:
//  Структура - Данные для проверки:
// * ДокументПроверки - ДокументСсылка.УсловияРетроБонусовКлиентов - 
// * ИзменениеСогласованногоДокумента - Булево
// 
Функция ДанныеДляПроверки()
	
	Результат = Новый Структура;
	Результат.Вставить("ДокументПроверки", Документы.УсловияРетроБонусовКлиентов.ПустаяСсылка());
	Результат.Вставить("ИзменениеСогласованногоДокумента", Ложь);
	
	Если НЕ ИсправляемыйДокумент.Пустая() Тогда
		
		Результат.ДокументПроверки = ИсправляемыйДокумент;
		
	Иначе
		
		Если НЕ Ссылка.Пустая() Тогда
			
			СтатусСсылки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Статус");
			
			Если СтатусСсылки = Перечисления.СтатусыДокументовРетроБонусов.Согласован Тогда
				
				Результат.ДокументПроверки = Ссылка;
				Результат.ИзменениеСогласованногоДокумента = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверка наличия изменений, в таблицах без периода действия запрещенных к изменению
// 
// Параметры:
//  Отбор - ПеречислениеСсылка.СоставыСписковРетроБонусов
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  Отказ - Булево
//
Процедура ПроверитьИзмененияУсловийВТаблицеБезПериодовДействия(Отбор, ПредставлениеТаблицы, Отказ)
	
	Если Отбор = Перечисления.СоставыСписковРетроБонусов.Все Тогда
		Возврат;
	КонецЕсли;
	
	Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" запрещены изменения, так как в период действия условия были введены начисления'");
	
	Для Каждого ТекущаяСтрокаТаблицы Из Товары Цикл
		
		БылиКорректировки = Ложь;
			
		Если НЕ ТекущаяСтрокаТаблицы.ИсходнаяСтрока
		 ИЛИ ТекущаяСтрокаТаблицы.Отменено Тогда
			
			БылиКорректировки = Истина;
			
		КонецЕсли;
		
		Если БылиКорректировки Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Шаблон,
				Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
				ПредставлениеТаблицы);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"Товары", ТекущаяСтрокаТаблицы.НомерСтроки, "НомерСтроки");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Проверка корректности внесенных изменения по отношению к периоду начислений
// 
// Параметры:
//  МаксимальнаяДата - Дата
//  ПроверятьСуммуПлана - Булево
//  Отбор - ПеречислениеСсылка.СоставыСписковРетроБонусов
//  ИмяТаблицы - Строка - Имя проверяемой табличной части
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  ВыполнятьПроверкуПериодовВТаблицах - Булево
//  Отказ - Булево
//
Процедура ПроверитьИзмененияУсловийВТаблице(МаксимальнаяДата, ПроверятьСуммуПлана, Отбор, ИмяТаблицы, ПредставлениеТаблицы, ВыполнятьПроверкуПериодовВТаблицах, Отказ)
	
	Если Отбор = Перечисления.СоставыСписковРетроБонусов.Все
	 ИЛИ ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка()
	 ИЛИ (НЕ ВыполнятьПроверкуПериодовВТаблицах
	      И Отбор = Перечисления.СоставыСписковРетроБонусов.Выбранные) Тогда
		Возврат;
	КонецЕсли;
	
	ПустаяДата = Дата(1, 1, 1);
	
	ТабличнаяЧастьОбъекта = ЭтотОбъект[ИмяТаблицы]; // ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары 
	Для Каждого ТекущаяСтрокаТаблицы Из ТабличнаяЧастьОбъекта Цикл
		
		Если ТекущаяСтрокаТаблицы.НачалоДействия = ПустаяДата Тогда
			ТекущаяДатаНачала = НачалоДействия
		Иначе
			ТекущаяДатаНачала = ТекущаяСтрокаТаблицы.НачалоДействия;
		КонецЕсли;
		
		Если ТекущаяСтрокаТаблицы.ОкончаниеДействия = ПустаяДата Тогда
			ТекущаяДатаОкончания = ОкончаниеДействия;
		Иначе
			ТекущаяДатаОкончания = ТекущаяСтрокаТаблицы.ОкончаниеДействия;
		КонецЕсли;
		
		Если Отбор = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
			
			РетроБонусыСервер.ПроверитьДопустимостьНачалаПериодаСтроки(
				МаксимальнаяДата,
				ИмяТаблицы,
				ПредставлениеТаблицы,
				ТекущаяДатаНачала,
				ТекущаяСтрокаТаблицы,
				Отказ);
			
			РетроБонусыСервер.ПроверитьИзменениеНачалоПериодаИсходнойСтроки(
				МаксимальнаяДата,
				ИмяТаблицы,
				ПредставлениеТаблицы,
				ТекущаяДатаНачала,
				ТекущаяСтрокаТаблицы,
				Отказ);
			
			РетроБонусыСервер.ПроверитьДопустимостьНачалаПериодаИсходнойСтроки(
				МаксимальнаяДата,
				ИмяТаблицы,
				ПредставлениеТаблицы,
				ТекущаяДатаНачала,
				ТекущаяСтрокаТаблицы,
				Отказ);
			
			РетроБонусыСервер.ПроверитьДопустимостьОкончанияПериодаСтроки(
				МаксимальнаяДата,
				ИмяТаблицы,
				ПредставлениеТаблицы,
				ТекущаяДатаОкончания,
				ТекущаяСтрокаТаблицы,
				Отказ);
				
			РетроБонусыСервер.ПроверитьИзменениеОкончанияПериодаИсходнойСтроки(
				МаксимальнаяДата,
				ИмяТаблицы,
				ПредставлениеТаблицы,
				ТекущаяДатаОкончания,
				ТекущаяСтрокаТаблицы,
				Отказ);
				
			РетроБонусыСервер.ПроверитьДопустимостьИзменениеОкончанияПериодаИсходнойСтроки(
				МаксимальнаяДата,
				ИмяТаблицы,
				ПредставлениеТаблицы,
				ТекущаяДатаОкончания,
				ТекущаяСтрокаТаблицы,
				Отказ);
			
			Если ТекущаяСтрокаТаблицы.Отменено
			   И ТекущаяСтрокаТаблицы.НачалоДействияДоИзменений <= МаксимальнаяДата Тогда
				
				Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" отмена невозможна. В период действия условия были зарегистрированы начисления'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Шаблон,
					Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
					ПредставлениеТаблицы);
				
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "НомерСтроки");
					
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
				
			КонецЕсли;
			
			Если ПроверятьСуммуПлана Тогда
				
				Если ТекущаяСтрокаТаблицы.СуммаПлан = 0 Тогда
					СуммаПланТекущая = СуммаПлан;
				Иначе
					СуммаПланТекущая = ТекущаяСтрокаТаблицы.СуммаПлан;
				КонецЕсли;
				
				Если ТекущаяСтрокаТаблицы.ИсходнаяСтрока
				   И СуммаПланТекущая <> ТекущаяСтрокаТаблицы.СуммаПланДоИзменений
				   И ТекущаяСтрокаТаблицы.НачалоДействияДоИзменений <= МаксимальнаяДата Тогда
					
					Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" сумму плана менять запрещено. В период действия условия были зарегистрированы начисления'");
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						Шаблон,
						Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
						ПредставлениеТаблицы);
					
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "СуммаПлан");
						
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			БылиКорректировки = Ложь;
			
			Если НЕ ТекущаяСтрокаТаблицы.ИсходнаяСтрока
			 ИЛИ ТекущаяСтрокаТаблицы.Отменено
			 ИЛИ ТекущаяСтрокаТаблицы.ОкончаниеДействия <> ПустаяДата
			 ИЛИ ТекущаяСтрокаТаблицы.НачалоДействия <> ПустаяДата Тогда
				
				БылиКорректировки = Истина;
				
			КонецЕсли;
			
			Если ПроверятьСуммуПлана
			   И ТекущаяСтрокаТаблицы.СуммаПлан <> 0
			   И ТекущаяСтрокаТаблицы.СуммаПлан <> ТекущаяСтрокаТаблицы.СуммаПланДоИзменений Тогда
				
				БылиКорректировки = Истина;
				
			КонецЕсли;
			
			Если БылиКорректировки Тогда
				
				Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" запрещены изменения, так как в период действия условия были введены начисления'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Шаблон,
					Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
					ПредставлениеТаблицы);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "НомерСтроки");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли