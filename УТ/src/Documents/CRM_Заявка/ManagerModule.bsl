#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	 
#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьКонтактЗаявки(Объект) Экспорт

	Если Не ЗначениеЗаполнено(Объект.Контакт) Тогда
		
		Если ЗначениеЗаполнено(Объект.Телефон) Или ЗначениеЗаполнено(Объект.ЭлектроннаяПочта) Тогда
			
			КэшПоиска = CRM_ПоискКонтактов.СтруктураДанныхКэша();
			КэшЗаполнения = CRM_ПоискКонтактов.СтруктураДанныхКэша();
			УсловияПоиска = Новый Массив;
			Если ЗначениеЗаполнено(Объект.Телефон) Тогда
				СтрокаПоискаНормализованная = CRM_ПоискКонтактов.НормализоватьСтрокуПоискаИДополнитьКэш(Объект.Телефон,
					 КэшПоиска,
					 КэшЗаполнения);
				УсловияПоиска.Добавить(СтрокаПоискаНормализованная);
			КонецЕсли;
			Если ЗначениеЗаполнено(Объект.ЭлектроннаяПочта) Тогда
				СтрокаПоискаНормализованная = CRM_ПоискКонтактов.НормализоватьСтрокуПоискаИДополнитьКэш(Объект.ЭлектроннаяПочта,
					 КэшПоиска,
					 КэшЗаполнения);
				УсловияПоиска.Добавить(СтрокаПоискаНормализованная);
			КонецЕсли;
			РезультатПоиска = CRM_ПоискКонтактов.ВыполнитьПоискКонтактов(УсловияПоиска, 2, Истина);
			Если НЕ ЗначениеЗаполнено(РезультатПоиска.КодОшибки) Тогда
				МассивСсылок = РезультатПоиска.Элементы;
				Если МассивСсылок.Количество() > 0 Тогда
					Объект.Контакт = МассивСсылок[0];
				КонецЕсли;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Объект.Контакт) Тогда
				CRM_КлиентыСервер.СоздатьКонтактВзаимодействия(Объект);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
