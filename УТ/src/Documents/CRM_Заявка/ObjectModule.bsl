#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	CRM_СкрытьВАРМ = Ложь;
	
	Пользователь	= Пользователи.АвторизованныйПользователь();	
	Автор 			= Пользователь;
		
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее") Тогда
		ДокументОснование = ДанныеЗаполнения;
		ЭлектроннаяПочта = ДанныеЗаполнения.ОтправительАдрес;
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ЭтоНовый() Или ДополнительныеСвойства.Свойство("ЗаписьИзФормы") Тогда
		Документы.CRM_Заявка.ЗаполнитьКонтактЗаявки(ЭтотОбъект);
	КонецЕсли;
		
	Если ЭтоНовый() Тогда
		Если Не ЗначениеЗаполнено(Автор) Тогда
			Автор = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
		Если ИсточникПолучения.Сценарий = Перечисления.CRM_CallTrakingСценарии.РучнойВвод Тогда
			Справочники.CRM_ПравилаОбработкиОбращений.ПрименитьПравило(ЭтотОбъект);
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ЭтоНовый", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЭтоНовый") И ДополнительныеСвойства.ЭтоНовый Тогда
		Если ИсточникПолучения.Сценарий = Перечисления.CRM_CallTrakingСценарии.РучнойВвод Тогда
			Справочники.CRM_ПравилаОбработкиОбращений.ОбработатьОбращение(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли