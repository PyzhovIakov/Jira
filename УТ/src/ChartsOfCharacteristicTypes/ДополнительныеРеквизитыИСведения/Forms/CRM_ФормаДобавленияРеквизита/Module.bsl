
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	CRM_ОбщегоНазначенияСервер.СбросНастроекПоложенияОкна(ЭтотОбъект);
	
	Если Объект.Ссылка.Пустая() Тогда
		Заголовок = НСтр("ru='Новое поле';en='New Field'");
		Элементы.УстановитьПометкуУдаления.Видимость = Ложь;
	Иначе
		Элементы.ТипЗначения.ТолькоПросмотр = Истина;
		Элементы.НаборСвойств.ТолькоПросмотр = Истина;
		Элементы.ГруппаЧисло.ТолькоПросмотр = Истина;
		Элементы.СтрокаДлина.ТолькоПросмотр = Истина;
		Элементы.СтрокаДлина.ТолькоПросмотр = Истина;
		Элементы.ГруппаДата.ТолькоПросмотр = Истина;
		Заголовок = НСтр("ru='Поле';en='Field'");
		ТипПоля = Строка(Объект.ТипЗначения);
		Если Объект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ЗначенияСвойствОбъектов") Тогда
			ТипПоля = "ДополнительноеЗначение";
			ЗаполнитьСписокДопЗначений();
		ИначеЕсли Объект.ТипЗначения.СодержитТип(Тип("Строка")) Тогда
			СтрокаДлина = Объект.ТипЗначения.КвалификаторыСтроки.Длина;
		ИначеЕсли Объект.ТипЗначения.СодержитТип(Тип("Число")) Тогда
			ЧислоДлина = Объект.ТипЗначения.КвалификаторыЧисла.Разрядность;
			ЧислоТочность = Объект.ТипЗначения.КвалификаторыЧисла.РазрядностьДробнойЧасти;
		КонецЕсли;
	КонецЕсли;
	
	СписокНаборовДопРеквизитов = Неопределено;
	Если Параметры.Свойство("СписокНаборовДопРеквизитов", СписокНаборовДопРеквизитов) Тогда
		Если СписокНаборовДопРеквизитов.Количество() > 0 Тогда
			Если НЕ ЗначениеЗаполнено(Объект.НаборСвойств) Тогда
				Объект.НаборСвойств = СписокНаборовДопРеквизитов[0].Значение;
			КонецЕсли;	
		КонецЕсли;
		Для каждого Набор Из СписокНаборовДопРеквизитов Цикл
			Элементы.НаборСвойств.СписокВыбора.Добавить(Набор.Значение, Набор.Представление);
		КонецЦикла;
		Если СписокНаборовДопРеквизитов.Количество() > 1 Тогда
			Элементы.НаборСвойств.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаДлина = 0 Тогда
		СтрокаДлина = 1024;
	КонецЕсли;
	Если ЧислоДлина = 0 Тогда
		ЧислоДлина = 15;
		ЧислоТочность = 3;
	КонецЕсли;
	Если ДатаСостав = "" Тогда
		ДатаСостав = "ДатаВремя";
	КонецЕсли;
	
	CRM_ОсновнойГолубой = ЦветаСтиля.CRM_ОсновнойГолубой;
	CRM_СерыйДляВторостепенныхФункций = ЦветаСтиля.CRM_СерыйДляВторостепенныхФункций;
		
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Мультиязычность") Тогда
		МодульМультиязычностьСервер = ОбщегоНазначения.ОбщийМодуль("МультиязычностьСервер");
		МодульМультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	КонецЕсли;
	
	ОбновитьСоставЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РасцветкаМногострочного();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Не CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		// СтандартныеПодсистемы.УправлениеДоступом
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
			МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
			МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
		КонецЕсли;
		// Конец СтандартныеПодсистемы.УправлениеДоступом
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Мультиязычность") Тогда
		МодульМультиязычностьСервер = ОбщегоНазначения.ОбщийМодуль("МультиязычностьСервер");
		МодульМультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	
	Элементы.УстановитьПометкуУдаления.Заголовок = ?(Объект.ПометкаУдаления,
		 НСтр("ru='Возобновить';en='Resume'"),
		 НСтр("ru='Удалить'"));
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Мультиязычность") Тогда
		МодульМультиязычностьСервер = ОбщегоНазначения.ОбщийМодуль("МультиязычностьСервер");
		МодульМультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		// СтандартныеПодсистемы.УправлениеДоступом
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
			МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
			МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
		КонецЕсли;
		// Конец СтандартныеПодсистемы.УправлениеДоступом
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Мультиязычность") Тогда
		МодульМультиязычностьСервер = ОбщегоНазначения.ОбщийМодуль("МультиязычностьСервер");
		МодульМультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗавершениеРаботы Тогда
			
		Отказ = Истина;
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеОЗакрытии, НСтр("ru = 'Данные были изменены. Сохранить изменения?'"),
			 РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Готово(Неопределено);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		Модифицированность = Ложь;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Мультиязычность") Тогда
		МодульМультиязычностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("МультиязычностьКлиент");
		МодульМультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Однострочное(Команда)
	Объект.МногострочноеПолеВвода = 0;
	РасцветкаМногострочного();
КонецПроцедуры

&НаКлиенте
Процедура Многострочное(Команда)
	Объект.МногострочноеПолеВвода = 3;
	РасцветкаМногострочного();
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Объект.Наименование = Объект.Заголовок + " (" + Строка(Объект.НаборСвойств) + ")";
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Виден = Истина;
		Объект.Доступен = Истина;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Имя) Тогда
		ЗаголовокОбъекта = Объект.Заголовок;
		ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ЗаголовокОбъекта, "");
		ЗаголовокОбъектаЧастями = СтрРазделить(ЗаголовокОбъекта, " ", Ложь);
		Для Каждого ЧастьЗаголовка Из ЗаголовокОбъектаЧастями Цикл
			Объект.Имя = Объект.Имя + ВРег(Лев(ЧастьЗаголовка, 1)) + Сред(ЧастьЗаголовка, 2);
		КонецЦикла;
		УИД = Новый УникальныйИдентификатор();
		СтрокаУИД = СтрЗаменить(Строка(УИД), "-", "");
		Объект.Имя = Объект.Имя + "_" + СтрокаУИД;
	КонецЕсли;
	
	Если Записать() Тогда
		
		ЗаписатьНаборДопРеквизитов();
		
		Если Объект.ДополнительныеЗначенияИспользуются Тогда
			ЗаписатьЗначенияСвойства();
		КонецЕсли;
		Оповестить("Запись_ДополнительныеРеквизитыИСведения",
			Новый Структура("Ссылка", Объект.Ссылка), Объект.Ссылка);
		
		Закрыть(Новый Структура("Ссылка, Заголовок, НаборСвойств, ПометкаУдаления", Объект.Ссылка,
			 Объект.Заголовок, Объект.НаборСвойств,
			 Объект.ПометкаУдаления));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ТипЗначенияПриИзменении(Элемент)
	Если ТипПоля = "Строка" Тогда
		Объект.ТипЗначения = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(СтрокаДлина));
	ИначеЕсли ТипПоля = "Число" Тогда
		Объект.ТипЗначения = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(ЧислоДлина, ЧислоТочность));
	ИначеЕсли ТипПоля = "Дата" Тогда
		Объект.ТипЗначения = Новый ОписаниеТипов("Дата", Новый КвалификаторыДаты(ЧастиДаты[ДатаСостав]));
	ИначеЕсли ТипПоля = "Булево" Тогда
		Объект.ТипЗначения = Новый ОписаниеТипов("Булево");
	ИначеЕсли ТипПоля = "ДополнительноеЗначение" Тогда
		Объект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ЗначенияСвойствОбъектов");
	КонецЕсли;
	Объект.ДополнительныеЗначенияИспользуются =
		(Объект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ЗначенияСвойствОбъектов"));
	ОбновитьСоставЭлементовФормы();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РасцветкаМногострочного()
	Если Объект.МногострочноеПолеВвода = 0 Тогда
		ГолубаяКнопка = Элементы.Однострочное;
		СераяКнопка = Элементы.Многострочное;
	Иначе
		ГолубаяКнопка = Элементы.Многострочное;
		СераяКнопка = Элементы.Однострочное;
	КонецЕсли;
	ГолубаяКнопка.Пометка = Истина;
	ГолубаяКнопка.ЦветФона = CRM_ОсновнойГолубой;
	ГолубаяКнопка.ЦветРамки = CRM_ОсновнойГолубой;
	ГолубаяКнопка.Шрифт = Новый Шрифт(, , Истина);
	СераяКнопка.ЦветФона = CRM_СерыйДляВторостепенныхФункций;
	СераяКнопка.Пометка = Ложь;
	СераяКнопка.ЦветРамки = CRM_СерыйДляВторостепенныхФункций;
	ГолубаяКнопка.Шрифт = Новый Шрифт();
КонецПроцедуры

&НаСервере
Процедура ОбновитьСоставЭлементовФормы()
	
	Элементы.ГруппаСтрока.Видимость = Ложь;
	Элементы.ГруппаЧисло.Видимость = Ложь;
	Элементы.ГруппаДата.Видимость = Ложь;
	Элементы.ГруппаДополнительныеЗначения.Видимость = Ложь;
	Если ТипПоля = "Строка" Тогда
		Элементы.ГруппаСтрока.Видимость = Истина;
		Высота = 15;
	ИначеЕсли ТипПоля = "Число" Тогда
		Элементы.ГруппаЧисло.Видимость = Истина;
		Высота = 13;
	ИначеЕсли ТипПоля = "Дата" Тогда
		Элементы.ГруппаДата.Видимость = Истина;
		Высота = 13;
	ИначеЕсли ТипПоля = "ДополнительноеЗначение" Тогда
		Элементы.ГруппаДополнительныеЗначения.Видимость = Истина;
		Высота = 21;
	Иначе
		Высота = 9;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДопЗначений()
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЗначенияСвойствОбъектов.Ссылка КАК Ссылка,
	                      |	ЗначенияСвойствОбъектов.Наименование КАК Наименование
	                      |ИЗ
	                      |	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	                      |ГДЕ
	                      |	ЗначенияСвойствОбъектов.Владелец = &Владелец
	                      |	И НЕ ЗначенияСвойствОбъектов.ЭтоГруппа");
	Запрос.УстановитьПараметр("Владелец", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДополнительныеЗначения.Добавить(Выборка.Ссылка, Выборка.Наименование);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗначенияСвойства()
	Для каждого ДопЗначение Из ДополнительныеЗначения Цикл
		Если ЗначениеЗаполнено(ДопЗначение.Значение) Тогда
			Если ДопЗначение.Представление <> ДопЗначение.Значение.Наименование Тогда
				ДопЗначениеОбъект = ДопЗначение.Значение.ПолучитьОбъект();
				ДопЗначениеОбъект.Наименование = ДопЗначение.Представление;
				ДопЗначениеОбъект.Записать();
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(ДопЗначение.Представление) Тогда
			ДопЗначениеОбъект = Справочники.ЗначенияСвойствОбъектов.СоздатьЭлемент();
			ДопЗначениеОбъект.Владелец = Объект.Ссылка;
			ДопЗначениеОбъект.Наименование = ДопЗначение.Представление;
			ДопЗначениеОбъект.Записать();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаборДопРеквизитов()
	Если ЗначениеЗаполнено(Объект.НаборСвойств) Тогда
		Если Объект.НаборСвойств.ДополнительныеРеквизиты.Найти(Объект.Ссылка, "Свойство") = Неопределено Тогда
			ОбъектНаборСвойств = Объект.НаборСвойств.ПолучитьОбъект();
			НоваяСтрока = ОбъектНаборСвойств.ДополнительныеРеквизиты.Добавить();
			НоваяСтрока.Свойство = Объект.Ссылка;
			ОбъектНаборСвойств.Записать();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
