//@strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения, Неопределено - Форма отчета или форма настроек отчета.
//       Неопределено когда вызов без контекста.
//   КлючВарианта - Строка, Неопределено - Имя предопределенного
//       или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов без контекста.
//   Настройки - См. ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт 
	
	Если КлючВарианта = "ДействующиеРетроБонусыПоставщиковКонтекст" Тогда
		
		Настройки.ФормироватьСразу = Истина;
		Настройки.РазрешеноИзменятьВарианты = Ложь;
		
	КонецЕсли;
	
	Настройки.События.ПриСозданииНаСервере = Истина; 
	
КонецПроцедуры

// См. ОтчетыПереопределяемый.ПриСозданииНаСервере
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = Форма.Параметры;
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		ПараметрКоманды = Параметры.ПараметрКоманды; // СправочникСсылка.Контрагенты, СправочникСсылка.Партнеры, СправочникСсылка.ДоговорыКонтрагентов, СправочникСсылка.СоглашенияСПоставщиками
		ТипПараметраКоманды = ТипЗнч(ПараметрКоманды);
		
		Форма.НастройкиОтчета.РазрешеноВыбиратьВарианты = Ложь;
		
		Отбор = Форма.ФормаПараметры.Отбор;  // Структура
		
		Если ТипПараметраКоманды = Тип("СправочникСсылка.Контрагенты") Тогда
			
			Отбор.Вставить("Контрагент", ПараметрКоманды);
			
		ИначеЕсли ТипПараметраКоманды = Тип("СправочникСсылка.Партнеры") Тогда
			
			Отбор.Вставить("Партнер", ПараметрКоманды);
			
		ИначеЕсли ТипПараметраКоманды = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			
			Отбор.Вставить("Договор", ПараметрКоманды);
			
		ИначеЕсли ТипПараметраКоманды = Тип("СправочникСсылка.СоглашенияСПоставщиками") Тогда
			
			Отбор.Вставить("Соглашение", ПараметрКоманды);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
