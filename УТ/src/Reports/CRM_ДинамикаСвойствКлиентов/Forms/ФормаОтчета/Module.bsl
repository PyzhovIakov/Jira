
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	МодульУправлениеДоступом = CRM_ОбщегоНазначенияСервер.ОбщийМодуль("CRM_УправлениеДоступом");
	Если МодульУправлениеДоступом <> Неопределено Тогда
		МодульУправлениеДоступом.ОграничитьВыводКлиентскойБазы(ЭтотОбъект, "Результат");
	КонецЕсли;
	
	СписокВыбораРеквизитовКлиента.Очистить();
	СписокВерсионируемыхРеквизитов =
		РегистрыСведений.CRM_НастройкаВерсионированияРеквизитовПартнеров.ПолучитьСписокВерсионируемыхРеквизитов();
	Для Каждого ЗначениеСписка Из СписокВерсионируемыхРеквизитов Цикл
		СписокВыбораРеквизитовКлиента.Добавить(ЗначениеСписка.Значение, ЗначениеСписка.Представление);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСкомпоноватьРезультат(Команда)
	СкомпоноватьРезультат(РежимКомпоновкиРезультата.Непосредственно);
	
	ПослеКомпоновкиРезультата();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеКомпоновкиРезультата()
	Для Каждого Рисунок Из Результат.Рисунки Цикл
		Попытка
			РисунокОбъект = Рисунок.Объект;
		
		Исключение	Продолжить;
		КонецПопытки;
		
		Если ТипЗнч(РисунокОбъект) = Тип("Диаграмма") Тогда
			Рисунок.Ширина = 250;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекПользовательскиеНастройкиЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТекДанные = Элементы.КомпоновщикНастроекПользовательскиеНастройки.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		Попытка
			бЭтоПолеРеквизита = (ТекДанные.Настройка = "Реквизит");
		
		Исключение	бЭтоПолеРеквизита = Ложь;
		КонецПопытки;
		
		Если бЭтоПолеРеквизита И Элемент.Имя = "КомпоновщикНастроекПользовательскиеНастройкиЗначение" Тогда
			ДанныеВыбора = СписокВыбораРеквизитовКлиента.Скопировать();
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
