///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВыведенныеПоля = Новый Соответствие;
	ДоступныеПоля  = Параметры.ДоступныеПоля;
	
	Если ДоступныеПоля = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаПолей = Параметры.НастройкаПолей;
	Если НастройкаПолей <> Неопределено Тогда
		Для Каждого Поле Из НастройкаПолей Цикл
			
			ОписаниеПоля = ДоступныеПоля[Поле.Идентификатор];
			Если Не ПоискКонтрагентовСлужебныйКлиентСервер.ЭтоСтруктура(ОписаниеПоля) Тогда
				Продолжить;
			КонецЕсли;
			
			ПредставлениеДоступногоПоля = ПоискКонтрагентовСлужебный.ПредставлениеДоступногоПоля(
				ОписаниеПоля);
			
			Поля.Добавить(
				Поле.Идентификатор,
				ПредставлениеДоступногоПоля,
				Поле.Пометка);
			
			ВыведенныеПоля.Вставить(Поле.Идентификатор, Истина);
			
		КонецЦикла;
	КонецЕсли;
	
	ДополнитьПоля = Новый СписокЗначений;
	Для Каждого КлючЗначение Из ДоступныеПоля Цикл
		Если ВыведенныеПоля[КлючЗначение.Ключ] = Неопределено Тогда
			
			ПредставлениеДоступногоПоля = ПоискКонтрагентовСлужебный.ПредставлениеДоступногоПоля(
				КлючЗначение.Значение);
				
			ДополнитьПоля.Добавить(
				КлючЗначение.Ключ,
				ПредставлениеДоступногоПоля);
			
		КонецЕсли;
	КонецЦикла;
	
	Если ДополнитьПоля.Количество() > 0 Тогда
		ДополнитьПоля.СортироватьПоПредставлению();
		Для Каждого Поле Из ДополнитьПоля Цикл
			Поля.Добавить(Поле.Значение, Поле.Представление);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	
	ВыбранныеПоля = Новый Массив;
	
	Для Каждого Поле Из Поля Цикл
		
		// Структура с описание поля определяется в методе
		// ПоискКонтрагентовСлужебныйКлиентСервер.ОбработатьНастройкиПолейПослеЗагрузки
		ВыбранныеПоля.Добавить(
			Новый Структура(
				"Идентификатор, Пометка",
				Поле.Значение,
				Поле.Пометка));
		
	КонецЦикла;
	
	Закрыть(ВыбранныеПоля);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
