#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не Параметры.Свойство("ИмяМакетаОтчета") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

    ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
    СхемаКомпоновкиДанных = ОтчетОбъект.ПолучитьМакет(Параметры.ИмяМакетаОтчета);
    Отчет.АдресСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
    Отчет.КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Отчет.АдресСхемы));
	ВариантНастроек = Неопределено;
	Если Параметры.Свойство("ВариантНастроек") Тогда
		ВариантНастроек = СхемаКомпоновкиДанных.ВариантыНастроек.Найти(Параметры.ВариантНастроек);
	КонецЕсли;
	Если ВариантНастроек = Неопределено Тогда
		НастройкиОтчета = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	Иначе
		НастройкиОтчета = ВариантНастроек.Настройки;
	КонецЕсли;
	Если Параметры.Свойство("ЦветаСерий") Тогда
		Для каждого Элемент Из Параметры.ЦветаСерий Цикл
			ЭлементУО = НастройкиОтчета.УсловноеОформление.Элементы.Добавить();
			
			ЭлементОтбораДанных = ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Источник");
			ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбораДанных.ПравоеЗначение = Элемент.Ключ;
			ЭлементОтбораДанных.Использование = Истина;
			
			ЭлементЦветаОформления = ЭлементУО.Оформление.Элементы.Найти("ЦветВДиаграмме");
			ЭлементЦветаОформления.Значение =  Элемент.Значение;
			ЭлементЦветаОформления.Использование = Истина;
		КонецЦикла;
	КонецЕсли;
	Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиОтчета);
    Отчет.КомпоновщикНастроек.Восстановить();    

КонецПроцедуры

#КонецОбласти
