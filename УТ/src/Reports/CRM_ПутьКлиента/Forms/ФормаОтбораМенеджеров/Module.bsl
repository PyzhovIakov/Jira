
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ОтборМенеджеры") Тогда
		ОтборМенеджеры.ЗагрузитьЗначения(Параметры.ОтборМенеджеры);
	КонецЕсли;
	ОтборМенеджеры.ДоступныеЗначения = Новый СписокЗначений;
	ОтборМенеджеры.ДоступныеЗначения.ЗагрузитьЗначения(CRM_УправлениеЦелевымиПоказателямиСервер.ДоступныеПользователи(Ложь));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборМенеджерыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Для каждого Значение Из ВыбранноеЗначение Цикл
			Если ОтборМенеджеры.НайтиПоЗначению(Значение) = Неопределено Тогда
				ОтборМенеджеры.Добавить(Значение);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если ОтборМенеджеры.НайтиПоЗначению(ВыбранноеЗначение) = Неопределено Тогда
			ОтборМенеджеры.Добавить(ВыбранноеЗначение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	Результат = Новый Массив;
	Для каждого Элемент Из ОтборМенеджеры Цикл
		Если ЗначениеЗаполнено(Элемент.Значение) Тогда
			Результат.Добавить(Элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	Если Результат.Количество() > 0 Тогда	
		Закрыть(Результат);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Список пользователей не заполнен!'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("РежимВыбора", Истина);
	ПараметрыПодбора.Вставить("МножественныйВыбор", Истина);
	ПараметрыПодбора.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыПодбора.Вставить("ТолькоДоступные", Истина);
	ОткрытьФорму("Справочник.Пользователи.Форма.CRM_ФормаСписка", ПараметрыПодбора,
		 Элементы.ОтборМенеджеры, , , , ,
		 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	ОтборМенеджеры.Очистить();
КонецПроцедуры

#КонецОбласти
