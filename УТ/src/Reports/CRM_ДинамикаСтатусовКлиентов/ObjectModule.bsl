////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции модуля объекта "Отчеты.CRM_ДинамикаСтатусовКлиентов".
//
////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкаПериодОтчета = CRM_ОбщегоНазначенияКлиентСервер.ПолучитьПараметр(
		КомпоновщикНастроек, "ПериодОтчета"); //СтандартныйПериод
	НастройкаПериодичностьОтчета = CRM_ОбщегоНазначенияКлиентСервер.ПолучитьПараметр(
		КомпоновщикНастроек, "Периодичность");
	
	ТаблицаПериодов = Отчеты.CRM_ДинамикаСтатусовКлиентов.ПериодыСДатойНаКонецПериода(
		НастройкаПериодОтчета.Значение.ДатаНачала,
		НастройкаПериодОтчета.Значение.ДатаОкончания,
		НастройкаПериодичностьОтчета.Значение);
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаПериодов", ТаблицаПериодов);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры // ПриКомпоновкеРезультата()

#Область ДляВызоваИзДругихПодсистем

// +СтандартныеПодсистемы.ВариантыОтчетов

// Задать настройки формы отчета.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//         - Неопределено
//   КлючВарианта - Строка
//                - Неопределено
//   Настройки - см. ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Если КлючВарианта = "ДанныеВиджета" Или КлючВарианта = "ДанныеВиджетаРасшифровка" Тогда
		Настройки.ФормироватьСразу = Истина;
		Настройки.РазрешеноИзменятьВарианты = Ложь;
		Настройки.РазрешеноИзменятьСтруктуру = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ОпределитьНастройкиФормы()

// -СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

////////////////////////////////////////////////////////////////////////////////