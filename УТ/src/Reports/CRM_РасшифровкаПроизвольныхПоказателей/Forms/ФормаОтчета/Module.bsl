
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МодульУправлениеДоступом = CRM_ОбщегоНазначенияСервер.ОбщийМодуль("CRM_УправлениеДоступом");
	Если МодульУправлениеДоступом <> Неопределено Тогда
		МодульУправлениеДоступом.ОграничитьВыводКлиентскойБазы(ЭтотОбъект, "Результат");
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	СхемаКомпоновкиДанных = Неопределено;
	Если Параметры.Свойство("ИсточникСКД") Тогда
		СхемаКомпоновкиДанных = Параметры.ИсточникСКД.ХранилищеСхемыКомпоновкиДанныхРасшифровки.Получить();
		Заголовок = Строка(Параметры.ИсточникСКД);
	КонецЕсли;
	Если СхемаКомпоновкиДанных = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Для расшифровки показателя не задана схема компоновки данных!';
			|en='Decrypt the indicator,
			| no data composition scheme has been specified!'"));
		Возврат;
	КонецЕсли;
	
	// Загрузка настроек
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	ОтчетОбъект.СхемаКомпоновкиДанных = СхемаКомпоновкиДанных;
	ОтчетОбъект.КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	// Установка параметров данных
	
	Если Параметры.Свойство("ПараметрыДанных") Тогда
		Для каждого Параметр Из Параметры.ПараметрыДанных Цикл
			Если Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(Параметр.Ключ) <> Неопределено Тогда
				Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(Параметр.Ключ, Параметр.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") Тогда
		Для каждого Элемент Из Параметры.Отбор Цикл
			ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Элемент.Ключ);
			Если Отчет.КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.НайтиПоле(ЛевоеЗначение) <> Неопределено Тогда
				ЭлементОтбора = Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Элемент.Ключ);
				ЭлементОтбора.ПравоеЗначение = Элемент.Значение;
				Если ТипЗнч(Элемент.Значение) = Тип("Массив") Или ТипЗнч(Элемент.Значение) = Тип("СписокЗначений") Тогда
					ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
				Иначе
					ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				КонецЕсли;
			ИначеЕсли Элемент.Ключ = "Подразделение" 
				И Отчет.КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.НайтиПоле(Новый ПолеКомпоновкиДанных("Менеджер")) <> Неопределено Тогда
				ЭлементОтбора = Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Менеджер.Подразделение");
				ЭлементОтбора.ПравоеЗначение = Элемент.Значение;
				Если ТипЗнч(Элемент.Значение) = Тип("Массив") Или ТипЗнч(Элемент.Значение) = Тип("СписокЗначений") Тогда
					ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
				Иначе
					ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДанныеРасшифровки = Неопределено;
	ОтчетОбъект.СкомпоноватьРезультат(Результат, ДанныеРасшифровки);
	ОтчетДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПараметрыРасшифровки = ПолучитьПараметрыРасшифровкиОтчета(Расшифровка);
	
	Если Не ЗначениеЗаполнено(ПараметрыРасшифровки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыРасшифровки.ОткрытьОбъект Тогда
		ПоказатьЗначение( , ПараметрыРасшифровки.Значение);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПараметрыРасшифровкиОтчета(Расшифровка)
	
	ДанныеРасшифровки   = ПолучитьИзВременногоХранилища(ОтчетДанныеРасшифровки);
	ЗначениеРасшифровки = ДанныеРасшифровки.Элементы[Расшифровка].ПолучитьПоля()[0].Значение;
	Если ЗначениеРасшифровки = NULL Тогда
		ПараметрыРасшифровки = Неопределено;
	Иначе
		ПараметрыРасшифровки = Новый Структура;
		ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
		ПараметрыРасшифровки.Вставить("Значение",      ЗначениеРасшифровки);
	КонецЕсли;
	
	Возврат ПараметрыРасшифровки;
	
КонецФункции

#КонецОбласти
