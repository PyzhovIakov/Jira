#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьПриНачалеРаботы = CRM_СтраницаПриветствие.ЗначениеНастройкиПоказыватьПриНачалеРаботы();
	
	ВерсияСтраницы = НайтиВерсиюСтраницыПриветствия();
	ТекущаяВерсияСтраницы = Константы.CRM_ВерсияСтраницыПриветствия.Получить();
	
	Если ВерсияСтраницы <> 0 И ВерсияСтраницы > ТекущаяВерсияСтраницы Тогда
		
		Если ЗагрузитьНовыйМакет() Тогда
			Константы.CRM_ВерсияСтраницыПриветствия.Установить(ВерсияСтраницы);
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстМакетаИзКонстанты = ПолучитьТекстМакетаИзКонстанты();
	
	Если ТекстМакетаИзКонстанты = Неопределено Тогда
		
		НовыйМакет = Обработки.CRM_СтраницаПриветствия.ПолучитьМакет("СтраницаПриветствия").ПолучитьТекст();
		МакетВХранилище = CRM_СтраницаПриветствие.ЗаполнитьМетки(НовыйМакет);
		ХранилищеВКонстанту = Новый ХранилищеЗначения(МакетВХранилище);
		Константы.CRM_СтраницаПриветствия.Установить(ХранилищеВКонстанту);
		ТекстМакетаИзКонстанты = ПолучитьТекстМакетаИзКонстанты();
	КонецЕсли;
	
	HTMLстрока = ТекстМакетаИзКонстанты;
	
	ВерсияРелизаТекущая = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(Метаданные.Версия);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"CRM_СтраницаПриветствия",
		"ВерсияРелизаВХранилище",
		ВерсияРелизаТекущая, , ИмяПользователя());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьПриНачалеРаботыПриИзменении(Элемент)
	
	НастройкаПоказПриСтартеПриИзмененииНаСервере(ПоказыватьПриНачалеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура НастройкаПоказПриСтартеПриИзмененииНаСервере(Знач НастройкаПоказПриСтарте)
	
	Если НастройкаПоказПриСтарте = 0 Тогда
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"CRM_СтраницаПриветствия",
			"ВсегдаПоказыватьПриСтартеПрограммы",
			Истина , , ИмяПользователя());
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"CRM_СтраницаПриветствия",
			"ПоказПриСтартеТолькоПриИзменении",
			Ложь , , ИмяПользователя());
	ИначеЕсли НастройкаПоказПриСтарте = 1 Тогда
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"CRM_СтраницаПриветствия",
			"ВсегдаПоказыватьПриСтартеПрограммы",
			Истина , , ИмяПользователя());
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
			"CRM_СтраницаПриветствия",
			"ПоказПриСтартеТолькоПриИзменении",
			Истина , , ИмяПользователя());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НайтиВерсиюСтраницыПриветствия()
	
	Сервер = "files.1crm.ru";
	URL = "files.1crm.ru/api/v1/startPage/version/";
	СтруктураURI = сфпОбщегоНазначенияКлиентСервер.СтруктураURI(URL);
	
	HTTPЗапрос = Новый HTTPЗапрос();
	HTTPЗапрос.АдресРесурса = "/api/v1/startPage/version/";
	
	Ответ = Неопределено;
	
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(
		Сервер,
		СтруктураURI.Порт, , , Прокси, 20,
		Новый ЗащищенноеСоединениеOpenSSL);
	Попытка
		Ответ = HTTPСоединение.ВызватьHTTPМетод("GET", HTTPЗапрос);
	Исключение
		Возврат 0;
	КонецПопытки;
	
	Если Ответ.КодСостояния = 200 Тогда
		ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
		СтруктураОтвета = CRM_РаботаСМессенджерамиСервер.ПолучитьЗначениеИзОтветаJSON(ТекстОтвета);
		СтруктураДанных = СтруктураОтвета.data;
		Версия = СтруктураДанных.id;
	Иначе
		Версия = 0;
	КонецЕсли;
		
	Возврат Версия;
	
КонецФункции

&НаСервере
Функция ЗагрузитьНовыйМакет()
	
	Результат = Истина;
	
	АдресФайла = "https://files.1crm.ru/api/v1/startPage/download/";
	МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
	РезультатПолученияФайла = МодульПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(АдресФайла);
	
	Если Не РезультатПолученияФайла.Статус Тогда
		Возврат Ложь;
	КонецЕсли;
	
	КаталогДляРаспаковки = ПолучитьИмяВременногоФайла();
	
	Попытка
		Архив = Новый ЧтениеZipФайла(РезультатПолученияФайла.Путь);
		Архив.ИзвлечьВсе(КаталогДляРаспаковки);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	ФайлОписания = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогДляРаспаковки) + "index.html";
	
	ЧтениеТекст = Новый ЧтениеТекста(ФайлОписания, КодировкаТекста.UTF8);
	СтрокаНовая = ЧтениеТекст.Прочитать();
	ЧтениеТекст.Закрыть();
	
	ФайловаяСистема.УдалитьВременныйФайл(КаталогДляРаспаковки);
	ФайловаяСистема.УдалитьВременныйФайл(РезультатПолученияФайла.Путь);
	
	МакетВХранилище = CRM_СтраницаПриветствие.ЗаполнитьМетки(СтрокаНовая);
	ХранилищеВКонстанту = Новый ХранилищеЗначения(МакетВХранилище);
	Константы.CRM_СтраницаПриветствия.Установить(ХранилищеВКонстанту);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьТекстМакетаИзКонстанты()
	
	ЗначениеХранилища = Константы.CRM_СтраницаПриветствия.Получить();
	МакетИзХранилища = ЗначениеХранилища.Получить();
	Возврат МакетИзХранилища;
	
КонецФункции

#КонецОбласти
