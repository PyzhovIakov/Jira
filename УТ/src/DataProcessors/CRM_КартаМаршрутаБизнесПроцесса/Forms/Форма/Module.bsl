#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВремяНачалаЗамера = ОценкаПроизводительности.НачатьЗамерВремени();
	
	Если ЗначениеЗаполнено(Параметры.БизнесПроцесс) Тогда
		БизнесПроцесс = Параметры.БизнесПроцесс;
		Заголовок = НСтр("ru='Карта маршрута бизнес-процесса:';en='Flow Chart of business process:'") + " " 
			+ CRM_БизнесПроцессыСервер.СформироватьПредставлениеБизнесПроцесса(БизнесПроцесс);
		Элементы.БизнесПроцесс.Видимость = Ложь;
	Иначе
		Элементы.БизнесПроцесс.Видимость = Истина;
	КонецЕсли;
	
	CRM_ОбщегоНазначенияСервер.ЗакончитьЗамерВремениСозданияФормы(ЭтотОбъект, ВремяНачалаЗамера);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	CRM_ОбщегоНазначенияКлиент.НачатьЗамерВремениОткрытияФормы(ЭтотОбъект);
	ОбновитьКартуМаршрута();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗадачаВыполнена" Тогда
		Если ЗначениеЗаполнено(БизнесПроцесс) Тогда
			ОбработатьОповещение(Параметр);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура БизнесПроцессПриИзменении(Элемент)
	ОбновитьКартуМаршрута();
КонецПроцедуры

&НаКлиенте
Процедура КартаМаршрутаВыбор(Элемент)
	ОткрытьСписокЗадачТочкиМаршрута();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьВыполнить()
	ОбновитьКартуМаршрута();
КонецПроцедуры

&НаКлиенте
Процедура ЗадачиВыполнить()
	ОткрытьСписокЗадачТочкиМаршрута();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСтрокуИсполнителей(ТочкаМаршрута, ТипДействия)
	
	Запрос = Новый Запрос;
	
	Если ТипДействия = 2 Тогда // Данные берем из регистра сведений исполнители.
		ТекстЗапроса = "ВЫБРАТЬ
		|	CRM_ИсполнителиЭтаповБизнесПроцессов.Исполнитель КАК Исполнитель,
		|	"""" КАК РольИсполнителя
		|ИЗ
		|	РегистрСведений.CRM_ИсполнителиЭтаповБизнесПроцессов КАК CRM_ИсполнителиЭтаповБизнесПроцессов
		|ГДЕ
		|	CRM_ИсполнителиЭтаповБизнесПроцессов.Объект = &БизнесПроцесс
		|	И CRM_ИсполнителиЭтаповБизнесПроцессов.ТочкаМаршрута = &CRM_ТочкаМаршрута";
	Иначе
		ТекстЗапроса = "ВЫБРАТЬ
		|	ЗадачаИсполнителя.Исполнитель КАК Исполнитель,
		|	ЗадачаИсполнителя.РольИсполнителя КАК РольИсполнителя
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
		|	И ЗадачаИсполнителя.CRM_ТочкаМаршрута = &CRM_ТочкаМаршрута";
		Если ТипДействия = 0 Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|	И НЕ ЗадачаИсполнителя.Выполнена";
		Иначе
			ТекстЗапроса = ТекстЗапроса + "
			|	И ЗадачаИсполнителя.Выполнена";
		КонецЕсли;
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("БизнесПроцесс"		, БизнесПроцесс);
	Запрос.УстановитьПараметр("CRM_ТочкаМаршрута"	, ТочкаМаршрута);
	
	Выборка = Запрос.Выполнить().Выбрать();
	СтрокаИсполнителей = "";
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Исполнитель) Тогда
			СтрокаИсполнителей = СтрокаИсполнителей + ?(СтрокаИсполнителей = "", "", ";") + Строка(Выборка.Исполнитель);
		Иначе
			СтрокаИсполнителей = СтрокаИсполнителей + ?(СтрокаИсполнителей = "", "", ";") + Строка(Выборка.РольИсполнителя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрокаИсполнителей;
	
КонецФункции

&НаСервере
Процедура ОбновитьКартуМаршрута()

	Если ЗначениеЗаполнено(БизнесПроцесс) Тогда
		
		СтрокаСхемыМаршрута = БизнесПроцесс.КартаМаршрута.ВерсииКартыМаршрута.Найти(БизнесПроцесс.НомерВерсииКартыМаршрута,
			 "НомерВерсии");
		КартаМаршрута = СтрокаСхемыМаршрута.КартаМаршрута.Получить();
		
	Иначе
		КартаМаршрута = Новый ГрафическаяСхема;
	КонецЕсли;
	
	МассивВидов = Новый Массив;
	МассивВидов.Добавить(Перечисления.CRM_ВидыТочекМаршрута.ВложенныйБизнесПроцесс);
	МассивВидов.Добавить(Перечисления.CRM_ВидыТочекМаршрута.Действие);
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВложенныйЗапрос.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ Точки
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		CRM_ТочкиМаршрутовВходящиеТочки.Ссылка КАК Ссылка
	               |	ИЗ
	               |		Справочник.CRM_ТочкиМаршрутов.ВходящиеТочки КАК CRM_ТочкиМаршрутовВходящиеТочки
	               |	ГДЕ
	               |		CRM_ТочкиМаршрутовВходящиеТочки.Ссылка.Владелец = &Владелец
	               |		И CRM_ТочкиМаршрутовВходящиеТочки.НомерВерсии = &НомерВерсии
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		CRM_ТочкиМаршрутовИсходящиеТочки.Ссылка
	               |	ИЗ
	               |		Справочник.CRM_ТочкиМаршрутов.ИсходящиеТочки КАК CRM_ТочкиМаршрутовИсходящиеТочки
	               |	ГДЕ
	               |		CRM_ТочкиМаршрутовИсходящиеТочки.Ссылка.Владелец = &Владелец
	               |		И CRM_ТочкиМаршрутовИсходящиеТочки.НомерВерсии = &НомерВерсии) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Точки.Ссылка КАК Ссылка,
	               |	CRM_ТочкиМаршрутов.Имя КАК Имя
	               |ПОМЕСТИТЬ ТочкиИмя
	               |ИЗ
	               |	Точки КАК Точки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.CRM_ТочкиМаршрутов КАК CRM_ТочкиМаршрутов
	               |		ПО Точки.Ссылка = CRM_ТочкиМаршрутов.Ссылка
	               |ГДЕ
	               |	CRM_ТочкиМаршрутов.Владелец = &Владелец
	               |	И CRM_ТочкиМаршрутов.Вид В(&МассивВидов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТочкиИмя.Ссылка КАК Точка,
	               |	ТочкиИмя.Имя КАК Имя,
	               |	ЕСТЬNULL(ВыполненныеЗадачи.Количество, 0) КАК ВыполненоКоличество,
	               |	ЕСТЬNULL(НеВыполненныеЗадачи.Количество, 0) КАК НеВыполненоКоличество,
	               |	ВложенныйЗапрос.Используется КАК Используется
	               |ИЗ
	               |	ТочкиИмя КАК ТочкиИмя
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗадачаИсполнителя.Ссылка) КАК Количество,
	               |			ЗадачаИсполнителя.CRM_ТочкаМаршрута КАК CRM_ТочкаМаршрута
	               |		ИЗ
	               |			Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
	               |		ГДЕ
	               |			ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
	               |			И ЗадачаИсполнителя.Выполнена
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			ЗадачаИсполнителя.CRM_ТочкаМаршрута) КАК ВыполненныеЗадачи
	               |		ПО ТочкиИмя.Ссылка = ВыполненныеЗадачи.CRM_ТочкаМаршрута
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗадачаИсполнителя.Ссылка) КАК Количество,
	               |			ЗадачаИсполнителя.CRM_ТочкаМаршрута КАК CRM_ТочкаМаршрута
	               |		ИЗ
	               |			Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
	               |		ГДЕ
	               |			ЗадачаИсполнителя.БизнесПроцесс = &БизнесПроцесс
	               |			И НЕ ЗадачаИсполнителя.Выполнена
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			ЗадачаИсполнителя.CRM_ТочкаМаршрута) КАК НеВыполненныеЗадачи
	               |		ПО ТочкиИмя.Ссылка = НеВыполненныеЗадачи.CRM_ТочкаМаршрута
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			CRM_НастройкиЭтаповБизнесПроцессов.ТочкаМаршрута КАК ТочкаМаршрута,
	               |			CRM_НастройкиЭтаповБизнесПроцессов.Используется КАК Используется
	               |		ИЗ
	               |			РегистрСведений.CRM_НастройкиЭтаповБизнесПроцессов КАК CRM_НастройкиЭтаповБизнесПроцессов
	               |		ГДЕ
	               |			CRM_НастройкиЭтаповБизнесПроцессов.Объект = &БизнесПроцесс) КАК ВложенныйЗапрос
	               |		ПО ТочкиИмя.Ссылка = ВложенныйЗапрос.ТочкаМаршрута";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Владелец"		, БизнесПроцесс.КартаМаршрута);
	Запрос.УстановитьПараметр("НомерВерсии"		, БизнесПроцесс.НомерВерсииКартыМаршрута);
	Запрос.УстановитьПараметр("БизнесПроцесс"	, БизнесПроцесс);
	Запрос.УстановитьПараметр("МассивВидов"		, МассивВидов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЭлементСхемы = КартаМаршрута.ЭлементыГрафическойСхемы.Найти(Выборка.Имя);
		Если ЭлементСхемы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.ВыполненоКоличество > 0 Тогда
			ЭлементСхемы.ЦветФона = Новый Цвет(192, 192, 192); // дымчато-белый
		КонецЕсли;
		
		Если Выборка.НеВыполненоКоличество > 0 Тогда
			ЭлементСхемы.ЦветРамки	= Новый Цвет(255, 0, 0); // красный
			ЭлементСхемы.рамка		= Новый Линия(ТипСоединительнойЛинии.Пунктир);
		КонецЕсли;
		
		Если ТипЗнч(Выборка.Используется) = Тип("Булево") Тогда
			ЭлементСхемы.ПрозрачныйФон = НЕ Выборка.Используется;
		КонецЕсли;
		
		Если Выборка.Точка.Вид = Перечисления.CRM_ВидыТочекМаршрута.Действие Тогда
			Если Выборка.НеВыполненоКоличество > 0 Тогда
				ЭлементСхемы.Пояснение = ПолучитьСтрокуИсполнителей(Выборка.Точка, 0);
			ИначеЕсли Выборка.ВыполненоКоличество > 0 Тогда
				ЭлементСхемы.Пояснение = ПолучитьСтрокуИсполнителей(Выборка.Точка, 1);
			ИначеЕсли Выборка.ВыполненоКоличество = 0 Тогда
				ЭлементСхемы.Пояснение = ПолучитьСтрокуИсполнителей(Выборка.Точка, 2);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Функция ПолучитьТочкуМаршрута(ИмяЭлемента, Групповая)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	CRM_ТочкиМаршрутов.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.CRM_ТочкиМаршрутов КАК CRM_ТочкиМаршрутов
	               |ГДЕ
	               |	CRM_ТочкиМаршрутов.Владелец = &Владелец
	               |	И CRM_ТочкиМаршрутов.Имя = &Имя
	               |	И CRM_ТочкиМаршрутов.Групповая = &Групповая";
	Запрос.УстановитьПараметр("Владелец", БизнесПроцесс.КартаМаршрута);
	Запрос.УстановитьПараметр("Имя", ИмяЭлемента);
	Запрос.УстановитьПараметр("Групповая", Групповая);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьСписокЗадачТочкиМаршрута()

#Если ВебКлиент ИЛИ МобильныйКлиент Тогда
	ПоказатьПредупреждение(, НСтр("ru = 'Действие недоступно в веб-клиенте или в мобильном клиенте.
		|Запустите тонкий клиент.'"));
	Возврат;
#КонецЕсли
	ОчиститьСообщения();
	ТекЭлемент = Элементы.КартаМаршрута.ТекущийЭлемент;
	
	Если Не ЗначениеЗаполнено(БизнесПроцесс) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Укажите бизнес-процесс.'"), ,
			"БизнесПроцесс");
		Возврат;
	КонецЕсли;
	
	Если ТекЭлемент = Неопределено
		Или НЕ (ТипЗнч(ТекЭлемент) = Тип("ЭлементГрафическойСхемыДействие")
		Или ТипЗнч(ТекЭлемент) = Тип("ЭлементГрафическойСхемыВложенныйБизнесПроцесс")) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для просмотра списка задач выберите точку действия или вложенный бизнес-процесс карты маршрута.'"), ,
			"КартаМаршрута");
		Возврат;
	КонецЕсли;
	
	Групповая = Ложь;
	Если ТипЗнч(ТекЭлемент) = Тип("ЭлементГрафическойСхемыДействие") Тогда
		Групповая = ТекЭлемент.Групповая;
	КонецЕсли;
	
	ТекТочкаМаршрута = ПолучитьТочкуМаршрута(ТекЭлемент.Имя, Групповая);
	
	ЗаголовокФормы = НСтр("ru = 'Задачи по точке маршрута бизнес-процесса'");
	
	ОткрытьФорму("Задача.ЗадачаИсполнителя.Форма.CRM_ЗадачиПоБизнесПроцессу", 
		Новый Структура("Отбор,ЗаголовокФормы,ПоказыватьЗадачи,ВидимостьОтборов,БлокировкаОкнаВладельца,Задача,БизнесПроцесс", 
			Новый Структура("БизнесПроцесс,ТочкаМаршрута", БизнесПроцесс, ТекТочкаМаршрута),
			ЗаголовокФормы, 0, Ложь, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца, Строка(ТекТочкаМаршрута), Строка(БизнесПроцесс)),
		ЭтотОбъект, БизнесПроцесс);

КонецПроцедуры

&НаСервере
Процедура ОбработатьОповещение(Массив)
	
	Если ТипЗнч(Массив) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		Если БизнесПроцесс = Массив.БизнесПроцесс Тогда
			ОбновитьКартуМаршрута();
		КонецЕсли;
	ИначеЕсли ТипЗнч(Массив) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из Массив Цикл
			Если БизнесПроцесс = ЭлементМассива.БизнесПроцесс Тогда
				ОбновитьКартуМаршрута();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
