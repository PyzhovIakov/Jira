
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Проверка = Строка(Новый УникальныйИдентификатор());
	ПутьКСтраницеАвторизации = "https://www.amocrm.ru/oauth?client_id=" + Параметры.clientId 
		+ "&state=" + Проверка 
		+ "&mode=popup";
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	HTML = ПутьКСтраницеАвторизации;
	ПодключитьОбработчикОжидания("HTMLДокументСформирован", 1, Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура HTMLДокументСформирован()
	Ресурс = Элементы.HTML.Документ.URL;
	Если Проверка = ПолучитьПараметр(Ресурс, "state") Тогда
		Ошибка = ПолучитьПараметр(Ресурс, "error");
		Если ЗначениеЗаполнено(Ошибка) Тогда
			Если Ошибка = "access_denied" Тогда
				Закрыть();
			КонецЕсли;
		Иначе	
			КодАвторизации = ПолучитьПараметр(Ресурс, "code");
			Если ЗначениеЗаполнено(КодАвторизации) Тогда
				Домен = ПолучитьПараметр(Ресурс, "referer");
				Результат = Новый Структура("КодАвторизации, Домен", КодАвторизации, Домен);
				HTML = "";
				Закрыть(Результат);
			Иначе	
				ПодключитьОбработчикОжидания("HTMLДокументСформирован", 0.1, Истина);
			КонецЕсли;
		КонецЕсли;
	Иначе	
		ПодключитьОбработчикОжидания("HTMLДокументСформирован", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьПараметр(Ресурс, ИмяПараметра)
	Позиция = СтрНайти(Ресурс, ИмяПараметра + "=");
	Если Позиция > 0 Тогда
		Позиция = Позиция + СтрДлина(ИмяПараметра) + 1;
		Конец = СтрНайти(Ресурс, "&", , Позиция) - Позиция;
		Если Конец > 0 Тогда
			Возврат Сред(Ресурс, Позиция, Конец);
		Иначе
			Возврат Сред(Ресурс, Позиция);
		КонецЕсли;
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

#КонецОбласти
