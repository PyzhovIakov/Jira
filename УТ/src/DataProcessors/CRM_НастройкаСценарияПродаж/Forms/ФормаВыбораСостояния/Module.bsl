
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Сценарий) Тогда
		Сценарий = Параметры.Сценарий;
	КонецЕсли;
	
	Если Параметры.КопированиеТриггера Тогда
		Заголовок = "Копирование триггера";
	Иначе
		Заголовок = "Перемещение триггера";
	КонецЕсли;
	
	ТекСостояние = СтрЗаменить(Параметры.ТекСостояние, "Этап_", "");
	ТекСостояние = СтрЗаменить(ТекСостояние, "_", "-");
	
	ЗаполнитьДеревоСостояний(ЭтотОбъект, ДеревоСостояний, ТекСостояние);
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьСтруктуруДляВозвратаПоОК()
	
	ДеревоЗначений = РеквизитФормыВЗначение("ДеревоСостояний");
	ТаблицаСтрок = ПреобразоватьДеревоВТаблицу(ДеревоЗначений);
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат ТаблицаСтрок;
	
КонецФункции

&НаСервере
Функция ПреобразоватьДеревоВТаблицу(Дерево)
	
	Список = Новый Массив;
	
	ЗаполнитьСписок(Список, Дерево); 
	
	Возврат Список;
	
КонецФункции

Процедура ЗаполнитьСписок(Список, Дерево)
	
	Для Каждого СтрокаВерхнегоУровня Из Дерево.Строки Цикл
		Для Каждого СтрокаДерева Из СтрокаВерхнегоУровня.Строки Цикл
			Если Не СтрокаДерева.Пометка Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = Новый Структура;
			Для Каждого Колонка Из Дерево.Колонки Цикл
				НоваяСтрока.Вставить(Колонка.Имя, СтрокаДерева[Колонка.Имя]);
			КонецЦикла;
			Список.Добавить(НоваяСтрока);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)

	Результат = ПодготовитьСтруктуруДляВозвратаПоОК();
	Закрыть(Результат);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДеревоСостояний(Форма, ДеревоСостоянийФормы, ТекСостояние)
	
	Результат = ПолучитьВыборкуВсехСостояний();
	
	ВыборкаСостояний = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ДобавитьСтрокиВДеревоРазделов(ВыборкаСостояний, ДеревоСостоянийФормы, ТекСостояние);
	
	ДеревоОбъект = РеквизитФормыВЗначение("ДеревоСостояний");
	ДеревоОбъект.Строки.Сортировать("Сортировка Убыв");
	ЗначениеВРеквизитФормы(ДеревоОбъект, "ДеревоСостояний");
	
	Элементы.ДеревоСостояний.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВыборкуВсехСостояний()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	CRM_СостоянияИнтересов.Ссылка КАК Родитель
	|ПОМЕСТИТЬ ГруппыПоддержки
	|ИЗ
	|	Справочник.CRM_СостоянияИнтересов КАК CRM_СостоянияИнтересов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	CRM_СостоянияИнтересов.Ссылка КАК Состояние,
	|	CRM_СостоянияИнтересов.Родитель КАК Родитель
	|ИЗ
	|	Справочник.CRM_СостоянияИнтересов КАК CRM_СостоянияИнтересов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппыПоддержки КАК ГруппыПоддержки
	|		ПО CRM_СостоянияИнтересов.Родитель = ГруппыПоддержки.Родитель
	|ГДЕ
	|	НЕ CRM_СостоянияИнтересов.ПометкаУдаления
	|	И CRM_СостоянияИнтересов.Используется
	|
	|ИТОГИ ПО
	|	Родитель
	|";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

&НаСервере
Процедура ДобавитьСтрокиВДеревоРазделов(ВыборкаРазделы, Родитель, ТекСостояние)

	ЭлементыДереваРодителя = Родитель.ПолучитьЭлементы();
	Пока ВыборкаРазделы.Следующий() Цикл
		
		Если ЗначениеЗаполнено(ВыборкаРазделы.Состояние)
			 И Строка(ВыборкаРазделы.Состояние.УникальныйИдентификатор()) = ТекСостояние Тогда
			Продолжить;
		КонецЕсли;
		НовыйЭлементДерева = ЭлементыДереваРодителя.Добавить();
		Если Не ЗначениеЗаполнено(ВыборкаРазделы.Состояние) Тогда
			НовыйЭлементДерева.Ссылка = ВыборкаРазделы.Родитель;
			НовыйЭлементДерева.УровеньДерева = 1;
			Если ВыборкаРазделы.Родитель = Сценарий Тогда
				НовыйЭлементДерева.Сортировка = 1;
			КонецЕсли;
		Иначе
			НовыйЭлементДерева.Ссылка = ВыборкаРазделы.Состояние;
		КонецЕсли;
		НовыйЭлементДерева.РодительСостояния = ВыборкаРазделы.Родитель;
		
		ДобавитьСтрокиВДеревоРазделов(ВыборкаРазделы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией),
			НовыйЭлементДерева, ТекСостояние);
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
