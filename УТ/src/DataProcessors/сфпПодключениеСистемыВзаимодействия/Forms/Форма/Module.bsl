
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ПравоДоступа("РегистрацияИнформационнойБазыСистемыВзаимодействия", Метаданные) Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	сфпВариантПодключения = Константы.сфпВариантПодключенияСервераВзаимодействия.Получить();
	
	Если НЕ ЗначениеЗаполнено(сфпВариантПодключения) ИЛИ сфпВариантПодключения = "1С" Тогда
		ВариантПодключения = 0;

	Иначе	
		ВариантПодключения = 1;
		АдресСервера = сфпВариантПодключения;
	КонецЕсли;
	
	КонтейнерСостояний = Новый Структура();
	КонтейнерСостояний.Вставить("СерверВзаимодействия", СтрокаПодключения(ВариантПодключения, АдресСервера));
	КонтейнерСостояний.Вставить("ИмяИнформационнойБазы", Метаданные.КраткаяИнформация);
	КонтейнерСостояний.Вставить("СостояниеРегистрации", ТекущееСостояниеРегистрации());
	// Возможные значения:
	// "НеЗарегистрирована"
	// "Зарегистрирована"
	// "ОжиданиеВводаКодаПодтверждения"
	// "ОжиданиеОтветаСервераВзаимодействия".
	
	ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект);
	
	ТекущийПользователь = сфпСофтФонПроСервер.сфпТекущийПользователь();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Зарегистрироваться(Команда)
	
	Если ВариантПодключения = 1 И ПустаяСтрока(АдресСервера) Тогда
		ПоказатьПредупреждение(,
			 НСтр("ru='Адрес Сервера взаимодействия не заполнен';
			|en='Interaction server address is not filled'"));
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(АдресЭлектроннойПочты) Тогда 
		ПоказатьПредупреждение(, НСтр("ru='Адрес электронной почты не заполнен';en='Email address is not filled in'"));
		Возврат;
	КонецЕсли;
	
	Если Не сфпОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(АдресЭлектроннойПочты) Тогда 
		ПоказатьПредупреждение(, НСтр("ru='Адрес электронной почты содержит ошибки';en='Email address contains errors'"));
		Возврат;
	КонецЕсли;
	
	КонтейнерСостояний.СерверВзаимодействия = СтрокаПодключения(ВариантПодключения, АдресСервера);
	СохранитьАдресСервера(ВариантПодключения, АдресСервера);
	
	СостояниеРегистрации = КонтейнерСостояний.СостояниеРегистрации;
	
	Если СостояниеРегистрации = "НеЗарегистрирована" Тогда 
		ПриПолученииКодаРегистрации();
	ИначеЕсли СостояниеРегистрации = "ОжиданиеВводаКодаПодтверждения" Тогда 
		ПриРегистрации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	СостояниеРегистрации = КонтейнерСостояний.СостояниеРегистрации;
	
	Если СостояниеРегистрации = "ОжиданиеВводаКодаПодтверждения" Тогда 
		ПриОтказеОтВводаКодаПодтверждения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеОбработчикиСобытий

&НаКлиенте
Процедура ПриПолученииКодаРегистрации()
	
	СерверВзаимодействия = КонтейнерСостояний.СерверВзаимодействия;
	
	ПараметрыРегистрации = Новый ПараметрыРегистрацииИнформационнойБазыСистемыВзаимодействия;
	ПараметрыРегистрации.АдресСервера = СерверВзаимодействия;
	ПараметрыРегистрации.АдресЭлектроннойПочты = АдресЭлектроннойПочты;
	
	Оповещение = Новый ОписаниеОповещения("ПослеУспешногоПолученияКодаРегистрации", ЭтотОбъект, ,
		"ПриОбработкеОшибкиПолученияКодаРегистрации", ЭтотОбъект);
	
	Попытка
		СистемаВзаимодействия.НачатьРегистрациюИнформационнойБазы(Оповещение, ПараметрыРегистрации);
	Исключение
		КонтейнерСостояний.СостояниеРегистрации = "НеЗарегистрирована";
		ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект);
		
		Возврат;
	КонецПопытки;
	
	КонтейнерСостояний.СостояниеРегистрации = "ОжиданиеОтветаСервераВзаимодействия";
	ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУспешногоПолученияКодаРегистрации(РегистрацияВыполнена, ТекстСообщения, Контекст) Экспорт
	
	ПоказатьПредупреждение(, ТекстСообщения);
	
	КонтейнерСостояний.СостояниеРегистрации = "ОжиданиеВводаКодаПодтверждения";
	ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОбработкеОшибкиПолученияКодаРегистрации(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт 
	
	СтандартнаяОбработка = Ложь;
	ОбработкаОшибок.ПоказатьИнформациюОбОшибке(ИнформацияОбОшибке);
	
	КонтейнерСостояний.СостояниеРегистрации = "НеЗарегистрирована";
	ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриРегистрации()
	
	Если ПустаяСтрока(КодРегистрации) Тогда 
		ПоказатьПредупреждение(, НСтр("ru='Код регистрации не заполнен';en='Registration code is required'"));
		Возврат;
	КонецЕсли;
	
	СерверВзаимодействия  = КонтейнерСостояний.СерверВзаимодействия;
	ИмяИнформационнойБазы = КонтейнерСостояний.ИмяИнформационнойБазы;
	
	ПараметрыРегистрации = Новый ПараметрыРегистрацииИнформационнойБазыСистемыВзаимодействия;
	ПараметрыРегистрации.АдресСервера = СерверВзаимодействия;
	ПараметрыРегистрации.АдресЭлектроннойПочты = АдресЭлектроннойПочты;
	ПараметрыРегистрации.ИмяИнформационнойБазы = ИмяИнформационнойБазы;
	ПараметрыРегистрации.КодАктивации = СокрЛП(КодРегистрации);
	
	Оповещение = Новый ОписаниеОповещения("ПослеУспешнойРегистрации", ЭтотОбъект, ,
		"ПриОбработкеОшибкиРегистрации", ЭтотОбъект);
	
	Попытка
		СистемаВзаимодействия.НачатьРегистрациюИнформационнойБазы(Оповещение, ПараметрыРегистрации);
	Исключение
		КонтейнерСостояний.СостояниеРегистрации = "ОжиданиеВводаКодаПодтверждения";
		ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект);
		
		Возврат;
	КонецПопытки;
	
	КонтейнерСостояний.СостояниеРегистрации = "ОжиданиеОтветаСервераВзаимодействия";
	ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУспешнойРегистрации(РегистрацияВыполнена, ТекстСообщения, Контекст) Экспорт
	
	Если РегистрацияВыполнена Тогда
		ОчиститьИдентификаторЖурналаЗвонковСистемыВзаимодействия();
		
		Оповестить("ОбсужденияПодключены", Истина);
		КонтейнерСостояний.СостояниеРегистрации = "Зарегистрирована";
	Иначе 
		ПоказатьПредупреждение(, ТекстСообщения);
		КонтейнерСостояний.СостояниеРегистрации = "ОжиданиеВводаКодаПодтверждения";
	КонецЕсли;
	
	ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОбработкеОшибкиРегистрации(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт 
	
	СтандартнаяОбработка = Ложь;
	ОбработкаОшибок.ПоказатьИнформациюОбОшибке(ИнформацияОбОшибке);
	
	КонтейнерСостояний.СостояниеРегистрации = "ОжиданиеВводаКодаПодтверждения";
	ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОтказеОтВводаКодаПодтверждения()

	Оповещение = Новый ОписаниеОповещения("ПослеПодтвержденияОтказаВводаКодаПодтверждения", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, 
		НСтр("ru='При отказе от ввода высланный на электронную почту код станет недействительным."
"Продолжение будет возможно только с запросом нового кода.';en='If you refuse to enter code sent by e-mail will become invalid."
"Continuation will be possible only with request for new code.'"), 
		РежимДиалогаВопрос.ОКОтмена, , КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодтвержденияОтказаВводаКодаПодтверждения(РезультатВопроса, Контекст) Экспорт 
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда 
		КонтейнерСостояний.СостояниеРегистрации = "НеЗарегистрирована";
		ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииСостоянияФормы(КонтейнерСостояний, ЭтотОбъект) 
	
	СостояниеРегистрации = КонтейнерСостояний.СостояниеРегистрации;
	
	Если СостояниеРегистрации = "ОжиданиеВводаКодаПодтверждения" Тогда
		КодРегистрации = "";
	КонецЕсли;
	
	УстановитьСтраницу(КонтейнерСостояний, ЭтотОбъект);
	
	Элементы = ЭтотОбъект.Элементы;
	
	ОбновитьВидимостьКнопокКоманднойПанели(КонтейнерСостояний, 
		Элементы.Зарегистрироваться, Элементы.Закрыть, Элементы.Назад);
	
КонецПроцедуры

#КонецОбласти

#Область МодельПредставления

&НаСервереБезКонтекста
Функция ТекущееСостояниеРегистрации()
	Возврат ?(СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована(), "Зарегистрирована", "НеЗарегистрирована");
КонецФункции

#КонецОбласти

#Область Представления

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтраницу(КонтейнерСостояний, ЭтотОбъект)
	
	СостояниеРегистрации = КонтейнерСостояний.СостояниеРегистрации;
	Элементы = ЭтотОбъект.Элементы;
	
	Если СостояниеРегистрации = "НеЗарегистрирована" Тогда 
		Элементы.Страницы.ТекущаяСтраница = Элементы.ПредложениеРегистрации;
	ИначеЕсли СостояниеРегистрации = "ОжиданиеВводаКодаПодтверждения" Тогда 
		Элементы.Страницы.ТекущаяСтраница = Элементы.ВводКодаРегистрации;
	ИначеЕсли СостояниеРегистрации = "ОжиданиеОтветаСервераВзаимодействия" Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.ДлительнаяОперация;
	ИначеЕсли СостояниеРегистрации = "Зарегистрирована" Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.РегистрацияЗавершена;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьВидимостьКнопокКоманднойПанели(КонтейнерСостояний,
	Зарегистрироваться, Закрыть, Назад)
	
	СостояниеРегистрации = КонтейнерСостояний.СостояниеРегистрации;
	
	Если СостояниеРегистрации = "НеЗарегистрирована" Тогда 
		Зарегистрироваться.Видимость = Истина;
		Назад.Видимость = Ложь;
	ИначеЕсли СостояниеРегистрации = "ОжиданиеВводаКодаПодтверждения" Тогда 
		Зарегистрироваться.Видимость = Истина;
		Назад.Видимость = Истина;
	ИначеЕсли СостояниеРегистрации = "ОжиданиеОтветаСервераВзаимодействия" Тогда 
		Зарегистрироваться.Видимость = Ложь;
		Назад.Видимость = Ложь;
	ИначеЕсли СостояниеРегистрации = "Зарегистрирована" Тогда 
		Зарегистрироваться.Видимость = Ложь;
		Назад.Видимость = Ложь;
		Закрыть.КнопкаПоУмолчанию = Истина;
		Закрыть.Заголовок = НСтр("ru='Готово'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантПодключенияПриИзменении(Элемент)
	
	Элементы.АдресСервера.Видимость = (ВариантПодключения = 1);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ВариантПодключенияПриИзменении(Элементы.ВариантПодключения);

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьРегистрациюВРучную()
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.ПредложениеРегистрации;

	Элементы.Назад.Видимость = Истина;
	Элементы.ОтменаАвторегистрации.Видимость = Ложь;
	Элементы.ПраваяКоманднаяПанель.Видимость = Истина;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьПочту()
	
	ПолученоПисем = 0;
	ДоступноУчетныхЗаписей = 0;
	ЕстьОшибки = Ложь;
	
	СписокПолученныхПисем = Новый СписокЗначений();
	
	Результат = ПолучитьПочтуНаСервере(ПолученоПисем, ДоступноУчетныхЗаписей, ЕстьОшибки,
		 СписокПолученныхПисем,
		 УчетнаяЗапись);
	
	Если НЕ Результат.ЗаданиеВыполнено Тогда
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилищаЗадания = Результат.АдресХранилища;
		
		//ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияПолученияПочты", 1, Истина);
		//ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтотОбъект, ИдентификаторЗадания);
		
	Иначе
		//Элементы.СписокПисем.Обновить();
		//ПоказатьОповещениеПользователя(,,НСтр("ru='Почта получена ...'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияПолученияПочты()
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
			Результат = ПолучитьИзВременногоХранилища(АдресХранилищаЗадания);
			Если ТипЗнч(Результат) = Тип("Строка") И ЗначениеЗаполнено(Результат) Тогда 
				КодРегистрации = ПолучитьКодРегистрацииИзПисьма(УчетнаяЗапись);
				Если ЗначениеЗаполнено(КодРегистрации) Тогда
					СерверВзаимодействия  = КонтейнерСостояний.СерверВзаимодействия;
					ИмяИнформационнойБазы = КонтейнерСостояний.ИмяИнформационнойБазы;
					
					ПараметрыРегистрации = Новый ПараметрыРегистрацииИнформационнойБазыСистемыВзаимодействия;
					ПараметрыРегистрации.АдресСервера = СерверВзаимодействия;
					ПараметрыРегистрации.АдресЭлектроннойПочты = АдресЭлектроннойПочты;
					ПараметрыРегистрации.ИмяИнформационнойБазы = ИмяИнформационнойБазы;
					ПараметрыРегистрации.КодАктивации = СокрЛП(КодРегистрации);
					
					Оповещение = Новый ОписаниеОповещения("ПослеУспешнойАвторегистрации", ЭтотОбъект, ,
						 "ПриОбработкеОшибкиАвторегистрации",
						 ЭтотОбъект);
					СистемаВзаимодействия.НачатьРегистрациюИнформационнойБазы(Оповещение, ПараметрыРегистрации);
				КонецЕсли;
			КонецЕсли;

		Иначе
			ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияПолученияПочты", 1, Истина);
		КонецЕсли;
	Исключение
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаСервере
Функция ПолучитьПочтуНаСервере(ПолученоПисем, ДоступноУчетныхЗаписей, ЕстьОшибки,
	 СписокПолученныхПисем,
	 УчетнаяЗаписьОтбор)
	
	ПараметрыВыгрузки = Новый Структура();
	ПараметрыВыгрузки.Вставить("УчетнаяЗаписьОтбор", УчетнаяЗаписьОтбор);
	
	НаименованиеЗадания = НСтр("ru='Получение почты'") + " ";
	
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор,
		"Обработки.CRM_МенеджерПочты.ЗагрузитьПочтуПользователя", 
		ПараметрыВыгрузки,
		НаименованиеЗадания);
		
	АдресХранилищаЗадания = Результат.АдресХранилища;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ПослеУспешнойАвторегистрации(РегистрацияВыполнена, ТекстСообщения, Контекст) Экспорт
	
	Если РегистрацияВыполнена Тогда 
		Оповестить("ОбсужденияПодключены", Истина);
		Закрыть();
		
	Иначе 
		ПоказатьПредупреждение(, ТекстСообщения);
			
		ПродолжитьРегистрациюВРучную();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОбработкеОшибкиАвторегистрации(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт 
	
	СтандартнаяОбработка = Ложь;
	ОбработкаОшибок.ПоказатьИнформациюОбОшибке(ИнформацияОбОшибке);
	
	ПродолжитьРегистрациюВРучную();

КонецПроцедуры

&НаСервере
Функция ПолучитьКодРегистрацииИзПисьма(УчетнаяЗапись)
	
	КодРегистрации = "";
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Ссылка,
	|	ОтправительАдрес,
	|	Тема,
	|	Текст,
	|	ТекстHTML
	|ИЗ
	|	Документ.ЭлектронноеПисьмоВходящее
	|ГДЕ
	|	УчетнаяЗапись = &УчетнаяЗапись
	|	И НЕ ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПолучения УБЫВ");
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Тема = "Код подтверждения регистрации информационной базы" Тогда
			Позиция = СтрНайти(Выборка.Текст, "используйте код:");
			Если Позиция > 0 Тогда
				ОстатокТекста = Сред(Выборка.Текст, Позиция + 16);
				
				Позиция = СтрНайти(ОстатокТекста, "Используйте данный адрес");
				Если Позиция > 0 Тогда
					КодРегистрации = СокрЛП(Лев(ОстатокТекста, Позиция - 1));
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат КодРегистрации;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаПодключения(ВариантПодключения, АдресСервера)
	
	СтрокаПодключения = "wss://1cdialog.com:443";
	
	Если ВариантПодключения = 1 Тогда
		СтрокаПодключения = "wss://" + АдресСервера;
	КонецЕсли;
	
	Возврат СтрокаПодключения;

КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьАдресСервера(ВариантПодключения, АдресСервера)
	
	сфпВариантПодключения = "1С";
	Если ВариантПодключения = 1 Тогда
		сфпВариантПодключения = АдресСервера;
	КонецЕсли;
	
	Константы.сфпВариантПодключенияСервераВзаимодействия.Установить(сфпВариантПодключения);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОтменаАвторегистрации(Команда)
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьПочту");
	
	ПродолжитьРегистрациюВРучную();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчиститьИдентификаторЖурналаЗвонковСистемыВзаимодействия()
	
	УстановитьПривилегированныйРежим(Истина);
	Константы.сфпИдентификаторЖурналаЗвонковСистемыВзаимодействия.Установить("");
	
КонецПроцедуры	
		
#КонецОбласти

#КонецОбласти
