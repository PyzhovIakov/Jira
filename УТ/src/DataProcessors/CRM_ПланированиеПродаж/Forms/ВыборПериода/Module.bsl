
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "НачалоПериода,КонецПериода");
	
	ДатаНачалаПлана = Параметры.ДатыПлана.ДатаНачалаПлана;
	ДатаОкончанияПлана = Параметры.ДатыПлана.ДатаОкончанияПлана;
	
	МинимальнаяПериодичность = Параметры.МинимальнаяПериодичность;
	
	ДатаНачалаГода = ?(ЗначениеЗаполнено(КонецПериода), НачалоГода(КонецПериода), НачалоГода(ТекущаяДатаСеанса()));
	
	Элементы.ВыбратьГод.Заголовок = Формат(ДатаНачалаГода, "ДФ='yyyy'") + " год";
	
	ЦветТекущегоПериода = WebЦвета.Золотой;
	Если Параметры.Свойство("ВариантПериода") Тогда
		Период.Вариант = Параметры.ВариантПериода;
		Если Период.Вариант = ВариантСтандартногоПериода.Месяц Тогда
			Для каждого Элемент Из Элементы Цикл
				Если ТипЗнч(Элемент) = Тип("КнопкаФормы") И СтрНайти(Элемент.Имя, "ВыбратьКвартал") > 0 Тогда
					Элемент.Видимость = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьАктивныйПериод();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиНаГодНазад(Команда)
	
	ДатаНачалаГода = НачалоГода(ДатаНачалаГода - 1);
	
	УстановитьАктивныйПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаГодВперед(Команда)
	
	ДатаНачалаГода = КонецГода(ДатаНачалаГода) + 1;
	
	УстановитьАктивныйПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц1(Команда)
	
	ВыбратьМесяц(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц2(Команда)
	
	ВыбратьМесяц(2);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц3(Команда)
	
	ВыбратьМесяц(3);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц4(Команда)
	
	ВыбратьМесяц(4);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц5(Команда)
	
	ВыбратьМесяц(5);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц6(Команда)
	
	ВыбратьМесяц(6);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц7(Команда)
	
	ВыбратьМесяц(7);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц8(Команда)
	
	ВыбратьМесяц(8);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц9(Команда)
	
	ВыбратьМесяц(9);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц10(Команда)
	
	ВыбратьМесяц(10);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц11(Команда)
	
	ВыбратьМесяц(11);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМесяц12(Команда)
	
	ВыбратьМесяц(12);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКвартал1(Команда)
	
	ВыбратьКвартал(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКвартал2(Команда)
	
	ВыбратьКвартал(2);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКвартал3(Команда)
	
	ВыбратьКвартал(3);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКвартал4(Команда)
	
	ВыбратьКвартал(4);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьАктивныйПериод()

	ЗадатьЦветФона = Истина;
	Если НачалоМесяца(НачалоПериода) = НачалоМесяца(КонецПериода) Тогда
		НомерМесяца = Месяц(НачалоПериода);
		ТекущийЭлемент = Элементы["ВыбратьМесяц" + НомерМесяца];
	ИначеЕсли НачалоКвартала(НачалоПериода) = НачалоКвартала(КонецПериода) Тогда
		НомерМесяца = Месяц(НачалоПериода);
		НомерКвартала = Цел((НомерМесяца + 3) / 3);
		ТекущийЭлемент = Элементы["ВыбратьКвартал" + НомерКвартала];
	ИначеЕсли НачалоГода(НачалоПериода) = НачалоГода(КонецПериода) Тогда
		НомерМесяцаНачала = Месяц(НачалоПериода);
		НомерМесяцаКонца  = Месяц(КонецПериода);
		Если НомерМесяцаНачала <= 3 И НомерМесяцаКонца <= 6 Тогда
			ТекущийЭлемент = Элементы["ВыбратьПолугодие1"];
		ИначеЕсли НомерМесяцаНачала <= 3 И НомерМесяцаКонца <= 9 Тогда
			ТекущийЭлемент = Элементы["Выбрать9Месяцев"];
		Иначе
			//ТекущийЭлемент = Элементы["ВыбратьГод"];
			ЗадатьЦветФона = Ложь;
		КонецЕсли;
	Иначе
		//ТекущийЭлемент = Элементы["ВыбратьГод"];
		ЗадатьЦветФона = Ложь;
	КонецЕсли;

	Если ЗадатьЦветФона Тогда
		ТекущийЭлемент.ЦветФона = ЦветТекущегоПериода;
	КонецЕсли;
	
	// Сбросить доступность
	Элементы.ПерейтиНаГодНазад.Доступность = Истина;
	Элементы.ПерейтиНаГодВперед.Доступность = Истина;
	
	Для Сч = 1 По 4 Цикл
		Элементы["ВыбратьКвартал" + Сч].Доступность = Истина;
	КонецЦикла;
	
	Для Сч = 1 По 12 Цикл
		Элементы["ВыбратьМесяц" + Сч].Доступность = Истина;
	КонецЦикла;
	
	// Установить доступность
	Если Год(ДобавитьМесяц(ДатаНачалаГода, -12)) < Год(ДатаНачалаПлана) Тогда
		Элементы.ПерейтиНаГодНазад.Доступность = Ложь;
	КонецЕсли;
	Если Год(ДобавитьМесяц(ДатаНачалаГода, 12)) > Год(ДатаОкончанияПлана) Тогда
		Элементы.ПерейтиНаГодВперед.Доступность = Ложь;
	КонецЕсли;
	
	Если Год(ДатаНачалаГода) = Год(ДатаНачалаПлана) Тогда
		
		МесяцНачалаПлана = Месяц(ДатаНачалаПлана);
		
		Для Сч = 1 По МесяцНачалаПлана - 1 Цикл
			
			Элементы["ВыбратьМесяц" + Сч].Доступность = Ложь;
			
			Если Сч % 3 = 0 Тогда // Весь квартал недоступен
				Элементы["ВыбратьКвартал" + (Сч / 3)].Доступность = Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Год(ДатаНачалаГода) = Год(ДатаОкончанияПлана) Тогда
		
		МесяцОкончанияПлана = Месяц(ДатаОкончанияПлана);
		
		Для Сч = 1 По (12 - МесяцОкончанияПлана) Цикл
			
			МесяцСчетчика = 12 - Сч + 1;
			Элементы["ВыбратьМесяц" + МесяцСчетчика].Доступность = Ложь;
			
			Если Сч % 3 = 0 Тогда // Весь квартал недоступен
				Элементы["ВыбратьКвартал" + ((МесяцСчетчика - 1) / 3 + 1)].Доступность = Ложь;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	// Доступность по минимальной периодичности
	Если МинимальнаяПериодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		ДоступностьПериодичности("ВыбратьМесяц", 12, Ложь);
	ИначеЕсли МинимальнаяПериодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		ДоступностьПериодичности("ВыбратьКвартал", 4, Ложь);
		ДоступностьПериодичности("ВыбратьМесяц", 12, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступностьПериодичности(ПрефиксЭлемента, КоличествоЭлементов, ДоступностьЭлемента)
	
	Для Сч = 1 По КоличествоЭлементов Цикл
		Элементы[ПрефиксЭлемента + Сч].Доступность = ДоступностьЭлемента;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыборПериода(ПериодичностьКонтроля, ИнтервалПериодичности)

	ПредставлениеПериода = ПредставлениеПериода(НачалоПериода, КонецПериода);

	РезультатВыбора = Новый Структура("НачалоПериода,КонецПериода,ПредставлениеПериода,
		|ПериодичностьКонтроля,ИнтервалПериодичности",
		НачалоПериода, КонецДня(КонецПериода), ПредставлениеПериода, ПериодичностьКонтроля, ИнтервалПериодичности);
		
	Если ЗакрыватьПриВыборе Тогда
		ОповеститьОВыборе(РезультатВыбора);
	Иначе
		Период.ДатаНачала = НачалоПериода;
		Период.ДатаОкончания = КонецПериода;
		Закрыть(Период);
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Процедура ВыбратьМесяц(НомерМесяца)
	
	НачалоПериода = Дата(Год(ДатаНачалаГода), НомерМесяца, 1);
	КонецПериода  = КонецМесяца(НачалоПериода);
	
	ВыполнитьВыборПериода(ПредопределенноеЗначение("Перечисление.Периодичность.Месяц"), НомерМесяца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКвартал(НомерКвартала)
	
	НачалоПериода = Дата(Год(ДатаНачалаГода), 1 + (НомерКвартала - 1) * 3, 1);
	
	КонецПериода  = КонецКвартала(НачалоПериода);
	
	ВыполнитьВыборПериода(ПредопределенноеЗначение("Перечисление.Периодичность.Квартал"), НомерКвартала);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьГод(Команда)
	
	ВыполнитьВыборПериода(ПредопределенноеЗначение("Перечисление.Периодичность.Год"), ДатаНачалаГода);
	
КонецПроцедуры

#КонецОбласти
