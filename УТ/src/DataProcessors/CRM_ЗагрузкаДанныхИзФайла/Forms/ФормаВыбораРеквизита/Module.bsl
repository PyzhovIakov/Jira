
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("РежимЗагрузкиВДокумент") И Параметры.РежимЗагрузкиВДокумент Тогда
		Дерево = СформироватьДеревоПриРежимеВыбора();
		
		Если Параметры.Свойство("ИсключитьРеквизиты") Тогда
			Для Каждого ИсключаемыйРеквизит Из Параметры.ИсключитьРеквизиты Цикл
				Строки = Дерево.Строки.НайтиСтроки(Новый Структура("ПредставлениеРеквизита", ИсключаемыйРеквизит), Истина);
				Для Каждого Строка Из Строки Цикл
					Строка.Родитель.Строки.Удалить(Строка);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		ЗначениеВРеквизитФормы(Дерево, "ДеревоРеквизитов");
	ИначеЕсли Параметры.Свойство("ИмяМакета") Тогда
		Отбор = Неопределено;
		Параметры.Свойство("Отбор", Отбор);
		Дерево = Обработки.CRM_ЗагрузкаДанныхИзФайла.СформироватьДеревоРеквизитов(Параметры.ИмяМакета, Отбор);
		
		Если Параметры.Свойство("ИсключитьРеквизиты") Тогда
			Для Каждого ИсключаемыйРеквизит Из Параметры.ИсключитьРеквизиты Цикл
				Строки = Дерево.Строки.НайтиСтроки(Новый Структура("ПредставлениеРеквизита", ИсключаемыйРеквизит), Истина);
				Для Каждого Строка Из Строки Цикл
					Строка.Родитель.Строки.Удалить(Строка);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		Для Каждого Строка Из Дерево.Строки Цикл
			Идентификатор = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Строка.Объект);
			МетаданныеОбъекта = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(Идентификатор);
			КонтактнаяИнформация = (ПравоДоступа("Добавление", Метаданные.Справочники.ВидыКонтактнойИнформации)
										И МетаданныеОбъекта.ТабличныеЧасти.Найти("КонтактнаяИнформация") <> Неопределено);
			ДополнительныеРеквизиты = (ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения")
										И ПравоДоступа("Добавление", Метаданные.ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения)
										И МетаданныеОбъекта.ТабличныеЧасти.Найти("ДополнительныеРеквизиты") <> Неопределено);
			Если КонтактнаяИнформация ИЛИ ДополнительныеРеквизиты Тогда
				НовОбъект = ОбъектыСДопРекИКИ.Добавить();
				НовОбъект.Объект = Идентификатор;
				НовОбъект.КонтактнаяИнформация = КонтактнаяИнформация;
				НовОбъект.ДополнительныеРеквизиты = ДополнительныеРеквизиты;
				СписокОбъектов.Добавить(Идентификатор);
			КонецЕсли;
		КонецЦикла;
		
		Если (ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения")
			 И ПравоДоступа("Добавление", Метаданные.ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения))
			ИЛИ ПравоДоступа("Добавление", Метаданные.Справочники.ВидыКонтактнойИнформации) Тогда
			Элементы.ДекорацияНовоеПоле.Видимость = Истина;
		КонецЕсли;
		
		ЗначениеВРеквизитФормы(Дерево, "ДеревоРеквизитов");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияОтменитьНажатие(Элемент)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНовоеПолеНажатие(Элемент)
	СозданиеПоляНачать();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоРеквизитов

&НаКлиенте
Процедура ДеревоРеквизитовПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ПриАктивизацииСтроки", 0.2, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Выбрать(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ТекДанные = Элементы.ДеревоРеквизитов.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(ТекДанные.ИмяРеквизита) Тогда
			СтруктураРеквизита = Новый Структура;
			СтруктураРеквизита.Вставить("Объект", ТекДанные.Объект);
			СтруктураРеквизита.Вставить("ИмяРеквизита", ТекДанные.ИмяРеквизита);
			СтруктураРеквизита.Вставить("ПредставлениеРеквизита", ТекДанные.ПредставлениеРеквизита);
			СтруктураРеквизита.Вставить("ТипРеквизита", ТекДанные.ТипРеквизита);
			СтруктураРеквизита.Вставить("Обязательный", ТекДанные.Обязательный);
			СтруктураРеквизита.Вставить("Создавать", ТекДанные.Создавать);
			ОповеститьОВыборе(СтруктураРеквизита);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьДеревоПриРежимеВыбора()
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("Объект");
	Дерево.Колонки.Добавить("ИмяРеквизита");
	Дерево.Колонки.Добавить("ПредставлениеРеквизита", , "Реквизит");
	Дерево.Колонки.Добавить("ТипРеквизита");
	Дерево.Колонки.Добавить("Обязательный");
	Дерево.Колонки.Добавить("Создавать");
	
	НоваяСтрокаИерархии = Дерево.Строки.Добавить();
	НоваяСтрокаИерархии.ПредставлениеРеквизита = "Реквизиты загрузки";
	
	НоваяСтрока = НоваяСтрокаИерархии.Строки.Добавить();
	НоваяСтрока.ПредставлениеРеквизита	= "Клиент (Наименование)";
	НоваяСтрока.ИмяРеквизита			= "Клиент";
	НоваяСтрока.ТипРеквизита			= Новый ОписаниеТипов("Строка");
	
	НоваяСтрока = НоваяСтрокаИерархии.Строки.Добавить();
	НоваяСтрока.ПредставлениеРеквизита	= "Контакт (Наименование)";
	НоваяСтрока.ИмяРеквизита			= "Контакт";
	НоваяСтрока.ТипРеквизита			= Новый ОписаниеТипов("Строка");
	
	НоваяСтрока = НоваяСтрокаИерархии.Строки.Добавить();
	НоваяСтрока.ПредставлениеРеквизита	= "Телефон";
	НоваяСтрока.ИмяРеквизита			= "Телефон";
	НоваяСтрока.ТипРеквизита			= Новый ОписаниеТипов("Строка");
	
	НоваяСтрока = НоваяСтрокаИерархии.Строки.Добавить();
	НоваяСтрока.ПредставлениеРеквизита	= "Электронная почта";
	НоваяСтрока.ИмяРеквизита			= "Почта";
	НоваяСтрока.ТипРеквизита			= Новый ОписаниеТипов("Строка");
	
	Возврат Дерево;
	
КонецФункции // СформироватьДеревоПриРежимеВыбора()

&НаКлиенте
Процедура ПриАктивизацииСтроки()
	ТекДанные = Элементы.ДеревоРеквизитов.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Элементы.Выбрать.Доступность = ЗначениеЗаполнено(ТекДанные.ИмяРеквизита);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПоляНачать()
	ОписаниеОпопвещения = Новый ОписаниеОповещения("СозданиеПоляПродолжить", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	СтруктураОбъектыСДопРекИКИ = Новый Соответствие;
	Для каждого ОбъектСДопРекИКИ Из ОбъектыСДопРекИКИ Цикл
		СтруктураОбъектыСДопРекИКИ.Вставить(ОбъектСДопРекИКИ.Объект,
			 Новый Структура("ДополнительныеРеквизиты, КонтактнаяИнформация",
			 ОбъектСДопРекИКИ.ДополнительныеРеквизиты,
			 ОбъектСДопРекИКИ.КонтактнаяИнформация));
	КонецЦикла;
	ПараметрыОткрытия.Вставить("ОбъектыСДопРекИКИ", СтруктураОбъектыСДопРекИКИ);
	ОткрытьФорму("Обработка.CRM_ЗагрузкаДанныхИзФайла.Форма.ФормаСозданияПоля", ПараметрыОткрытия,
		 ЭтотОбъект, , , , ОписаниеОпопвещения,
		 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПоляПродолжить(СтруктураРеквизита, ДопРеквизиты) Экспорт
	
	Если СтруктураРеквизита <> Неопределено Тогда
		ОповеститьОВыборе(СтруктураРеквизита);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
