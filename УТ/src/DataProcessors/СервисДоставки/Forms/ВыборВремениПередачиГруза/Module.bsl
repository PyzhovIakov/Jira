#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТипГрузоперевозки", ТипГрузоперевозки);
	
	Если Не ЗначениеЗаполнено(ТипГрузоперевозки) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не выбран тип грузоперевозки'"));
		Отказ = Истина;
		Возврат;
	ИначеЕсли Не СервисДоставки.ТипГрузоперевозкиДоступен(ТипГрузоперевозки) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Выбранный тип грузоперевозки не доступен'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Дата", Дата);
	Параметры.Свойство("ВремяРаботыС", ВремяРаботыС);
	Параметры.Свойство("ВремяРаботыПо", ВремяРаботыПо);
	Параметры.Свойство("ВремяОбедС", ВремяОбедаС);
	Параметры.Свойство("ВремяОбедПо", ВремяОбедаПо);
	Параметры.Свойство("ВариантВыбораВремени", ВариантВыбораВремени);
	
	НастроитьФормуПоТипуГрузоперевозки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ВремяЗаполнено() Тогда
		
		Если ВремяЗаполненоКорректно() Тогда
			
			ПараметрыЗакрытияФормы = НовыйПараметрыЗакрытияФормы();
			
			Если СервисДоставкиКлиентСервер.ЭтоСДЭК(ТипГрузоперевозки) Тогда
				ПараметрыЗакрытияФормы.Дата = Дата;
			КонецЕсли;
			ПараметрыЗакрытияФормы.ВремяРаботыС = ВремяРаботыС;
			ПараметрыЗакрытияФормы.ВремяРаботыПо = ВремяРаботыПо;
			ПараметрыЗакрытияФормы.ВремяОбедС = ВремяОбедаС;
			ПараметрыЗакрытияФормы.ВремяОбедПо = ВремяОбедаПо;
			ПараметрыЗакрытияФормы.ВариантВыбораВремени = ВариантВыбораВремени;
			
			Закрыть(ПараметрыЗакрытияФормы);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьФормуПоТипуГрузоперевозки()
	
	Заголовок = СтрШаблон(НСтр("ru = '%1: Период времени'"),
		СервисДоставкиКлиентСервер.ПредставлениеТипаГрузоперевозки(ТипГрузоперевозки));
	
	Элементы.Дата.Видимость = Параметры.ВариантОтображения = ВариантОтображенияДатаВремя();
	Элементы.Дата.ТолькоПросмотр = ТолькоПросмотр Или ВариантВыбораВремени = 2;
	Элементы.ВремяРаботыС.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ВремяРаботыПо.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ВремяОбедаС.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ВремяОбедаПо.ТолькоПросмотр = ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиенте
Функция ВремяЗаполненоКорректно()
	
	Отказ = Ложь;
	ОписаниеОшибки = "";
	
	Если ВремяРаботыС >= ВремяРаботыПо И (ЗначениеЗаполнено(ВремяРаботыПо))Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						 + НСтр("ru='Время окончания работы должно быть больше времени начала работы'");
	КонецЕсли;
	
	Если (ЗначениеЗаполнено(ВремяОбедаПо))
		И ВремяОбедаС >= ВремяОбедаПо Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						+ НСтр("ru='Время окончания обеда должно быть больше времени начала обеда'");
	КонецЕсли;
	
	Если (ЗначениеЗаполнено(ВремяОбедаС) ИЛИ ЗначениеЗаполнено(ВремяОбедаПо)) 
		И (ВремяОбедаС < ВремяРаботыС) Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						+ НСтр("ru='Время начала обеда должно быть больше времени начала работы'");
	КонецЕсли;
	
	Если (ЗначениеЗаполнено(ВремяОбедаС) ИЛИ ЗначениеЗаполнено(ВремяОбедаПо)) 
		И (ВремяОбедаС > ВремяРаботыПо) Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						+ НСтр("ru='Время начала обеда должно быть больше времени начала работы'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВремяОбедаПо) И (ВремяОбедаПо > ВремяРаботыПо) Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						+ НСтр("ru='Время окончания обеда должно быть меньше времени окончания работы'");
	КонецЕсли;
	
	Если ОписаниеОшибки <> "" Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(,ОписаниеОшибки);
	КонецЕсли;
		
	Возврат Не Отказ;
	
КонецФункции

&НаКлиенте
Функция ВремяЗаполнено()
	
	Отказ = Ложь;
	
	ОписаниеОшибки = "";
	
	Если (ЗначениеЗаполнено(ВремяРаботыС)) И (Не ЗначениеЗаполнено(ВремяРаботыПо)) Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						 + НСтр("ru='Не заполнено время окончания работы'");
	КонецЕсли;
	
	Если (ЗначениеЗаполнено(ВремяОбедаС)) И (Не ЗначениеЗаполнено(ВремяОбедаПо)) Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						 + НСтр("ru='Не заполнено время окончания обеда'");
	КонецЕсли;
	
	Если ОписаниеОшибки <> "" Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(,ОписаниеОшибки);
	КонецЕсли;
		
	Возврат Не Отказ;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантОтображенияДатаВремя()
	Возврат "ДатаВремя";
КонецФункции

// Новый параметры закрытия формы.
// 
// Возвращаемое значение:
//  Структура - Новый параметры закрытия формы:
//   * ВариантВыбораВремени - Число - 
//   * Дата - Дата - 
//   * ВремяРаботыС - Дата - 
//   * ВремяРаботыПо - Дата - 
//   * ВремяОбедС - Дата - 
//   * ВремяОбедПо - Дата - 
&НаКлиенте
Функция НовыйПараметрыЗакрытияФормы()
	
	Результат = Новый Структура();
	
	Результат.Вставить("ВариантВыбораВремени", 0);
	Результат.Вставить("Дата", Дата(1, 1, 1));
	Результат.Вставить("ВремяРаботыС", Дата(1, 1, 1));
	Результат.Вставить("ВремяРаботыПо", Дата(1, 1, 1));
	Результат.Вставить("ВремяОбедС", Дата(1, 1, 1));
	Результат.Вставить("ВремяОбедПо", Дата(1, 1, 1));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
