#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Загружает данные информационной базы.
//
// Параметры:
//	Контейнер - ОбработкаОбъект.ВыгрузкаЗагрузкаДанныхМенеджерКонтейнера - менеджер
//		контейнера, используемый в процессе выгрузи данных. Подробнее см. комментарий
//		к программному интерфейсу обработки ВыгрузкаЗагрузкаДанныхМенеджерКонтейнера.
//	Обработчики - ОбщийМодуль
//				- ОбработкаМенеджер
//	СоответствиеСсылок - Соответствие из ЛюбаяСсылка - соответствие ссылок
// Возвращаемое значение:
//	ОбработкаОбъект.ВыгрузкаЗагрузкаДанныхПотокЗаменыСсылок
//
Функция ЗагрузитьДанныеИнформационнойБазы(Контейнер, Обработчики, СоответствиеСсылок = Неопределено) Экспорт
	
	ЗагружаемыеТипы = Контейнер.ПараметрыЗагрузки().ЗагружаемыеТипы;
	ИсключаемыеТипы = ВыгрузкаЗагрузкаДанныхСлужебныйСобытия.ПолучитьТипыИсключаемыеИзВыгрузкиЗагрузки();
	
	МенеджерЗагрузки = Создать();
	МенеджерЗагрузки.Инициализировать(
		Контейнер,
		ЗагружаемыеТипы,
		ИсключаемыеТипы,
		Обработчики,
		СоответствиеСсылок);
	МенеджерЗагрузки.ЗагрузитьДанные();
	
	Возврат МенеджерЗагрузки.ТекущийПотокЗаменыСсылок();
	
КонецФункции

// Загрузить данные информационной базы в потоке.
// 
// Параметры:
//  ПараметрыПотока - Структура - см. свойство Параметры в
//  	ОбработкаОбъект.ВыгрузкаЗагрузкаДанныхМенеджерЗагрузкиДанныхИнформационнойБазы.ЗапуститьПотокиЗагрузкиДанных
// 
Процедура ЗагрузитьДанныеИнформационнойБазыВПотоке(ПараметрыПотока) Экспорт
	
	Контейнер = Обработки.ВыгрузкаЗагрузкаДанныхМенеджерКонтейнера.Создать();
	Контейнер.ИнициализироватьЗагрузкуВПотоке(ПараметрыПотока.Контейнер);
	
	Обработчики = Обработки.ВыгрузкаЗагрузкаДанныхМенеджерОбработчиковЗагрузкиДанных.Создать();
	Обработчики.Инициализировать();
	
	ЗагружаемыеТипы = Неопределено;
	ИсключаемыеТипы = Неопределено;
	СоответствиеСсылок = Новый Соответствие(ПараметрыПотока.ПотокЗаменыСсылок.СоответствиеСсылок);
		
	МенеджерЗагрузки = Создать();
	МенеджерЗагрузки.Инициализировать(
		Контейнер,
		ЗагружаемыеТипы,
		ИсключаемыеТипы,
		Обработчики,
		СоответствиеСсылок);
	МенеджерЗагрузки.ЗагрузитьДанныеВПотоке();
	
КонецПроцедуры

// Запустить потоки загрузки данных.
// 
// Параметры:
//  ПараметрыПроцессаЗагрузки - см. НовыеПараметрыПроцессаЗагрузкиДанныхИнформационнойБазы
//  КоличествоПотоков - Число
//  ВидПотоков - ПеречислениеСсылка.ВидыПотоковВыгрузкиЗагрузкиДанных
// 
// Возвращаемое значение:
//  Массив из ФоновоеЗадание - Запущенные задания загрузки данных
Функция ЗапуститьПотокиЗагрузкиДанных(ПараметрыПроцессаЗагрузки, КоличествоПотоков = 1, ВидПотоков = Неопределено) Экспорт
	
	ПараметрыПотоков = ВыгрузкаЗагрузкаДанныхСлужебный.НовыеПараметрыПотоковВыгрузкиЗагрузкиДанных();
	ПараметрыПотоков.ЭтоЗагрузка = Истина;
	ПараметрыПотоков.КоличествоПотоков = КоличествоПотоков;
	
	Если ЗначениеЗаполнено(ВидПотоков) Тогда
		ПараметрыПотоков.ВидПотоков = ВидПотоков;
	КонецЕсли;
	
	ПараметрыПотоков.Параметры.Вставить(
		"Контейнер",
		ПараметрыПроцессаЗагрузки.Контейнер.ПолучитьПараметрыИнициализацииВПотоке());
	ПараметрыПотоков.Параметры.Вставить(
		"ПотокЗаменыСсылок",
		ПараметрыПроцессаЗагрузки.ПотокЗаменыСсылок.ПолучитьПараметрыИнициализацииВПотоке());
	
	Возврат ВыгрузкаЗагрузкаДанныхСлужебный.ЗапуститьПотокиВыгрузкиЗагрузкиДанных(
		ПараметрыПотоков, ПараметрыПроцессаЗагрузки.Контейнер);
	
КонецФункции

// Новые параметры процесса загрузки данных информационной базы.
// 
// Параметры:
//  Контейнер - ОбработкаОбъект.ВыгрузкаЗагрузкаДанныхМенеджерКонтейнера
//  ПотокЗаменыСсылок - ОбработкаОбъект.ВыгрузкаЗагрузкаДанныхПотокЗаменыСсылок
// 
// Возвращаемое значение:
//  Структура - Новые параметры процесса выгрузки данных информационной базы:
// * Контейнер - ОбработкаОбъект.ВыгрузкаЗагрузкаДанныхМенеджерКонтейнера 
// * ПотокЗаменыСсылок - ОбработкаОбъект.ВыгрузкаЗагрузкаДанныхПотокЗаменыСсылок 
Функция НовыеПараметрыПроцессаЗагрузкиДанныхИнформационнойБазы(Контейнер, ПотокЗаменыСсылок) Экспорт
	
	ПараметрыПроцессаЗагрузки = Новый Структура();
	ПараметрыПроцессаЗагрузки.Вставить("Контейнер", Контейнер);
	ПараметрыПроцессаЗагрузки.Вставить("ПотокЗаменыСсылок", ПотокЗаменыСсылок);
	
	Возврат ПараметрыПроцессаЗагрузки;
	
КонецФункции

#КонецОбласти

#КонецЕсли
