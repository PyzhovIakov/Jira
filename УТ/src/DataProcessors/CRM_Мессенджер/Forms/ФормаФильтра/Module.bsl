#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьПодразделения = CRM_ОбщегоНазначенияПовтИсп.ИспользоватьПодразделения();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.СтруктураФильтра);
	ОтборПользовательСтарый = ОтборПользователь;
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Партнеры"));
	МассивТипов.Добавить(Тип("СправочникСсылка.КонтактныеЛицаПартнеров"));
	Если ПолучитьФункциональнуюОпцию("CRM_ИспользоватьПотенциальныхКлиентов") Тогда
		МассивТипов.Добавить(Тип("СправочникСсылка.CRM_ПотенциальныеКлиенты"));
	КонецЕсли;
	
	Элементы.ОтборКонтакты.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.CRM_УчетныеЗаписиМессенджеров"));
	
	Элементы.ПриоритетыКаналовЗначение.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	Элементы.ГруппаПриоритетыКаналов.Заголовок = НСтр("ru = 'Приоритеты каналов'") 
		+ ?(ПриоритетыКаналов.Количество() > 0, " (" + Строка(ПриоритетыКаналов.Количество()) + ")", "");
		
	ОтображатьЗакрытыеИсходноеЗначение = Параметры.СтруктураФильтра.ОтображатьЗакрытые;
	ОтображатьТолькоСвоиИсходноеЗначение = Параметры.СтруктураФильтра.ОтображатьТолькоСвои;
	
	Элементы.ОтборПодразделение.Видимость = ИспользоватьПодразделения;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	Закрыть(ПолучитьСтруктуруФильтра());
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)

	ОчиститьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПриоритетыКаналовПриИзменении(Элемент)
	Элементы.ГруппаПриоритетыКаналов.Заголовок = НСтр("ru = 'Приоритеты каналов'") 
		+ ?(ПриоритетыКаналов.Количество() > 0, " (" + Строка(ПриоритетыКаналов.Количество()) + ")", "");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСтруктуруФильтра()
	СтруктураФильтра = Новый Структура;
	СтруктураФильтра.Вставить("ОтображатьЗакрытые", ОтображатьЗакрытые);
	СтруктураФильтра.Вставить("ОтображатьТолькоСвои", ОтображатьТолькоСвои);
	СтруктураФильтра.Вставить("ОтображатьЗаблокированные", ОтображатьЗаблокированные);
	СтруктураФильтра.Вставить("ОтборПользователь", ОтборПользователь);
	СтруктураФильтра.Вставить("ОтборПодразделение", ОтборПодразделение);
	//СтруктураФильтра.Вставить("ОтборКонтакты", ОтборКонтакты);
	СтруктураФильтра.Вставить("ОтборКонтакты", Новый СписокЗначений); // данный фильтр отключен
	СтруктураФильтра.Вставить("ОтборКанал", ОтборКанал);
	СтруктураФильтра.Вставить("ПриоритетыКаналов", ПриоритетыКаналов);
	СтруктураФильтра.Вставить("ОтборПериод", ОтборПериод);
	
	Если ОтображатьЗакрытые И Не ОтображатьЗакрытыеИсходноеЗначение Тогда
		CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.АРМДиалоги.Удобство.ФильтрОтображатьЗакрытые");
	КонецЕсли;
	
	Если ОтображатьТолькоСвои И Не ОтображатьТолькоСвоиИсходноеЗначение Тогда
		CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.АРМДиалоги.Удобство.ФильтрТолькоСвои");
	КонецЕсли;
	
	Возврат СтруктураФильтра;
КонецФункции	

&НаСервере
Процедура ОчиститьНаСервере()
	ОтборКонтакты = Неопределено;
	ОтборПользователь = Пользователи.ТекущийПользователь();
	ОтборПодразделение = Неопределено;
	ОтборКанал = Справочники.CRM_УчетныеЗаписиМессенджеров.ПустаяСсылка();
КонецПроцедуры

#КонецОбласти
