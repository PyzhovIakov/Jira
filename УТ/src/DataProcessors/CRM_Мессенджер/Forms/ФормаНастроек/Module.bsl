
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Работа с пользователями
	Если Параметры.Свойство("Пользователь") Тогда
		ТекущийПользователь = Параметры.Пользователь;
	КонецЕсли;
	
	Если Параметры.Свойство("ВариантОформленияМессенджера") Тогда
		ВариантОформленияМессенджера = Параметры.ВариантОформленияМессенджера;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда
		ТекущийПользователь = Пользователи.ТекущийПользователь();
	КонецЕсли; 
	
	// Загрузка настроек
	НастройкиХранилище = CRM_Взаимодействия.ПолучитьНастройкуРаботаСМессенджерами();
	
	СпособВеденияДиалогов = ?(
		НастройкиХранилище.Свойство("СпособВеденияДиалогов"),
		НастройкиХранилище.СпособВеденияДиалогов, 0);
	
КонецПроцедуры

// Процедура - обработчик события формы "ПередЗакрытием".
//
&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Если ЗавершениеРаботы Тогда
			Отказ = Истина;
			ТекстПредупреждения = НСтр("ru='При завершении работы в форме ""';en='Upon completion of work in the form of ""'") 
				+ Заголовок 
				+ НСтр("ru='"" все изменения будут утеряны.'");
			Возврат;
		Иначе
			Отказ = Истина;
			ОбратныйВызов = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
			ПоказатьВопрос(ОбратныйВызов,
				 НСтр("ru='Настройки были изменены. Сохранить изменения?';
				|en='Settings were changed. Save changes?'"),
				 РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик ответа на вопрос перед закрытием формы.
//
// Параметры:
//	Результат				- КодВозвратаДиалога	- Ответ на вопрос.
//	ДополнительныеПараметры	- Структура				- Структура дополнительных параметров.
//
&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Отказ = Ложь;
	РезультатФормы = Ложь;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		РезультатФормы = Истина;
		Отказ = НЕ ВыполнитьСохранениеНастроекПередЗакрытием();
	Иначе
		Модифицированность = Ложь;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Закрыть(РезультатФормы);
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытиемЗавершение()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	ВыполнитьСохранениеНастроекПередЗакрытием();
	
	Если СпособВеденияДиалогов = 0 Тогда
		CRM_ЦентрМониторингаКлиент.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.АРМДиалоги.Удобство.СпособВеденияДиалогов.ВыборСпособаВеденияДиалогаСтруктурированно");
	ИначеЕсли СпособВеденияДиалогов = 1 Тогда
		CRM_ЦентрМониторингаКлиент.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.АРМДиалоги.Удобство.СпособВеденияДиалогов.ВыборСпособаВеденияДиалогаЕдинымОкном");
	КонецЕсли;
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыполнитьСохранениеНастроекПередЗакрытием()
	
	МассивСтруктур = Новый Массив;
	
	ХранилищеОбщихНастроекСохранитьМассив(МассивСтруктур);
	
	СохранитьНастройки();
	
	Модифицированность = Ложь;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ХранилищеОбщихНастроекСохранитьМассив(МассивСтруктур, НужноОбновитьПовторноИспользуемыеЗначения = Ложь)
	
	ПользовательИБ =
		ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ТекущийПользователь.ИдентификаторПользователяИБ);
	
	Если ПользовательИБ <> Неопределено Тогда
		
		ИмяПользователяИБ = ПользовательИБ.Имя;
		Для Каждого Элемент Из МассивСтруктур Цикл
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(Элемент.Объект, Элемент.Настройка,
				 Элемент.Значение, , ИмяПользователяИБ,
				 НужноОбновитьПовторноИспользуемыеЗначения);
		КонецЦикла;
		
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Для выбранного пользователя не сопоставлен пользователь информационной базы. Настройки не были сохранены.';
			|en='The user of the information database is not mapped for the selected user. The settings were not saved.'"));
	КонецЕсли;
	
КонецПроцедуры // ХранилищеОбщихНастроекСохранитьМассив()

&НаСервере
Процедура СохранитьНастройки()
	
	СтруктураНастройки = Новый Структура;
	СтруктураНастройки.Вставить("СпособВеденияДиалогов", СпособВеденияДиалогов);
	
	CRM_Взаимодействия.СохранитьНастройкуРаботаСМессенджерами(СтруктураНастройки);
	
	/////////////////////////////////////////////
	
	Менеджер = РегистрыСведений.CRM_НастройкиПользователей.СоздатьМенеджерЗаписи();
	Менеджер.Настройка		= ПланыВидовХарактеристик.CRM_НастройкиПользователей.ВариантОформленияМессенджера;
	Менеджер.Пользователь	= ТекущийПользователь;
	Менеджер.Значение		= ВариантОформленияМессенджера;
	Менеджер.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти
