#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Канал") Тогда
		Канал = Параметры.Канал;
	КонецЕсли;
	Если Параметры.Свойство("Пользователь") Тогда
		Пользователь = Параметры.Пользователь;
	КонецЕсли;
	Если Параметры.Свойство("РолиПользователя") Тогда
		РолиПользователя = Параметры.РолиПользователя;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	Если Не ЗначениеЗаполнено(ВидПолучателя) Тогда
		ВидПолучателя = "Клиенты";
	КонецЕсли;
	ВидПолучателяПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаСервере
Процедура ВидПолучателяПриИзмененииНаСервере()
	Если ВидПолучателя = "Клиенты" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКлиенты;
	ИначеЕсли ВидПолучателя = "ПотенциальныеКлиенты" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПК;
	ИначеЕсли ВидПолучателя = "Пользователи" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПользователи;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидПолучателяПриИзменении(Элемент)
	ВидПолучателяПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДСПриАктивизацииСтроки(Элемент)
	ЗаполнитьДеревоКИ(Элемент.ТекущаяСтрока);
	РазвернутьСтрокиДереваКИ();
КонецПроцедуры


&НаСервере
Функция ДеревоКИВыборНаСервере(Контакт, УчетнаяЗапись, ТипМессенджера, ПредставлениеКИ, ЗначениеКИ)
	МодульМенеджера = CRM_РаботаСМессенджерамиСерверПовтИсп.МенеджерМессенджера(ТипМессенджера);
	ПараметрыМессенджера = МодульМенеджера.ПараметрыМессенджера();
	ТипКИ = МодульМенеджера.ТипКИМессенджера();
	
	Если ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
		idПолучателя = CRM_УправлениеКонтактнойИнформацией.НомерТелефонаПоЗначениюКИ(ЗначениеКИ, ТипКИ);
	Иначе
		idПолучателя = СтрЗаменить(ПредставлениеКИ, МодульМенеджера.НачалоАдресаСтраницыПользователя(), "");
	КонецЕсли;
	
	ПараметрыДиалога = Новый Структура;
	ПараметрыДиалога.Вставить("ID_Пользователя", idПолучателя);
	ПараметрыДиалога.Вставить("Группа", idПолучателя);
	ПараметрыДиалога.Вставить("ГруппаПредставление", "");
	ПараметрыДиалога.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	ПараметрыДиалога.Вставить("Контакт", Контакт);
	ПараметрыДиалога.Вставить("КонтактПредставление", Контакт.Наименование);
	ПараметрыДиалога.Вставить("Ответственный", Пользователь);

	Если ПараметрыМессенджера.ВозможностьПисатьПервыми Тогда
		Если УчетнаяЗапись.ТипМессенджера = "WhatsApp" Тогда
			ПараметрыДиалога.Вставить("ЭтоНомерТелефона", Истина);
		КонецЕсли;
		Диалог = Справочники.CRM_Диалоги.ПолучитьАктивныйДиалог(ПараметрыДиалога);
	Иначе
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			|	CRM_Диалоги.Ссылка КАК Ссылка,
			|	ВЫБОР
			|		КОГДА CRM_Диалоги.Статус = ЗНАЧЕНИЕ(Перечисление.CRM_СтатусыДиалогов.Закрыт)
			|			ТОГДА 10
			|		ИНАЧЕ 1
			|	КОНЕЦ КАК Порядок,
			|	CRM_Диалоги.Статус <> ЗНАЧЕНИЕ(Перечисление.CRM_СтатусыДиалогов.Закрыт) КАК Активный,
			|	CRM_Диалоги.Группа КАК Группа,
			|	CRM_Диалоги.ГруппаПредставление КАК ГруппаПредставление
			|ИЗ
			|	Справочник.CRM_Диалоги КАК CRM_Диалоги
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.CRM_СообщениеМессенджера КАК CRM_СообщениеМессенджера
			|		ПО CRM_Диалоги.Ссылка = CRM_СообщениеМессенджера.Диалог
			|ГДЕ
			|	CRM_Диалоги.ID_Пользователя = &ID_Пользователя
			|	И CRM_Диалоги.УчетнаяЗапись = &УчетнаяЗапись
			|	И CRM_Диалоги.Контакт = &Контакт
			|	И НЕ CRM_Диалоги.Служебный
			|	И НЕ CRM_СообщениеМессенджера.Ссылка = ЗНАЧЕНИЕ(Документ.CRM_СообщениеМессенджера.ПустаяСсылка)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Порядок");
		
		Запрос.УстановитьПараметр("ID_Пользователя", idПолучателя);
		Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
		Запрос.УстановитьПараметр("Контакт", Контакт);

		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.Активный Тогда
				Диалог = Выборка.Ссылка;
			Иначе
				ПараметрыДиалога.Вставить("Группа", Выборка.Группа);
				ПараметрыДиалога.Вставить("ГруппаПредставление", Выборка.ГруппаПредставление);
				Диалог = Справочники.CRM_Диалоги.ПолучитьАктивныйДиалог(ПараметрыДиалога);
			КонецЕсли;
		Иначе
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'В данный мессенджер нельзя писать первыми!
			                                           |С указанным контактом не было диалогов.'"));
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Диалог;
	
КонецФункции

&НаКлиенте
Процедура ДеревоКИВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Элемент.ТекущиеДанные.ВидКИ) Тогда
		Возврат;
	КонецЕсли;
	ДанныеКИ = Элемент.ТекущиеДанные;
	Диалог = ДеревоКИВыборНаСервере(ДанныеКИ.Контакт, ДанныеКИ.Значение, ДанныеКИ.ТипМессенджера,
		ДанныеКИ.ПредставлениеКИ, ДанныеКИ.ЗначениеКИ);
		
	Если Диалог <> Неопределено Тогда
		Закрыть(Диалог);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДеревоКИ(Ссылка)

	ДеревоКИ.ПолучитьЭлементы().Очистить();
	
	Если Ссылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДЗ = РеквизитФормыВЗначение("ДеревоКИ");
	
	ТипСсылки = ТипЗнч(Ссылка);
	Если ТипСсылки = Тип("СправочникСсылка.Партнеры") Тогда
		Запрос = ЗапросДеревоКИ_Партнеры(Ссылка);
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка.CRM_ПотенциальныеКлиенты") Тогда
		Запрос = ЗапросДеревоКИ_ПК(Ссылка);
	ИначеЕсли ТипСсылки = Тип("СправочникСсылка.Пользователи") Тогда
		Запрос = ЗапросДеревоКИ_Пользователи(Ссылка);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("РолиПользователя", РолиПользователя);
	
	ВыборкаКонтакт = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаКонтакт.Следующий() Цикл
		Если ВыборкаКонтакт.Контакт <> Ссылка Тогда
			СтрокаКонтакт = ДЗ.Строки.Добавить();
			СтрокаКонтакт.Значение = ВыборкаКонтакт.Контакт;
			СтрокаКонтакт.Иконка = 99;
			СтрокиКИ = СтрокаКонтакт.Строки;
		Иначе
			СтрокиКИ = ДЗ.Строки;
		КонецЕсли;
		
		ВыборкаКИ = ВыборкаКонтакт.Выбрать();
		Пока ВыборкаКИ.Следующий() Цикл
			СтрокаКИ = СтрокиКИ.Добавить();
			СтрокаКИ.Значение = ВыборкаКИ.УчетнаяЗапись;
			СтрокаКИ.ВидКИ = ВыборкаКИ.Вид;
			СтрокаКИ.Контакт = ВыборкаКИ.Контакт;
			СтрокаКИ.ПредставлениеКИ = ВыборкаКИ.Представление;
			СтрокаКИ.ЗначениеКИ = ВыборкаКИ.Значение;
			СтрокаКИ.ТипМессенджера = ВыборкаКИ.ТипМессенджера;
			СтрокаКИ.Иконка = ВыборкаКИ.Иконка;
		КонецЦикла;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДЗ, "ДеревоКИ");
	
КонецПроцедуры

&НаСервере
Функция ЗапросДеревоКИ_Партнеры(Ссылка)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	КонтактнаяИнформация.Ссылка КАК Контакт,
	|	КонтактнаяИнформация.Представление КАК Представление,
	|	КонтактнаяИнформация.Значение КАК Значение,
	|	КонтактнаяИнформация.Вид КАК Вид,
	|	CRM_УчетныеЗаписиМессенджеров.Ссылка КАК УчетнаяЗапись,
	|	CRM_УчетныеЗаписиМессенджеров.ТипМессенджера КАК ТипМессенджера,
	|	ВЫБОР
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""ВКонтакте""
	|			ТОГДА 1
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""InstagramDirect""
	|			ТОГДА 2
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Telegram""
	|			ТОГДА 3
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""WhatsApp""
	|			ТОГДА 4
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Facebook""
	|			ТОГДА 5
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Viber""
	|			ТОГДА 6
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Instagram""
	|			ТОГДА 7
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""ВКонтактеКомментарии""
	|			ТОГДА 8
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""СистемаВзаимодействия""
	|			ТОГДА 9
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""TelegramBot""
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Иконка
	|ИЗ
	|	Справочник.Партнеры.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ВидыКонтактнойИнформацииМессенджеров КАК CRM_ВидыКонтактнойИнформацииМессенджеров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.CRM_УчетныеЗаписиМессенджеров КАК CRM_УчетныеЗаписиМессенджеров
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.CRM_УчетныеЗаписиМессенджеров КАК НастройкиУчетныхЗаписей
	|				ПО (НастройкиУчетныхЗаписей.УчетнаяЗапись = CRM_УчетныеЗаписиМессенджеров.Ссылка)
	|					И (НастройкиУчетныхЗаписей.Пользователь = &Пользователь
	|						ИЛИ НастройкиУчетныхЗаписей.Пользователь В (&РолиПользователя)
	|						ИЛИ CRM_УчетныеЗаписиМессенджеров.РазрешитьПросмотрСообщенийВсемПользователям)
	|			ПО CRM_ВидыКонтактнойИнформацииМессенджеров.ТипМессенджера = CRM_УчетныеЗаписиМессенджеров.ТипМессенджера
	|				И CRM_УчетныеЗаписиМессенджеров.Включена
	|				И  НЕ CRM_УчетныеЗаписиМессенджеров.ПометкаУдаления
	|		ПО КонтактнаяИнформация.Вид = CRM_ВидыКонтактнойИнформацииМессенджеров.ВидКИ_Клиент
	|			И (КонтактнаяИнформация.Представление <> """")
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтактнаяИнформация.Ссылка,
	|	КонтактнаяИнформация.Представление,
	|	КонтактнаяИнформация.Значение,
	|	КонтактнаяИнформация.Вид,
	|	CRM_УчетныеЗаписиМессенджеров.Ссылка,
	|	CRM_УчетныеЗаписиМессенджеров.ТипМессенджера,
	|	ВЫБОР
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""ВКонтакте""
	|			ТОГДА 1
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""InstagramDirect""
	|			ТОГДА 2
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Telegram""
	|			ТОГДА 3
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""WhatsApp""
	|			ТОГДА 4
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Facebook""
	|			ТОГДА 5
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Viber""
	|			ТОГДА 6
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Instagram""
	|			ТОГДА 7
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""ВКонтактеКомментарии""
	|			ТОГДА 8
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""СистемаВзаимодействия""
	|			ТОГДА 9
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""TelegramBot""
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Иконка
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ВидыКонтактнойИнформацииМессенджеров КАК CRM_ВидыКонтактнойИнформацииМессенджеров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.CRM_УчетныеЗаписиМессенджеров КАК CRM_УчетныеЗаписиМессенджеров
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.CRM_УчетныеЗаписиМессенджеров КАК НастройкиУчетныхЗаписей
	|				ПО (НастройкиУчетныхЗаписей.УчетнаяЗапись = CRM_УчетныеЗаписиМессенджеров.Ссылка)
	|					И (НастройкиУчетныхЗаписей.Пользователь = &Пользователь
	|						ИЛИ НастройкиУчетныхЗаписей.Пользователь В (&РолиПользователя)
	|						ИЛИ CRM_УчетныеЗаписиМессенджеров.РазрешитьПросмотрСообщенийВсемПользователям)
	|			ПО CRM_ВидыКонтактнойИнформацииМессенджеров.ТипМессенджера = CRM_УчетныеЗаписиМессенджеров.ТипМессенджера
	|				И CRM_УчетныеЗаписиМессенджеров.Включена
	|				И  НЕ CRM_УчетныеЗаписиМессенджеров.ПометкаУдаления
	|		ПО КонтактнаяИнформация.Вид = CRM_ВидыКонтактнойИнформацииМессенджеров.ВидКИ_КЛ
	|			И (КонтактнаяИнформация.Представление <> """")
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка.Владелец = &Ссылка
	|ИТОГИ ПО
	|	Контакт");
	
	Возврат Запрос;
КонецФункции

&НаСервере
Функция ЗапросДеревоКИ_ПК(Ссылка)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	КонтактнаяИнформация.Ссылка КАК Контакт,
	|	КонтактнаяИнформация.Представление КАК Представление,
	|	КонтактнаяИнформация.Значение КАК Значение,
	|	КонтактнаяИнформация.Вид КАК Вид,
	|	CRM_УчетныеЗаписиМессенджеров.Ссылка КАК УчетнаяЗапись,
	|	CRM_УчетныеЗаписиМессенджеров.ТипМессенджера КАК ТипМессенджера,
	|	ВЫБОР
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""ВКонтакте""
	|			ТОГДА 1
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""InstagramDirect""
	|			ТОГДА 2
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Telegram""
	|			ТОГДА 3
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""WhatsApp""
	|			ТОГДА 4
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Facebook""
	|			ТОГДА 5
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Viber""
	|			ТОГДА 6
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Instagram""
	|			ТОГДА 7
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""ВКонтактеКомментарии""
	|			ТОГДА 8
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""СистемаВзаимодействия""
	|			ТОГДА 9
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""TelegramBot""
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Иконка
	|ИЗ
	|	Справочник.CRM_ПотенциальныеКлиенты.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ВидыКонтактнойИнформацииМессенджеров КАК CRM_ВидыКонтактнойИнформацииМессенджеров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.CRM_УчетныеЗаписиМессенджеров КАК CRM_УчетныеЗаписиМессенджеров
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.CRM_УчетныеЗаписиМессенджеров КАК НастройкиУчетныхЗаписей
	|				ПО (НастройкиУчетныхЗаписей.УчетнаяЗапись = CRM_УчетныеЗаписиМессенджеров.Ссылка)
	|					И (НастройкиУчетныхЗаписей.Пользователь = &Пользователь
	|						ИЛИ НастройкиУчетныхЗаписей.Пользователь В (&РолиПользователя)
	|						ИЛИ CRM_УчетныеЗаписиМессенджеров.РазрешитьПросмотрСообщенийВсемПользователям)
	|			ПО CRM_ВидыКонтактнойИнформацииМессенджеров.ТипМессенджера = CRM_УчетныеЗаписиМессенджеров.ТипМессенджера
	|				И CRM_УчетныеЗаписиМессенджеров.Включена
	|				И  НЕ CRM_УчетныеЗаписиМессенджеров.ПометкаУдаления
	|		ПО КонтактнаяИнформация.Вид = CRM_ВидыКонтактнойИнформацииМессенджеров.ВидКИ_ПК
	|			И (КонтактнаяИнформация.Представление <> """")
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка = &Ссылка
	|ИТОГИ ПО
	|	Контакт");
	Возврат Запрос;
	
КонецФункции

&НаСервере
Функция ЗапросДеревоКИ_Пользователи(Ссылка)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	КонтактнаяИнформация.Ссылка КАК Контакт,
	|	КонтактнаяИнформация.Представление КАК Представление,
	|	КонтактнаяИнформация.Значение КАК Значение,
	|	КонтактнаяИнформация.Вид КАК Вид,
	|	CRM_УчетныеЗаписиМессенджеров.Ссылка КАК УчетнаяЗапись,
	|	CRM_УчетныеЗаписиМессенджеров.ТипМессенджера КАК ТипМессенджера,
	|	ВЫБОР
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""ВКонтакте""
	|			ТОГДА 1
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""InstagramDirect""
	|			ТОГДА 2
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Telegram""
	|			ТОГДА 3
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""WhatsApp""
	|			ТОГДА 4
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Facebook""
	|			ТОГДА 5
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Viber""
	|			ТОГДА 6
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""Instagram""
	|			ТОГДА 7
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""ВКонтактеКомментарии""
	|			ТОГДА 8
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""СистемаВзаимодействия""
	|			ТОГДА 9
	|		КОГДА CRM_УчетныеЗаписиМессенджеров.ТипМессенджера = ""TelegramBot""
	|			ТОГДА 3
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Иконка
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ВидыКонтактнойИнформацииМессенджеров КАК CRM_ВидыКонтактнойИнформацииМессенджеров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.CRM_УчетныеЗаписиМессенджеров КАК CRM_УчетныеЗаписиМессенджеров
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.CRM_УчетныеЗаписиМессенджеров КАК НастройкиУчетныхЗаписей
	|				ПО (НастройкиУчетныхЗаписей.УчетнаяЗапись = CRM_УчетныеЗаписиМессенджеров.Ссылка)
	|					И (НастройкиУчетныхЗаписей.Пользователь = &Пользователь
	|						ИЛИ НастройкиУчетныхЗаписей.Пользователь В (&РолиПользователя)
	|						ИЛИ CRM_УчетныеЗаписиМессенджеров.РазрешитьПросмотрСообщенийВсемПользователям)
	|			ПО CRM_ВидыКонтактнойИнформацииМессенджеров.ТипМессенджера = CRM_УчетныеЗаписиМессенджеров.ТипМессенджера
	|				И CRM_УчетныеЗаписиМессенджеров.Включена
	|				И  НЕ CRM_УчетныеЗаписиМессенджеров.ПометкаУдаления
	|		ПО КонтактнаяИнформация.Вид = CRM_ВидыКонтактнойИнформацииМессенджеров.ВидКИ_Пользователь
	|			И (КонтактнаяИнформация.Представление <> """")
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка = &Ссылка
	|ИТОГИ ПО
	|	Контакт");
	Возврат Запрос;
	
КонецФункции

&НаКлиенте
Процедура РазвернутьСтрокиДереваКИ()
	
	СтрокиДерева = ЭтотОбъект.ДеревоКИ.ПолучитьЭлементы();
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Элементы.ДеревоКИ.Развернуть(СтрокаДерева.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
