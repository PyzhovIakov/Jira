
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СписокОбъектов.ЗагрузитьЗначения(CRM_КлиентыСервер.ПолучитьВсеЭлементыПВХОбъектыЖурнала());
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗарегистрировать(Команда)
	МассивПомеченныхОбъектов = ПолучитьМассивПомеченныхОбъектов(ЭтотОбъект);
	Если МассивПомеченныхОбъектов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаЗарегистрироватьЗавершение", ЭтотОбъект, Истина);
	ПоказатьВопрос(ОписаниеОповещения,
		 НСтр("ru='Выбраные объекты будут зарегистрированы в журнале. Продолжить?';
		|en='Selected objects will be register in log. Continue?'"),
		 РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура КомандаРазрегистрировать(Команда)
	МассивПомеченныхОбъектов = ПолучитьМассивПомеченныхОбъектов(ЭтотОбъект);
	Если МассивПомеченныхОбъектов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаЗарегистрироватьЗавершение", ЭтотОбъект, Ложь);
	ПоказатьВопрос(ОписаниеОповещения,
		 НСтр("ru='Внимание! Удаление документов из Журнала может привести к некорректной работе связывания документов с Интересами и отображения их в Ленте. Продолжить?';
	                                        |en='Attention! Removing documents from the Log may cause an incorrect work of the documents linking to Leads and displaying them in the Feed. Continue?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗарегистрироватьЗавершение(Ответ, Зарегистрировать) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗарегистрироватьРазрегистрироватьОбъектыНаСервере(Зарегистрировать);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбратьПериод(Команда)
	ДиалогВыбораПериода = Новый ДиалогРедактированияСтандартногоПериода();
	ДиалогВыбораПериода.Период.ДатаНачала = ПериодНачало;
	ДиалогВыбораПериода.Период.ДатаОкончания = ПериодОкончание;
	Если ТипЗнч(ОтборТекущийВариантСтандартногоПериода) = Тип("ВариантСтандартногоПериода") Тогда
		ДиалогВыбораПериода.Период.Вариант = ОтборТекущийВариантСтандартногоПериода;
		Если НачалоДня(ДиалогВыбораПериода.Период.ДатаНачала) <> НачалоДня(ПериодНачало)
			 Или НачалоДня(ДиалогВыбораПериода.Период.ДатаОкончания) <> НачалоДня(ПериодОкончание) Тогда
			ДиалогВыбораПериода.Период.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
		КонецЕсли;
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаВыбратьПериодЗавершение", ЭтотОбъект);
	ДиалогВыбораПериода.Показать(ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбратьПериодЗавершение(Период, ДополнительныеПараметры) Экспорт
	Если Период <> Неопределено Тогда
		ПериодНачало = Период.ДатаНачала;
		ПериодОкончание = Период.ДатаОкончания;
		ОтборТекущийВариантСтандартногоПериода = Период.Вариант;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьМассивПомеченныхОбъектов(Форма)
	МассивПомеченныхОбъектов = Новый Массив();
	
	Для Каждого СтрокаСписка Из Форма.СписокОбъектов Цикл
		Если СтрокаСписка.Пометка Тогда
			МассивПомеченныхОбъектов.Добавить(СтрокаСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивПомеченныхОбъектов;
КонецФункции

&НаСервере
Процедура ЗарегистрироватьРазрегистрироватьОбъектыНаСервере(Зарегистрировать)
	ДатаНачала    = НачалоДня(ПериодНачало);
	ДатаОкончания = ?(ЗначениеЗаполнено(ПериодОкончание), КонецДня(ПериодОкончание), ПериодОкончание);
	Обработки.CRM_РегистрацияОбъектовВУниверсальномЖурналеДокументов.ЗарегистрироватьРазрегистрироватьОбъекты(ПолучитьМассивПомеченныхОбъектов(ЭтотОбъект),
		 Зарегистрировать, ДатаНачала,
		 ДатаОкончания);
КонецПроцедуры

#КонецОбласти
