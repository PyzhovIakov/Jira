
#Область ОписаниеПеременных

&НаКлиенте
Перем НачальноеЗначениеНастройкиТелефония;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВремяНачалаЗамера = ОценкаПроизводительности.НачатьЗамерВремени();
	
	ТекущийПользовательАдминистратор = РольДоступна("ПолныеПрава") ИЛИ РольДоступна("сфпУправлениеМаршрутизацией");
	ТекущийПользователь = сфпСофтФонПроСервер.сфпТекущийПользователь();
		
	ЗаполнитьПерсональныеНастройкиНаСервере();
	
	// +CRM
	ЦветФонаКнопки = ЦветаСтиля.CRM_СерыйДляВторостепенныхФункций;
	ЦветФонаВыбраннойКнопки = ЦветаСтиля.CRM_ОсновнойГолубой;
	// -CRM
	
	МассивДействийЗвонка = сфпСофтФонПроСерверПереопределяемый.сфпПолучитьМассивДоступныхДействий();
	Для Каждого ТекДействие Из МассивДействийЗвонка Цикл
		Элементы.НастройкаДействиеВходящийЗвонок.СписокВыбора.Добавить(ТекДействие.Наименование, ТекДействие.Наименование);
		Элементы.НастройкаДействиеИсходящийЗвонок.СписокВыбора.Добавить(ТекДействие.Наименование, ТекДействие.Наименование);
		
		Если ПустаяСтрока(ТекДействие.Действие) ИЛИ ТекДействие.Действие = "сфпОткрытьПанельЗвонка" Тогда
			Продолжить;
		КонецЕсли;
		
		ДействияПоЗвонку.Добавить(ТекДействие.Действие, ТекДействие.Наименование);
	КонецЦикла;	
	
	ПринадлежностьЗвонка = 1;
	
	Если CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		ТекущиеПодчиненныеПодразделения.ЗагрузитьЗначения(сфпСофтФонПроСерверПовтИсп.ПолучитьПодразделенияВКоторыхРуководитель(ТекущийПользователь));
	Иначе
		// Тут реквизит "ТекущийРуководитель" ссылается на спр. "ФизическиеЛица"
		ТекущиеПодчиненныеПодразделения.ЗагрузитьЗначения(сфпСофтФонПроСерверПовтИсп.ПолучитьПодразделенияВКоторыхРуководитель(ТекущийПользователь.ФизическоеЛицо));
	КонецЕсли;
	
	ПолучитьСписокДоступныхПодразделений = ПолучитьСписокДоступныхПодразделений();
	Если Не ПолучитьСписокДоступныхПодразделений.Количество() = 0 Тогда
		Для Каждого ЭлементСпискаДоступныхПодразделений Из ПолучитьСписокДоступныхПодразделений Цикл
			ТекущиеПодчиненныеПодразделения.Добавить(ЭлементСпискаДоступныхПодразделений.Значение);
		КонецЦикла;
	КонецЕсли;
	
	МассивПрослушиваемыхПользователей = сфпСофтФонПроСервер.сфпПолучитьМассивПрослушиваемыхПользователей(ТекущийПользователь);
	Если Не ТекущиеПодчиненныеПодразделения.Количество() = 0 Тогда
		ВсеПрослушиваемыеПользователи.ЗагрузитьЗначения(МассивПрослушиваемыхПользователей);
		ТекПользователь = ВсеПрослушиваемыеПользователи.НайтиПоЗначению(ТекущийПользователь);
		Если ТекПользователь = Неопределено Тогда
			ВсеПрослушиваемыеПользователи.Добавить(ТекущийПользователь);
		КонецЕсли;
	Иначе
		ВсеПрослушиваемыеПользователи.Добавить(ТекущийПользователь);
	КонецЕсли;
	
	ТекПозиция = МассивПрослушиваемыхПользователей.Найти(ТекущийПользователь);
	Если ТекПозиция <> Неопределено Тогда
		МассивПрослушиваемыхПользователей.Удалить(ТекПозиция);
	КонецЕсли;
	ТекущиеПрослушиваемыеПользователи.ЗагрузитьЗначения(МассивПрослушиваемыхПользователей);
	
	Если ТекущиеПодчиненныеПодразделения.Количество() = 0 И ТекущиеПрослушиваемыеПользователи.Количество() = 0 Тогда
		Элементы.ГруппаФильтрЗвонковПоПринадлежности.Видимость = Ложь;
	КонецЕсли;
	
	ЭлементыСпискаОтбора = ТелефонныеЗвонки.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	Для Каждого ТекущийОтбор Из ЭлементыСпискаОтбора Цикл
		Если ТекущийОтбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
			Если ТекущийОтбор.Представление = "Принадлежность звонков" Тогда
				Для Каждого ПодчиненныйОтбор Из ТекущийОтбор.Элементы Цикл
					Если ПодчиненныйОтбор.Представление = "Мои звонки" Тогда
						Для Каждого ЭлементГруппы Из ПодчиненныйОтбор.Элементы Цикл
							ЭлементГруппы.ПравоеЗначение = ВсеПрослушиваемыеПользователи;
						КонецЦикла;

					ИначеЕсли ПодчиненныйОтбор.Представление = "Мои подразделения" Тогда
						Для Каждого ЭлементГруппы Из ПодчиненныйОтбор.Элементы Цикл
							ЭлементГруппы.ПравоеЗначение = ТекущиеПодчиненныеПодразделения;
						КонецЦикла;
					
					ИначеЕсли ПодчиненныйОтбор.Представление = "Контролируемые пользователи" Тогда
						Для Каждого ЭлементГруппы Из ПодчиненныйОтбор.Элементы Цикл
							ЭлементГруппы.ПравоеЗначение = ТекущиеПрослушиваемыеПользователи;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
        КонецЕсли;
	КонецЦикла;

	ИспользуетсяОблачнаяАТС = Константы.сфпИспользоватьОблачнуюТелефонию.Получить();
	
	ДоступностьКомандAPI = сфпСофтФонПроСервер.ПолучитьДоступностьКомандAPI();
	ДоступностьКомандAPI_Ответ = ДоступностьКомандAPI.Ответ;
	ДоступностьКомандAPI_Завершить = ДоступностьКомандAPI.Завершить;
	ДоступностьКомандAPI_Пауза = ДоступностьКомандAPI.Пауза;
	ДоступностьКомандAPI_Перевод = ДоступностьКомандAPI.Перевод;

	Элементы.ТелефонныеЗвонкиНецелевойЗвонок.Видимость = ТекущийПользовательАдминистратор;
		
	Если НЕ ТекущийПользовательАдминистратор Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ТелефонныеЗвонки,
			 "НецелевойЗвонок", Ложь, ВидСравненияКомпоновкиДанных.Равно, ,
			 Истина);
	КонецЕсли;
	
	// +CRM
	Элементы.ПринятьОбращение.Видимость = (Метаданные.Обработки.Найти("CRM_МастерРегистрацииОбращения") <> Неопределено);
	Элементы.СоздатьПоручение.Видимость = (Метаданные.Обработки.Найти("CRM_МастерФормированияПоручений") <> Неопределено);

	// +Рабочий стол
	Если Метаданные.ОбщиеМодули.Найти("CRM_ОбщегоНазначенияПовтИсп") <> Неопределено Тогда
		МодульCRM_ОбщегоНазначенияПовтИсп = ОбщегоНазначения.ОбщийМодуль("CRM_ОбщегоНазначенияПовтИсп");
	КонецЕсли;
	Если Метаданные.ОбщиеМодули.Найти("CRM_СобытияФорм") <> Неопределено Тогда
		МодульCRM_СобытияФорм = ОбщегоНазначения.ОбщийМодуль("CRM_СобытияФорм");
		МодульCRM_СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	КонецЕсли;
	СуществуетМодуль_CRM_СобытияФормКлиент = (Метаданные.ОбщиеМодули.Найти("CRM_СобытияФормКлиент") <> Неопределено);
	СуществуетМодуль_CRM_РабочийСтолКлиент = (Метаданные.ОбщиеМодули.Найти("CRM_РабочийСтолКлиент") <> Неопределено);
	// -Рабочий стол	
	// -CRM

	CRM_ОбщегоНазначенияСервер.ЗакончитьЗамерВремениСозданияФормы(ЭтотОбъект, ВремяНачалаЗамера);
	
КонецПроцедуры	//	ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	CRM_ОбщегоНазначенияКлиент.НачатьЗамерВремениОткрытияФормы(ЭтотОбъект);
	
	ОбновитьЛиниюПользователя();
		
	ФильтрЗвонковПоВиду("Все", Ложь);
	ФильтрЗвонковПоПериоду("Сегодня");
	
	ОтобразатьКонтактыНомеронабиратель(Истина);

	ПроверитьНеобходимостьНастройки();
	
	ТекГруппировка = АбонентыТелефонныхКниг.Группировка.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ТекГруппировка.Использование = Истина;
	ТекГруппировка.Поле = Новый ПолеКомпоновкиДанных("ТелефоннаяКнига");
	
	Если ТребуетсяНастройка И ТекущийПользовательАдминистратор Тогда
		НастройкиТелефонииОбщии();
	КонецЕсли;
	
	НачальноеЗначениеНастройкиТелефония = НастройкаИспользоватьСофтФон;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНеобходимостьНастройки()
	
	ТребуетсяНастройка = сфпСофтФонПроСервер.сфпТребуетсяНастройкаТелефонии();
	
	Элементы.ГруппаФильтрЗвонковПоПринадлежности.Доступность = НЕ ТребуетсяНастройка;
	Элементы.ДополнениеТелефонныеЗвонкиСтрокаПоиска.Доступность = НЕ ТребуетсяНастройка;
	Элементы.ПрослушатьЗапись.Доступность = НЕ ТребуетсяНастройка;
	Элементы.СформироватьОтчет.Доступность = НЕ ТребуетсяНастройка;
	
	Элементы.ГруппаЗвонкиПанель.Видимость = НЕ ТребуетсяНастройка;
	Элементы.ДекорацияНеобходимостьНастройки.Видимость = ТребуетсяНастройка;
	
	Если ТекущийПользовательАдминистратор Тогда
		Элементы.ДекорацияНеобходимостьНастройки.Заголовок = "Необходимо выполнить настройку телефонии.";

	Иначе	
		Элементы.ДекорацияНеобходимостьНастройки.Заголовок = "Для настройки телефонии обратитесь к администратору.";
		Элементы.НастройкиТелефонииОбщии.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ НастройкаИспользоватьСофтФон Тогда
		НастройкиТелефонииПерсональные();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЛиниюПользователя() Экспорт // АПК:78 - исключить из проверки.
	
	ЗаголовокСтатуса = "Нет линии";

	Если ИспользуетсяОблачнаяАТС Тогда
		ВнутреннийНомер = сфпСофтФонПроСервер.ПолучитьВнутреннийНомерПоПользователю(ТекущийПользователь);
		
	Иначе
		ВнутреннийНомер = сфпСофтФонПроСервер.сфпТекущийВнутреннийНомер(ТекущийПользователь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВнутреннийНомер) Тогда
		ЗаголовокСтатуса = "Линия " + ВнутреннийНомер;
	КонецЕсли;
	
	Элементы.КартинкаСтатусЛинииОнлайн.Видимость = НЕ ТребуетсяНастройка;
	Элементы.КартинкаСтатусЛинииОфлайн.Видимость = ТребуетсяНастройка;
		
	Элементы.ДекорацияСтатусТекущегоПодключения.Заголовок = ЗаголовокСтатуса;
	
	Элементы.НастройкаЗакрыватьПанельПриЗавершенииРаботы.Видимость = НЕ ИспользуетсяОблачнаяАТС;
	Элементы.ГруппаНастройкаВнутреннийНомер.Видимость = НЕ ИспользуетсяОблачнаяАТС;

	ДоступностьКомандAPI = сфпСофтФонПроСервер.ПолучитьДоступностьКомандAPI();
	ДоступностьКомандAPI_Ответ = ДоступностьКомандAPI.Ответ;
	ДоступностьКомандAPI_Завершить = ДоступностьКомандAPI.Завершить;
	ДоступностьКомандAPI_Пауза = ДоступностьКомандAPI.Пауза;
	ДоступностьКомандAPI_Перевод = ДоступностьКомандAPI.Перевод;

КонецПроцедуры

&НаСервере
Процедура ОбновитьКонтактыПанелиЗвонка()
	сфпСофтФонПроСервер.ОбновитьКонтактыПанелиЗвонка(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	сфпСофтФонПроКлиент.ОбработкаОповещенияПанелиЗвонка(ЭтотОбъект, ИмяСобытия, Параметр, Источник, ИспользуетсяОблачнаяАТС);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область СписокТелефонныхЗвонков

&НаКлиенте
Процедура ТелефонныеЗвонкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элементы.ТелефонныеЗвонки.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		CRM_ЦентрМониторингаКлиент.НачатьЗамерОперацииБизнесСтатистики(
			"CRM_Статистика.Взамодействия.ТелефонныйЗвонок.ДлительностьСценариев.ВремяОткрытияФормы", Истина);
		Оповещение = Новый ОписаниеОповещения("ПриЗакрытииФормыТелефонногоЗвонка", ЭтотОбъект);
		ПараметрыФормы = Новый Структура("Ключ", ТекДанные.Ссылка);
		ОткрытьФорму("Документ.ТелефонныйЗвонок.Форма.сфпФормаДокумента", ПараметрыФормы, ЭтотОбъект, , , , Оповещение);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииФормыТелефонногоЗвонка(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Элементы.ТелефонныеЗвонки.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура АбонентыТелефонныхКнигВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ТекДанные.Свойство("ГруппировкаСтроки") Тогда
	    НастроитьОтображениеКонтактов("Развернуть", ТекДанные.ГруппировкаСтроки.Ключ);

	ИначеЕсли НЕ ЗначениеЗаполнено(ИдентификаторЗвонка) И (ЗначениеЗаполнено(ТекДанные.НомерТелефона)
		 ИЛИ ЗначениеЗаполнено(ТекДанные.ВнутреннийНомер)) Тогда
		Если Элементы.АбонентыТелефонныхКниг.ТекущийЭлемент = Элементы.АбонентыТелефонныхКнигВнутреннийНомер
			 ИЛИ Элементы.АбонентыТелефонныхКниг.ТекущийЭлемент = Элементы.АбонентыТелефонныхКнигНомерТелефона Тогда
			ТекущийЭлементДанныхКонтактаЗвонка = ТекущийЭлементДанныхКонтакта;
			ТекущаяЯчейкаДанныхКонтактаЗвонка = ТекущаяЯчейкаДанныхКонтакта;
			ПоднятьТрубку(, ТекущийЗвонокНомерАбонента);

		Иначе
			ПоднятьТрубку();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура АбонентыТелефонныхКнигПриАктивизацииЯчейки(Элемент = Неопределено)
	сфпСофтФонПроКлиент.ПанельЗвонка_АбонентыТелефонныхКнигПриАктивизацииЯчейки(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура АбонентыТелефонныхКнигПередРазворачиванием(Элемент, Строка, Отказ)
	
	Отказ = Истина;
	НастроитьОтображениеКонтактов("Развернуть", Строка.Ключ);

КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СписокВыбора = Новый СписокЗначений();
	СписокВыбора.Добавить("сфпЕженедельныйОтчет", "Еженедельный отчет по звонкам");
	СписокВыбора.Добавить("сфпОтчетПоВсемЗвонкам", "Отчет по всем звонкам");
	СписокВыбора.Добавить("сфпОтчетПоПропущеннымЗвонкам", "Отчет по пропущенным звонкам");
	СписокВыбора.Добавить("сфпОтчетПоСовершеннымЗвонкам", "Отчёт по совершенным звонкам");
	
	Оповещение = Новый ОписаниеОповещения("СформироватьОтчетПродолжить", ЭтотОбъект);
	ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элементы.СформироватьОтчет);

КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетПродолжить(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ПараметрыОткрытия = Новый Структура("Отчет, ТипОтчета, ИмяОтчета, КлючВарианта, СформироватьПриОткрытии");
		ПараметрыОткрытия.ТипОтчета = "Внутренний";
		ПараметрыОткрытия.ИмяОтчета = ВыбранныйЭлемент.Значение;
		ПараметрыОткрытия.КлючВарианта = СтрЗаменить(ВыбранныйЭлемент.Значение, "сфп", "");
		ПараметрыОткрытия.СформироватьПриОткрытии = Истина;
	
		ВариантыОтчетовКлиент.ОткрытьФормуОтчета(Неопределено, ПараметрыОткрытия, Новый Структура);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКоличествоЗвонков()
	сфпСофтФонПроКлиент.Телефония_ОбновитьКоличествоЗвонков(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
// Процедура устанавливает текущие отборы списка Телефонных звонков
//
// Параметры:
//	Нет.
//
Процедура УстановитьОтборыТелефонныхЗвонков()
	
    ЭлементыСпискаОтбора = ТелефонныеЗвонки.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	Для Каждого ТекущийОтбор Из ЭлементыСпискаОтбора Цикл
		Если ТекущийОтбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
			Если ТекущийОтбор.Представление = "Входящие" Тогда
				Если ТекущийФильтрЗвонков = "Входящие" Тогда
					ТекущийОтбор.Использование = Истина;

				Иначе
		            ТекущийОтбор.Использование = Ложь;
				КонецЕсли;

			ИначеЕсли ТекущийОтбор.Представление = "Исходящие" Тогда
				Если ТекущийФильтрЗвонков = "Исходящие" Тогда
					ТекущийОтбор.Использование = Истина;

			    Иначе
		            ТекущийОтбор.Использование = Ложь;
				КонецЕсли;

			ИначеЕсли ТекущийОтбор.Представление = "Пропущенные" Тогда
				Если ТекущийФильтрЗвонков = "Пропущенные" Тогда
					ТекущийОтбор.Использование	= Истина;

			    Иначе
		            ТекущийОтбор.Использование	= Ложь;
				КонецЕсли;
				
			ИначеЕсли ТекущийОтбор.Представление = "Период звонков" Тогда
				Если НЕ ПустаяСтрока(ТекущийПериодЗвонков) Тогда
					Если НЕ ЗначениеЗаполнено(НачалоПериодаЗвонков) И НЕ ЗначениеЗаполнено(КонецПериодаЗвонков) Тогда
						ТекущийОтбор.Использование = Ложь;

					Иначе	
						ТекущийОтбор.Использование = Истина;
						
						Для Каждого ЭлементГруппы Из ТекущийОтбор.Элементы Цикл
							Если ЭлементГруппы.Представление = "Начало периода" Тогда
								ЭлементГруппы.ПравоеЗначение = НачалоПериодаЗвонков;
								ЭлементГруппы.Использование = ЗначениеЗаполнено(НачалоПериодаЗвонков);

							ИначеЕсли ЭлементГруппы.Представление = "Конец периода" Тогда
								ЭлементГруппы.ПравоеЗначение = КонецПериодаЗвонков;
								ЭлементГруппы.Использование = ЗначениеЗаполнено(КонецПериодаЗвонков);
							КонецЕсли;	
						КонецЦикла;
					КонецЕсли;	
					
				Иначе
		            ТекущийОтбор.Использование = Ложь;
				КонецЕсли;
				
			ИначеЕсли ТекущийОтбор.Представление = "Принадлежность звонков" Тогда
				Для Каждого ПодчиненныйОтбор Из ТекущийОтбор.Элементы Цикл
					Если ПодчиненныйОтбор.Представление = "Мои звонки" Тогда
						ПодчиненныйОтбор.Использование = (ПринадлежностьЗвонка <> 2);
					ИначеЕсли ПодчиненныйОтбор.Представление = "Мои подразделения" Тогда
						Если ПринадлежностьЗвонка = 1 Тогда
							ПодчиненныйОтбор.Использование = Ложь;
							
						ИначеЕсли ТекущиеПодчиненныеПодразделения.Количество() > 0 Тогда
							ПодчиненныйОтбор.Использование = Истина;
						КонецЕсли;
						
					ИначеЕсли ПодчиненныйОтбор.Представление = "Контролируемые пользователи" Тогда
						Если ПринадлежностьЗвонка = 1 Тогда
							ПодчиненныйОтбор.Использование = Ложь;

						ИначеЕсли ТекущиеПрослушиваемыеПользователи.Количество() > 0 Тогда
							ПодчиненныйОтбор.Использование = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
            КонецЕсли;
        КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // УстановитьОтборыТелефонныхЗвонков()

&НаКлиенте
Процедура ТелефонныеЗвонкиПриАктивизацииСтроки(Элемент = Неопределено)
	сфпСофтФонПроКлиент.Телефония_ТелефонныеЗвонкиПриАктивизацииСтроки(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ПанельЗвонков

&НаКлиенте
Процедура ПринятьОбращение(Команда)
	
	СтруктураЗвонка = Новый Структура();
	Если ЗначениеЗаполнено(ИдентификаторЗвонка) Тогда
		СтруктураЗвонка = Новый Структура("ИдентификаторЗвонка,Контакт,НомерТелефона",
			ИдентификаторЗвонка, ТекущийЗвонокКонтакт, ТекущийЗвонокНомерАбонента);
		
	Иначе
		ТекДанные = Элементы.ТелефонныеЗвонки.ТекущиеДанные;
		Если ТекДанные <> Неопределено Тогда
			СтруктураЗвонка = Новый Структура("ИдентификаторЗвонка,Контакт,НомерТелефона,НовыйЗвонок",
				ТекДанные.сфпИдентификаторЗвонка, ТекДанные.АбонентКонтакт, ТекДанные.АбонентКакСвязаться, ТекДанные.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураЗвонка.Свойство("ИдентификаторЗвонка") Тогда
		сфпСофтФонПроКлиентПереопределяемый.сфпРегистрацияОбращения(СтруктураЗвонка);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПрослушатьЗапись(Команда)
	
	ТекДанные = Элементы.ТелефонныеЗвонки.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ПараметрыЗвонка = Новый Структура("ИдентификаторЗвонка, ИдентификаторЗаписи, Ответственный,
			| ДатаНачала, НомерТелефона, ВнутреннийНомер",
			ТекДанные.сфпИдентификаторЗвонка, ТекДанные.сфпИдентификаторЗаписи, ТекДанные.Ответственный, ТекДанные.Дата, ТекДанные.АбонентКакСвязаться, ТекДанные.ВнутреннийНомер);
		сфпСофтФонПроКлиент.НачатьПрослушиваниеЗаписиРазговора(ПараметрыЗвонка, ЭтотОбъект, Элементы.ПрослушатьЗапись);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоручение(Команда)
	
	ПараметрЗвонок = Неопределено;
	
	Если ЗначениеЗаполнено(ИдентификаторЗвонка) Тогда
		ПараметрЗвонок = ТекущийЗвонок;
	Иначе
		ТекДанные = Элементы.ТелефонныеЗвонки.ТекущиеДанные;
		Если ТекДанные <> Неопределено Тогда
			ПараметрЗвонок = ТекДанные.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрЗвонок) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОснования = Новый Структура("Основание,КартаМаршрута", ПараметрЗвонок,
		 ПредопределенноеЗначение("Справочник.CRM_КартыМаршрутов.Поручение"));
	ПараметрыФормы = Новый Структура("Основание", СтруктураОснования);
	ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаПоручения", ПараметрыФормы, ЭтотОбъект, 
					УникальныйИдентификатор, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиТелефонииПерсональные(Команда = Неопределено)

	ЗаполнитьПерсональныеНастройкиНаСервере();

	НастройкаПривязатьВнутреннийНомерПриИзменении();
	
	Элементы.СтраницыПанель.ТекущаяСтраница = Элементы.СтраницаПерсональныеНастройки;
	Элементы.ГруппаПравая.Видимость = Истина;

КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)

	Настройки = Новый Структура();
	Настройки.Вставить("сфпИспользоватьСофтФон", НастройкаИспользоватьСофтФон);
	Настройки.Вставить("сфпЗакрыватьПанельПриЗавершенииРаботы", НастройкаЗакрыватьПанельПриЗавершенииРаботы);
	Настройки.Вставить("сфпПривязатьВнутреннийНомер", НастройкаПривязатьВнутреннийНомер);
	Настройки.Вставить("сфпЛогинНаСерверСофтФон", НастройкаЛогинНаСерверСофтФон);
	Настройки.Вставить("сфпПарольНаСерверСофтФон", НастройкаПарольНаСерверСофтФон);
	Настройки.Вставить("сфпДействиеПриВходящемЗвонке", НастройкаДействиеВходящийЗвонок);
	Настройки.Вставить("сфпДействиеПриИсходящемЗвонке", НастройкаДействиеИсходящийЗвонок);
	
	Если НастройкаПривязатьВнутреннийНомер Тогда
		Настройки.Вставить("сфпТекущийВнутреннийНомер", НастройкаТекущийВнутреннийНомер);
		Если ПустаяСтрока(НастройкаЛогинНаСерверСофтФон) Или ПустаяСтрока(НастройкаПарольНаСерверСофтФон) Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Заполните логин и пароль на сервер СофтФона!'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СохранитьНастройкиНаСервере(ТекущийПользователь, Настройки);
	
	ПараметрыПриложения["сфпЗакрыватьПанельПриЗавершенииРаботы"] =
		сфпСофтФонПроСервер.сфпПолучитьЗначениеНастройкиПользователя("сфпЗакрыватьПанельПриЗавершенииРаботы");
		
	Элементы.СтраницыПанель.ТекущаяСтраница = Элементы.СтраницаПанельЗвонка;
	
	Если НастройкаИспользоватьСофтФон Тогда
		сфпСофтФонПроКлиент.сфпПодключитьСофтФон();

	Иначе
		сфпСофтФонПроКлиент.сфпОтключитьСофтФон();
	КонецЕсли;
	
	ОбновитьЛиниюПользователя();
	
	Если Не НачальноеЗначениеНастройкиТелефония Тогда
		ПроверитьНеобходимостьДобавитьНастройкуНаРабочийСтол();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьНастройки(Команда)
	Элементы.СтраницыПанель.ТекущаяСтраница = Элементы.СтраницаПанельЗвонка;
КонецПроцедуры

&НаКлиенте
Процедура ПоднятьТрубку(Команда = Неопределено, ВыбранныйНомерКонтакта = Неопределено)
	
	СтруктураЗвонка = Новый Структура("НомераТелефонов,АбонентКонтакт,ДанныеЗаполнения,
		|ИдентификаторЗвонка",
		 Новый СписокЗначений());
	
	Если ЗначениеЗаполнено(ИдентификаторЗвонка) Тогда
		Если НЕ ИспользуетсяОблачнаяАТС Тогда
			сфпПанельУправления.AnswerCall(ТекущийЗвонокНомерЛинии, ИдентификаторЗвонка);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НабранныйНомер) Тогда
			СтруктураЗвонка.НомераТелефонов.Добавить(НабранныйНомер);
			НабранныйНомер = "";
		КонецЕсли;
	
	Иначе
		Если ЗначениеЗаполнено(НабранныйНомер) Тогда
			СтруктураЗвонка.НомераТелефонов.Добавить(НабранныйНомер);
			НабранныйНомер = "";
		
		ИначеЕсли ТекущийЭлементДанныхКонтакта = "ТелефонныеЗвонки" Тогда
			ТекДанные = Элементы.ТелефонныеЗвонки.ТекущиеДанные;
			Если ТекДанные <> Неопределено Тогда
				Если ЗначениеЗаполнено(ТекДанные.АбонентКакСвязаться) Тогда
					СтруктураЗвонка.НомераТелефонов.Добавить(ТекДанные.АбонентКакСвязаться);
					СтруктураЗвонка.АбонентКонтакт = ТекДанные.АбонентКонтакт;
					СтруктураЗвонка.ДанныеЗаполнения = Новый Структура("ВзаимодействиеОснование", ТекДанные.Ссылка);
				КонецЕсли;	
			КонецЕсли;
			
		ИначеЕсли ТекущийЭлементДанныхКонтакта = "АбонентыТелефонныхКниг" Тогда
			ТекДанные = Элементы.АбонентыТелефонныхКниг.ТекущиеДанные;
			Если ТекДанные <> Неопределено Тогда
				Если ЗначениеЗаполнено(ТекДанные.Контакт) Тогда
					СтруктураЗвонка.АбонентКонтакт = ТекДанные.Контакт;
					
				ИначеЕсли ЗначениеЗаполнено(ТекДанные.ИдентификаторАТС) Тогда
					СтруктураЗвонка.АбонентКонтакт = ТекДанные.ИдентификаторАТС;	
				КонецЕсли;
					
				Если ЗначениеЗаполнено(ВыбранныйНомерКонтакта) Тогда
					СтруктураЗвонка.НомераТелефонов.Добавить(ВыбранныйНомерКонтакта);
					
				ИначеЕсли ЗначениеЗаполнено(ТекДанные.Контакт) Тогда
					ТелефоныКонтакта = сфпСофтФонПроСервер.сфпПолучитьМассивТелефоновИФаксов(ТекДанные.Контакт);
					Для Каждого Телефон Из ТелефоныКонтакта Цикл
						СтруктураЗвонка.НомераТелефонов.Добавить(Телефон.Представление, СокрЛП(Телефон.Вид) + ": " 
							+ Телефон.Представление);
					КонецЦикла;
					
				Иначе
					Если ТекущаяЯчейкаДанныхКонтакта = "АбонентыТелефонныхКнигНомерТелефона" Тогда
						Если ЗначениеЗаполнено(ТекДанные.НомерТелефона) Тогда
							СтруктураЗвонка.НомераТелефонов.Добавить(ТекДанные.НомерТелефона);
						КонецЕсли;	
						
					ИначеЕсли ТекущаяЯчейкаДанныхКонтакта = "АбонентыТелефонныхКнигВнутреннийНомер" Тогда
						Если ЗначениеЗаполнено(ТекДанные.ВнутреннийНомер) Тогда
							СтруктураЗвонка.НомераТелефонов.Добавить(ТекДанные.ВнутреннийНомер);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураЗвонка.НомераТелефонов.Количество() > 0 Тогда
		Если НЕ ЗначениеЗаполнено(СтруктураЗвонка.АбонентКонтакт) Тогда
			ГолыйНомерТелефона =
				сфпСофтФонПроСервер.сфпУбратьИзНомераТелефонаВсеПрефиксы(СтруктураЗвонка.НомераТелефонов[0].Значение);
			МассивЗвонящих = сфпСофтФонПроСервер.сфпНайтиОбъектВРегистреПоТелефону(ГолыйНомерТелефона);
			Если МассивЗвонящих.Количество() > 0 Тогда
				СтруктураЗвонка.АбонентКонтакт = МассивЗвонящих[0];
			КонецЕсли;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(ИдентификаторЗвонка) Тогда
			Если ИспользуетсяОблачнаяАТС Тогда
				Если ТекущийЗвонокДлительность = '00010101' Тогда
					РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПереводНеотвеченногоЗвонка",
						 ИдентификаторЗвонка, СтруктураЗвонка.НомераТелефонов[0].Значение, ВнутреннийНомер,
						 Истина);
					
				Иначе
					РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПереводОтвеченногоЗвонка",
						 ИдентификаторЗвонка, СтруктураЗвонка.НомераТелефонов[0].Значение, ВнутреннийНомер,
						 Истина);
				КонецЕсли;
			КонецЕсли;	
			
		Иначе	
			Если СтруктураЗвонка.НомераТелефонов.Количество() > 0 Тогда
				Если СтруктураЗвонка.НомераТелефонов.Количество() > 1 Тогда
					Оповещение = Новый ОписаниеОповещения("ПриВыбореНомера", ЭтотОбъект, СтруктураЗвонка);
					ПоказатьВыборИзМеню(Оповещение, СтруктураЗвонка.НомераТелефонов);
					
				Иначе
					ПриВыбореНомера(СтруктураЗвонка.НомераТелефонов[0], СтруктураЗвонка);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореНомера(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("НомерТелефона", ВыбранныйЭлемент.Значение);
		
		Если ДополнительныеПараметры.ДанныеЗаполнения = Неопределено Тогда
			ДополнительныеПараметры.ДанныеЗаполнения = Новый Структура();
		КонецЕсли;
		ДополнительныеПараметры.ДанныеЗаполнения.Вставить("CRM_КонтекстВызова", "АРМТелефония");
		
		Если НЕ сфпСофтФонПроКлиент.сфпПозвонить(ВыбранныйЭлемент.Значение,
			 ДополнительныеПараметры.АбонентКонтакт,
			 ДополнительныеПараметры.ДанныеЗаполнения) Тогда
			ТекущийЗвонокНомерАбонента = "";
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ПоложитьТрубку(Команда)
	
	Если ЗначениеЗаполнено(ИдентификаторЗвонка) Тогда
		Если ИспользуетсяОблачнаяАТС Тогда
			сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ЗавершениеВызова", ИдентификаторЗвонка);

		Иначе	
			Если ИдентификаторПереводимогоЗвонка = "" Тогда
				// Завершаем звонок
				сфпПанельУправления.DropCall(ТекущийЗвонокНомерЛинии, ИдентификаторЗвонка);

			Иначе
				// Отменяем перевод звонка
				сфпПанельУправления.CancelTransferCall(ТекущийЗвонокНомерЛинии, ИдентификаторПереводимогоЗвонка, "");
			КонецЕсли;	
			
			ИдентификаторПереводимогоЗвонка = "";
		КонецЕсли;	
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ПереключитьЗвонок(Команда)
	
	ПоказатьНомеронабиратель();
	ТекущийЭлемент = Элементы.НабранныйНомер;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДействие(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПриВыбореДействияПоЗвонку", ЭтотОбъект);
	ПоказатьВыборИзМеню(Оповещение, ДействияПоЗвонку, Элементы.ВыбратьДействиеДа);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореДействияПоЗвонку(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		СтруктураЗвонка = Новый Структура();
		Если ЗначениеЗаполнено(ИдентификаторЗвонка) Тогда
			МассивЗвонящих = Новый Массив();
			МассивЗвонящих.Добавить(ТекущийЗвонокКонтакт);
				
			СтруктураЗвонка = Новый Структура("ИдентификаторЗвонка,Контакт,НомерТелефона,НовыйЗвонок,МассивЗвонящих",
				ИдентификаторЗвонка, ТекущийЗвонокКонтакт, ТекущийЗвонокНомерАбонента, ТекущийЗвонок, МассивЗвонящих);
			
		Иначе
			ТекДанные = Элементы.ТелефонныеЗвонки.ТекущиеДанные;
			Если ТекДанные <> Неопределено Тогда
				МассивЗвонящих = Новый Массив();
				МассивЗвонящих.Добавить(ТекДанные.АбонентКонтакт);
				
				СтруктураЗвонка = Новый Структура("ИдентификаторЗвонка,Контакт,НомерТелефона,НовыйЗвонок,МассивЗвонящих",
					ТекДанные.сфпИдентификаторЗвонка, ТекДанные.АбонентКонтакт, ТекДанные.АбонентКакСвязаться, ТекДанные.Ссылка, МассивЗвонящих);
			КонецЕсли;
		КонецЕсли;
		
		Если СтруктураЗвонка.Свойство("ИдентификаторЗвонка") Тогда
			ИндексДействия = 2 + ДействияПоЗвонку.Индекс(ВыбранныйЭлемент);
			сфпСофтФонПроКлиентПереопределяемый.сфпВыполнитьДоступноеДействие(ИндексДействия, СтруктураЗвонка);
		КонецЕсли;	
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодтвердитьПереводНажатие(Элемент)
	
	Если ИспользуетсяОблачнаяАТС Тогда
		Если ЗначениеЗаполнено(ИдентификаторЗвонка) И ЗначениеЗаполнено(НабранныйНомер) Тогда
			Если ТекущийЗвонокДлительность = '00010101' Тогда
				РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПереводНеотвеченногоЗвонка",
					 ИдентификаторЗвонка, НабранныйНомер, ВнутреннийНомер,
					 Ложь);
				
			Иначе
				РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПереводОтвеченногоЗвонка",
					 ИдентификаторЗвонка, НабранныйНомер, ВнутреннийНомер,
					 Ложь);
			КонецЕсли;
			
			Если РезультатЗапроса.Успешно Тогда
				НабранныйНомер = "";
				Элементы.ДекорацияПодтвердитьПеревод.Видимость = Ложь;
			КонецЕсли;	
		КонецЕсли;

	Иначе	
		Если ИдентификаторПереводимогоЗвонка = "" Тогда
			Если ИдентификаторЗвонка <> 0 И ЗначениеЗаполнено(НабранныйНомер) Тогда
				сфпПанельУправления.RedirectCall(ТекущийЗвонокНомерЛинии, ИдентификаторЗвонка, НабранныйНомер, "");
				НабранныйНомер = "";
			КонецЕсли;

		Иначе
			// Подтверждаем перевод звонка
			сфпПанельУправления.FinishTransferCall(ТекущийЗвонокНомерЛинии, ИдентификаторПереводимогоЗвонка, "");

			ИдентификаторПереводимогоЗвонка = "";
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНомеронабирательНабратьНажатие(Элемент)
	
	Если ЗначениеЗаполнено(ИдентификаторЗвонка) И ЗначениеЗаполнено(НабранныйНомер) Тогда
		Если ИспользуетсяОблачнаяАТС Тогда
			РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПереводОтвеченногоЗвонка",
				 ИдентификаторЗвонка, НабранныйНомер, ВнутреннийНомер,
				 Истина);
			Если РезультатЗапроса.Успешно Тогда
				ИдентификаторПереводимогоЗвонка = ИдентификаторЗвонка;
				НабранныйНомер = "";
				Элементы.НомеронабирательНабрать.Видимость = Ложь;
			КонецЕсли;
		
		Иначе	
			сфпПанельУправления.StartTransferCall(ТекущийЗвонокНомерЛинии, ИдентификаторЗвонка, НабранныйНомер, "");
			ИдентификаторПереводимогоЗвонка = ИдентификаторЗвонка;
			НабранныйНомер = "";
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтобразатьКонтактыНомеронабиратель(ПоказатьКонтакты)
	
	Элементы.ПоказатьКонтакты.ЦветФона = ?(ПоказатьКонтакты, ЦветФонаВыбраннойКнопки, ЦветФонаКнопки);
	Элементы.ПоказатьКонтакты.ЦветРамки = Элементы.ПоказатьКонтакты.ЦветФона;
	
	Элементы.ПоказатьНомеронабиратель.ЦветФона = ?(НЕ ПоказатьКонтакты, ЦветФонаВыбраннойКнопки, ЦветФонаКнопки);
	Элементы.ПоказатьНомеронабиратель.ЦветРамки = Элементы.ПоказатьНомеронабиратель.ЦветФона;
	Элементы.ПоказатьНомеронабиратель.Картинка = ?(ПоказатьКонтакты,
		 БиблиотекаКартинок.сфпПанельЗвонка_НомеронабирательНет,
		 БиблиотекаКартинок.сфпПанельЗвонка_НомеронабирательДа);
	
	Элементы.СтраницыКонтактыНомеронабиратель.ТекущаяСтраница = ?(ПоказатьКонтакты,
		 Элементы.СтраницаКонтакты,
		 Элементы.СтраницаНомеронабиратель);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтакты(Команда)
	ОтобразатьКонтактыНомеронабиратель(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНомеронабиратель(Команда = Неопределено)
	ОтобразатьКонтактыНомеронабиратель(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НомеронабирательПриВводеНомера(ВведенныйНомер = Неопределено)
	
	Если ВведенныйНомер = Неопределено Тогда
		ИмяЭлемента = СтрЗаменить(ТекущийЭлемент.Имя, "Номеронабиратель", "");
		Если ИмяЭлемента = "Символ1" Тогда
			ВведенноеЗначение = "*";

		ИначеЕсли ИмяЭлемента = "Символ2" Тогда
			ВведенноеЗначение = "#";

		Иначе
			ВведенноеЗначение = Число(ИмяЭлемента);
		КонецЕсли;
	
		НабранныйНомер = НабранныйНомер + ВведенноеЗначение;
		ТекущийЗвонокНомерАбонента = НабранныйНомер;
		
	Иначе
		ТекущийЗвонокНомерАбонента = ВведенныйНомер;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗвонка) Тогда
		Если НЕ Элементы.ДекорацияОтветДа.Видимость Тогда
			Элементы.ДекорацияОтветДа.Видимость = Истина;
			Элементы.ДекорацияОтветНет.Видимость = Ложь;
		КонецЕсли;
		Если НЕ Элементы.ДекорацияПодтвердитьПеревод.Видимость Тогда
			Элементы.ДекорацияПодтвердитьПеревод.Видимость = Истина;
		КонецЕсли;
		
		Элементы.ДекорацияНомеронабирательНабрать.Видимость = ЗначениеЗаполнено(НабранныйНомер)
			 И (ТекущийЗвонокСостояние = 8 ИЛИ ТекущийЗвонокСостояние = 10
			 ИЛИ ТекущийЗвонокСостояние = 15);
		Элементы.ДекорацияПодтвердитьПеревод.Видимость = ЗначениеЗаполнено(НабранныйНомер);

	Иначе
		ТекДанные = Элементы.ТелефонныеЗвонки.ТекущиеДанные;
		
		ТекущийЗвонокПредставлениеКонтакта = "";
		ТекущийЗвонокВладелецКонтакта = "";

		Если ПустаяСтрока(ТекущийЗвонокНомерАбонента) И Элементы.ДекорацияОтветДа.Видимость И ТекДанные = Неопределено Тогда
			Элементы.ДекорацияОтветНет.Видимость = Истина;
			Элементы.ДекорацияОтветДа.Видимость = Ложь;
			
		ИначеЕсли НЕ Элементы.ДекорацияОтветДа.Видимость Тогда
			Элементы.ДекорацияОтветНет.Видимость = Ложь;
			Элементы.ДекорацияОтветДа.Видимость = Истина;
		КонецЕсли;
		
		Элементы.ДекорацияНомеронабирательНабрать.Видимость = Ложь;
		Элементы.ДекорацияПодтвердитьПеревод.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НомеронабирательНажатие(Команда)
	НомеронабирательПриВводеНомера();
КонецПроцедуры

&НаКлиенте
Процедура НомеронабирательУдалитьНажатие(Элемент)
	
	НабранныйНомер = Лев(НабранныйНомер, СтрДлина(НабранныйНомер) - 1);
	
	НомеронабирательПриВводеНомера(НабранныйНомер);
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторЗвонка) И ПустаяСтрока(НабранныйНомер) Тогда
		ТелефонныеЗвонкиПриАктивизацииСтроки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НабранныйНомерПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторЗвонка) Тогда
		ТекущийЗвонокНомерАбонента = НабранныйНомер;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НабранныйНомерИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	НомеронабирательПриВводеНомера(Текст);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьДлительностьРазговораПанелиЗвонка() Экспорт // АПК:78 - исключить из проверки.
	ТекущийЗвонокДлительность = '00010101' + (ОбщегоНазначенияКлиент.ДатаСеанса() - ТекущийЗвонокНачалоРазговора);	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПеревод(Команда)

	Если ИспользуетсяОблачнаяАТС Тогда
		Если ЗначениеЗаполнено(ИдентификаторЗвонка) И ЗначениеЗаполнено(НабранныйНомер) Тогда
			Если ТекущийЗвонокДлительность = '00010101' Тогда
				РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПереводНеотвеченногоЗвонка",
					 ИдентификаторЗвонка, НабранныйНомер, ВнутреннийНомер,
					 Ложь);
				
			Иначе
				РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПереводОтвеченногоЗвонка",
					 ИдентификаторЗвонка, НабранныйНомер, ВнутреннийНомер,
					 Ложь);
			КонецЕсли;
			
			Если РезультатЗапроса.Успешно Тогда
				НабранныйНомер = "";
				Элементы.ДекорацияПодтвердитьПеревод.Видимость = Ложь;
			КонецЕсли;	
		КонецЕсли;

	Иначе	
		Если ИдентификаторПереводимогоЗвонка = "" Тогда
			Если ИдентификаторЗвонка <> 0 И ЗначениеЗаполнено(НабранныйНомер) Тогда
				сфпПанельУправления.RedirectCall(ТекущийЗвонокНомерЛинии, ИдентификаторЗвонка, НабранныйНомер, "");
				НабранныйНомер = "";
			КонецЕсли;

		Иначе
			// Подтверждаем перевод звонка
			сфпПанельУправления.FinishTransferCall(ТекущийЗвонокНомерЛинии, ИдентификаторПереводимогоЗвонка, "");

			ИдентификаторПереводимогоЗвонка = "";
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура НомеронабирательНабрать(Команда)
	
	Если ЗначениеЗаполнено(ИдентификаторЗвонка) И ЗначениеЗаполнено(НабранныйНомер) Тогда
		Если ИспользуетсяОблачнаяАТС Тогда
			РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПереводОтвеченногоЗвонка",
				 ИдентификаторЗвонка, НабранныйНомер, ВнутреннийНомер,
				 Истина);
			Если РезультатЗапроса.Успешно Тогда
				НабранныйНомер = "";
				Элементы.НомеронабирательНабрать.Видимость = Ложь;
			КонецЕсли;

        Иначе
			сфпПанельУправления.StartTransferCall(ТекущийЗвонокНомерЛинии, ИдентификаторЗвонка, НабранныйНомер, "");
			ИдентификаторПереводимогоЗвонка = ИдентификаторЗвонка;
			НабранныйНомер = "";
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура НастройкиТелефонииОбщии(Команда = Неопределено)
	
	Оповещение = Новый ОписаниеОповещения("НастройкиТелефонииОбщииПриЗакрытии", ЭтотОбъект);
	ОткрытьФорму("Обработка.сфпАРМ_Телефония.Форма.ФормаНастройки", , ЭтотОбъект, , , , Оповещение,
		 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура НастройкиТелефонииОбщииПриЗакрытии(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ПроверитьНеобходимостьНастройки();
	
	ОбновитьЛиниюПользователя();

	Если РезультатЗакрытия = Истина Тогда
		ОбновитьКонтактыПанелиЗвонка();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// +CRM
// +Рабочий cтол
#Область Подключаемый_РабочийСтол

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	Если СуществуетМодуль_CRM_СобытияФормКлиент Тогда
		МодульCRM_СобытияФормКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("CRM_СобытияФормКлиент");
		МодульCRM_СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	КонецЕсли;

КонецПроцедуры	//	Подключаемый_ВыполнитьПереопределяемуюКоманду()

&НаКлиенте
Процедура Подключаемый_ПолеHTMLЗаметокПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если СуществуетМодуль_CRM_РабочийСтолКлиент Тогда
		МодульCRM_РабочийСтолКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("CRM_РабочийСтолКлиент");
		МодульCRM_РабочийСтолКлиент.ПолеHTMLЗаметокПриНажатии(ЭтотОбъект, Элемент, ДанныеСобытия, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры // Подключаемый_ПолеHTMLЗаметокПриНажатии()

#КонецОбласти
// -Рабочий cтол
// -CRM

&НаСервереБезКонтекста
Процедура СохранитьНастройкиНаСервере(Пользователь, Настройки)

	Если Метаданные.РегистрыСведений.Найти("CRM_НастройкиПользователей") <> Неопределено Тогда
		ИмяРегистраНастроек = "CRM_НастройкиПользователей";
		ИмяПВХНастроек = "CRM_НастройкиПользователей";

	Иначе
		ИмяРегистраНастроек = "сфпНастройкиПользователей";
		ИмяПВХНастроек = "сфпНастройкиПользователей";
	КонецЕсли;
	
	Для Каждого Настройка Из Настройки Цикл
		
		Если Настройка.Ключ = "сфпДействиеПриВходящемЗвонке" Или Настройка.Ключ = "сфпДействиеПриИсходящемЗвонке" 
			Или Настройка.Ключ = "сфпИспользоватьСофтФон" Тогда
			
			МенеджерЗаписи = РегистрыСведений[ИмяРегистраНастроек].СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Пользователь = Пользователь;
			МенеджерЗаписи.Настройка = ПланыВидовХарактеристик[ИмяПВХНастроек][Настройка.Ключ];
			МенеджерЗаписи.Прочитать();
			
			Если Настройка.Значение <> МенеджерЗаписи.Значение Тогда
				ЗаписатьДанныеБизнесСтатистикиНаСервере(Настройка);
			КонецЕсли;
		КонецЕсли;
		
		МенеджерЗаписи = РегистрыСведений[ИмяРегистраНастроек].СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь = Пользователь;
		МенеджерЗаписи.Настройка = ПланыВидовХарактеристик[ИмяПВХНастроек][Настройка.Ключ];
		МенеджерЗаписи.Значение = Настройка.Значение;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПерсональныеНастройкиНаСервере()

	Если Метаданные.РегистрыСведений.Найти("CRM_НастройкиПользователей") <> Неопределено Тогда
		ИмяРегистраНастроек = "CRM_НастройкиПользователей";
		ИмяПВХНастроек = "CRM_НастройкиПользователей";

	Иначе
		ИмяРегистраНастроек = "сфпНастройкиПользователей";
		ИмяПВХНастроек = "сфпНастройкиПользователей";
	КонецЕсли;
	
	Настройки = Новый Структура();
	Настройки.Вставить("сфпИспользоватьСофтФон", "НастройкаИспользоватьСофтФон");
	Настройки.Вставить("сфпЗакрыватьПанельПриЗавершенииРаботы", "НастройкаЗакрыватьПанельПриЗавершенииРаботы");
	Настройки.Вставить("сфпПривязатьВнутреннийНомер", "НастройкаПривязатьВнутреннийНомер");
	Настройки.Вставить("сфпТекущийВнутреннийНомер", "НастройкаТекущийВнутреннийНомер");
	Настройки.Вставить("сфпЛогинНаСерверСофтФон", "НастройкаЛогинНаСерверСофтФон");
	Настройки.Вставить("сфпПарольНаСерверСофтФон", "НастройкаПарольНаСерверСофтФон");
	Настройки.Вставить("сфпДействиеПриВходящемЗвонке", "НастройкаДействиеВходящийЗвонок");
	Настройки.Вставить("сфпДействиеПриИсходящемЗвонке", "НастройкаДействиеИсходящийЗвонок");
	
	Для Каждого Настройка Из Настройки Цикл
		ЗначениеНастройки = Неопределено;
		
		МенеджерЗаписи = РегистрыСведений[ИмяРегистраНастроек].СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь = ТекущийПользователь;
		МенеджерЗаписи.Настройка = ПланыВидовХарактеристик[ИмяПВХНастроек][Настройка.Ключ];
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			ЗначениеНастройки = МенеджерЗаписи.Значение;
		КонецЕсли;
		
		ЭтотОбъект[Настройка.Значение] = ЗначениеНастройки;
	КонецЦикла;	

КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковПоВиду(Фильтр, УстановитьОтбор = Истина)
	
	ТекущийФильтрЗвонков = Фильтр;

	Элементы.ФильтрЗвонковПоВидуПропущенные.ЦветФона = ?(ТекущийФильтрЗвонков = "Пропущенные",
		 ЦветФонаВыбраннойКнопки,
		 ЦветФонаКнопки);
	Элементы.ФильтрЗвонковПоВидуПропущенные.ЦветРамки = Элементы.ФильтрЗвонковПоВидуПропущенные.ЦветФона;
	
	Элементы.ФильтрЗвонковПоВидуВходящие.ЦветФона = ?(ТекущийФильтрЗвонков = "Входящие",
		 ЦветФонаВыбраннойКнопки,
		 ЦветФонаКнопки);
	Элементы.ФильтрЗвонковПоВидуВходящие.ЦветРамки = Элементы.ФильтрЗвонковПоВидуВходящие.ЦветФона;
	
	Элементы.ФильтрЗвонковПоВидуИсходящие.ЦветФона = ?(ТекущийФильтрЗвонков = "Исходящие",
		 ЦветФонаВыбраннойКнопки,
		 ЦветФонаКнопки);
	Элементы.ФильтрЗвонковПоВидуИсходящие.ЦветРамки = Элементы.ФильтрЗвонковПоВидуИсходящие.ЦветФона;
	
	Элементы.ФильтрЗвонковПоВидуВсе.ЦветФона = ?(ТекущийФильтрЗвонков = "Все", ЦветФонаВыбраннойКнопки, ЦветФонаКнопки);
	Элементы.ФильтрЗвонковПоВидуВсе.ЦветРамки = Элементы.ФильтрЗвонковПоВидуВсе.ЦветФона;

	Если УстановитьОтбор Тогда
		УстановитьОтборыТелефонныхЗвонков();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковВсе(Команда)
	ФильтрЗвонковПоВиду("Все");
КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковПропущенные(Команда)
	ФильтрЗвонковПоВиду("Пропущенные");
КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковВходящие(Команда)
	ФильтрЗвонковПоВиду("Входящие");
КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковИсходящие(Команда)
	ФильтрЗвонковПоВиду("Исходящие");
КонецПроцедуры

&НаСервере
Процедура УстановитьШрифтЭлемента(ИмяЭлемента, ЭлементВыбран)
	
	Элемент = Элементы.Найти(ИмяЭлемента);
	Если Элемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элемент.Шрифт = Новый Шрифт(Элемент.Шрифт, , , ЭлементВыбран, , ЭлементВыбран);
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковПоПериоду(Фильтр)
	
	ТекущийПериодЗвонков = Фильтр;
	
	ЭлементыФильтра = Новый Массив();
	ЭлементыФильтра.Добавить("Период");
	ЭлементыФильтра.Добавить("Месяц");
	ЭлементыФильтра.Добавить("Неделя");
	ЭлементыФильтра.Добавить("Вчера");
	ЭлементыФильтра.Добавить("Сегодня");
	
	Для Каждого ЭлементФильтра Из ЭлементыФильтра Цикл
		ТекЭлемент = Элементы["ФильтрЗвонковПоПериоду" + ЭлементФильтра];
		ЭлементВыбран = (ТекущийПериодЗвонков = ЭлементФильтра);
		УстановитьШрифтЭлемента(ТекЭлемент.Имя, ЭлементВыбран);
	КонецЦикла;

	ЗаголовокЭлементаПериод = "Период";

	Если ТекущийПериодЗвонков = "Месяц" Тогда
		НачалоПериодаЗвонков = НачалоМесяца(ОбщегоНазначенияКлиент.ДатаСеанса());
		КонецПериодаЗвонков = КонецМесяца(НачалоПериодаЗвонков);
		
	ИначеЕсли ТекущийПериодЗвонков = "Неделя" Тогда
		НачалоПериодаЗвонков = НачалоНедели(ОбщегоНазначенияКлиент.ДатаСеанса());
		КонецПериодаЗвонков = КонецНедели(НачалоПериодаЗвонков);
		
	ИначеЕсли ТекущийПериодЗвонков = "Вчера" Тогда
		НачалоПериодаЗвонков = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса()) - 86400;
		КонецПериодаЗвонков = КонецДня(НачалоПериодаЗвонков);
		
	ИначеЕсли ТекущийПериодЗвонков = "Сегодня" Тогда
		НачалоПериодаЗвонков = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса());
		КонецПериодаЗвонков = КонецДня(НачалоПериодаЗвонков);
		
	ИначеЕсли ТекущийПериодЗвонков = "Период" Тогда
		НачалоПериодаЗвонков = ПериодЗвонков.ДатаНачала;
		КонецПериодаЗвонков = ПериодЗвонков.ДатаОкончания;
		
		ПредставлениеПериода = ПредставлениеПериода(ПериодЗвонков.ДатаНачала, ПериодЗвонков.ДатаОкончания);
		ЗаголовокЭлементаПериод = ?(ЗначениеЗаполнено(ПредставлениеПериода), ПредставлениеПериода,
		 "За все время");
	КонецЕсли;
	
	Элементы.ФильтрЗвонковПоПериодуПериод.Заголовок = ЗаголовокЭлементаПериод;
					
	ОбновитьКоличествоЗвонков();
	УстановитьОтборыТелефонныхЗвонков();

КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковСегодня(Команда)
	ФильтрЗвонковПоПериоду("Сегодня");
КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковВчера(Команда)
	ФильтрЗвонковПоПериоду("Вчера");
КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковНеделя(Команда)
	ФильтрЗвонковПоПериоду("Неделя");
КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковМесяц(Команда)
	ФильтрЗвонковПоПериоду("Месяц");
КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковПериод(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ФильтрЗвонковПериодПродолжить", ЭтотОбъект);
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период.ДатаНачала = ПериодЗвонков.ДатаНачала;
	Диалог.Период.ДатаОкончания = ПериодЗвонков.ДатаОкончания;
	Диалог.Показать(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковПериодПродолжить(Период, ДополнительныеПараметры) Экспорт
	
	Если Период <> Неопределено Тогда
		ПериодЗвонков.ДатаНачала = Период.ДатаНачала;
		ПериодЗвонков.ДатаОкончания = Период.ДатаОкончания;

		ФильтрЗвонковПоПериоду("Период");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковПоПринадлежности(Фильтр)
	
	СписокВыбора = Новый СписокЗначений();
	СписокВыбора.Добавить(1, "Мои звонки", (ПринадлежностьЗвонка = 1), БиблиотекаКартинок.сфпФильтрМоиЗвонки);
	
	Если ТекущиеПодчиненныеПодразделения.Количество() > 0 Тогда
		СписокВыбора.Добавить(2, "Звонки моего подразделения", (ПринадлежностьЗвонка = 2),
			 БиблиотекаКартинок.сфпФильтрЗвонкиМоегоПодразделения);
		СписокВыбора.Добавить(0, "Все звонки", (ПринадлежностьЗвонка = 0),
			 БиблиотекаКартинок.сфпФильтрМоиЗвонкиИПодразделения);
		
	ИначеЕсли ТекущиеПрослушиваемыеПользователи.Количество() > 0 Тогда
		СписокВыбора.Добавить(2, "Звонки контролируемых пользователей", (ПринадлежностьЗвонка = 2),
			 БиблиотекаКартинок.сфпФильтрЗвонкиМоегоПодразделения);
		СписокВыбора.Добавить(0, "Все звонки", (ПринадлежностьЗвонка = 0),
			 БиблиотекаКартинок.сфпФильтрМоиЗвонкиИПодразделения);
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ФильтрЗвонковПоПринадлежностиПродолжить", ЭтотОбъект);	
	ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элементы.ФильтрЗвонковПоПринадлежности);

КонецПроцедуры

&НаКлиенте
Процедура ФильтрЗвонковПоПринадлежностиПродолжить(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ПринадлежностьЗвонка = ВыбранныйЭлемент.Значение;
		Элементы.КартинкаФильтрЗвонковПоПринадлежности.Подсказка = ВыбранныйЭлемент.Представление;
		Элементы.КартинкаФильтрЗвонковПоПринадлежности.Картинка = ВыбранныйЭлемент.Картинка;
		
		ОбновитьКоличествоЗвонков();
		УстановитьОтборыТелефонныхЗвонков();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтображениеКонтактов(Режим, Группировка = "")
	
	Если ТипЗнч(Группировка) <> Тип("Строка") И ТипЗнч(Группировка) <> Тип("СправочникСсылка.сфпТелефонныеКниги") Тогда
		Возврат;
	КонецЕсли;

	ТекущаяГруппировкаКонтактов.Очистить();
	
	Если АбонентыТелефонныхКниг.КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество() > 0 Тогда
		  ОтборТелефоннаяКнига = АбонентыТелефонныхКниг.КомпоновщикНастроек.Настройки.Отбор.Элементы[0];
	Иначе ОтборТелефоннаяКнига = АбонентыТелефонныхКниг.Отбор.Элементы[0];
	КонецЕсли;	
	
	Если Режим = "Развернуть" Тогда
		НоваяСтрока = ТекущаяГруппировкаКонтактов.Добавить();
		НоваяСтрока.Группировка = Группировка;
		
		Элементы.ТекущаяГруппировкаКонтактов.Видимость = Истина;

		Элементы.АбонентыТелефонныхКнигКонтактПредставление.ГиперссылкаЯчейки = Ложь;
		
		АбонентыТелефонныхКниг.Группировка.Элементы[0].Использование = Ложь;
		
		ОтборТелефоннаяКнига.Использование = Истина;
		ОтборТелефоннаяКнига.ПравоеЗначение = Группировка;
		
	Иначе
		Элементы.АбонентыТелефонныхКниг.ТекущаяСтрока = Неопределено;

		Элементы.ТекущаяГруппировкаКонтактов.Видимость = Ложь;
		
		Элементы.АбонентыТелефонныхКнигКонтактПредставление.ГиперссылкаЯчейки = Истина;
		
		АбонентыТелефонныхКниг.Группировка.Элементы[0].Использование = Истина;
		
		ОтборТелефоннаяКнига.Использование = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РедактироватьКонтакт(Команда)
	
	ТекДанные = Элементы.АбонентыТелефонныхКниг.ТекущиеДанные;
	Если ТекДанные <> Неопределено И ТипЗнч(ТекДанные) = Тип("ДанныеФормыСтруктура") Тогда
		// BSLLS:MissingCodeTryCatchEx-off
		Попытка
			ПоказатьЗначение(, ТекДанные.Контакт);
		
		Исключение КонецПопытки;
		// BSLLS:MissingCodeTryCatchEx-on
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяГруппировкаКонтактовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НастроитьОтображениеКонтактов("Свернуть");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПривязатьВнутреннийНомерПриИзменении(Элемент = Неопределено)
	
	Элементы.НастройкаПривязатьВнутреннийНомер.Заголовок = "Привязать внутренний номер";
	Если НастройкаПривязатьВнутреннийНомер Тогда
		Элементы.НастройкаПривязатьВнутреннийНомер.Заголовок = Элементы.НастройкаПривязатьВнутреннийНомер.Заголовок + ":";
	КонецЕсли;
	
	Элементы.НастройкаТекущийВнутреннийНомер.Видимость = НастройкаПривязатьВнутреннийНомер
		 И Элементы.НастройкаПривязатьВнутреннийНомер.Видимость;
	Элементы.НастройкаЛогинНаСерверСофтФон.Видимость = НастройкаПривязатьВнутреннийНомер
		 И Элементы.НастройкаПривязатьВнутреннийНомер.Видимость;
	Элементы.НастройкаПарольНаСерверСофтФон.Видимость = НастройкаПривязатьВнутреннийНомер
		 И Элементы.НастройкаПривязатьВнутреннийНомер.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаТекущийВнутреннийНомерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура("ТекущийНомер", НастройкаТекущийВнутреннийНомер);
	Оповещение = Новый ОписаниеОповещения("ПриВыбореВнутреннегоНомера", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.сфпСписокВнутреннихНомеров", ПараметрыФормы, Элемент, , , , Оповещение,
		 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореВнутреннегоНомера(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура УдержаниеЗвонка(Команда)
	
	Если ИспользуетсяОблачнаяАТС Тогда
		// Зарезервировано на будущее
		РазрешенаПауза = Истина;
	
    Иначе
		Если ТекущийЗвонокСостояние = 10 Тогда 
			сфпПанельУправления.UnHoldCall(ТекущийЗвонокНомерЛинии, ИдентификаторЗвонка);

		Иначе
			ИД = Формат(Число(ИдентификаторЗвонка), "ЧГ=");
			сфпПанельУправления.HoldCall(ТекущийЗвонокНомерЛинии, ИД);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьДанныеБизнесСтатистикиНаСервере(Настройка)
	
	Значение = Настройка.Значение;
	
	Если Настройка.Ключ = "сфпДействиеПриВходящемЗвонке" Тогда
		
		Если Значение = "Открыть ведомость взаиморасчетов" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.ВедомостьВзаиморасчетов");
		ИначеЕсли Значение = "Открыть дебиторскую задолженность" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.ДебиторскаяЗадолженность");
		ИначеЕсли Значение = "Открыть досье клиента" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.ДосьеКлиента");
		ИначеЕсли Значение = "Открыть карточку контакта" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.КарточкаКонтакта");
		ИначеЕсли Значение = "Краткая форма Телефонного звонка" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.КраткаяФормаТелефонногоЗвонка");
		ИначеЕсли Значение = "" Или Значение = "Нет действий" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.НетДействий");
		ИначеЕсли Значение = "Открыть Панель звонка" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.ПанельЗвонка");
		ИначеЕсли Значение = "Открыть продажи" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.Продажи");
		ИначеЕсли Значение = "Регистрация обращения" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.РегистрацияОбращения");
		ИначеЕсли Значение = "Открыть список анкет" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.СписокАнкет");
		ИначеЕсли Значение = "Открыть счет на оплату" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.СчетНаОплату");
		ИначеЕсли Значение = "Открыть Телефонный звонок" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриВходящем.ТелефонныйЗвонок");
		КонецЕсли;
		
	ИначеЕсли Настройка.Ключ = "сфпДействиеПриИсходящемЗвонке" Тогда
		
		Если Значение = "Открыть ведомость взаиморасчетов" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.ВедомостьВзаиморасчетов");
		ИначеЕсли Значение = "Открыть дебиторскую задолженность" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.ДебиторскаяЗадолженность");
		ИначеЕсли Значение = "Открыть досье клиента" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.ДосьеКлиента");
		ИначеЕсли Значение = "Открыть карточку контакта" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.КарточкаКонтакта");
		ИначеЕсли Значение = "Краткая форма Телефонного звонка" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.КраткаяФормаТелефонногоЗвонка");
		ИначеЕсли Значение = "" Или Значение = "Нет действий" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.НетДействий");
		ИначеЕсли Значение = "Открыть Панель звонка" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.ПанельЗвонка");
		ИначеЕсли Значение = "Открыть продажи" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.Продажи");
		ИначеЕсли Значение = "Регистрация обращения" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.РегистрацияОбращения");
		ИначеЕсли Значение = "Открыть список анкет" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.СписокАнкет");
		ИначеЕсли Значение = "Открыть счет на оплату" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.СчетНаОплату");
		ИначеЕсли Значение = "Открыть Телефонный звонок" Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ПерсональныеНастройки.ДействиеПриИсходящем.ТелефонныйЗвонок");
		КонецЕсли;
		
	ИначеЕсли Настройка.Ключ = "сфпИспользоватьСофтФон" Тогда
		ВерсияСофтФон = Константы.сфпИспользуемаяВерсияСофтФон.Получить();
		Если ВерсияСофтФон = Перечисления.сфпВерсииСофтФон.СофтФотПроф Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ИнтеграцияСТелефонией.ПерсональныеНастройки.ИспользоватьСофтФон");
		ИначеЕсли ВерсияСофтФон = Перечисления.сфпВерсииСофтФон.СофтФотPROSTO Тогда
			CRM_ЦентрМониторингаВызовСервера.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.СофтФон.ОблачныеАТС.ПерсональныеНастройки.ИспользоватьСофтФон");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНеобходимостьДобавитьНастройкуНаРабочийСтол()
	
	Если НастройкаИспользоватьСофтФон Тогда
		
		сфпСофтФонПроСервер.ДобавитьНастройкуТелефонииНаРабочийСтол();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 
