
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТипВатс = ПолучитьТипВатс();
	Если ТипВатс = 0 Тогда
		ПоказатьПредупреждение(,
			 НСтр("ru='Загрузить вручную историю звонков можно только с АТС Манго/Мегафон/Билайн!';
			|en='Manually download the call history directly from the Mango/Megafon/Beeline PBX.'"));
	КонецЕсли;
	ПриИзмененииВАТС();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьТипВатс()
	
	ТипВАТС = 0;
	Объект.ИспользуемаяАТС = Константы.сфпИспользуемаяАТС.Получить();
	
	Если Объект.ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.MangoOffice Тогда
		
		ТипВАТС = 1;
		
	ИначеЕсли Объект.ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Билайн Тогда
		
		ТипВАТС = 2;
		
	ИначеЕсли Объект.ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Мегафон 
		Или сфпСофтФонПроСерверПереопределяемый.ЭтоПлатформаITooLabs(Объект.ИспользуемаяАТС) Тогда
		
		ТипВАТС = 3;
		
	КонецЕсли;
	
	Возврат ТипВАТС;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииВАТС()
	
	НастройкиТелефонии = сфпСофтФонПроСервер.ПолучитьНастройкиТелефонии();
	
	Если ТипВАТС = 1 Тогда
		ApiKey = НастройкиТелефонии.vpbx_api_key;
		ApiSalt = НастройкиТелефонии.vpbx_api_salt;
		Если Не ApiKey = "" И Не ApiSalt = "" Тогда
			Объект.ApiKey = ApiKey;
			Объект.ApiSalt = ApiSalt;
		КонецЕсли;
		
	ИначеЕсли ТипВАТС = 2 Тогда
		Токен = НастройкиТелефонии.КлючДляАвторизацииВОблачнойАТС;
		Если Не Токен = "" Тогда
			Объект.Токен = Токен;
		КонецЕсли;
		
	ИначеЕсли ТипВАТС = 3 Тогда
		
		Если Не ЗначениеЗаполнено(Объект.ТокенМегафон) Тогда
			Объект.ТокенМегафон = НастройкиТелефонии.КлючДляАвторизацииВОблачнойАТС;
		КонецЕсли;
		Объект.АдресОблачнойАТС = ПолучитьАдресСервераМегафон(НастройкиТелефонии.АдресОблачнойАТС);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьАдресСервераМегафон(АдресСервера)
	
	Позиция = СтрНайти(АдресСервера, "//");
	Если Позиция > 0 Тогда
		ОстатокПути = Сред(АдресСервера, Позиция + 2);
		
		ПозТочки = СтрНайти(ОстатокПути, "/");
		АдресСервера = Лев(ОстатокПути, ПозТочки - 1);
		
	КонецЕсли;
	Возврат АдресСервера;
КонецФункции

&НаКлиенте
Процедура ЗагрузитьЗвонкиФоновая(ПараметрыПроцедуры)
	
	ДлительнаяОперация = сфпСофтФонПроСервер.сфпРучнаяЗагрузкаЗвонковВФоне(ПараметрыПроцедуры);
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
	ИначеЕсли ДлительнаяОперация.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется загрузка звонков.'; en = 'Calls are being uploaded.'");
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузкаЗвонкиЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаЗвонкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Настройки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	УдалитьИзВременногоХранилища(Результат.АдресРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИсториюЗвонков(Команда)
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ДатаНачалаВыгрузки", Объект.ДатаНачалаВыгрузки);
	ПараметрыПроцедуры.Вставить("ДатаОкончанияВыгрузки", Объект.ДатаОкончанияВыгрузки);
	ПараметрыПроцедуры.Вставить("ТипВатс", ТипВатс);
	
	Если ТипВатс = 1 Тогда
		Ресурс = "";
		ПараметрыПроцедуры.Вставить("Ресурс", Ресурс);
		ПараметрыПроцедуры.Вставить("ApiKey", СокрЛП(Объект.ApiKey));
		ПараметрыПроцедуры.Вставить("ApiSalt", СокрЛП(Объект.ApiSalt));
		ПараметрыПроцедуры.Вставить("Токен", "");
		
		ЗагрузитьЗвонкиФоновая(ПараметрыПроцедуры);
	ИначеЕсли ТипВатс = 2 Тогда
		Ресурс = "/apis/portal/statistics";
		ПараметрыПроцедуры.Вставить("Ресурс", Ресурс);
		ПараметрыПроцедуры.Вставить("ApiKey", "");
		ПараметрыПроцедуры.Вставить("ApiSalt", "");
		ПараметрыПроцедуры.Вставить("Токен", СокрЛП(Объект.Токен));
		
		ЗагрузитьЗвонкиФоновая(ПараметрыПроцедуры);
	ИначеЕсли ТипВатс = 3 Тогда
		Ресурс = "/sys/crm.api.wc";
		ПараметрыПроцедуры.Вставить("Ресурс", Ресурс);
		ПараметрыПроцедуры.Вставить("АдресСервера", Объект.АдресОблачнойАТС);
		ПараметрыПроцедуры.Вставить("ApiKey", "");
		ПараметрыПроцедуры.Вставить("ApiSalt", "");
		ПараметрыПроцедуры.Вставить("Токен", СокрЛП(Объект.ТокенМегафон));
		
		ЗагрузитьЗвонкиФоновая(ПараметрыПроцедуры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти
