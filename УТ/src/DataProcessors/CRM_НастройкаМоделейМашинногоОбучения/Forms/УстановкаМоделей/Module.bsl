
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ CRM_ЛицензированиеЭкспортныеМетоды.ПодсистемаCRMИспользуется() Тогда
		CRM_ЛицензированиеЭкспортныеМетоды.ВывестиУведомлениеОНедоступности(НСтр("ru = 'форму установки Моделей обучения'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	ЗащищенныйОбъект = CRM_ЛицензированиеЭкспортныеМетоды.ПолучитьЗащищеннуюОбработку();
	ЗащищенныйОбъект.МоделиМашинногоОбученияПриСозданииНаСервере(ЭтотОбъект, Отказ);
	
	СостоянияПроверки = CRM_МоделиМашинногоОбученияКлиентСервер.СостоянияПроверкиНастроек();
	
	Состояния = Новый Массив;
	Состояния.Добавить("УстановкаPythonНеПроверялась");
	Состояния.Добавить("УстановкаМоделейНеПроверялась");
	Состояния.Добавить("ВыгрузкаДанныхНеВыполнялась");
	Состояния.Добавить("ПодключениеКВебСерверуНеПроверялось");
	CRM_МоделиМашинногоОбученияКлиентСервер.ОбновитьСостояниеПроверки(Состояния, ЭтотОбъект);
	
	НастройкиМоделей = CRM_МоделиМашинногоОбучения.ПолучитьНастройкиМоделей();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиМоделей);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		Отказ = Истина;
		ТекстОшибки = НСтр("ru = 'Установка моделей машинного обучения не может быть запущена в веб-клиенте.'");
		ПоказатьПредупреждение(, ТекстОшибки);
	#КонецЕсли
	
	ТекущийЭлемент = Элементы.Установка_Шаг1;
	
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаПутиКМоделям(ЭтотОбъект, ПутьКМоделям);
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаНастроекДоступа(ЭтотОбъект, Сервер, Порт);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПутьКМоделямНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ПутьКМоделямНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура);
	ЗаголовокДиалога = НСтр("ru = 'Путь к каталогу моделей машинного обучения'");
	
	ФайловаяСистемаКлиент.ВыбратьКаталог(ОповещениеЗавершения, ЗаголовокДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКМоделямНачалоВыбораЗавершение(ВыбранныйКаталог, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ВыбранныйКаталог) Тогда
		ПутьКМоделям = ВыбранныйКаталог;
		CRM_МоделиМашинногоОбученияКлиент.ПроверкаПутиКМоделям(ЭтотОбъект, ПутьКМоделям);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКМоделямПриИзменении(Элемент)
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаПутиКМоделям(ЭтотОбъект, ПутьКМоделям);
КонецПроцедуры

&НаКлиенте
Процедура СерверПриИзменении(Элемент)
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаНастроекДоступа(ЭтотОбъект, Сервер, Порт);
КонецПроцедуры

&НаКлиенте
Процедура ПортПриИзменении(Элемент)
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаНастроекДоступа(ЭтотОбъект, Сервер, Порт);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ЗаписатьНастройкиЗавершение", ЭтотОбъект);
	
	Если Не УстановкаPythonУспех
		Или Не УстановкаМоделейУспех
		Или Не ПодключениеКВебСерверуУспех
		Или Не ВыгрузкаДанныхУспех Тогда
		
		ТекстВопроса = НСтр("ru = 'Установка моделей не завершена. Все равно закрыть?'");
		ПоказатьВопрос(ОповещениеЗавершения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Или Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиМоделей = CRM_МоделиМашинногоОбучения.ПолучитьНастройкиМоделей();
	ЗаполнитьЗначенияСвойств(НастройкиМоделей, ЭтотОбъект);
	CRM_МоделиМашинногоОбучения.СохранитьНастройкиМоделей(НастройкиМоделей);
	Закрыть();
	
	ОткрытьФорму("Обработка.CRM_НастройкаМоделейМашинногоОбучения.Форма.НастройкаМоделей", , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьPython(Команда)
	ПерейтиПоНавигационнойСсылке("https://www.python.org/downloads/");
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьУстановкуPython(Команда)
	
	СтрокаКоманды = "python";
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаPython(ЭтотОбъект, СтрокаКоманды);
	
КонецПроцедуры

&НаКлиенте
Процедура ПропуститьУстановкуPython(Команда)
	CRM_МоделиМашинногоОбученияКлиентСервер.ОбновитьСостояниеПроверки("УстановкаPythonПропущена", ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФайлыМодели(Команда)
	CRM_МоделиМашинногоОбученияКлиент.УстановитьФайлыМодели(ЭтотОбъект, ПутьКМоделям);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьУстановкуМодели(Команда)
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаФайловМоделей(ЭтотОбъект, ПутьКМоделям);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанные(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	
	ДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	ДатаНачала = НачалоГода(НачалоГода(ДатаСеанса) - 1);
	ДатаОкончания = КонецГода(ДатаСеанса);
	
	Попытка
		ВыгрузитьВсеДанные(ДатаНачала, ДатаОкончания);
		CRM_МоделиМашинногоОбученияКлиентСервер.ОбновитьСостояниеПроверки("ВыгрузкаДанныхУспешно", ЭтотОбъект);
	Исключение
		CRM_МоделиМашинногоОбученияКлиентСервер.ОбновитьСостояниеПроверки("ВыгрузкаДанныхНеудачно", ЭтотОбъект);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьВебСервер(Команда)
	
	Если Не CRM_МоделиМашинногоОбученияКлиент.ПроверкаНастроекДоступа(ЭтотОбъект, Сервер, Порт) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		СтрокаКоманды = "ai_server.cmd";
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияБезДействия", CRM_ОбщегоНазначенияКлиент);	
		НачатьЗапускПриложения(ОписаниеОповещения, СтрокаКоманды, ПутьКМоделям, Ложь);
	Исключение
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключениеКВебСерверу(Команда)
	CRM_МоделиМашинногоОбученияКлиент.ПроверитьПодключение(ЭтотОбъект, Сервер, Порт);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыгрузитьВсеДанные(ДатаНачала, ДатаОкончания)
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("ПутьКМоделям", ПутьКМоделям);
	ПараметрыВыгрузки.Вставить("ДатаНачала", ДатаНачала);
	ПараметрыВыгрузки.Вставить("ДатаОкончания", ДатаОкончания);
	
	ПараметрыВыгрузки.Вставить("ИмяЗадачи", "ПоискРешений");
	CRM_МоделиМашинногоОбученияКлиент.ВыгрузитьДанныеЗадачи(ПараметрыВыгрузки);
	
	ПараметрыВыгрузки.Вставить("ИмяЗадачи", "ИнформированиеОКонверсии");
	CRM_МоделиМашинногоОбученияКлиент.ВыгрузитьДанныеЗадачи(ПараметрыВыгрузки);
	
КонецПроцедуры

#КонецОбласти
