
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ CRM_ЛицензированиеЭкспортныеМетоды.ПодсистемаCRMИспользуется() Тогда
		CRM_ЛицензированиеЭкспортныеМетоды.ВывестиУведомлениеОНедоступности(НСтр("ru = 'форму настроек'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗащищенныйОбъект = CRM_ЛицензированиеЭкспортныеМетоды.ПолучитьЗащищеннуюОбработку();
	ЗащищенныйОбъект.МоделиМашинногоОбученияПриСозданииНаСервере(ЭтотОбъект, Отказ);
	
	СостоянияПроверки = CRM_МоделиМашинногоОбученияКлиентСервер.СостоянияПроверкиНастроек();
	
	Состояния = Новый Массив;
	Состояния.Добавить("УстановкаМоделейНеПроверялась");
	Состояния.Добавить("ПодключениеКВебСерверуНеПроверялось");
	CRM_МоделиМашинногоОбученияКлиентСервер.ОбновитьСостояниеПроверки(Состояния, ЭтотОбъект);
	
	НастройкиМоделей = CRM_МоделиМашинногоОбучения.ПолучитьНастройкиМоделей();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиМоделей);
	ИспользоватьКонсультанта = (НастройкиМоделей.ИспользоватьКлассификациюОбращений
		Или НастройкиМоделей.ИспользоватьПоискРешений);
	ИспользоватьПродавца = НастройкиМоделей.ИспользоватьИнформированиеОКонверсии;
	
	СведенияМоделей = CRM_МоделиМашинногоОбучения.ПолучитьСведенияМоделей();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СведенияМоделей);
	
	Элементы.Консультант_Параметры.Доступность = ИспользоватьКонсультанта;
	Элементы.Продавец_Параметры.Доступность = ИспользоватьПродавца;
	Элементы.Консультант_Параметры_ПоискРешений.Доступность = ИспользоватьПоискРешений;
	Элементы.Консультант_Параметры_КлассификацияОбращений.Доступность = ИспользоватьКлассификациюОбращений;
	
	Если КлассификацияОбращенийРежимРаботы = "ГотоваяМодель" Тогда
		Элементы.Консультант_КлассификацияОбращений_Варианты.ТекущаяСтраница =
			Элементы.Консультант_КлассификацияОбращений_ГотоваяМодель;
	Иначе
		Элементы.Консультант_КлассификацияОбращений_Варианты.ТекущаяСтраница =
			Элементы.Консультант_КлассификацияОбращений_СвояМодель;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		Отказ = Истина;
		ТекстОшибки = НСтр("ru = 'Настройка моделей машинного обучения не может быть запущена в веб-клиенте.'");
		ПоказатьПредупреждение(, ТекстОшибки);
	#КонецЕсли
	
	ТекущийЭлемент = Элементы.Консультант_Использовать;
	
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаФайловМоделей(ЭтотОбъект, ПутьКМоделям);
	CRM_МоделиМашинногоОбученияКлиент.ПроверитьПодключение(ЭтотОбъект, Сервер, Порт);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Консультант_ИспользоватьПриИзменении(Элемент)
	
	Элементы.Консультант_Параметры.Доступность = ИспользоватьКонсультанта;
	ИспользоватьПоискРешений = ИспользоватьКонсультанта;
	ИспользоватьКлассификациюОбращений = ИспользоватьКонсультанта;
	Элементы.Консультант_Параметры_ПоискРешений.Доступность = ИспользоватьПоискРешений;
	Элементы.Консультант_Параметры_КлассификацияОбращений.Доступность = ИспользоватьКлассификациюОбращений;
	
КонецПроцедуры

&НаКлиенте
Процедура Консультант_Использовать_ПоискРешенийПриИзменении(Элемент)
	
	Элементы.Консультант_Параметры_ПоискРешений.Доступность = ИспользоватьПоискРешений;
	Если Не (ИспользоватьПоискРешений
		Или ИспользоватьКлассификациюОбращений) Тогда
		ИспользоватьКонсультанта = Ложь;
		Элементы.Консультант_Параметры.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Консультант_Использовать_КлассификациюОбращенийПриИзменении(Элемент)
	
	Элементы.Консультант_Параметры_КлассификацияОбращений.Доступность = ИспользоватьКлассификациюОбращений;
	Если Не (ИспользоватьПоискРешений
		Или ИспользоватьКлассификациюОбращений) Тогда
		ИспользоватьКонсультанта = Ложь;
		Элементы.Консультант_Параметры.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Продавец_ИспользоватьПриИзменении(Элемент)
	ИспользоватьИнформированиеОКонверсии = ИспользоватьПродавца;
	Элементы.Продавец_Параметры.Доступность = ИспользоватьПродавца;
КонецПроцедуры

&НаКлиенте
Процедура ПутьКМоделямНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ПутьКМоделямНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура);
	ЗаголовокДиалога = НСтр("ru = 'Путь к каталогу моделей машинного обучения'");
	
	ФайловаяСистемаКлиент.ВыбратьКаталог(ОповещениеЗавершения, ЗаголовокДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКМоделямНачалоВыбораЗавершение(ВыбранныйКаталог, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ВыбранныйКаталог) Тогда
		ПутьКМоделям = ВыбранныйКаталог;
		CRM_МоделиМашинногоОбученияКлиент.ПроверкаПутиКМоделям(ЭтотОбъект, ПутьКМоделям);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКМоделямПриИзменении(Элемент)
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаПутиКМоделям(ЭтотОбъект, ПутьКМоделям);
КонецПроцедуры

&НаКлиенте
Процедура СерверПриИзменении(Элемент)
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаНастроекДоступа(ЭтотОбъект, Сервер, Порт);
КонецПроцедуры

&НаКлиенте
Процедура ПортПриИзменении(Элемент)
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаНастроекДоступа(ЭтотОбъект, Сервер, Порт);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)
	
	НастройкиМоделей = CRM_МоделиМашинногоОбучения.ПолучитьНастройкиМоделей();
	ЗаполнитьЗначенияСвойств(НастройкиМоделей, ЭтотОбъект);
	CRM_МоделиМашинногоОбучения.СохранитьНастройкиМоделей(НастройкиМоделей);
	Попытка
		CRM_МоделиМашинногоОбученияКлиент.СохранитьКонфигурационныйФайл(НастройкиМоделей);
	Исключение
		ТекстПредупреждения =
			НСтр("ru = 'Не удалось обновить конфигурационный файл. Проверьте доступность каталога моделей.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецПопытки;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Консультант_НастроитьПоискРешений(Команда)
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("НастройкаЗавершение", ЭтотОбъект);
	
	ПараметрыОткрытия = CRM_МоделиМашинногоОбученияКлиентСервер.НастройкиПоискаРешений();
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ЭтотОбъект);
	
	ОткрытьФорму("Обработка.CRM_НастройкаМоделейМашинногоОбучения.Форма.ПоискРешенийНастройки",
		ПараметрыОткрытия, ЭтотОбъект, , , , ОповещениеЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура Консультант_НастроитьКлассификацияОбращений(Команда)
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("НастройкаЗавершение", ЭтотОбъект);
	
	ПараметрыОткрытия = CRM_МоделиМашинногоОбученияКлиентСервер.НастройкиКлассификацииОбращений();
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ЭтотОбъект);
	
	ОткрытьФорму("Обработка.CRM_НастройкаМоделейМашинногоОбучения.Форма.КлассификацияОбращенийНастройки",
		ПараметрыОткрытия, ЭтотОбъект, , , , ОповещениеЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
	
	Если КлассификацияОбращенийРежимРаботы = "ГотоваяМодель" Тогда
		Элементы.Консультант_КлассификацияОбращений_Варианты.ТекущаяСтраница =
			Элементы.Консультант_КлассификацияОбращений_ГотоваяМодель;
	Иначе
		Элементы.Консультант_КлассификацияОбращений_Варианты.ТекущаяСтраница =
			Элементы.Консультант_КлассификацияОбращений_СвояМодель;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Консультант_ВыгрузитьПоискРешений(Команда)
	
	ИмяЗадачи = "ПоискРешений";
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ВыгрузитьДанныеЗавершение", ЭтотОбъект,
		 Новый Структура("ИмяЗадачи",
		 ИмяЗадачи));
	
	ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Новый Структура("ДатаНачала, ДатаОкончания"));
	
КонецПроцедуры

&НаКлиенте
Процедура Консультант_ВыгрузитьКлассификацияОбращений(Команда)
	
	ИмяЗадачи = "КлассификацияОбращений";
	
	ПараметрыОткрытия = CRM_МоделиМашинногоОбучения.ПолучитьПериодВыгрузки(ИмяЗадачи);
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ВыгрузитьДанныеЗавершение", ЭтотОбъект,
		 Новый Структура("ИмяЗадачи",
		 ИмяЗадачи));
	
	ОткрытьФорму("Обработка.CRM_НастройкаМоделейМашинногоОбучения.Форма.ВыборПериодаВыгрузки",
		 ПараметрыОткрытия, ЭтотОбъект, , , ,
		 ОповещениеЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура Продавец_ВыгрузитьИнформированиеОКонверсии(Команда)
	
	ИмяЗадачи = "ИнформированиеОКонверсии";
	
	ПараметрыОткрытия = CRM_МоделиМашинногоОбучения.ПолучитьПериодВыгрузки(ИмяЗадачи);
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("ВыгрузитьДанныеЗавершение", ЭтотОбъект,
		 Новый Структура("ИмяЗадачи",
		 ИмяЗадачи));
	
	ОткрытьФорму("Обработка.CRM_НастройкаМоделейМашинногоОбучения.Форма.ВыборПериодаВыгрузки",
		 ПараметрыОткрытия, ЭтотОбъект, , , ,
		 ОповещениеЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("ИмяЗадачи", ДополнительныеПараметры.ИмяЗадачи);
	ПараметрыВыгрузки.Вставить("ПутьКМоделям", ПутьКМоделям);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыВыгрузки, Результат);
	
	РезультатВыгрузки = CRM_МоделиМашинногоОбученияКлиент.ВыгрузитьДанныеЗадачи(ПараметрыВыгрузки);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатВыгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФайлыМодели(Команда)
	CRM_МоделиМашинногоОбученияКлиент.УстановитьФайлыМодели(ЭтотОбъект, ПутьКМоделям);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьУстановкуМодели(Команда)
	CRM_МоделиМашинногоОбученияКлиент.ПроверкаФайловМоделей(ЭтотОбъект, ПутьКМоделям);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключениеКВебСерверу(Команда)
	CRM_МоделиМашинногоОбученияКлиент.ПроверитьПодключение(ЭтотОбъект, Сервер, Порт);
КонецПроцедуры

#КонецОбласти
