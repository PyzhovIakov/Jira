
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПолучитьФункциональнуюОпцию("CRM_ИспользоватьПоддержку") Тогда
		Элементы.ГруппаИнтерес.Заголовок = НСтр("ru = 'Поля Интереса/Обращения в поддержку'");
	КонецЕсли;
	Настройки = Обработки.CRM_МастерРегистрацииОбращения.НастройкаСоставаРеквизитовПоУмолчанию();
	ТекущиеНастройки = ПараметрыСеанса.CRM_НастройкаСоставаРеквизитовМастераРегистрации.Получить();
	Настройки.Свойство("ОбщиеНастройки", ОбщиеНастройки);
	Для Каждого Настройка Из ОбщиеНастройки Цикл
		Если Не ТекущиеНастройки.ОбщиеНастройки.Свойство(Настройка.Ключ) Тогда
			ТекущиеНастройки.ОбщиеНастройки.Вставить(Настройка.Ключ, Новый СписокЗначений);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство КАК Свойство,
	                      |	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.Заголовок КАК СвойствоЗаголовок
	                      |ИЗ
	                      |	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
	                      |ГДЕ
	                      |	НЕ НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство.ПометкаУдаления
	                      |	И НЕ НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.ПометкаУдаления
	                      |	И НЕ НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка.ПометкаУдаления
	                      |	И НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка В ИЕРАРХИИ(&Наборы)");
	
	Для каждого НастройкаРеквизита Из ОбщиеНастройки.Интерес Цикл
		Если ТипЗнч(НастройкаРеквизита.Значение) = Тип("Строка") Тогда
			ТекНастройка = ТекущиеНастройки.ОбщиеНастройки.Интерес.НайтиПоЗначению(НастройкаРеквизита.Значение);
			СписокРеквизитовИнтереса.Добавить(НастройкаРеквизита.Значение, НастройкаРеквизита.Представление, 
				?(ТекНастройка = Неопределено, НастройкаРеквизита.Пометка, ТекНастройка.Пометка));	
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Наборы", УправлениеСвойствами.НаборСвойствПоИмени("Документ_CRM_Интерес"));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЕстьВНастройке = ТекущиеНастройки.ОбщиеНастройки.Интерес.НайтиПоЗначению(Выборка.Свойство);
		Если ЕстьВНастройке <> Неопределено Тогда
			Пометка = ЕстьВНастройке.Пометка;
		Иначе
			Пометка = Ложь;
		КонецЕсли;
		СписокРеквизитовИнтереса.Добавить(Выборка.Свойство, Выборка.СвойствоЗаголовок, Пометка);	
	КонецЦикла;
	
	Для каждого НастройкаРеквизита Из ОбщиеНастройки.Клиент Цикл
		Если ТипЗнч(НастройкаРеквизита.Значение) = Тип("Строка") Тогда
			ТекНастройка = ТекущиеНастройки.ОбщиеНастройки.Клиент.НайтиПоЗначению(НастройкаРеквизита.Значение);
			СписокРеквизитовКлиента.Добавить(НастройкаРеквизита.Значение, НастройкаРеквизита.Представление, 
				?(ТекНастройка = Неопределено, НастройкаРеквизита.Пометка, ТекНастройка.Пометка));	
		КонецЕсли;
	КонецЦикла;
		
	Наборы = Новый Массив;
	Наборы.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Партнеры_Общие"));
	Наборы.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Партнеры_Компании_CRM"));
	Наборы.Добавить(УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Партнеры_ЧастныеЛица_CRM"));
	Запрос.УстановитьПараметр("Наборы", Наборы);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЕстьВНастройке = ТекущиеНастройки.ОбщиеНастройки.Клиент.НайтиПоЗначению(Выборка.Свойство);
		Если ЕстьВНастройке <> Неопределено Тогда
			Пометка = ЕстьВНастройке.Пометка;
		Иначе
			Пометка = Ложь;
		КонецЕсли;
		СписокРеквизитовКлиента.Добавить(Выборка.Свойство, Выборка.СвойствоЗаголовок, Пометка);
	КонецЦикла;
	
	Для каждого НастройкаРеквизита Из ОбщиеНастройки.Контакт Цикл
		Если ТипЗнч(НастройкаРеквизита.Значение) = Тип("Строка") Тогда
			ТекНастройка = ТекущиеНастройки.ОбщиеНастройки.Контакт.НайтиПоЗначению(НастройкаРеквизита.Значение);
			СписокРеквизитовКонтакта.Добавить(НастройкаРеквизита.Значение, НастройкаРеквизита.Представление, 
				?(ТекНастройка = Неопределено, НастройкаРеквизита.Пометка, ТекНастройка.Пометка));	
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Наборы", УправлениеСвойствами.НаборСвойствПоИмени("Справочник_КонтактныеЛицаПартнеров"));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЕстьВНастройке = ТекущиеНастройки.ОбщиеНастройки.Контакт.НайтиПоЗначению(Выборка.Свойство);
		Если ЕстьВНастройке <> Неопределено Тогда
			Пометка = ЕстьВНастройке.Пометка;
		Иначе
			Пометка = Ложь;
		КонецЕсли;
		СписокРеквизитовКонтакта.Добавить(Выборка.Свойство, Выборка.СвойствоЗаголовок, Пометка);
	КонецЦикла;
	
	Для каждого НастройкаРеквизита Из ОбщиеНастройки.ПотенциальныйКлиент Цикл
		Если ТипЗнч(НастройкаРеквизита.Значение) = Тип("Строка") Тогда
			ТекНастройка = ТекущиеНастройки.ОбщиеНастройки.ПотенциальныйКлиент.НайтиПоЗначению(НастройкаРеквизита.Значение);
			СписокРеквизитовПотенциальногоКлиента.Добавить(НастройкаРеквизита.Значение, НастройкаРеквизита.Представление, 
				?(ТекНастройка = Неопределено, НастройкаРеквизита.Пометка, ТекНастройка.Пометка));	
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Наборы", УправлениеСвойствами.НаборСвойствПоИмени("Справочник_CRM_ПотенциальныеКлиенты"));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЕстьВНастройке = ТекущиеНастройки.ОбщиеНастройки.ПотенциальныйКлиент.НайтиПоЗначению(Выборка.Свойство);
		Если ЕстьВНастройке <> Неопределено Тогда
			Пометка = ЕстьВНастройке.Пометка;
		Иначе
			Пометка = Ложь;
		КонецЕсли;
		СписокРеквизитовПотенциальногоКлиента.Добавить(Выборка.Свойство, Выборка.СвойствоЗаголовок, Пометка);	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	ГотовоНаСервере();
	Закрыть(Истина);
КонецПроцедуры

&НаСервере
Процедура ГотовоНаСервере()
	
	Настройки = Новый Структура;
	ОбщиеНастройки.Вставить("Интерес", СписокРеквизитовИнтереса);
	ОбщиеНастройки.Вставить("Клиент", СписокРеквизитовКлиента);
	ОбщиеНастройки.Вставить("Контакт", СписокРеквизитовКонтакта);
	ОбщиеНастройки.Вставить("ПотенциальныйКлиент", СписокРеквизитовПотенциальногоКлиента);
	Настройки.Вставить("ОбщиеНастройки", ОбщиеНастройки);
	ХранилищеНастроек = Новый ХранилищеЗначения(Настройки);
	Константы.CRM_НастройкаСоставаРеквизитовМастераРегистрации.Установить(ХранилищеНастроек);
	ПараметрыСеанса.CRM_НастройкаСоставаРеквизитовМастераРегистрации = ХранилищеНастроек;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти
