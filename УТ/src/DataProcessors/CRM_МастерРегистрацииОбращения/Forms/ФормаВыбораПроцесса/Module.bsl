
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Карты = CRM_БизнесПроцессыСервер.ВернутьСпискиДоступныхКарт(Истина);
	
	ЗаполнитьДоступныеСценарииИнтересов(СписокДоступныхПроцессов, Параметры.Ответственный, Параметры.ТипУслуги);
	
	Для Каждого ЭлементСписка Из Карты Цикл
		СписокДоступныхПроцессов.Добавить(ЭлементСписка.Значение, Строка(ЭлементСписка.Значение), ,
			 БиблиотекаКартинок.БизнесПроцессОбъект);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СписокДоступныхПроцессов.Количество() = 1 Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, СписокДоступныхПроцессов[0].Значение);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.СписокДоступныхПроцессов.ТекущаяСтрока = 0;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДоступныхПроцессов

&НаКлиенте
Процедура СписокДоступныхПроцессовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(СписокДоступныхПроцессов[ВыбраннаяСтрока].Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДоступныхПроцессовПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокДоступныхПроцессовПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокДоступныхПроцессовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(СписокДоступныхПроцессов[Элементы.СписокДоступныхПроцессов.ТекущаяСтрока].Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДоступныеСценарииИнтересов(СписокСценариев, Ответственный, ТипУслуги)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ тмпПустыеПодраздеоения
	|ИЗ
	|	(ВЫБРАТЬ
	|		CRM_СостоянияИнтересов.Ссылка КАК Ссылка,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ CRM_СостоянияИнтересовПодразделения.Ссылка) КАК Ссылка1
	|	ИЗ
	|		Справочник.CRM_СостоянияИнтересов КАК CRM_СостоянияИнтересов
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.CRM_СостоянияИнтересов.Подразделения КАК CRM_СостоянияИнтересовПодразделения
	|			ПО CRM_СостоянияИнтересов.Ссылка = CRM_СостоянияИнтересовПодразделения.Ссылка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		CRM_СостоянияИнтересов.Ссылка) КАК ВложенныйЗапрос
	|ГДЕ
	|	ЕСТЬNULL(ВложенныйЗапрос.Ссылка1, 0) = 0
	|	И ВложенныйЗапрос.Ссылка.Родитель = ЗНАЧЕНИЕ(Справочник.CRM_СостоянияИнтересов.Пустаяссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	CRM_СостоянияИнтересовПодразделения.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ тмпДоступныеПоПодразделению
	|ИЗ
	|	Справочник.CRM_СостоянияИнтересов.Подразделения КАК CRM_СостоянияИнтересовПодразделения
	|ГДЕ
	|	CRM_СостоянияИнтересовПодразделения.Подразделение В ИЕРАРХИИ(&Подразделение)
	|	И CRM_СостоянияИнтересовПодразделения.Ссылка.Родитель = ЗНАЧЕНИЕ(Справочник.CRM_СостоянияИнтересов.Пустаяссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	тмпПустыеПодраздеоения.Ссылка
	|ИЗ
	|	тмпПустыеПодраздеоения КАК тмпПустыеПодраздеоения
	|
	|СГРУППИРОВАТЬ ПО
	|	тмпПустыеПодраздеоения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	CRM_СостоянияИнтересовТипыУслуг.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ тмпДоступныеПоФильтрам
	|ИЗ
	|	Справочник.CRM_СостоянияИнтересов.ТипыУслуг КАК CRM_СостоянияИнтересовТипыУслуг
	|ГДЕ
	|	ВЫБОР
	|		КОГДА &ИспользоватьТипыУслуг 
	|			И НЕ CRM_СостоянияИнтересовТипыУслуг.ТипУслуги = ЗНАЧЕНИЕ(Справочник.CRM_ТипУслуги.Пустаяссылка)
	|			ТОГДА CRM_СостоянияИнтересовТипыУслуг.ТипУслуги = &ТипУслуги
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И НЕ CRM_СостоянияИнтересовТипыУслуг.Ссылка.ПометкаУдаления
	|	И CRM_СостоянияИнтересовТипыУслуг.Ссылка В
	|			(ВЫБРАТЬ
	|				тмпДоступныеПоПодразделению.Ссылка КАК Ссылка
	|			ИЗ
	|				тмпДоступныеПоПодразделению КАК тмпДоступныеПоПодразделению)
	|	И CRM_СостоянияИнтересовТипыУслуг.Ссылка.Родитель = ЗНАЧЕНИЕ(Справочник.CRM_СостоянияИнтересов.Пустаяссылка)
	|	И НЕ CRM_СостоянияИнтересовТипыУслуг.Ссылка.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	CRM_СостоянияИнтересов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ тмпБезТиповУслуг
	|ИЗ
	|	Справочник.CRM_СостоянияИнтересов КАК CRM_СостоянияИнтересов
	|ГДЕ
	|	НЕ CRM_СостоянияИнтересов.Ссылка В
	|				(ВЫБРАТЬ
	|					CRM_СостоянияИнтересовТипыУслуг.Ссылка КАК Ссылка
	|				ИЗ
	|					Справочник.CRM_СостоянияИнтересов.ТипыУслуг КАК CRM_СостоянияИнтересовТипыУслуг
	|				ГДЕ
	|					CRM_СостоянияИнтересовТипыУслуг.Ссылка.Родитель = ЗНАЧЕНИЕ(Справочник.CRM_СостоянияИнтересов.Пустаяссылка)
	|				СГРУППИРОВАТЬ ПО
	|					CRM_СостоянияИнтересовТипыУслуг.Ссылка)
	|	И НЕ CRM_СостоянияИнтересов.Ссылка.ПометкаУдаления
	|	И CRM_СостоянияИнтересов.Ссылка В
	|			(ВЫБРАТЬ
	|				тмпДоступныеПоПодразделению.Ссылка КАК Ссылка
	|			ИЗ
	|				тмпДоступныеПоПодразделению КАК тмпДоступныеПоПодразделению)
	|	И CRM_СостоянияИнтересов.Ссылка.Родитель = ЗНАЧЕНИЕ(Справочник.CRM_СостоянияИнтересов.Пустаяссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	тмпДоступныеПоФильтрам.Ссылка КАК Ссылка
	|ИЗ
	|	тмпДоступныеПоФильтрам КАК тмпДоступныеПоФильтрам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	тмпБезТиповУслуг.Ссылка
	|ИЗ
	|	тмпБезТиповУслуг КАК тмпБезТиповУслуг";
	Запрос.УстановитьПараметр("ТипУслуги", ТипУслуги);
	Запрос.УстановитьПараметр("ИспользоватьТипыУслуг", ПолучитьФункциональнуюОпциюФормы("CRM_ИспользоватьТипыУслуг"));
	Запрос.УстановитьПараметр("Подразделение", Ответственный.Подразделение);

	СписокСценариев.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

#КонецОбласти
