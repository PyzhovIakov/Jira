
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураРеквизитов = Неопределено;
	Параметры.Свойство("СтруктураРеквизитов", СтруктураРеквизитов);
	
	Если СтруктураРеквизитов = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Параметры.Свойство("Заголовок", Заголовок);
	
	ЗапретитьИзменение = Неопределено;
	Параметры.Свойство("ЗапретитьИзменение", ЗапретитьИзменение);
	
	РеквизитыДобавление = Новый Массив;
	Для Каждого КиЗ Из СтруктураРеквизитов Цикл
		НовыйРеквизит = Новый РеквизитФормы(КиЗ.Ключ, КиЗ.Значение.Тип, , КиЗ.Значение.Заголовок);
		РеквизитыДобавление.Добавить(НовыйРеквизит);
	КонецЦикла;
	
	ИзменитьРеквизиты(РеквизитыДобавление);
	
	Элементы.ФормаСохранить.Видимость = ЗапретитьИзменение <> Истина;
	
	Для Каждого КиЗ Из СтруктураРеквизитов Цикл
		НовыйЭлемент = Элементы.Добавить(КиЗ.Ключ, Тип("ПолеФормы"));	
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = КиЗ.Ключ;
		Если КиЗ.Значение.СписокВыбора.Количество() > 0 Тогда
			Для Каждого ЭлементСписка Из КиЗ.Значение.СписокВыбора Цикл
				НовыйЭлемент.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);	
			КонецЦикла;
			НовыйЭлемент.РежимВыбораИзСписка = Истина;
		КонецЕсли;
		НовыйЭлемент.АвтоОтметкаНезаполненного = КиЗ.Значение.АвтоОтметкаНезаполненного;
		НовыйЭлемент.Заголовок = КиЗ.Значение.Заголовок;
		НовыйЭлемент.МногострочныйРежим = КиЗ.Значение.МногострочныйРежим;
		НовыйЭлемент.РастягиватьПоГоризонтали = Истина;
		НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
		НовыйЭлемент.ТолькоПросмотр = ЗапретитьИзменение = Истина;
		
		КиЗ.Значение.Свойство("Значение", ЭтотОбъект[КиЗ.Ключ]);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Закрыть(ПодготовитьСтруктуруВозврата());
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПодготовитьСтруктуруВозврата()
	
	Результат = Новый Структура;
	
	Для Каждого Реквизит Из ПолучитьРеквизиты() Цикл
		Результат.Вставить(Реквизит.Имя, ЭтотОбъект[Реквизит.Имя]);	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
