
#Область ОписаниеПеременных

// Используется для реализации выбора цвета двойным нажатием ЛКМ.
&НаКлиенте
Перем ИмяПоследнейНажатойКоманды;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КоличествоКнопокЦветаНаФорме = 15;
	ИсточникВызова = СокрЛП(Параметры.ИсточникВызова);
	ИсторияЦветов = ХранилищеНастроекДанныхФорм.Загрузить("РедакторHTMLВыборЦвета" + ИсточникВызова,
		 "ТаблицаЦветов", , ИмяПользователя());
	
	Если ТипЗнч(ИсторияЦветов) = Тип("ТаблицаЗначений") Тогда 
		Для каждого Цвет Из ИсторияЦветов Цикл
			ЗаполнитьЗначенияСвойств(ТабличноеПоле.Добавить(), Цвет);
			НаименованиеЭлементаЦвета = "История" + Строка(ТабличноеПоле.Количество());
			СоздатьКнопкуЦвета(Цвет, НаименованиеЭлементаЦвета, Элементы.ГруппаИсторияВыбораЦветов);
			Если ТабличноеПоле.Количество() >= КоличествоКнопокЦветаНаФорме Тогда 
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДобавитьОсновныеЦвета();
	
	Если ТабличноеПоле.Количество() > 0 Тогда 
		Красный = ТабличноеПоле[0].Красный;
		Зеленый = ТабличноеПоле[0].Зеленый;
		Синий 	= ТабличноеПоле[0].Синий;
	КонецЕсли;
	
	Шаг = 100 / (КоличествоКнопокЦветаНаФорме + 2);
	НомКнопки = 0;
	
	Для Сч = 1 По КоличествоКнопокЦветаНаФорме Цикл
		
		НомКнопки = НомКнопки + 1;
		Процент = ?(
			НомКнопки <> 8,
			Мин(Шаг * Сч * 2 / 100, 2),
			1);
		
		ЦветФона = Новый Цвет(ЦветНовый(Красный, Процент), ЦветНовый(Зеленый, Процент), ЦветНовый(Синий, Процент));
		СоздатьКнопкуЦвета(ЦветФона, "Яркость" + НомКнопки, Элементы.ГруппаЯркость);
		
	КонецЦикла;
	
	Оттенок = 100;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновлениеОтображенияПолеКартинкиЦвета();
	
КонецПроцедуры // ПриОткрытии()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеОттенокЦветаПриИзменении(Элемент)
	
	ОбновлениеОтображения();
	
КонецПроцедуры // ПолеОттенокЦветаПриИзменении()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОбновлениеОтображения();
	
	ЦветВозврата = ПрименитьФильтрыЦвета();
	
	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("Красный",	ЦветВозврата.Красный);
	СписокПараметров.Вставить("Синий",		ЦветВозврата.Синий);
	СписокПараметров.Вставить("Зеленый",	ЦветВозврата.Зеленый);
	
	СохранитьНаСервере(СписокПараметров);
	
	Закрыть(СписокПараметров);
	
КонецПроцедуры // ОК()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЦветНовый(ЦветВход, Процент)
	
	Если Процент < 1 Тогда 
		Возврат Цел(ЦветВход * Процент);
	Иначе 
		Если ЦветВход = 0 Тогда   
			Возврат Цел(255 * (Процент - 1));
		Иначе 
			Возврат Цел(ЦветВход + (255 - ЦветВход) * (Процент - 1));
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СоздатьКнопкуЦвета(Цвет, НомЦвета, ГруппаФормыДляДобавления)
	
	Если Элементы.Найти(НомЦвета) = Неопределено Тогда 
		НоваяКоманда = Команды.Добавить("КомандаВыборЦвета" + НомЦвета);
		НоваяКоманда.Действие 					= "ВыборЦвета";
		НоваяКоманда.Заголовок 					= "Выбор цвета";
		НоваяКоманда.ИзменяетСохраняемыеДанные 	= Ложь;
		НоваяКоманда.Отображение				= ОтображениеКнопки.Авто;
		НоваяКоманда.Подсказка					= "Выбор цвета";
		
		НовыйЭлемент = Элементы.Вставить("Цвет" + НомЦвета, Тип("КнопкаФормы"),
			 ГруппаФормыДляДобавления, Неопределено);
		НовыйЭлемент.Вид 		= ВидКнопкиФормы.КнопкаКоманднойПанели;
		НовыйЭлемент.Заголовок	= " ";
		НовыйЭлемент.ИмяКоманды	= "КомандаВыборЦвета" + НомЦвета;
		НовыйЭлемент.ЦветФона	= Новый Цвет(Цвет.Красный, Цвет.Зеленый, Цвет.Синий);
	Иначе 
		Элементы[НомЦвета].ЦветФона = Новый Цвет(Цвет.Красный, Цвет.Зеленый, Цвет.Синий);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОсновныеЦвета()
	
	МассивЦветов = Новый Массив;
	МассивЦветов.Добавить(Новый Цвет(000, 000, 000));
	МассивЦветов.Добавить(Новый Цвет(200, 200, 200));
	МассивЦветов.Добавить(Новый Цвет(255, 255, 255));
	МассивЦветов.Добавить(Новый Цвет(255, 000, 000));
	МассивЦветов.Добавить(Новый Цвет(255, 098, 000));
	МассивЦветов.Добавить(Новый Цвет(255, 144, 000));
	МассивЦветов.Добавить(Новый Цвет(255, 200, 000));
	МассивЦветов.Добавить(Новый Цвет(255, 255, 000));
	МассивЦветов.Добавить(Новый Цвет(140, 200, 000));
	МассивЦветов.Добавить(Новый Цвет(014, 172, 000));
	МассивЦветов.Добавить(Новый Цвет(000, 160, 196));
	МассивЦветов.Добавить(Новый Цвет(000, 095, 178));
	МассивЦветов.Добавить(Новый Цвет(000, 017, 168));
	МассивЦветов.Добавить(Новый Цвет(097, 000, 161));
	МассивЦветов.Добавить(Новый Цвет(198, 000, 125));
	
	СписокПараметров = Новый Структура("Красный,Синий, Зеленый");
	
	НомСтроки = 0;
	Для Каждого Цвет Из МассивЦветов Цикл 
		НомСтроки = НомСтроки + 1;
		СоздатьКнопкуЦвета(Цвет, "Стандарт" + НомСтроки, Элементы.ГруппаСтандартныйНабор);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере(СписокПараметров)
	
	НайденныеСтроки = ТабличноеПоле.НайтиСтроки(СписокПараметров);
	
	Если НайденныеСтроки.Количество() = 0 Тогда 
		Строка = ТабличноеПоле.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, СписокПараметров);
	Иначе 
		Строка = НайденныеСтроки[0];
	КонецЕсли;
	
	Строка.ДатаИспользования = ТекущаяДатаСеанса();
	
	ТабличноеПоле.Сортировать("ДатаИспользования Убыв");	
	ХранилищеНастроекДанныхФорм.Сохранить("РедакторHTMLВыборЦвета" + ИсточникВызова, "ТаблицаЦветов",
		 ТабличноеПоле.Выгрузить(), , ИмяПользователя());
	
КонецПроцедуры

&НаКлиенте
Функция ЦветНовыйК(ЦветВход, Процент)
	
	Если Процент < 1 Тогда 
		Возврат Цел(ЦветВход * Процент);
	Иначе 
		Если ЦветВход = 0 Тогда
			Возврат Цел(255 * (Процент - 1));
		Иначе 
			Возврат Цел(ЦветВход + (255 - ЦветВход) * (Процент - 1));
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбновлениеОтображенияПолеКартинкиЦвета()
	
	Элементы.ПолеКартинкиЦвета.ЦветФона = ПрименитьФильтрыЦвета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеОтображения()
	
	ОбновлениеОтображенияПолеКартинкиЦвета();
	
	Шаг = 100 / (КоличествоКнопокЦветаНаФорме + 2);
	НомКнопки = 0;
	
	Для Сч = 2 По КоличествоКнопокЦветаНаФорме + 1 Цикл
		
		НомКнопки = НомКнопки + 1;
		
		Процент = ?(
			НомКнопки <> 8,
			Мин(Шаг * Сч * 2 / 100, 2),
			1);
		
		Элементы["ЦветЯркость" + НомКнопки].ЦветФона = Новый Цвет(ЦветНовыйК(Красный, Процент),
			 ЦветНовыйК(Зеленый, Процент), ЦветНовыйК(Синий, Процент));
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборЦвета(Кнопка)
	
	НомерЦвета = СтрЗаменить(Кнопка.Имя, "КомандаВыборЦвета", "");
	ЦветКнопки = Элементы["Цвет" + НомерЦвета].ЦветФона;
	
	Если ИмяПоследнейНажатойКоманды = Неопределено Тогда
		ОтключитьОбработчикОжидания("ПослеВыбораЦветаОжиданиеВторогоКлика");
		
		ИмяПоследнейНажатойКоманды = Кнопка.Имя;
		
		Красный	= ЦветКнопки.Красный;
		Зеленый	= ЦветКнопки.Зеленый;
		Синий	= ЦветКнопки.Синий;
		
		Оттенок = 100;
		
		ПодключитьОбработчикОжидания("ПослеВыбораЦветаОжиданиеВторогоКлика", 0.25, Истина);
	ИначеЕсли ИмяПоследнейНажатойКоманды = Кнопка.Имя Тогда
		ОтключитьОбработчикОжидания("ПослеВыбораЦветаОжиданиеВторогоКлика");
		
		ОК(Команды.Найти("ОК"));
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораЦветаОжиданиеВторогоКлика()
	
	Если СтрНайти(ИмяПоследнейНажатойКоманды, "Стандарт") > 0
			Или СтрНайти(ИмяПоследнейНажатойКоманды, "История") > 0 Тогда
		ОбновлениеОтображения();
	Иначе
		ОбновлениеОтображенияПолеКартинкиЦвета();
	КонецЕсли;
	
	ИмяПоследнейНажатойКоманды = Неопределено;
	
КонецПроцедуры // ПослеВыбораЦветаОжиданиеВторогоКлика()

&НаКлиенте
Функция ПрименитьФильтрыЦвета()
	
	ЯркостьНормализованноеЗначение = Оттенок / 100;
	
	Возврат Новый Цвет(
		ЦветНовыйК(Красный, ЯркостьНормализованноеЗначение),
		ЦветНовыйК(Зеленый, ЯркостьНормализованноеЗначение),
		ЦветНовыйК(Синий, ЯркостьНормализованноеЗначение));
	
КонецФункции // ПрименитьФильтрыЦвета()

#КонецОбласти
