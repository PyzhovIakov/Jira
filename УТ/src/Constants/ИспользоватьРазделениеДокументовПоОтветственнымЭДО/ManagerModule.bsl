#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// СтандартныеПодсистемы.ОбновлениеВерсииИБ

// Отключает константу ИспользоватьРазделениеДокументовПоОтветственнымЭДО.
//
// Параметры:
//  Параметры - Структура - параметры обработчика обновления.
//                          См. документацию по подсистеме СтандартныеПодсистемы.ОбновлениеВерсииИБ.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Параметры.ПрогрессВыполнения.ВсегоОбъектов = 1;
	
	МетаданныеОбъекта = Метаданные.Константы.ИспользоватьРазделениеДокументовПоОтветственнымЭДО;
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();
	ПараметрыОтметкиВыполнения = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ОбработанныхОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Записать = Ложь;
		
		МенеджерЗначения = СоздатьМенеджерЗначения();
		МенеджерЗначения.Прочитать();
		СтароеЗначение = МенеджерЗначения.Значение;
		
		//Проверим сколько пользователей на данный момент есть в базе
		МассивПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей();
		Если МассивПользователей.Количество() < 2 Тогда
			МенеджерЗначения.Значение = Ложь;
		Иначе
			МассивПользователейИмеющихПрава = Новый Массив;
			Для Каждого ПользовательИБ Из МассивПользователей Цикл
				Если ПравоДоступа("Изменение", Метаданные.Документы.СообщениеЭДО, ПользовательИБ) Тогда
					МассивПользователейИмеющихПрава.Добавить(ПользовательИБ);
				КонецЕсли;
				Если МассивПользователейИмеющихПрава.Количество() > 1 Тогда
					МенеджерЗначения.Значение = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;

		Если Не МенеджерЗначения.Значение = СтароеЗначение Тогда
			Записать = Истина;
		КонецЕсли;

		Если Записать Тогда
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначения);
		Иначе
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(МенеджерЗначения, ПараметрыОтметкиВыполнения);
		КонецЕсли;
		
		ОбработанныхОбъектов = ОбработанныхОбъектов + 1;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
		ТекстСообщения =
			НСтр("ru = 'Не удалось обработать константу ""Использовать разделение документов по ответственным"" по причине:'")
			+ Символы.ПС + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение, МетаданныеОбъекта, , ТекстСообщения);
	КонецПопытки;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов
		+ ОбработанныхОбъектов;
	
	Параметры.ОбработкаЗавершена = (ОбработанныхОбъектов > 0);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ОбновлениеВерсииИБ

#КонецОбласти

#КонецЕсли