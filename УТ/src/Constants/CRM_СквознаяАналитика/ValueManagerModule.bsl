#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Значение Тогда
		ЗащищенныйОбъект = CRM_ЛицензированиеЭкспортныеМетоды.ПолучитьЗащищеннуюОбработку();
		ЗащищенныйОбъект.СквознаяАналитикаПриВключенииНастройки(Значение, Отказ);
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Если Значение Тогда
			CRM_ПользователиПереопределяемый.СоздатьИзменитьСлужебногоПользователяСервисов();
			СоздатьДополнительныеРеквизиты("Документ_CRM_Заявка", "ИДСквознаяАналитика", "ID Сквозной аналитики");
			СоздатьДополнительныеРеквизиты("Справочник_МаркетинговыеМероприятия",
				"ИДМаркетинговоеМероприятие", "ID Мероприятия");
			СоздатьДополнительныеРеквизиты("Справочник_CRM_ИсточникиПолученияЛидов", "Roistat_Маркер", "Roistat маркер");
			СоздатьИУстановитьСостояниеЛидаСозданоПоЗвонку();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьДополнительныеРеквизиты(ИмяНабора = "", ИмяДополнительногоРеквизита, Заголовок, ДлинаСтроки = 200)
	
	Если ЗначениеЗаполнено(ИмяНабора) Тогда
		
		ТекНаборСсылка = УправлениеСвойствами.НаборСвойствПоИмени(ИмяНабора);
		Если ТекНаборСсылка = Неопределено Тогда
			ТекНаборСсылка = Справочники.НаборыДополнительныхРеквизитовИСведений[ИмяНабора];
		КонецЕсли;
		ТекНабор = ТекНаборСсылка.ПолучитьОбъект();
		
		НаборСвойств = ТекНабор.Ссылка;
		ПредставлениеНабора = " (" + ТекНабор.Наименование + ")";
		
	Иначе
		НаборСвойств = Справочники.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка();
		ПредставлениеНабора = "";
	КонецЕсли;

	Структура = Новый Структура();
	Структура.Вставить("Заголовок", Заголовок);
	Структура.Вставить("ТипЗначения", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(ДлинаСтроки)));
	Структура.Вставить("Доступен", Истина);
	Структура.Вставить("Виден", Истина);
	Структура.Вставить("НаборСвойств", НаборСвойств);
	Структура.Вставить("Имя", ИмяДополнительногоРеквизита);
	
	ПВХ_Ссылка =  ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя",
		 ИмяДополнительногоРеквизита);
	Если ПВХ_Ссылка.Пустая() Тогда
		ТекОбъект = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(ТекОбъект, Структура);
		ТекОбъект.Наименование = ТекОбъект.Заголовок + ПредставлениеНабора;
		ТекОбъект.Записать();
		
		ПВХ_Ссылка = ТекОбъект.Ссылка;
	КонецЕсли;

	Если ЗначениеЗаполнено(ИмяНабора) Тогда
		ЗаписатьНабор = Ложь;
		
		Если НЕ ТекНабор.Используется Тогда
			ТекНабор.Используется = Истина;
			ЗаписатьНабор = Истина;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура("Свойство", ПВХ_Ссылка);
		НайденныеСтроки = ТекНабор.ДополнительныеРеквизиты.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = ТекНабор.ДополнительныеРеквизиты.Добавить();
			НоваяСтрока.Свойство = ПВХ_Ссылка;
			
			ЗаписатьНабор = Истина;
		КонецЕсли;
		
		Если ЗаписатьНабор Тогда
			ТекНабор.Записать();
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьИУстановитьСостояниеЛидаСозданоПоЗвонку()
	
	Если ЗначениеЗаполнено(Константы.CRM_СостояниеЛидаСозданоПоЗвонку.Получить()) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СостояниеЗаявкаПоЗвонку = Справочники.CRM_СостоянияЛидов.НайтиПоНаименованию("Создано по звонку", Истина);

	Если НЕ ЗначениеЗаполнено(СостояниеЗаявкаПоЗвонку) Тогда
		
		СостояниеЗаявкаПоЗвонкуОбъект = Справочники.CRM_СостоянияЛидов.СоздатьЭлемент();
		СостояниеЗаявкаПоЗвонкуОбъект.Наименование = "Создано по звонку";
		СостояниеЗаявкаПоЗвонкуОбъект.Записать();
		СостояниеЗаявкаПоЗвонку = СостояниеЗаявкаПоЗвонкуОбъект.Ссылка;
		
	КонецЕсли;
	
	Константы.CRM_СостояниеЛидаСозданоПоЗвонку.Установить(СостояниеЗаявкаПоЗвонку);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли
