
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее") Тогда
		CRM_ЦентрМониторингаКлиент.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.Взаимодействия.ЭлектронноеПисьмоВходящее.Удобство.КоличествоНажатийКнопкаСоздать");
	ИначеЕсли ТипЗнч(ПараметрКоманды) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее") Тогда
		CRM_ЦентрМониторингаКлиент.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.Взаимодействия.ЭлектронноеПисьмоОтправленное.Удобство.КоличествоНажатийКнопкаСоздать");
	КонецЕсли;
	
	Если ПроверитьПараметрКоманды(ПараметрКоманды) Тогда
		
		ПараметрыФормы = Новый Структура("ПредметОснование, ТолькоВернутьЭлемент", ПараметрКоманды, Истина);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПредметОснование", ПараметрКоманды);
		ДополнительныеПараметры.Вставить("ФормаИсточник", ПараметрыВыполненияКоманды.Источник);

		НовоеОповещение = Новый ОписаниеОповещения("ПослеВыбораКарты", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаВыборИзДерева", 
			ПараметрыФормы, 
			ПараметрыВыполненияКоманды.Источник, 
			ПараметрыВыполненияКоманды.Уникальность, 
			ПараметрыВыполненияКоманды.Окно, 
			ПараметрыВыполненияКоманды.НавигационнаяСсылка, НовоеОповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКарты(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Основание = Новый Структура("КартаМаршрута, Основание", Результат, ДополнительныеПараметры.ПредметОснование); 
		
		ПараметрыФормы = Новый Структура("Основание", Основание);
		Если CRM_БизнесПроцессыИЗадачиСервер.ЭтоПоручение(Результат) Тогда
			ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаПоручения", ПараметрыФормы,
				 ДополнительныеПараметры.ФормаИсточник);
		ИначеЕсли CRM_БизнесПроцессыИЗадачиСервер.ЭтоНезависимыйПроцесс(Результат) Тогда
			ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаСтартаНезависимого", ПараметрыФормы,
				 ДополнительныеПараметры.ФормаИсточник);
		Иначе
			ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.ФормаОбъекта", ПараметрыФормы);
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПроверитьПараметрКоманды(ПараметрКоманды)
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ПараметрКоманды))
		 И ОбщегоНазначения.ЭтоСправочник(ПараметрКоманды.Метаданные()) Тогда
		Возврат НЕ ПараметрКоманды.ЭтоГруппа;
	Иначе
		Возврат ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ПараметрКоманды));
	КонецЕсли;
	
КонецФункции

#КонецОбласти
