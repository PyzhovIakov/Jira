
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.СписокСсылок.Количество() > 0 Тогда
		
		Если Параметры.Свойство("БизнесПроцесс") Тогда
			// Добавляем в корень элемент Бизнес-Процесс
			КоллекцияЭлементов = ДеревоОснований.ПолучитьЭлементы();
			НоваяСтрока = КоллекцияЭлементов.Добавить();
			НоваяСтрока.Объект	= Параметры.БизнесПроцесс;
			НоваяСтрока.ЭтоЭтап	= Ложь;
			НоваяСтрока.ЭтоБизнесПроцесс = Истина;
		КонецЕсли;
		
		Для Каждого ЭлементСписка Из Параметры.СписокСсылок Цикл
			
			КоллекцияЭлементов = ДеревоОснований.ПолучитьЭлементы();
			НоваяСтрока = КоллекцияЭлементов.Добавить();
			НоваяСтрока.Объект	= ЭлементСписка.Представление;
			НоваяСтрока.ЭтоЭтап	= Истина;
			
			ПодчиненнаяКоллекция = НоваяСтрока.ПолучитьЭлементы();
			Для Каждого ЭлементМассива Из ЭлементСписка.Значение Цикл
				
				ПолноеИмяМД = Метаданные.НайтиПоТипу(ТипЗнч(ЭлементМассива)).ПолноеИмя();
				Если СтрНачинаетсяС(ПолноеИмяМД, "Справочник")
					И Не ТипЗнч(ЭлементМассива) = Тип("СправочникСсылка.Партнеры") Тогда
					
					Продолжить;
				КонецЕсли;
				
				ПодчиненнаяСтрока = ПодчиненнаяКоллекция.Добавить();
				ПодчиненнаяСтрока.Объект	= ЭлементМассива;
				ПодчиненнаяСтрока.ЭтоЭтап	= Ложь;
				ПодчиненнаяСтрока.Гиперссылка = НСтр("ru='Открыть';en='Open'");
			КонецЦикла;
			
			Если ПодчиненнаяКоллекция.Количество() = 0 Тогда
				КоллекцияЭлементов.Удалить(НоваяСтрока);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	Элементы.ИнформационнаяНадпись.Заголовок = НСтр("ru='Выберите документ,
		| на основании которого будет создан документ %ИмяДокумента%';
		|en='Select the document on the basis of which the document of %ИмяДокумента% will be created'");
	Элементы.ИнформационнаяНадпись.Заголовок = СтрЗаменить(Элементы.ИнформационнаяНадпись.Заголовок,
		 "%ИмяДокумента%",
		 Параметры.ИмяДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоОснований

&НаКлиенте
Процедура ДеревоОснованийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыборПроизведенИзСписка = (Элемент.ТекущийЭлемент.Имя = "ДеревоОснованийГиперссылка");
	
	Если ВыборПроизведенИзСписка И НЕ Элементы.ДеревоОснований.ТекущиеДанные.ЭтоЭтап Тогда
		ПоказатьЗначение(, Элементы.ДеревоОснований.ТекущиеДанные.Объект);
	ИначеЕсли НЕ Элементы.ДеревоОснований.ТекущиеДанные.ЭтоЭтап Тогда
		СтандартнаяОбработка = Ложь;
		Если ТипЗнч(Элементы.ДеревоОснований.ТекущиеДанные.Объект) = Тип("СправочникСсылка.Партнеры") Тогда
			ДанныеЗаполнения = Новый Структура("Партнер", Элементы.ДеревоОснований.ТекущиеДанные.Объект);
			Закрыть(ДанныеЗаполнения);
		Иначе
			Закрыть(Элементы.ДеревоОснований.ТекущиеДанные.Объект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОснованийВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка	= ВыборПроизведенИзСписка;
	ВыборПроизведенИзСписка	= Ложь;
	Если НЕ СтандартнаяОбработка И НЕ Элементы.ДеревоОснований.ТекущиеДанные.ЭтоЭтап Тогда
		Если ТипЗнч(Элементы.ДеревоОснований.ТекущиеДанные.Объект) = Тип("СправочникСсылка.Партнеры") Тогда
			ДанныеЗаполнения = Новый Структура("Партнер", Элементы.ДеревоОснований.ТекущиеДанные.Объект);
			Закрыть(ДанныеЗаполнения);
		Иначе
			Закрыть(Элементы.ДеревоОснований.ТекущиеДанные.Объект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
