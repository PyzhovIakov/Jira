
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если CRM_ЛицензированиеЭкспортныеМетоды.ВариантПоставкиСтартИлиСтандарт() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Бизнес-процессы доступны только для ""ПРОФ"" и ""КОРП"" поставки конфигурации!';
				 |en='The function is only available for ""PROF"" and ""CORP"" configurations versions!'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИспользоватьПодразделения = CRM_ОбщегоНазначенияПовтИсп.ИспользоватьПодразделения();
	
	Если Параметры.Свойство("ТолькоВернутьЭлемент") Тогда
		ТолькоВернутьЭлемент = Параметры.ТолькоВернутьЭлемент;
	КонецЕсли;
	
	Если Параметры.Свойство("ПредметОснование") Тогда
		ПредметОснование = Параметры.ПредметОснование;
	КонецЕсли;
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ТекущееПодразделение = ТекущийПользователь.Подразделение;
	
	Элементы.ОтборПодразделение.Видимость = ИспользоватьПодразделения;
	
	Если ЗначениеЗаполнено(ТекущееПодразделение) Тогда
		Элементы.ОтборПодразделение.СписокВыбора.Добавить(ТекущееПодразделение, ТекущееПодразделение);
		ОтборПодразделение = ТекущееПодразделение;
	Иначе
		Элементы.ОтборПодразделение.СписокВыбора.Добавить("Подразделение не выбрано",
			 НСтр("ru='Подразделение не выбрано';en='Department not selected'"));
		ОтборПодразделение = "Подразделение не выбрано";
	КонецЕсли;
	
	Если Параметры.Свойство("ВыводитьВсеКарты") Тогда
		ВыводитьВсеКарты = Параметры.ВыводитьВсеКарты;
	КонецЕсли;
	
	ОбновитьДеревоНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДерево

&НаКлиенте
Процедура ДеревоПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Описание = СформироватьОписаниеHTML(ТекущиеДанные.КартаПроект);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	Изменить(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекДанные = Элементы.Дерево.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ТолькоВернутьЭлемент Тогда
		Основание = Новый Структура("КартаМаршрута, Основание", ТекДанные.КартаПроект, ПредметОснование); 
		ПараметрыФормы = Новый Структура("Основание", Основание);
		Если CRM_БизнесПроцессыИЗадачиСервер.ЭтоПоручение(ТекДанные.КартаПроект) Тогда
			ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаПоручения", ПараметрыФормы);
		ИначеЕсли CRM_БизнесПроцессыИЗадачиСервер.ЭтоНезависимыйПроцесс(ТекДанные.КартаПроект) Тогда
			ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаСтартаНезависимого", ПараметрыФормы);
		Иначе
			ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.ФормаОбъекта", ПараметрыФормы);
		КонецЕсли;
		
		Закрыть();
	Иначе
		Закрыть(Элементы.Дерево.ТекущиеДанные.КартаПроект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтборПодразделение

&НаКлиенте
Процедура ОтборПодразделениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Элементы.ОтборПодразделение.СписокВыбора.Очистить();
	Если ВыбранноеЗначение = "ВыбратьИзСписка" Тогда
		ВыборПодразделенияЗавершение = Новый ОписаниеОповещения("ВыборПодразделенияЗавершение", ЭтотОбъект);
		ОткрытьФорму("Справочник.СтруктураПредприятия.ФормаВыбора", , ЭтотОбъект, ,
			 ВариантОткрытияОкна.ОтдельноеОкно, , ВыборПодразделенияЗавершение,
			 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		Элементы.ОтборПодразделение.СписокВыбора.Добавить(ВыбранноеЗначение,
			 ?(ВыбранноеЗначение = ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"),
			 "Все подразделения",
			 ВыбранноеЗначение));
		Элементы.ОтборПодразделение.СписокВыбора.Добавить(?(ЗначениеЗаполнено(ТекущееПодразделение),
			 ТекущееПодразделение, "Подразделение не выбрано"),
			?(ЗначениеЗаполнено(ТекущееПодразделение), НСтр("ru='Мое подразделение (';en='My Department ('") + ТекущееПодразделение 
				+ ")",
				 НСтр("ru='Подразделение не выбрано'")));
		ОтборПодразделение = ВыбранноеЗначение;
		ОбновитьДерево();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Элементы.ОтборПодразделение.СписокВыбора.Очистить();
	Элементы.ОтборПодразделение.СписокВыбора.Добавить(ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"),
		 НСтр("ru='Все подразделения';en='All Departments'"));
	Элементы.ОтборПодразделение.СписокВыбора.Добавить(?(ЗначениеЗаполнено(ТекущееПодразделение),
		 ТекущееПодразделение, "Подразделение не выбрано"),
			?(ЗначениеЗаполнено(ТекущееПодразделение), НСтр("ru='Мое подразделение (';en='My Department ('") + ТекущееПодразделение 
				+ ")",
				 НСтр("ru='Подразделение не выбрано'")));
	Элементы.ОтборПодразделение.СписокВыбора.Добавить("ВыбратьИзСписка",
		 НСтр("ru='Выбрать из списка...';en='Choose from the list ...'"));
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеПриИзменении(Элемент)
	Модифицированность = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеОчистка(Элемент, СтандартнаяОбработка)
	Элементы.ОтборПодразделение.СписокВыбора.Добавить(ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"),
		 "Все подразделения");
	ОтборПодразделение = ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка");
	ОбновитьДерево();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыводитьВсеКарты(Команда)
	
	ОставленоНаБудущее = "";
	//ВыводитьВсеКарты = Не ВыводитьВсеКарты;
	//Элементы.ФормаВыводитьВсеКарты.Пометка = ВыводитьВсеКарты;
	//Элементы.Отборы.Доступность = НЕ ВыводитьВсеКарты;
	//Элементы.ФормаВключатьЗавершенныеПроекты.Доступность = НЕ ВыводитьВсеКарты;
	//ОбновитьДерево();
	//CRM_ХранилищеНастроек.Сохранить(ИмяФормы, "ВыводитьВсеКарты", Элементы.ФормаВыводитьВсеКарты.Пометка);
	
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	Если Элементы.Дерево.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПоказатьЗначение(, Элементы.Дерево.ТекущиеДанные.КартаПроект);
КонецПроцедуры

&НаКлиенте
Процедура Развернуть(Команда)
	ЭлементыДерева = Дерево.ПолучитьЭлементы();
	Для Каждого тЭлемент Из ЭлементыДерева Цикл
		ИдентификаторСтроки = тЭлемент.ПолучитьИдентификатор();
		Элементы.Дерево.Развернуть(ИдентификаторСтроки, Истина);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Свернуть(Команда)
	ЭлементыДерева = Дерево.ПолучитьЭлементы();
	Для Каждого тЭлемент Из ЭлементыДерева Цикл
		ИдентификаторСтроки = тЭлемент.ПолучитьИдентификатор();
		Элементы.Дерево.Свернуть(ИдентификаторСтроки);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	ДеревоВыбор(Неопределено, Неопределено, Неопределено, Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьДерево()
	
	ОбновитьДеревоНаСервере();
	Развернуть(Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоНаСервере()

	Дерево.ПолучитьЭлементы().Очистить();
	
	Карты = CRM_БизнесПроцессыСервер.ВернутьСпискиДоступныхКарт();
	
	Для каждого Карта Из Карты Цикл
		НовыйЭлемент = Дерево.ПолучитьЭлементы().Добавить();
		НовыйЭлемент.КартаПроект = Карта.Значение;
		НовыйЭлемент.СтандартнаяКартинка = БиблиотекаКартинок.БизнесПроцесс;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПодразделенияЗавершение (Подразделение, ДопПараметры) Экспорт
	Если Подразделение <> Неопределено Тогда
		Элементы.ОтборПодразделение.СписокВыбора.Добавить(Подразделение, Подразделение);
		ОтборПодразделение = Подразделение;
		ОбновитьДерево();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьОписаниеHTML(Ссылка)
	
	ОписаниеПроцесса = Ссылка.Наименование + Символы.ПС + Символы.ПС + Ссылка.Комментарий;
	
	HTMLТекст = "<html>
		|<head>
		|<style type=""text/css"">
		|	body {
		|		overflow:    auto;
		|		margin-top:  2px;
		|		margin-left: 2px;
		|		font-family: Arial;
		|		font-size:   10pt;}
		|	table {
		|		width:       100%;
		|		font-family: Arial;
		|		font-size:   10pt;
		|		border: 0px solid;}
		|	td {vertical-align: top;}
		| 	a:link {
		|		color: #006699; text-decoration: none;}
		|	a:visited {
		|		color: #006699; text-decoration: none;}
		|	a:hover {
		|		color: #006699; text-decoration: underline;}
		|	p {
		|		margin-top: 15px;}
		|	table.frame {
		|		border-collapse: collapse;
		|		border: 1px solid #C8C8C8;}
		|	td.frame {
		|		border: 1px solid #C8C8C8;}
		|</style>
		|<body>";
		
	HTMLТекст = HTMLТекст + "<table>";
	
	HTMLТекст = HTMLТекст + "<tr>";
	HTMLТекст = HTMLТекст + "<td>";
	
	ОписаниеПроцесса = СтрЗаменить(ОписаниеПроцесса, Символы.ПС, "<br>");
	HTMLТекст = HTMLТекст + ОписаниеПроцесса;
	
	HTMLТекст = HTMLТекст + "</td>";
	HTMLТекст = HTMLТекст + "</tr>";
	
	HTMLТекст = HTMLТекст + "</table>";
	
	HTMLТекст = HTMLТекст + "</body></html>";
	CRM_РаботаСHTML.УдалитьВредоносныйКодИзТекста(HTMLТекст);
	
	Возврат HTMLТекст;
	
КонецФункции

#КонецОбласти
