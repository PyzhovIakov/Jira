
#Область СлужебныйПрограммныйИнтерфейс

Процедура ОткрытьФормуНачалаРаботыСПрограммой(ПараметрыНачалаРаботы) Экспорт
	
	Перем ПараметрыОткрытия, ОписаниеОповещения;
	
	ПараметрыОткрытияФормы(ПараметрыОткрытия, ПараметрыНачалаРаботы);
	ОписанияОповещенияЗакрытияФормы(ОписаниеОповещения, ПараметрыНачалаРаботы);
	
	ОткрытьФорму("Обработка.CRM_НачалоРаботыСПрограммой.Форма", ПараметрыОткрытия, , , , , ОписаниеОповещения);
	
КонецПроцедуры

Процедура ОбработкаЗакрытияФормыНачалаРаботы(Результат, ПараметрыНачалаРаботы) Экспорт
	
	Перем ОбработкаЗавершена;
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		ЗавершитьРаботуСистемы(Ложь, Ложь);
		Возврат;
	КонецЕсли;
	
	Если Результат.ДополнительныйПараметр = CRM_НачалоРаботыСПрограммойВызовСервера.ЗначениеДополнительногоПараметра() Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.НачатьРаботу <> Истина Тогда
		ЗавершитьРаботуСистемы(Ложь, Ложь);
		Возврат;
	КонецЕсли;
	
	CRM_НачалоРаботыСПрограммойВызовСервера.ОбработкаЗакрытияФормыНачалаРаботы(Результат,
		 ПараметрыНачалаРаботы,
		 ОбработкаЗавершена);
	Если ОбработкаЗавершена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПараметрыНачалаРаботы.ПараметрыРаботыКлиента.РазделениеВключено И
		Результат.СозданиеПользователя Тогда
		ДополнительныеПараметрыКоманднойСтроки = "";
		Если Результат.Свойство("ПользовательИмя") Тогда
			ДополнительныеПараметрыКоманднойСтроки = "/N" + """" + Результат.ПользовательИмя + """";
		КонецЕсли;
		
		Если СтрНайти(ВРег(ПараметрЗапуска), ВРег("АвтоТестПервогоЗапуска")) > 0 Тогда
			ЗавершитьРаботуСистемы(Ложь, Ложь);
		Иначе
			ЗавершитьРаботуСистемы(Ложь, Истина, ДополнительныеПараметрыКоманднойСтроки);
		КонецЕсли;
		
	КонецЕсли;
	
	// +Рабочий стол
	CRM_РабочийСтолКлиент.ПриНачалеРаботыСистемы();
	CRM_РабочийСтолКлиент.ПереинициализироватьРабочийСтолКлиент("Обработка.CRM_НастройкаРазделовРешения.Форма.Форма");
	// -Рабочий стол
	ОбновитьИнтерфейс();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПараметрыОткрытияФормы(ПараметрыОткрытия, ПараметрыНачалаРаботы)
	
	ПараметрыОткрытия = Новый Структура;
	CRM_НачалоРаботыСПрограммойВызовСервера.ДозаполнитьПараметрыОткрытияФормы(ПараметрыОткрытия, ПараметрыНачалаРаботы);
	
КонецПроцедуры

Процедура ОписанияОповещенияЗакрытияФормы(ОписаниеОповещения, ПараметрыНачалаРаботы)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаЗакрытияФормыНачалаРаботы",
		 CRM_НачалоРаботыСПрограммойКлиент,
		 ПараметрыНачалаРаботы);
	
КонецПроцедуры

#КонецОбласти
