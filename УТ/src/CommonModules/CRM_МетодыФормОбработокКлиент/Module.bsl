
////////////////////////////////////////////////////////////////////////////////
// Клиентские методы форм обработок (CRM)
//  
// В данный модуль вынесены методы подсистемы CRM, вызываемые из модулей типовых объектов. 
// Выносить можно только те методы, которые не вызывают стандартные методы типового модуля или обработчики форм. 
// Т.е. вызывают только те методы, что тоже вынесены из типового или не содержат таких вызовов.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Для каждого объекта необходимо задать свою #Область с именем объекта и модуля, как он называется в метаданных.

#Область Обработка_УниверсальныйОбменДаннымиXML_УправляемаяФорма

// Установить свойства элементов формы.
//
// Параметры:
//  Форма	 - ФормаКлиентскогоПриложения - Передаваемая форма. 
//
Процедура CRM_УстановитьСвойстваЭлементовФормы(Форма) Экспорт
	
	Форма.Элементы.Выгрузка.Видимость		= (Форма.CRM_ТолькоВыгрузка И НЕ Форма.CRM_ТолькоЗагрузка)
		 ИЛИ (НЕ Форма.CRM_ТолькоВыгрузка
		 И НЕ Форма.CRM_ТолькоЗагрузка);
	Форма.Элементы.Загрузка.Видимость		= (НЕ Форма.CRM_ТолькоВыгрузка И Форма.CRM_ТолькоЗагрузка)
		 ИЛИ (НЕ Форма.CRM_ТолькоВыгрузка
		 И НЕ Форма.CRM_ТолькоЗагрузка);
	Форма.Элементы.РежимВыгрузки.Видимость	= (Форма.CRM_ТолькоВыгрузка
		 И (Форма.CRM_ИмяТекущейКонфигурации = "CRM"
		 И Форма.CRM_ВерсияТекущейКонфигурации = "3"));
	
	Если Форма.CRM_ТолькоВыгрузка ИЛИ Форма.CRM_ТолькоЗагрузка Тогда
		//Форма.Элементы.ГлавнаяПанельФормы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Форма.Заголовок = ?(Форма.CRM_ТолькоВыгрузка,
			 НСтр("ru='Выгрузка счетов на оплату';en='Invoices Export'"),
			 НСтр("ru='Загрузка данных из файла'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Обработка_ГрупповоеИзменениеРеквизитов_ВыбранныеЭлементы

// Процедура - обработчик команды "CRM_ЗагрузитьИзРезультатаОтчета".
//
// Параметры:
//  Форма	 - ФормаКлиентскогоПриложения - Передаваемая форма. 
//
Процедура ЗагрузитьИзРезультатаОтчета(Форма) Экспорт
	СтруктураПараметры = Новый Структура("Режим, КомпоновщикНастроек, НеДляОтчетов", "",
		 Форма.КомпоновщикНастроек,
		 Истина);
	Если Не ЗначениеЗаполнено(Форма.КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки) Тогда
		СтруктураПараметры.Вставить("ИспользоватьПользовательскиеПоляОтбора");
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьРезультатВОтборЗавершение", ЭтотОбъект, Форма);
	ОткрытьФорму("ОбщаяФорма.CRM_ЗагрузкаРезультатовОтчетовВОтбор", СтруктураПараметры, Форма, , , ,
		 ОписаниеОповещения,
		 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

// Процедура - обработчик завершения загрузки результатов отчета.
//
// Параметры:
//  РезультатЗакрытия	 - Структура - Результат загрузки.
//  Форма	 - ФормаКлиентскогоПриложения - Передаваемая форма. 
//
Процедура ЗагрузитьРезультатВОтборЗавершение(РезультатЗакрытия, Форма) Экспорт
	Если НЕ ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	CRM_ОбщегоНазначенияКлиент.УстановитьОтборПоСпискуРезультатаОтчета(
	РезультатЗакрытия.Наименование,
	РезультатЗакрытия.Поле,
	Форма.КомпоновщикНастроек.Настройки,
	Форма.КомпоновщикНастроек.ПользовательскиеНастройки);
	
	Форма.ОтключитьОбработчикОжидания("ОбновитьСписокВыбранных");
	Если Форма.Элементы.ГруппаВыбранныеОбъекты.Видимость Тогда
		Форма.ПодключитьОбработчикОжидания("ОбновитьСписокВыбранных", 1, Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти
