
#Область СлужебныйПрограммныйИнтерфейс

Процедура СкопироватьФайлыВБуфер(ВыделенныеСтроки, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если ВыделенныеСтроки.Количество() = 0 Или Не СтандартныеПодсистемыКлиент.ЭтоЭлементДинамическогоСписка(Форма.Элементы.Список) Тогда
		Возврат;
	КонецЕсли;
	
	МассивСсылок = Новый Массив;
	Для Каждого ЭлементСписка Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Форма.Элементы.Список.ДанныеСтроки(ЭлементСписка);
		
		Если ДанныеСтроки.Зашифрован Тогда
			Продолжить;
		КонецЕсли;
		
		//Если НЕ ДанныеСтроки.ФайлРедактируется
		//	Или НЕ ДанныеСтроки.ФайлРедактируетТекущийПользователь Тогда
		//	
		//	Продолжить;
		//КонецЕсли;
		
		ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаДляОткрытия(ДанныеСтроки.Ссылка,
			 Неопределено,
			 Форма.УникальныйИдентификатор);
		Если ДанныеФайла.Зашифрован Тогда
			// Файл может быть изменен в другом сеансе.
			ОповеститьОбИзменении(ДанныеСтроки.Ссылка);
			Продолжить;
		КонецЕсли;
		
		МассивСсылок.Добавить(ДанныеСтроки.Ссылка);
		
	КонецЦикла;
	
	CRM_ПрисоединенныеФайлы.СкопироватьФайлыВБуфер(МассивСсылок);
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Копирование в буфер обмена'"), Форма.Окно.ПолучитьНавигационнуюСсылку(), 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Скопировано файлов: %1'"), МассивСсылок.Количество()));
	
КонецПроцедуры

Процедура ВставитьФайлыИзБуфера(ВыделенныеСтроки, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	
	Если НЕ CRM_ПрисоединенныеФайлы.БуферЗаполненФайлами() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Вставка из буфера обмена'"), Форма.Окно.ПолучитьНавигационнуюСсылку(), 
										НСтр("ru = 'Буфер обмена пуст'"));
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные = ТекущиеДанные(Форма);
	
	ГруппаФайлов = Неопределено;
	Если ТекущиеДанные <> Неопределено И ТекущиеДанные.ЭтоГруппа Тогда
		ГруппаФайлов = ТекущиеДанные.Ссылка;
	ИначеЕсли ТекущиеДанные <> Неопределено Тогда
		ГруппаФайлов = ГруппаФайла(ТекущиеДанные.Ссылка);
	КонецЕсли;
	
	СсылкаНаПоследнийФайл = Неопределено;
	CRM_ПрисоединенныеФайлы.ВставитьФайлыИзБуфера(ГруппаФайлов, Форма.ВладелецФайла, СсылкаНаПоследнийФайл);
	
	ОповеститьОбИзменении(Форма.ВладелецФайла);
	Оповестить("Запись_Файл", Новый Структура("ЭтоНовый, ВладелецФайла", Истина, Форма.ВладелецФайла),
				СсылкаНаПоследнийФайл);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекущиеДанные(Форма)
	
	Если Не СтандартныеПодсистемыКлиент.ЭтоЭлементДинамическогоСписка(Форма.Элементы.Список) Тогда
		Возврат Неопределено;
	ИначеЕсли Форма.Элементы.Список.ТекущиеДанные.ВладелецФайла = Форма.ВладелецФайла Тогда
		Возврат Форма.Элементы.Список.ТекущиеДанные;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции // ТекущиеДанные()

Функция ГруппаФайла(Файл)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Файл, "Родитель") Тогда
		Возврат Файл.Родитель;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти
