////////////////////////////////////////////////////////////////////////////////
// Общего назначения привилегированный
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверка на возможность изменения установленной валюты учета.
//
// Возвращаемое значение:
//	Булево - Истина, если изменить валюту учета невозможно.
//
Функция ОтказИзменитьВалютаУчета() Экспорт
	
	ЕстьДвижения = Ложь;
	
	СписокРегистров = Новый СписокЗначений;
	СписокРегистров.Добавить("CRM_РасчетыСПокупателями");
	СписокРегистров.Добавить("CRM_Продажи");
	СписокРегистров.Добавить("CRM_Предложения");
	
	СчетчикРегистровНакопления = 0;
	Запрос = Новый Запрос;
	Для каждого РегистрНакопления Из СписокРегистров Цикл
		
		Запрос.Текст = Запрос.Текст + 
			?(Запрос.Текст = "",
				"ВЫБРАТЬ ПЕРВЫЕ 1", 
				" 
				|
				|ОБЪЕДИНИТЬ ВСЕ 
				|
				|ВЫБРАТЬ ПЕРВЫЕ 1 ") + "
				|
				|	РегистрНакопления" + РегистрНакопления.Значение + ".Организация
				|ИЗ
				|	РегистрНакопления." + РегистрНакопления.Значение + " КАК РегистрНакопления" + РегистрНакопления.Значение;
		
		СчетчикРегистровНакопления = СчетчикРегистровНакопления + 1;
		
		Если СчетчикРегистровНакопления > 3 Тогда
			СчетчикРегистровНакопления = 0;
			Попытка
				// BSLLS:CreateQueryInCycle-off
				// Выполняются различные запросы
				// С текущим списком регистров не выполняется 
				РезультатЗапроса = Запрос.Выполнить();
				// BSLLS:CreateQueryInCycle-on
				ЕстьДвижения = НЕ РезультатЗапроса.Пустой();
			Исключение
				ЕстьДвижения = Ложь;
			КонецПопытки;
			
			Если ЕстьДвижения Тогда
				Прервать;
			КонецЕсли; 
			Запрос.Текст = "";
		КонецЕсли;
		
	КонецЦикла;
	
	Если СчетчикРегистровНакопления > 0 Тогда
		Попытка
			// BSLLS:CreateQueryInCycle-off
			// Выполняются различные запросы
			// С текущим списком регистров выполняется один раз 
			РезультатЗапроса = Запрос.Выполнить();
			// BSLLS:CreateQueryInCycle-on
			Если НЕ РезультатЗапроса.Пустой() Тогда
				ЕстьДвижения = Истина;
			КонецЕсли;
		Исключение
			ЕстьДвижения = Ложь;
		КонецПопытки;
	КонецЕсли;
	
	Если ЕстьДвижения Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции // ОтказИзменитьВалютаУчета()

#КонецОбласти
