////////////////////////////////////////////////////////////////////////////////
// Серверные процедуры и функции СофтФон.
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

// Функция - Ссылка на запись разговора
//  Возвращает ссылку на запись разговора
//
// Параметры:
//  ИдентификаторЗвонка	 - Строка	 - Идентификатор звонка
//  ВнутреннийНомер		 - Строка	 - Внутренний номер
//  Ошибка				 - Строка	 - Возвращаемое описание возникшей ошибки
// 
// Возвращаемое значение:
//  Строка - URI записи звонка.
//
Функция СсылкаНаЗаписьРазговора(ИдентификаторЗвонка, ВнутреннийНомер, Ошибка) Экспорт
	
	Возврат сфпСофтФонПроСерверПереопределяемый.СсылкаНаЗаписьРазговора(ИдентификаторЗвонка, ВнутреннийНомер, Ошибка);
	
КонецФункции

// Процедура - Параметры работы клиента при запуске
// Заполняет параметры работы клиента при запуске.
//
// Параметры:
//  Параметры	 - Структура	 -  Структура параметров
//
Процедура ПараметрыРаботыКлиентаПриЗапуске(Параметры) Экспорт
	
	ИспользуетсяТелефония = ИспользуетсяТелефония();
	
	Параметры.Вставить("ИспользуетсяТелефония", ИспользуетсяТелефония);
	
	Если НЕ ИспользуетсяТелефония Тогда
		Возврат;
	КонецЕсли;
	
	ЖурналЗвонков = КлючОбсужденияЖурналЗвонков();
	
	Если ЖурналЗвонков <> Неопределено Тогда
		Параметры.Вставить("СлужебныйЖурналЗвонков", ЖурналЗвонков);
		УстановитьПривилегированныйРежим(Истина);
		Если НЕ Константы.сфпЖурналЗвонковОбновлен.Получить() Тогда
			Параметры.Вставить("ОбновитьЖурналЗвонков", Истина);
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;

КонецПроцедуры

// Функция - Используется телефония
//  Функция возвращает признак использования телефонии
// 
// Возвращаемое значение:
//  Булево - Признак использования телефонии.
//
Функция ИспользуетсяТелефония() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("сфпИспользоватьОблачнуюТелефонию")
		 ИЛИ ПолучитьФункциональнуюОпцию("сфпИспользоватьМобильнуюТелефонию");
	
КонецФункции

// Процедура - Маршрутизировать вызов манго
//  Процедура предназначена для изменения маршрута вызова, еще не распределенного сотруднику
//  ВАТС (т.е. находящегося в голосовом меню или в очереди ожидания на группе); а также для
//  перехвата вызова, распределенного на сотрудника, до снятия им трубки (в состоянии Appeared).
//  В случае успешной обработки команды генерируется новый вызов.
//
// Параметры:
//  call_id			 - Строка	 - Идентификатор вызова, маршрут которого необходимо изменить.
//  ВнутреннийНомер	 - Строка	 - Внутренний номер
//
Процедура МаршрутизироватьВызовМанго(call_id, ВнутреннийНомер) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("command_id");
	ЗаписьJSON.ЗаписатьЗначение(Строка(Новый УникальныйИдентификатор));
	
	ЗаписьJSON.ЗаписатьИмяСвойства("call_id");
	ЗаписьJSON.ЗаписатьЗначение(call_id);
	
	ЗаписьJSON.ЗаписатьИмяСвойства("to_number");
	ЗаписьJSON.ЗаписатьЗначение(ВнутреннийНомер);
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	json = ЗаписьJSON.Закрыть();
	
	НастройкиТелефонии = ПолучитьНастройкиТелефонии();
	sign = ПолучитьSign(НастройкиТелефонии.vpbx_api_key, json, НастройкиТелефонии.vpbx_api_salt);
	
	ПараметрыТела = Новый Массив;
	ПараметрыТела.Добавить("vpbx_api_key=" + НастройкиТелефонии.vpbx_api_key);
	ПараметрыТела.Добавить("sign=" + sign);
	ПараметрыТела.Добавить(КодироватьСтроку("json=" + json, СпособКодированияСтроки.URLВКодировкеURL));
	
	URL = КорневойАдресАТС() + "commands/route";
	СтруктураURI = сфпОбщегоНазначенияКлиентСервер.СтруктураURI(URL);
	
	HTTPЗапрос = Новый HTTPЗапрос();
	HTTPЗапрос.АдресРесурса = СтруктураURI.ПутьНаСервере;
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	HTTPЗапрос.УстановитьТелоИзСтроки(сфпОбщегоНазначения.сфпСтрСоединить(ПараметрыТела, "&"),
		КодировкаТекста.UTF8,
		ИспользованиеByteOrderMark.НеИспользовать
	);
	
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(
		СтруктураURI.Хост,
		СтруктураURI.Порт, , , Прокси, 60,
		Новый ЗащищенноеСоединениеOpenSSL);
	
	HTTPОтвет = HTTPСоединение.ВызватьHTTPМетод("POST", HTTPЗапрос);
	
	ЗаписьЖурналаРегистрации(
		сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации() + "./commands/route",
		УровеньЖурналаРегистрации.Примечание,
		,
		,
		json + Символы.ПС + НСтр("ru='Код состояния HTTP ответа: ';en='HTTP response status code: '") + HTTPОтвет.КодСостояния
	);
	
КонецПроцедуры

// Функция - Создать изменить служебного пользователя телефонии
//  Функция создает нового пользователя. Если пользователь уже создан, то изменяет настройки его аутентификации.
//
// Параметры:
//  Включить - Булево	 - Признак необходимости использования аутентификации.
// 
// Возвращаемое значение:
//  Булево - Результат выполнения функции. Истина, если пользователь создан или изменен.
//
Функция СоздатьИзменитьСлужебногоПользователяТелефонии(Включить = Ложь) Экспорт
	
	Логин = СлужебныйПользовательТелефонииЛогин();
	Пароль = СлужебныйПользовательТелефонииПароль();
	
	УстановитьПривилегированныйРежим(Истина);
	ПользовательИзСправочника = сфпОбщегоНазначения.НайтиПользователяПоИмени(Логин);
	УстановитьПривилегированныйРежим(Ложь);
	
	Попытка
		Если ПользовательИзСправочника = Неопределено Тогда
			
			ИмяСобытия = "Телефония.СозданиеСлужебногоПользователя";
			
			ОписаниеПользователяИБ = сфпОбщегоНазначения.НовоеОписаниеПользователяИБ();
			ОписаниеПользователяИБ.Имя = Логин;
			ОписаниеПользователяИБ.ПолноеИмя = НСтр("ru='Служебный пользователь телефонии';en='Service user of telephony'");
			ОписаниеПользователяИБ.АутентификацияСтандартная = Включить;
			ОписаниеПользователяИБ.ПоказыватьВСпискеВыбора = Ложь;
			ОписаниеПользователяИБ.Вставить("Действие", "Записать");
			ОписаниеПользователяИБ.Вставить("ВходВПрограммуРазрешен", Истина);
			ОписаниеПользователяИБ.ЗапрещеноИзменятьПароль = Истина;
			ОписаниеПользователяИБ.Пароль = Пароль;
			ОписаниеПользователяИБ.Роли = Новый Массив;
			ОписаниеПользователяИБ.Роли.Добавить(Метаданные.Роли.сфпУправлениеМаршрутизацией.Имя);
			ОписаниеПользователяИБ.Роли.Добавить(Метаданные.Роли.CRM_СлужебныеПраваДляВнешнихСервисов.Имя);
			
			НовыйПользователь = Справочники.Пользователи.СоздатьЭлемент();
			НовыйПользователь.Наименование = ОписаниеПользователяИБ.ПолноеИмя;
			НовыйПользователь.Служебный = Истина;
			НовыйПользователь.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", ОписаниеПользователяИБ);
			НовыйПользователь.Записать();
			
		Иначе
			
			ИмяСобытия = "Телефония.ИзменениеДоступаСлужебногоПользователя";
			ИзменитьДоступВБазу(Включить, Пароль, ПользовательИзСправочника);
			
		КонецЕсли;
		
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Информация,
			 Метаданные.Справочники.Пользователи,
			 ПользовательИзСправочника);
		Возврат Истина;
		
	Исключение
		
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , ,
			 ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура обработчик подписки "ОчиститьВходящиеЗвонки". Очищает вспомогательные данные по удаляемому абоненту.
//
Процедура ОчиститьТекущиеЗвонкиПередУдалением(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ Пользователь
	|ИЗ РегистрСведений.сфпТекущиеЗвонки
	|ГДЕ АбонентКонтакт = &УдаляемыйАбонент");
	Запрос.УстановитьПараметр("УдаляемыйАбонент", Источник.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.сфпТекущиеЗвонки.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(Выборка.Пользователь);
		НаборЗаписей.Записать(Истина);
	КонецЦикла;
	
КонецПроцедуры

Функция КлючСлужебногоОбсужденияСистемыВзаимодействия() Экспорт
	
	Возврат "ТелефонияЖурналЗвонков";
	
КонецФункции

Процедура ПроверитьИдентификаторЖурналаЗвонков(СохраненныйИдентификатор) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Ключ = КлючСлужебногоОбсужденияСистемыВзаимодействия();
	
	Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(Ключ);
	Если Обсуждение = Неопределено Тогда
		  ОбсуждениеЖурналИдентификатор = сфпСофтФонПроСервер.СоздатьОбсуждениеЖурналЗвонков();
	Иначе ОбсуждениеЖурналИдентификатор = Обсуждение.Идентификатор;
	КонецЕсли;
	
	Если СохраненныйИдентификатор <> ОбсуждениеЖурналИдентификатор Тогда
		Константы.сфпИдентификаторЖурналаЗвонковСистемыВзаимодействия.Установить(ОбсуждениеЖурналИдентификатор);
		СохраненныйИдентификатор = ОбсуждениеЖурналИдентификатор;
	КонецЕсли;
	
КонецПроцедуры	

Функция СлужебныйПользовательТелефонииЛогин() Экспорт
	Возврат "TelephonyService";
КонецФункции

Функция СлужебныйПользовательТелефонииПароль() Экспорт
	Возврат "Rhp5931QwL";
КонецФункции

Функция СлужебныйПользовательТелефонииАвторизацияВСтрокеПодключения() Экспорт
	Возврат сфпОбщегоНазначения.сфпПодставитьПараметрыВСтроку("Usr=%1;Pwd=&quot;%2&quot;;",
		 СлужебныйПользовательТелефонииЛогин(),
		 СлужебныйПользовательТелефонииПароль());
КонецФункции

Функция ВосстановлениеJSON(Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Если Свойство = "timestamp" Тогда
		Возврат МестноеВремя(Дата(1970, 01, 01) + Число(Значение), ЧасовойПоясСеанса());
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьSign(vpbx_api_key, json, vpbx_api_salt) Экспорт
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA256);
	ХешированиеДанных.Добавить(vpbx_api_key + json + vpbx_api_salt);
	
	Возврат НРег(СтрЗаменить(Строка(ХешированиеДанных.ХешСумма), " ", ""));
	
КонецФункции

Функция ПолучитьИдентификаторЗаписиРазговора(Звонок) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ИдентификаторЗаписи
	|ИЗ РегистрСведений.сфпИсторияЗвонков
	|ГДЕ Звонок = &Звонок");	
	Запрос.УстановитьПараметр("Звонок", Звонок);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ИдентификаторЗаписи;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьАбонентаПоНомеруТелефона(Знач НомерТелефонаАбонента) Экспорт
	
	ПоисковоеВыражение = СтрЗаменить(НомерТелефонаАбонента, "+", "") + "%";
	Если сфпОбщегоНазначения.сфпСтрНачинаетсяС(ПоисковоеВыражение, "7")
		 Или сфпОбщегоНазначения.сфпСтрНачинаетсяС(ПоисковоеВыражение,
		 "8") Тогда
		ПоисковоеВыражение = "[78]" + Сред(ПоисковоеВыражение, 2);
	КонецЕсли;
	
	ТипыАбонентов = Новый Массив;
	Если Метаданные.Справочники.Найти("Партнеры") <> Неопределено Тогда
		ТипыАбонентов.Добавить(Метаданные.Справочники["Партнеры"]);
	КонецЕсли;
	Если Метаданные.Справочники.Найти("КонтактныеЛицаПартнеров") <> Неопределено Тогда
		ТипыАбонентов.Добавить(Метаданные.Справочники["КонтактныеЛицаПартнеров"]);
	КонецЕсли;
	Если Метаданные.Справочники.Найти("Контрагенты") <> Неопределено Тогда
		ТипыАбонентов.Добавить(Метаданные.Справочники["Контрагенты"]);
	КонецЕсли;
	Если Метаданные.Справочники.Найти("КонтактныеЛица") <> Неопределено Тогда
		ТипыАбонентов.Добавить(Метаданные.Справочники["КонтактныеЛица"]);
	КонецЕсли;
	Если Метаданные.Справочники.Найти("CRM_ПотенциальныеКлиенты") <> Неопределено Тогда
		ТипыАбонентов.Добавить(Метаданные.Справочники["CRM_ПотенциальныеКлиенты"]);
	КонецЕсли;
	
	СекцияОбъединить = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	
	БазаЗапроса = "
	|ВЫБРАТЬ
	|	КонтактнаяИнформация_1.Ссылка
	|ИЗ
	|	СправочникИсточник.КонтактнаяИнформация КАК КонтактнаяИнформация_1
	|ГДЕ
	|	КонтактнаяИнформация_1.НомерТелефона ПОДОБНО &ПоисковоеВыражение
	|	И КонтактнаяИнформация_1.Тип = &ТипТелефон";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПоисковоеВыражение", ПоисковоеВыражение);
	Запрос.УстановитьПараметр("ТипТелефон", Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.Текст = "";
	НомерОбъединения = 0;
	
	Для Каждого ТипАбонента Из ТипыАбонентов Цикл
		
		НомерОбъединения = НомерОбъединения + 1;
		ЧастьОбъединения = СтрЗаменить(БазаЗапроса, "СправочникИсточник", ТипАбонента.ПолноеИмя());
		ЧастьОбъединения = СтрЗаменить(ЧастьОбъединения, "КонтактнаяИнформация_1", "КонтактнаяИнформация_" 
			+ НомерОбъединения);
		
		Запрос.Текст = Запрос.Текст + ?(ПустаяСтрока(Запрос.Текст), "", СекцияОбъединить) + ЧастьОбъединения;
		
	КонецЦикла;
	
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ЗарегистрироватьТекущийВходящийЗвонок(НомерТелефонаАбонента, АбонентОтКого,
	 ПользовательКому, ДатаЗвонка, Событие, Входящий,
	 ИдентификаторЗвонкаВАТС = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПользовательКому) ИЛИ ТипЗнч(ПользовательКому) <> Тип("СправочникСсылка.Пользователи") Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.сфпТекущиеЗвонки.СоздатьНаборЗаписей();
	Набор.Отбор.Пользователь.Установить(ПользовательКому);
	Набор.Отбор.ИдентификаторЗвонка.Установить(ИдентификаторЗвонкаВАТС);
	
	ЗаписьНабора = Набор.Добавить();
	ЗаписьНабора.Пользователь = ПользовательКому;
	ЗаписьНабора.ИдентификаторЗвонка = ИдентификаторЗвонкаВАТС;
	ЗаписьНабора.НомерТелефона = НомерТелефонаАбонента;
	ЗаписьНабора.АбонентКонтакт = АбонентОтКого;
	ЗаписьНабора.ДатаЗвонка = ?(ЗначениеЗаполнено(ДатаЗвонка), ДатаЗвонка, ТекущаяДатаСеанса());
	ЗаписьНабора.Звонок = Событие;
	ЗаписьНабора.Входящий = Входящий;

	Набор.Записать(Истина);
	
КонецПроцедуры

Функция НастройкиТелефонииПоУмолчанию()
	
	Результат = Новый Структура();
	
	Результат.Вставить("МаршрутизироватьВходящиеНаОтветственных", Ложь);
	
	Результат.Вставить("ДействиеВходящегоЗвонка", "ОткрытьТелефонныйЗвонок");
	Результат.Вставить("ДействиеИсходящегоЗвонка", "НичегоНеДелать");
	
	// СофтФонWebModule, MangoOffice, Ростелеком
	Результат.Вставить("vpbx_api_key", "");
	Результат.Вставить("vpbx_api_salt", "");
	
	// Ростелеком
	Результат.Вставить("Domain", "");
	
	// Itoolabs
	Результат.Вставить("АдресОблачнойАТС", "");
	Результат.Вставить("КлючДляАвторизацииВОблачнойАТС", "");
	Результат.Вставить("КлючДляАвторизацииВИБ", Строка(Новый УникальныйИдентификатор));
	
	// Яндекс
	Результат.Вставить("КлючДляАвторизацииАТСЯндекс", "");
	
	// Билайн
	Результат.Вставить("КлючДляАвторизацииВОблачнойАТС", "");
	Результат.Вставить("КлючПодпискиНаСобытия", "");
	
	// МТТ
	Результат.Вставить("АдресОблачнойАТС", "");
	Результат.Вставить("КлючДляАвторизацииВОблачнойАТС", "");
	Результат.Вставить("КлючДляАвторизацииВИБ", Строка(Новый УникальныйИдентификатор));
	
	// UIS
	Результат.Вставить("КлючДляАвторизацииАТСUIS", "");

	Возврат Результат;
	
КонецФункции

Функция ПолучитьПользователяПоВнутреннемуНомеру(ВнутреннийНомер) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ Объект КАК Абонент
	|ИЗ РегистрСведений.сфпКонтактыТелефонии
	|ГДЕ ВнутреннийНомерАТС = &ВнутреннийНомер");
	Запрос.УстановитьПараметр("ВнутреннийНомер", ВнутреннийНомер);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Абонент;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ДанныеПользователяАТС(Знач Пользователь = Неопределено)
	Возврат сфпЛицензированиеЭкспортныеМетоды.ДанныеПользователяАТС(Пользователь);	
КонецФункции

Функция ПолучитьВнутреннийНомерПоПользователю(Пользователь = Неопределено) Экспорт
	
	ДанныеПользователяАТС = ДанныеПользователяАТС(Пользователь);
	Если ДанныеПользователяАТС = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат ДанныеПользователяАТС.ВнутреннийНомер;
	
КонецФункции

Процедура ИзменитьДоступВБазу(Включить, Знач Пароль, Знач ПользовательИзСправочника)
	
	ОбновляемыеСвойства = Новый Структура;
	ОбновляемыеСвойства.Вставить("СтарыйПароль", Пароль);
	ОбновляемыеСвойства.Вставить("АутентификацияСтандартная", Включить);
	ОбновляемыеСвойства.Вставить("Роли", Новый Массив());
	ОбновляемыеСвойства.Роли.Добавить(Метаданные.Роли.сфпУправлениеМаршрутизацией.Имя);
	ОбновляемыеСвойства.Роли.Добавить(Метаданные.Роли.CRM_СлужебныеПраваДляВнешнихСервисов.Имя);
	
	ОписаниеОшибки = "";
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторПользователяИБ = сфпОбщегоНазначения.сфпЗначениеРеквизитаОбъекта(ПользовательИзСправочника,
		 "ИдентификаторПользователяИБ");
	
	ПользовательЗаписан = сфпОбщегоНазначения.ЗаписатьПользователяИБ(
		ИдентификаторПользователяИБ, ОбновляемыеСвойства, Ложь, ОписаниеОшибки, ПользовательИзСправочника);
			
	Если НЕ ПользовательЗаписан Тогда
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьДанныеЗвонка(Звонок, ИдентификаторЗвонкаВАТС, ДатаСобытия,
	 НачалоРазговора = Неопределено, Входящий, Контрагент, НомерТелефона, ВнутреннийНомер,
	 Ответственный) Экспорт
	
	ИдентификаторЗаписи = "";
	
	НаборЗаписей = РегистрыСведений.сфпИсторияЗвонков.СоздатьНаборЗаписей(); 
	НаборЗаписей.Отбор.НомерТелефона.Установить(ИдентификаторЗвонкаВАТС);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() > 0 Тогда
		ИдентификаторЗаписи = НаборЗаписей[0].ИдентификаторЗаписи;
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать();
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.сфпИсторияЗвонков.СоздатьНаборЗаписей(); 
	НаборЗаписей.Отбор.Звонок.Значение = Звонок;
	НаборЗаписей.Отбор.Звонок.Использование = Истина;
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда
		  Запись = НаборЗаписей[0];
	Иначе Запись = НаборЗаписей.Добавить();
	КонецЕсли;
	
	Запись.Звонок = Звонок;
	Запись.ИдентификаторЗвонка = ИдентификаторЗвонкаВАТС;
	Если НЕ ЗначениеЗаполнено(Запись.ИдентификаторЗаписи) Тогда
		Запись.ИдентификаторЗаписи = ИдентификаторЗаписи;
	КонецЕсли;
	Запись.ДатаНачала = ДатаСобытия;
	Если НачалоРазговора <> Неопределено Тогда
		Запись.ДатаОтвета = НачалоРазговора;
	КонецЕсли;
	Запись.Входящий = Входящий;
	Запись.АбонентКонтакт = Контрагент;
	Запись.ВнутреннийНомер = ВнутреннийНомер;
	Запись.НомерТелефона = НомерТелефона;
	Запись.Ответственный = Ответственный;
	
	НачатьТранзакцию();
	
	Попытка
		НаборЗаписей.Записать(Истина);
		
		Если ЗначениеЗаполнено(ИдентификаторЗвонкаВАТС) Тогда
			Запрос = Новый Запрос("
			|ВЫБРАТЬ ДатаНачала, НомерТелефона, Звонок
			|ИЗ РегистрСведений.сфпИсторияЗвонков
			|ГДЕ ИдентификаторЗвонка = &ИдентификаторЗвонка");
			Запрос.УстановитьПараметр("ИдентификаторЗвонка", ИдентификаторЗвонкаВАТС);
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если НЕ ЗначениеЗаполнено(Выборка.Звонок) Тогда
					МенеджерЗаписи = РегистрыСведений.сфпИсторияЗвонков.СоздатьМенеджерЗаписи();
					МенеджерЗаписи.ДатаНачала = Выборка.ДатаНачала;
					МенеджерЗаписи.НомерТелефона = Выборка.НомерТелефона;
					МенеджерЗаписи.Звонок = Выборка.Звонок;
					МенеджерЗаписи.Прочитать();
					Если МенеджерЗаписи.Выбран() Тогда
						МенеджерЗаписи.Удалить();
					КонецЕсли;
				КонецЕсли;	
			КонецЦикла;		
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение       
		ОтменитьТранзакцию();
	КонецПопытки; 
	
КонецПроцедуры

Функция СоздатьТелефонныйЗвонок(Абонент, НомерТелефонаАбонента, Пользователь, Сотрудник,
	 ВнутреннийНомерПользователя, Входящий, ДатаНачалаРазговора, ИдентификаторЗвонкаВАТС = Неопределено,
	 ОбъектОснование = Неопределено) Экспорт
	
	ТелефонныйЗвонок = Неопределено;
	Если ИдентификаторЗвонкаВАТС <> Неопределено Тогда
		ТелефонныйЗвонок = НайтиТелефонныйЗвонокПоДаннымЗвонка(, , ИдентификаторЗвонкаВАТС);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТелефонныйЗвонок) Тогда
		Возврат ТелефонныйЗвонок;
	КонецЕсли;
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	ИмяДокументаТелефонныйЗвонок = СтрЗаменить(сфпСофтФонПроСервер.сфпИмяДокументаТелефонныйЗвонок(), "Документ.", "");
	РеквизитыДокумента = Метаданные.Документы[ИмяДокументаТелефонныйЗвонок].Реквизиты;
	
	ТелефонныйЗвонокОбъект	= Документы[ИмяДокументаТелефонныйЗвонок].СоздатьДокумент();
	ТелефонныйЗвонокОбъект.Входящий = Входящий;
	ТелефонныйЗвонокОбъект.Дата = ТекущаяДатаСеанса();
	ТелефонныйЗвонокОбъект.Ответственный = ?(ЗначениеЗаполнено(Сотрудник), Сотрудник,
		 сфпСофтФонПроСервер.сфпТекущийПользователь());
	ТелефонныйЗвонокОбъект.Автор = ТелефонныйЗвонокОбъект.Ответственный;
	ТелефонныйЗвонокОбъект.АбонентКакСвязаться = НомерТелефонаАбонента; 
	ТелефонныйЗвонокОбъект.АбонентПредставление = НСтр("ru='!!!Не определен!!!';en='!!!Undefined!!!'");
	ТелефонныйЗвонокОбъект.сфпИдентификаторЗвонка = ИдентификаторЗвонкаВАТС;
	ТелефонныйЗвонокОбъект.сфпДлительностьЗвонка = 0;
	ТелефонныйЗвонокОбъект.Описание =
		сфпСофтФонПроСервер.сфпЗаполнитьОписаниеТелефонногоЗвонка(ТелефонныйЗвонокОбъект.сфпДлительностьЗвонка);
	
	КаналПервичногоИнтереса = "";
	ИсточникПервичногоИнтереса = "";
	ИзменитьCRM_СостоянияЛидов = Ложь; // +CRM
				
	Если ТелефонныйЗвонокОбъект.Входящий Тогда
		ТелефонныйЗвонокОбъект.сфпСостояниеЗвонка = Перечисления.сфпСостоянияЗвонков.Пропущенный;
		
		МенеджерЗаписи = РегистрыСведений.сфпДанныеКоллтрекинга.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.НомерТелефона = Прав(ТелефонныйЗвонокОбъект.АбонентКакСвязаться, 10);
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			Если МенеджерЗаписи.Оператор = Перечисления.CRM_CallTrakingСценарии.Comagic
				ИЛИ МенеджерЗаписи.Оператор = Перечисления.CRM_CallTrakingСценарии.UIS Тогда
				
				ТелефонныйЗвонокОбъект["сфпCoMagicID"] = МенеджерЗаписи.Идентификатор;
			КонецЕсли;
			
			ТелефонныйЗвонокОбъект["Комментарий"] = ТелефонныйЗвонокОбъект["Комментарий"] 
				+ ?(НЕ ЗначениеЗаполнено(ТелефонныйЗвонокОбъект["Комментарий"]), "", Символы.ПС)
				+ НСтр("ru='Сайт: ';en='Website: '") + МенеджерЗаписи.Сайт + Символы.ПС
				+ НСтр("ru='Ключевые слова: ';en='Keywords: '") + МенеджерЗаписи.utm_term + Символы.ПС
				+ НСтр("ru='Объявление: ';en='Content: '") + МенеджерЗаписи.utm_content;
					
			// +CRM
			КаналПервичногоИнтереса = МенеджерЗаписи.КаналПервичногоИнтереса;
			ИсточникПервичногоИнтереса = МенеджерЗаписи.ИсточникПервичногоИнтереса;
			
			Если ЗначениеЗаполнено(МенеджерЗаписи.Заявка) Тогда
				ТелефонныйЗвонокОбъект["ВзаимодействиеОснование"] = МенеджерЗаписи.Заявка;
				ИзменитьCRM_СостоянияЛидов = Истина;
				
				ЗаявкаОбъект = МенеджерЗаписи.Заявка.ПолучитьОбъект();
				Если НЕ ЗаявкаОбъект.CRM_СкрытьВАРМ Тогда
					ЗаявкаОбъект.CRM_СкрытьВАРМ = Истина;
					ЗаявкаОбъект.ДополнительныеСвойства.Вставить("НеЗапускатьТриггер", Истина);
					ЗаявкаОбъект.Записать();
				КонецЕсли;
			КонецЕсли;
			// -CRM
			
			МенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЕсли;
	
	Если ТелефонныйЗвонокОбъект.Важность.Метаданные().Имя = "ВариантыВажностиЗадачи" Тогда
		  ТелефонныйЗвонокОбъект.Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
	Иначе ТелефонныйЗвонокОбъект.Важность = Перечисления.ВариантыВажностиВзаимодействия.Обычная;
	КонецЕсли;
	
	Если РеквизитыДокумента.Найти("Тема") <> Неопределено Тогда
		ТелефонныйЗвонокОбъект.Тема = сфпСофтФонПроСервер.сфпЗаполнитьТемуТелефонногоЗвонка(ТелефонныйЗвонокОбъект.Входящий,
			 ТелефонныйЗвонокОбъект.Дата);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Абонент) Тогда
		ТелефонныйЗвонокОбъект.АбонентПредставление = "" + Абонент;
		ТелефонныйЗвонокОбъект.АбонентКонтакт = Абонент;
	ИначеЕсли ТелефонныйЗвонокОбъект.Входящий Тогда	
		CRM_КлиентыСервер.СоздатьКонтактВзаимодействия(ТелефонныйЗвонокОбъект);
		Абонент = ТелефонныйЗвонокОбъект.АбонентКонтакт;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ОбъектОснование) Тогда
		ТелефонныйЗвонокОбъект["ВзаимодействиеОснование"] = ОбъектОснование;
	КонецЕсли;
	
	Попытка
		ТелефонныйЗвонокОбъект.УстановитьНовыйНомер();
		ТелефонныйЗвонокОбъект.Записать();
		ТелефонныйЗвонок = ТелефонныйЗвонокОбъект.Ссылка;
		
		// РегистрСведений.сфпИсторияЗвонков
		Если ЗначениеЗаполнено(ДатаНачалаРазговора) Тогда
			ДатаСобытия = ДатаНачалаРазговора;
		Иначе
			ДатаСобытия = ТелефонныйЗвонокОбъект.Дата;
		КонецЕсли;
		
		ЗаписатьДанныеЗвонка(ТелефонныйЗвонок, ИдентификаторЗвонкаВАТС, ДатаСобытия, , Входящий, Абонент,
			 НомерТелефонаАбонента, ВнутреннийНомерПользователя,
			 Сотрудник);
		
		// +CRM
		Если ИзменитьCRM_СостоянияЛидов Тогда
			СостояниеЗаявкаПоЗвонку = Константы.CRM_СостояниеЛидаСозданоПоЗвонку.Получить();
			Если ЗначениеЗаполнено(СостояниеЗаявкаПоЗвонку) Тогда
				РегистрыСведений.CRM_СостоянияЛидов.ЗаписатьСостояниеЛида(ЗаявкаОбъект.Ссылка, СостояниеЗаявкаПоЗвонку);

			Иначе
				РегистрыСведений.CRM_СостоянияЛидов.УдалитьСостояниеЛида(ЗаявкаОбъект.Ссылка);
			КонецЕсли;
		КонецЕсли;
		// -CRM
	
		Если ЗначениеЗаполнено(КаналПервичногоИнтереса) ИЛИ ЗначениеЗаполнено(ИсточникПервичногоИнтереса) Тогда
			// Зафиксируем актуальный источник привлечения
			НаборРегистра = РегистрыСведений.ИсточникиПервичногоИнтереса.СоздатьНаборЗаписей();
			НаборРегистра.Отбор.Сделка.Установить(ТелефонныйЗвонок);
		
			Партнер = Справочники.Партнеры.ПустаяСсылка();
			Если ЗначениеЗаполнено(ТелефонныйЗвонокОбъект.АбонентКонтакт) Тогда
				Если ТипЗнч(ТелефонныйЗвонокОбъект.АбонентКонтакт) = Тип("СправочникСсылка.Партнеры")
					 ИЛИ ТипЗнч(ТелефонныйЗвонокОбъект.АбонентКонтакт) = Тип("СправочникСсылка.CRM_ПотенциальныеКлиенты") Тогда
					Партнер = ТелефонныйЗвонокОбъект.АбонентКонтакт;
					
				ИначеЕсли ТипЗнч(ТелефонныйЗвонокОбъект.АбонентКонтакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда	
					Партнер = ТелефонныйЗвонокОбъект.АбонентКонтакт.Владелец;
				КонецЕсли;
			КонецЕсли;	
			
			Запись = НаборРегистра.Добавить();
			Запись.Период = ТелефонныйЗвонокОбъект.Дата;
			Запись.Партнер = Партнер;
			Запись.Сделка = ТелефонныйЗвонок;
			Запись.ИсточникПервичногоИнтереса = ИсточникПервичногоИнтереса;
			Запись.КаналПервичногоИнтереса = КаналПервичногоИнтереса;
			
			НаборРегистра.Записать();
		КонецЕсли;
	
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ТелефонныйЗвонок;
	
КонецФункции

Функция ПолучитьВходящийЗвонокПользователя(Пользователь = Неопределено) Экспорт
	
	Результат = Новый Структура("АбонентКонтакт, ДатаЗвонка, НомерТелефона, ПредставлениеАбонента");
	
	Если Пользователь = Неопределено Тогда
		Пользователь = сфпТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИдентификаторЗвонка,
	|	АбонентКонтакт,
	|	ДатаЗвонка,
	|	НомерТелефона,
	|	ПРЕДСТАВЛЕНИЕ(АбонентКонтакт) КАК ПредставлениеАбонента
	|ИЗ
	|	РегистрСведений.сфпТекущиеЗвонки
	|ГДЕ
	|	Пользователь = &ПользовательКому
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаЗвонка УБЫВ");
	Запрос.УстановитьПараметр("ПользовательКому", Пользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.сфпТекущиеЗвонки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	НаборЗаписей.Записать(Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьДанныеЗвонка(ИдентификаторЗвонка, ОчиститьДанные = Неопределено,
	 ДанныеЗаполнения = Неопределено) Экспорт
	
	Если ОчиститьДанные = Неопределено Тогда
		ОчиститьДанные = Истина;
	КонецЕсли;
	ОчиститьДанные = Ложь;
	
	Результат = ПолучитьНастройкиТелефонии();
	ТекущийПользователь = сфпТекущийПользователь();
	
	ДанныеЗвонка = Новый Структура("Пользователь,АбонентКонтакт,ДатаЗвонка,НомерТелефона,Звонок,
		|ПредставлениеАбонента,Входящий,
		|ИдентификаторЗвонка");
	ДанныеЗвонка.Вставить("Пользователь", ТекущийПользователь);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИдентификаторЗвонка,
	|	АбонентКонтакт,
	|	ДатаНачала КАК ДатаЗвонка,
	|	НомерТелефона,
	|	Звонок,
	|	Входящий,
	|	ВнутреннийНомер
	|ИЗ
	|	РегистрСведений.сфпИсторияЗвонков
	|ГДЕ
	|	ИдентификаторЗвонка = &ИдентификаторЗвонка И ДатаНачала > &ДатаНачала");
	Запрос.УстановитьПараметр("ИдентификаторЗвонка", ИдентификаторЗвонка);
	Запрос.УстановитьПараметр("ДатаНачала", (ТекущаяДатаСеанса() - 3600));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ДанныеЗвонка, Выборка);
		ДанныеЗвонка.Вставить("Контакт", ДанныеЗвонка.АбонентКонтакт);
		ДанныеЗвонка.Вставить("ВходящийЗвонок", ДанныеЗвонка.Входящий);
		ДанныеЗвонка.Вставить("НовыйЗвонок", ДанныеЗвонка.Звонок);
		
		МассивЗвонящих = Новый Массив();
		МассивЗвонящих.Добавить(ДанныеЗвонка.АбонентКонтакт);
		ДанныеЗвонка.Вставить("МассивЗвонящих", МассивЗвонящих);
						
		Если ОчиститьДанные Тогда
			НаборЗаписей = РегистрыСведений.сфпТекущиеЗвонки.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Пользователь.Установить(ТекущийПользователь);
			НаборЗаписей.Записать();
			
			Запрос = Новый Запрос("
			|ВЫБРАТЬ РАЗЛИЧНЫЕ Пользователь
			|ИЗ РегистрСведений.сфпТекущиеЗвонки
			|ГДЕ Звонок = &Звонок");
			Запрос.УстановитьПараметр("Звонок", ДанныеЗвонка.Звонок);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				НаборЗаписей = РегистрыСведений.сфпТекущиеЗвонки.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Пользователь.Установить(Выборка.Пользователь);
				НаборЗаписей.Записать();
			КонецЦикла;
		КонецЕсли;		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗвонка.АбонентКонтакт) Тогда
		ДанныеЗвонка.ПредставлениеАбонента = Строка(ДанныеЗвонка.АбонентКонтакт);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗвонка.Звонок) И ЗначениеЗаполнено(ДанныеЗвонка.ИдентификаторЗвонка) Тогда
		ДанныеЗвонка.Звонок = НайтиТелефонныйЗвонокПоДаннымЗвонка(, , ДанныеЗвонка.ИдентификаторЗвонка);
	КонецЕсли;
	
	Если ДанныеЗаполнения <> Неопределено И ЗначениеЗаполнено(ДанныеЗвонка.Звонок) Тогда
		ЗвонокОбъект = ДанныеЗвонка.Звонок.ПолучитьОбъект();
		Если НЕ ЗначениеЗаполнено(ЗвонокОбъект.ВзаимодействиеОснование) Тогда
			Попытка
				Если ЗначениеЗаполнено(ДанныеЗаполнения.Основание)
					 И ТипЗнч(ДанныеЗаполнения.Основание) = Тип("ДокументСсылка.CRM_Интерес") Тогда
					ЗвонокОбъект.ВзаимодействиеОснование = ДанныеЗаполнения.Основание;
					ЗвонокОбъект.Записать();
					
					Запрос = Новый Запрос("
					|ВЫБРАТЬ
					|	ГлавнаяЗапись,
					|	Клиент,
					|	Объект,
					|	CRM_Интерес
					|ИЗ
					|	РегистрСведений.CRM_ЖурналДокументов
					|ГДЕ
					|	Объект = &Объект");
					Запрос.УстановитьПараметр("Объект", ДанныеЗвонка.Звонок);
					
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() Тогда
						МенеджерЗаписи = РегистрыСведений.CRM_ЖурналДокументов.СоздатьМенеджерЗаписи();
						ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
						МенеджерЗаписи.Прочитать();
						Если МенеджерЗаписи.Выбран() Тогда
							Если НЕ ЗначениеЗаполнено(МенеджерЗаписи.CRM_Интерес) Тогда
								МенеджерЗаписи.CRM_Интерес = ДанныеЗаполнения.Основание;
								МенеджерЗаписи.Записать();
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			Исключение
				ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
					УровеньЖурналаРегистрации.Ошибка, , , 
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;	
		КонецЕсли;
	КонецЕсли;
	
	сфпОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ДанныеЗвонка, Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеСоответствияСтрокой(Соответствие) Экспорт
	
	Строка = "";
	Для Каждого КлючИЗначение Из Соответствие Цикл
		Если ЗначениеЗаполнено(Строка) Тогда
			Строка = Строка + Символы.ПС;
		КонецЕсли;
		Строка = Строка + КлючИЗначение.Ключ + ":" + КлючИЗначение.Значение;
	КонецЦикла;
	
	Возврат Строка;
	
КонецФункции

Функция ИспользуемаяВерсияСофтФона() Экспорт
	
	ВерсияСофтФон = Константы.сфпИспользуемаяВерсияСофтФон.Получить();
	Если НЕ ЗначениеЗаполнено(ВерсияСофтФон) Тогда
		ВерсияСофтФон = Перечисления.сфпВерсииСофтФон.СофтФотПроф;
	КонецЕсли;

	Возврат ВерсияСофтФон;

КонецФункции

Процедура ОбновитьКонтактыПанелиЗвонка(ЭтаФорма) Экспорт
	
	// Запрос = Новый Запрос("
	// |ВЫБРАТЬ
	// |	ТелефоннаяКнига, Абонент, Владелец, НомерТелефона, Представление
	// |ИЗ
	// |	(ВЫБРАТЬ
	//|		ВЫБОР КОГДА ТелефоннаяКнига = ЗНАЧЕНИЕ(Справочник.сфпТелефонныеКниги.ПустаяСсылка) ТОГДА ""АТС"" ИНАЧЕ ТелефоннаяКнига.Наименование КОНЕЦ КАК ТелефоннаяКнига,
	// |		Абонент КАК Абонент,
	// |		"""" КАК Владелец,
	//|		ВЫБОР КОГДА ТелефоннаяКнига = ЗНАЧЕНИЕ(Справочник.сфпТелефонныеКниги.ПустаяСсылка) ТОГДА 1 ИНАЧЕ 2 КОНЕЦ КАК Порядок,
	//|		ВЫБОР КОГДА ТелефоннаяКнига = ЗНАЧЕНИЕ(Справочник.сфпТелефонныеКниги.ПустаяСсылка) ТОГДА Абоненты.ВнутреннийНомер ИНАЧЕ КИ.НомерТелефона КОНЕЦ КАК НомерТелефона,
	//|		ВЫБОР КОГДА ТелефоннаяКнига = ЗНАЧЕНИЕ(Справочник.сфпТелефонныеКниги.ПустаяСсылка) ТОГДА Абоненты.ВнутреннийНомер ИНАЧЕ КИ.Представление КОНЕЦ КАК Представление
	// |	 ИЗ
	//|	 	РегистрСведений.сфпАбонентыТелефонныхКниг КАК Абоненты
	//|	 	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК КИ
	// |	 	ПО КИ.Ссылка = Абоненты.Абонент
	//|	 	И КИ.Вид = Абоненты.ВидТелефона
	// |	
	// |	 ОБЪЕДИНИТЬ ВСЕ
	// |
	// |	 ВЫБРАТЬ
	//|		""Клиенты"" КАК ТелефоннаяКнига,
	// |		Объект,
	// |		Объект.Владелец,
	// |		3,
	// |		НомерТелефона,
	// |		Представление
	// |	 ИЗ
	//|		РегистрСведений.сфпНомераТелефоновДляПоиска) КАК Подзапрос
	// |УПОРЯДОЧИТЬ ПО
	// |	Порядок,
	// |	ТелефоннаяКнига,
	// |	Абонент	
	// |ИТОГИ ПО
	// |	ТелефоннаяКнига
	//|АВТОУПОРЯДОЧИВАНИЕ");
	//
	//СтрокиДерева = ЭтаФорма.Контакты.ПолучитьЭлементы();
	//
	//ВыборкаКниг = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	// Пока ВыборкаКниг.Следующий() Цикл
	//	СтрокаДерева = СтрокиДерева.Добавить();
	//	СтрокаДерева.КнигаКонтакт = ВыборкаКниг.ТелефоннаяКнига;
	//	
	//	СтрокиАбонентов = СтрокаДерева.ПолучитьЭлементы();
	//	
	//	Выборка = ВыборкаКниг.Выбрать();
	//	Пока Выборка.Следующий() Цикл
	//		НоваяСтрока = СтрокиАбонентов.Добавить();
	//		НоваяСтрока.КнигаКонтакт = Выборка.Абонент;
	//		НоваяСтрока.Клиент = Выборка.Владелец;
	//		НоваяСтрока.ВнутреннийНомер = Выборка.ВнутреннийНомер;
	//		НоваяСтрока.Номер = Выборка.Представление;
	//	КонецЦикла;
	//КонецЦикла;

КонецПроцедуры

Функция ПредставлениеОшибкиОблачнойАТС(Ошибка) Экспорт
	
	Если Ошибка = "НеЗаполненыНастройкиПользователя" ИЛИ Ошибка = "НеЗаполненВнутреннийНомерПользователя" Тогда
		Возврат НСтр("ru='Необходимо указать внутренний номер пользователя АТС.';
			|en='It is necessary to specify user`s internal number of PBX.'");
		
	ИначеЕсли Ошибка = "НеЗаполненНомерИсходящегоЗвонкаПользователя" Тогда
		Возврат НСтр("ru='Необходимо указать номер исходящего звонка в настройках интеграции.';
			|en='You must specify the number of outgoing calls in the settings integration.'");
		
	ИначеЕсли Ошибка = "НеЗаполненАдресАТС" Тогда
		Возврат НСтр("ru='Не заполнен адрес вашей АТС в настройках интеграции.';
			|en='Address of your PBX not filled in integration settings.'");
		
	ИначеЕсли Ошибка = "НекорректныйВнутреннийНомерПользователя" Тогда
		Возврат НСтр("ru='Указан некорректный внутренний номер пользователя телефонии.';
			|en='Invalid internal user number for telephony specified.'");
		
	ИначеЕсли Ошибка = "НеЗаполненыНастройкиИнтеграции" Тогда
		Возврат НСтр("ru='Не заполнены настройки интеграции с выбранной АТС.';
			|en='Integration setting not set with selected PBX.'");
		
	ИначеЕсли Ошибка = "АккаунтЗаблокирован" Тогда
		Возврат НСтр("ru='Аккаунт заблокирован.';en='Account is locked.'");	
		
	Иначе
		Возврат Ошибка;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьДанныеПредставленияКонтакта(Контакт, УникальныйИдентификатор) Экспорт
	
	СтруктураДанных = Новый Структура("ВладелецКонтакта,Аватар", Неопределено, "");
	
	Если ЗначениеЗаполнено(Контакт) Тогда
		Если ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
			СтруктураДанных.ВладелецКонтакта = Контакт.Владелец;
		КонецЕсли;
		
		АдресКартинки = "";
		Если (Контакт.Метаданные().Реквизиты.Найти("CRM_Фотография") <> Неопределено)
			 И ЗначениеЗаполнено(Контакт.CRM_Фотография) Тогда
			АдресКартинки = РаботаСФайлами.ДанныеФайла(Контакт.CRM_Фотография,
				 УникальныйИдентификатор).СсылкаНаДвоичныеДанныеФайла;
		КонецЕсли;
				
		СтруктураДанных.Аватар = АдресКартинки;
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции

Процедура ПодключитьОбновлениеПодпискиНаСобытияОблачнойАТС() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекЗадание = Метаданные.РегламентныеЗадания.сфпОбновлениеПодпискиНаСобытияОблачнойАТС;
	
	ПараметрыПоиска = Новый Структура;
	//ПараметрыПоиска.Вставить("ИмяМетода", ТекЗадание.ИмяМетода);
	ПараметрыПоиска.Вставить("Метаданные", "сфпОбновлениеПодпискиНаСобытияОблачнойАТС");

	Задания = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыПоиска);
	
	ПараметрыЗадания = Новый Структура("Использование", Истина);
	
	Если Задания = Неопределено ИЛИ Задания.Количество() = 0 Тогда
		РегламентныеЗаданияСервер.УдалитьЗадание(ТекЗадание);
		
		Расписание = Новый РасписаниеРегламентногоЗадания();
		Расписание.ПериодНедель = 1;
		Расписание.ПериодПовтораВТечениеДня = 3540;
		Расписание.ПериодПовтораДней = 1;
		ПараметрыЗадания.Вставить("Расписание", Расписание);
		ПараметрыЗадания.Вставить("Метаданные", ТекЗадание);
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);

	Иначе	
		Для Каждого Задание Из Задания Цикл
			Если НЕ Задание.Использование Тогда
				РегламентныеЗаданияСервер.ИзменитьЗадание(Задание.УникальныйИдентификатор, ПараметрыЗадания);
				//ОчередьЗаданий.ИзменитьЗадание(Задания[0].Идентификатор, ПараметрыЗадания);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

Процедура ОтключитьОбновлениеПодпискиНаСобытияОблачнойАТС() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегламентныеЗаданияСервер.УдалитьЗадание(Метаданные.РегламентныеЗадания.сфпОбновлениеПодпискиНаСобытияОблачнойАТС);
	
	//ПараметрыПоиска = Новый Структура;
	//ПараметрыПоиска.Вставить("ИмяМетода",
		// Метаданные.РегламентныеЗадания.сфпОбновлениеПодпискиНаСобытияОблачнойАТС.ИмяМетода);
	//СписокЗаданий = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыПоиска);
	//
	// Для Каждого Задание Из СписокЗаданий Цикл
	//	ПараметрыЗадания = Новый Структура("Использование", Ложь);
	//	РегламентныеЗаданияСервер.ИзменитьЗадание(Задание, ПараметрыЗадания);
	//КонецЦикла;

	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

Процедура сфпОбновлениеПодпискиНаСобытияОблачнойАТС() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.сфпОбновлениеПодпискиНаСобытияОблачнойАТС);

	ВыполнятьОбновление = Ложь;
	
	ИспользоватьОблачнуюТелефонию =
		сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпИспользоватьОблачнуюТелефонию");
	Если ИспользоватьОблачнуюТелефонию Тогда
		ИспользуемаяАТС = сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпИспользуемаяАТС");
		Если ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Билайн Тогда
			ВыполнятьОбновление = Истина;	
		КонецЕсли;	
	КонецЕсли;
	
	Если ВыполнятьОбновление Тогда
		РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПолучитьДанныеПодпискиНаСобытия");
		Если РезультатЗапроса.Успешно Тогда
			json = Новый ЧтениеJSON();
			json.УстановитьСтроку(РезультатЗапроса.Результат);
			jsonСтруктура = ПрочитатьJSON(json);
			json.Закрыть();
			
			Если jsonСтруктура.Свойство("expires") И jsonСтруктура.expires > 3000 Тогда
				Возврат;	
			КонецЕсли;
			
			РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПодпискаНаСобытия");
			Если РезультатЗапроса.Успешно Тогда
				json = Новый ЧтениеJSON();
				json.УстановитьСтроку(РезультатЗапроса.Результат);
				jsonСтруктура = ПрочитатьJSON(json);
				json.Закрыть();
				
				Если jsonСтруктура.Свойство("subscriptionId") Тогда
					УстановитьНастройкуТелефонии("КлючПодпискиНаСобытия", jsonСтруктура.subscriptionId);
					
					ПодключитьОбновлениеПодпискиНаСобытияОблачнойАТС();
				КонецЕсли;				
			КонецЕсли;			
		КонецЕсли;
		
	Иначе
		ОтключитьОбновлениеПодпискиНаСобытияОблачнойАТС();
	КонецЕсли;	
	
КонецПроцедуры

Функция СтруктураJSON(СтрокаJSON) Экспорт
	
	СтруктураJSON = Новый Структура();
	
	Если ЗначениеЗаполнено(СтрокаJSON) Тогда
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
		СтруктураJSON = ПрочитатьJSON(ЧтениеJSON);
		ЧтениеJSON.Закрыть();
	КонецЕсли;	
	
	Возврат СтруктураJSON;			

КонецФункции

Процедура СохранитьНастройкиПубликацииСервисовТелефонии(АдресСервераПубликации, АдресРесурсаПубликации) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	Если Прав(АдресСервераПубликации, 1) = "/" Тогда
		АдресСервераПубликации = Лев(АдресСервераПубликации, СтрДлина(АдресСервераПубликации) - 1);
	КонецЕсли;
	Если Лев(АдресРесурсаПубликации, 1) = "/" Тогда
		АдресРесурсаПубликации = Сред(АдресРесурсаПубликации, 2);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(АдресСервераПубликации) И НЕ ЗначениеЗаполнено(АдресРесурсаПубликации) Тогда
		Настройки = Неопределено;
		
	Иначе
		Настройки = Новый Структура();
		Настройки.Вставить("АдресСервераОсновнойПубликации", АдресСервераПубликации);
		Настройки.Вставить("АдресРесурсаОсновнойПубликации", АдресРесурсаПубликации);
		
		// Если ОбщегоНазначения.РазделениеВключено() Тогда
		//	Шаблон = "[АдресСервера]/[АдресПубликации]/[ОбластьДанных]/hs/[КорневойURLСервиса]";
		// Иначе
			Шаблон = "[АдресСервера]/[АдресПубликации]/hs/[КорневойURLСервиса]";
		//КонецЕсли;
		
		ПараметрыСтроки = Новый Структура();
		Если ЗначениеЗаполнено(АдресСервераПубликации) Тогда
			ПараметрыСтроки.Вставить("АдресСервера", АдресСервераПубликации);
		КонецЕсли;
		Если ЗначениеЗаполнено(АдресРесурсаПубликации) Тогда
			ПараметрыСтроки.Вставить("АдресПубликации", АдресРесурсаПубликации);
		КонецЕсли;
		
		НастройкиПубликации = Новый Соответствие();
		
		КорневойURLСервиса = КорневойURLСервисаОсновнойПубликации(Перечисления.сфпДоступныеАТС.СофтФонWebModule);
		ПараметрыСтроки.Вставить("КорневойURLСервиса", КорневойURLСервиса);
		ПолныйПутьПубликации = сфпОбщегоНазначенияКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ПараметрыСтроки);
	    НастройкиПубликации.Вставить(Перечисления.сфпДоступныеАТС.СофтФонWebModule,
	    	 Новый Структура("АдресРесурсаОсновнойПубликации",
	    	 ПолныйПутьПубликации));
		
		КорневойURLСервиса = КорневойURLСервисаОсновнойПубликации(Перечисления.сфпДоступныеАТС.MangoOffice);
		ПараметрыСтроки.Вставить("КорневойURLСервиса", КорневойURLСервиса);
		ПолныйПутьПубликации = сфпОбщегоНазначенияКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ПараметрыСтроки);
		НастройкиПубликации.Вставить(Перечисления.сфпДоступныеАТС.MangoOffice,
			 Новый Структура("АдресРесурсаОсновнойПубликации",
			 ПолныйПутьПубликации));
		
		КорневойURLСервиса = КорневойURLСервисаОсновнойПубликации(Перечисления.сфпДоступныеАТС.Яндекс);
		ПараметрыСтроки.Вставить("КорневойURLСервиса", КорневойURLСервиса);
		ПолныйПутьПубликации = сфпОбщегоНазначенияКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ПараметрыСтроки);
		НастройкиПубликации.Вставить(Перечисления.сфпДоступныеАТС.Яндекс,
			 Новый Структура("АдресРесурсаОсновнойПубликации",
			 ПолныйПутьПубликации));
		
		КорневойURLСервиса = КорневойURLСервисаОсновнойПубликации(Перечисления.сфпДоступныеАТС.Билайн);
		ПараметрыСтроки.Вставить("КорневойURLСервиса", КорневойURLСервиса);
		ПолныйПутьПубликации = сфпОбщегоНазначенияКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ПараметрыСтроки);
		НастройкиПубликации.Вставить(Перечисления.сфпДоступныеАТС.Билайн,
			 Новый Структура("АдресРесурсаОсновнойПубликации",
			 ПолныйПутьПубликации));
		
		КорневойURLСервиса = КорневойURLСервисаОсновнойПубликации(Перечисления.сфпДоступныеАТС.МТТ);
		ПараметрыСтроки.Вставить("КорневойURLСервиса", КорневойURLСервиса);
		ПолныйПутьПубликации = сфпОбщегоНазначенияКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ПараметрыСтроки);
		НастройкиПубликации.Вставить(Перечисления.сфпДоступныеАТС.МТТ,
			 Новый Структура("АдресРесурсаОсновнойПубликации",
			 ПолныйПутьПубликации));

		КорневойURLСервиса = КорневойURLСервисаОсновнойПубликации(Перечисления.сфпДоступныеАТС.УниверсальныйItoolabs);
		ПараметрыСтроки.Вставить("КорневойURLСервиса", КорневойURLСервиса);
		ПолныйПутьПубликации = сфпОбщегоНазначенияКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ПараметрыСтроки);
		НастройкиПубликации.Вставить(Перечисления.сфпДоступныеАТС.УниверсальныйItoolabs,
			 Новый Структура("АдресРесурсаОсновнойПубликации",
			 ПолныйПутьПубликации));

		Настройки.Вставить("НастройкиПубликации", НастройкиПубликации);		
	КонецЕсли;
	
	Константы.сфпОбщиеНастройкиТелефонии.Установить(ЗначениеВСтрокуВнутр(Настройки));
	
КонецПроцедуры

Функция ДатуВTimestamp(пДата = Неопределено)
	Возврат Формат(Число(?(ТипЗнч(пДата) = Тип("Дата"), пДата, ТекущаяДатаСеанса()) - Дата("19700101") - 10800), 
		"ЧН=0; ЧГ=0");
КонецФункции

Функция TimestampВДату(пДатаТС)

	Попытка
		Возврат Дата("19700101") + 10800 + ?(ТипЗнч(пДатаТС) = Тип("Строка"), Число(пДатаТС), пДатаТС);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция TimeUnixВДату(юДата)
	Возврат Дата(1970, 1, 1, 1, 0, 0) + юДата / 1000;
КонецФункции

Процедура сфпОтключитьИспользованиеСофтФона(Пользователь) Экспорт
	
	МассивПользователей = Новый Массив;
	// Отключение настройки "сфпИспользоватьСофтФон"
	ИспользоватьСофтФон = РегистрыСведений.CRM_НастройкиПользователей.СоздатьНаборЗаписей();
	ИспользоватьСофтФон.Отбор.Настройка.Установить(ПланыВидовХарактеристик.CRM_НастройкиПользователей.сфпИспользоватьСофтФон);
	ИспользоватьСофтФон.Прочитать();
	Для Каждого ЗаписьНабора Из ИспользоватьСофтФон Цикл
		Если ЗаписьНабора.Пользователь = Пользователь Тогда
			ЗаписьНабора.Значение = Ложь;
			ОтключитьСофтФон = Истина;
		КонецЕсли;
	КонецЦикла;
	Если Не ОтключитьСофтФон Тогда
		ЗаписьНабора = ИспользоватьСофтФон.Добавить();
		ЗаписьНабора.Пользователь = Пользователь;
		ЗаписьНабора.Настройка = ПланыВидовХарактеристик.CRM_НастройкиПользователей.сфпИспользоватьСофтФон;
		ЗаписьНабора.Значение = Ложь;
	КонецЕсли;
	Попытка
		ИспользоватьСофтФон.Записать();
	Исключение
		МассивПользователей = Новый Массив;
	КонецПопытки;
	
	// Отключение настройки "сфпПривязатьВнутреннийНомер"
	ПривязатьВнутреннийНомер = РегистрыСведений.CRM_НастройкиПользователей.СоздатьНаборЗаписей();
	ПривязатьВнутреннийНомер.Отбор.Настройка.Установить(ПланыВидовХарактеристик.CRM_НастройкиПользователей.сфпПривязатьВнутреннийНомер);
	ПривязатьВнутреннийНомер.Прочитать();
	Для Каждого ЗаписьНабора Из ПривязатьВнутреннийНомер Цикл
		Если ЗаписьНабора.Пользователь = Пользователь Тогда
			ЗаписьНабора.Значение = Ложь;
			ОтключитьСофтФон = Истина;
		КонецЕсли;
	КонецЦикла;
	Если Не ОтключитьСофтФон Тогда
		ЗаписьНабора = ПривязатьВнутреннийНомер.Добавить();
		ЗаписьНабора.Пользователь = Пользователь;
		ЗаписьНабора.Настройка = ПланыВидовХарактеристик.CRM_НастройкиПользователей.сфпПривязатьВнутреннийНомер;
		ЗаписьНабора.Значение = Ложь;
	КонецЕсли;
	Попытка
		ПривязатьВнутреннийНомер.Записать();
	Исключение
		МассивПользователей = Новый Массив;
	КонецПопытки;
	
	сфпЗаписатьНомерПользователю(0, Пользователь);
	сфпЗаписатьДополнительныеНомераПользователю(Пользователь, 0); 
	
КонецПроцедуры // сфпОтключитьИспользованиеСофтФона()

/////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С НОМЕРОМ ТЕЛЕФОНА

// Функция возвращает номер телефона в котором убраны все символы, кроме цифр
//
// Параметры:
//	НомерТелефона	- Строка	- Номер телефона
//
// Возвращаемое значение:
//	Строка	- Очищенный номер телефона
//
Функция сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона, ЭтоВнутреннийНомер = Неопределено) Экспорт
	
	ПозицияРазделителя = СтрНайти(НомерТелефона, "@");
	Если ПозицияРазделителя > 0 Тогда
		НомерТелефона = Лев(НомерТелефона, ПозицияРазделителя - 1); 	
	КонецЕсли;	
	
	СтрокаЦифр = "0123456789";
	
	ОчищенныйНомер = "";
	ДлинаНомера = СтрДлина(НомерТелефона);
	Для НомерСимвола = 1 По ДлинаНомера Цикл
		ТекущийСимвол = Сред(НомерТелефона, НомерСимвола, 1);
		Если СтрНайти(СтрокаЦифр, ТекущийСимвол) > 0 Тогда
			ОчищенныйНомер = ОчищенныйНомер + ТекущийСимвол;
		КонецЕсли;
	КонецЦикла;
	
	КоличествоЗначимыхЦифрВНомереКонтакта =
		сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпПоследниеЦифрыТелефонногоНомера");
	
	Если ЭтоВнутреннийНомер = Неопределено Тогда
		ЭтоВнутреннийНомер = Ложь;
				
		Если СтрДлина(ОчищенныйНомер) < КоличествоЗначимыхЦифрВНомереКонтакта Тогда
			ЭтоВнутреннийНомер = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Убираем из начала полученного внутреннего номера лидирующие нули, если такие есть
	Если ЭтоВнутреннийНомер Тогда
		ЛевыйСимвол = Лев(ОчищенныйНомер, 1);
		Пока ЛевыйСимвол = "0" Цикл
			Если СтрДлина(ОчищенныйНомер) > 1 Тогда
				ОчищенныйНомер = Сред(ОчищенныйНомер, 2);
				ЛевыйСимвол = Лев(ОчищенныйНомер, 1);
				
			Иначе
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		//ПрефиксГород = сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпПрефиксВыходаВГород");
		//ПрефиксМежгород = сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпПрефиксВыходаВМежгород");
		//СтрокаПрефиксов = ПрефиксГород + ПрефиксМежгород;
		// Если ЗначениеЗаполнено(СтрокаПрефиксов) Тогда
		//	Если СтрДлина(ОчищенныйНомер) = КоличествоЗначимыхЦифрВНомереКонтакта + СтрДлина(СтрокаПрефиксов) Тогда
		//		Если Лев(ОчищенныйНомер, СтрДлина(СтрокаПрефиксов)) = СтрокаПрефиксов Тогда
		//			ОчищенныйНомер = Сред(ОчищенныйНомер, СтрДлина(СтрокаПрефиксов) + 1);
		//		КонецЕсли;
		//	КонецЕсли;
		//КонецЕсли;
	КонецЕсли;

	Возврат ОчищенныйНомер;

КонецФункции // сфпУбратьИзНомераТелефонаВсеПрефиксы()

// Функция убирает из номера все префиксы выхода на линию
//
// Параметры:
//	НомерТелефона - Строка	- Номер телефона
//
// Возвращаемое значение:
//	Строка	- Номер телефона без префиксов
//
Функция сфпУбратьИзНомераТелефонаВсеПрефиксыВыходаНаЛинию(НомерТелефона) Экспорт
	НомерТелефона = сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона);
	Если СтрДлина(НомерТелефона) > 10 Тогда
		ПрефиксВыхода = Константы.сфпПрефиксВыходаВГород.Получить();
		Если СтрНайти(НомерТелефона, ПрефиксВыхода) = 1 Тогда
			НомерТелефона = Сред(НомерТелефона, СтрДлина(ПрефиксВыхода) + 1);
		КонецЕсли;	
		Если СтрДлина(НомерТелефона) > 10 Тогда
			ПрефиксВыхода = Константы.сфпПрефиксВыходаВМежгород.Получить();
			Если СтрНайти(НомерТелефона, ПрефиксВыхода) = 1 Тогда
				НомерТелефона = Сред(НомерТелефона, СтрДлина(ПрефиксВыхода) + 1);
			КонецЕсли;	
		КонецЕсли;	
		Если СтрДлина(НомерТелефона) > 10 Тогда
			ПрефиксВыхода = Константы.сфпПрефиксВыходаНаМеждународную.Получить();
			Если СтрНайти(НомерТелефона, ПрефиксВыхода) = 1 Тогда
				НомерТелефона = Сред(НомерТелефона, СтрДлина(ПрефиксВыхода) + 1);
			КонецЕсли;	
		КонецЕсли;	
	ИначеЕсли СтрДлина(НомерТелефона) < 10 Тогда
		сфпПараметрыСервера	= сфпПараметрыСервера();
		КодГорода 	= сфпПараметрыСервера.КодГорода;
		Если НЕ ПустаяСтрока(КодГорода) Тогда
			Если СтрДлина(НомерТелефона) > (10 - СтрДлина(КодГорода)) Тогда
				ПрефиксВыхода = Константы.сфпПрефиксВыходаВГород.Получить();
				Если СтрНайти(НомерТелефона, ПрефиксВыхода) = 1 Тогда
					НомерТелефона = Сред(НомерТелефона, СтрДлина(ПрефиксВыхода) + 1);
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
	Возврат НомерТелефона;
КонецФункции // сфпУбратьИзНомераТелефонаВсеПрефиксыВыходаНаЛинию()

// Функция по полю "Представление" заполняет структуры полей для номера телефона
//
// Параметры:
//	Представление	- Строка	- Представление номера телефона
//
// Возвращаемое значение:
//	Структура	- Структура телефонного номера
//
Функция сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(Представление) Экспорт
	
	КодСтраны = "";
	КодГорода = "";
	НомерТелефона = "";
	Добавочный = "";
	Комментарий = "";
	
	ТекСтр = СокрЛП(Представление);
	
	// Вырежем добавочный номер с комментарием
	ПозДоб = СтрНайти(ВРЕГ(ТекСтр), "ДОБ.");
	Если ПозДоб = 0 Тогда
		ПозДоб = СтрНайти(ВРЕГ(ТекСтр), "EXT.");
	КонецЕсли;
	Если ПозДоб = 0 Тогда
		ПозДоб = СтрНайти(ТекСтр, ",");
	КонецЕсли;
	Если ПозДоб > 0 Тогда
		ДобавочныйСКомментарием = СокрЛП(Сред(ТекСтр, ПозДоб));
		Если Лев(ДобавочныйСКомментарием, 1) = "," Тогда
			ДобавочныйСКомментарием = СокрЛП(Сред(ДобавочныйСКомментарием, 2));
		КонецЕсли;
		ТекСтр = СокрЛП(Лев(ТекСтр, ПозДоб - 1));
		Если Прав(ТекСтр, 1) = "," Тогда
			ТекСтр = Лев(ТекСтр, СтрДлина(ТекСтр) - 1);
		КонецЕсли;
		ПозДоб = СтрНайти(ВРЕГ(ДобавочныйСКомментарием), ",");
		Если ПозДоб > 0 Тогда
			Добавочный	= СокрЛП(Лев(ДобавочныйСКомментарием, ПозДоб - 1));
			Комментарий	= СокрЛП(Сред(ДобавочныйСКомментарием, ПозДоб + 1));
		Иначе
			Добавочный = ДобавочныйСКомментарием;
		КонецЕсли;
		Добавочный = сфпУбратьИзНомераТелефонаВсеПрефиксы(Добавочный, Ложь); 
	КонецЕсли;
	
	// Вырежем код города
	Поз = СтрНайти(ТекСтр, "(");
	Если Поз > 0 Тогда
		КодСтраны = СокрЛП(Лев(ТекСтр, Поз - 1));
		ТекСтр = СокрЛП(Сред(ТекСтр, Поз + 1));
		Поз = СтрНайти(ТекСтр, ")");
		Если Поз > 0 Тогда
			КодГорода = СокрЛП(Лев(ТекСтр, Поз - 1));
			ТекСтр = СокрЛП(Сред(ТекСтр, Поз + 1));
		КонецЕсли;
		Если КодСтраны = "8" Или КодСтраны = "+8" Или КодСтраны = "" Тогда
			КодСтраны = "+7";
		КонецЕсли;
	КонецЕсли;
	
	Поз = СтрНайти(текСтр, ", ");
	// Если Добавочного номера нет - ориентируемся по номеру телефона и комментарию
	Если ПозДоб = 0 И Поз > 0 Тогда
		// Вырежем комментарий
		НомерТелефона = СокрЛП(Лев(ТекСтр, Поз - 1));
		Комментарий = СокрЛП(Сред(ТекСтр, Поз + 2));

	Иначе
		// Все оставшееся это номер
		НомерТелефона = ТекСтр;
	КонецЕсли;
	
	// Удаляем лишние символы из номера телефона
	НомерТелефона = сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона, Ложь);
	
	сфпПараметрыСервера	= сфпПараметрыСервера();
	СофтФонКодСтраны = сфпПараметрыСервера.КодСтраны;
	
	Если НЕ ЗначениеЗаполнено(СофтФонКодСтраны) Тогда
		СофтФонКодСтраны = "+7";
	КонецЕсли;
	
	Если СофтФонКодСтраны = "+84" ИЛИ СофтФонКодСтраны = "84" Тогда
		Если Лев(НомерТелефона, 1) = "0" Тогда
			НомерТелефона = "84" + Сред(НомерТелефона, 2);
		КонецЕсли;
	КонецЕсли;
	
	ПоследниеЦифрыТелефонногоНомера = сфпПараметрыСервера.ПоследниеЦифрыТелефонногоНомера;
	ПоследниеЦифрыТелефонногоНомера = ?(ПоследниеЦифрыТелефонногоНомера = 0, 10, ПоследниеЦифрыТелефонногоНомера);
	
	// Если номер телефона не разобран, то заново разбираем его
	ДлинаНомера = СтрДлина(НомерТелефона);
	Если СтрДлина(КодГорода) + ДлинаНомера > сфпПараметрыСервера.МаксимальнаяДлинаВнутреннихНомеров Тогда
		Если СофтФонКодСтраны = "+7" И (СтрНайти(сфпПараметрыСервера.КодГорода, "499") > 0
			 ИЛИ СтрНайти(сфпПараметрыСервера.КодГорода, "495") > 0) Тогда
			СофтФонКодГорода = "";

		Иначе
			СофтФонКодГорода = сфпПараметрыСервера.КодГорода;
		КонецЕсли;
		
		// Выделяем код страны
		Если СофтФонКодСтраны = "+84" ИЛИ СофтФонКодСтраны = "84" Тогда
			Если ДлинаНомера > ПоследниеЦифрыТелефонногоНомера Тогда
				Если Лев(НомерТелефона, 2) <> "84" Тогда
					// Номер другой страны
					СофтФонКодСтраны = "";
				КонецЕсли;
				
				Если Лев(НомерТелефона, 2) = "84" Тогда
					НомерТелефона = Сред(НомерТелефона, 3);
				КонецЕсли;
			КонецЕсли;

		Иначе
			Если ДлинаНомера > ПоследниеЦифрыТелефонногоНомера Тогда
				КодСтраны = Лев(НомерТелефона, ДлинаНомера - ПоследниеЦифрыТелефонногоНомера);
				НомерТелефона = Сред(НомерТелефона, ДлинаНомера - (ПоследниеЦифрыТелефонногоНомера - 1));
				Если КодСтраны = "7" ИЛИ КодСтраны = "8" ИЛИ КодСтраны = "+8" Тогда
					КодСтраны = "+7";

				ИначеЕсли НЕ СтрНайти(КодСтраны, "+") = 1 Тогда
					КодСтраны = "+" + КодСтраны;
				КонецЕсли;
			КонецЕсли;
			
			// Выделяем код города
			ДлинаНомера = СтрДлина(НомерТелефона);
			Если НЕ ПустаяСтрока(СофтФонКодГорода) И (ПустаяСтрока(КодСтраны) ИЛИ (КодСтраны = СофтФонКодСтраны)) Тогда
				Если ДлинаНомера = ПоследниеЦифрыТелефонногоНомера Тогда
					Если СтрНайти(НомерТелефона, СофтФонКодГорода) = 1 Тогда
						КодСтраны = СофтФонКодСтраны;
						КодГорода = СофтФонКодГорода;
						НомерТелефона = Сред(НомерТелефона, СтрДлина(СофтФонКодГорода) + 1);

					Иначе
						КодГорода = Лев(НомерТелефона, 3);
						НомерТелефона = Сред(НомерТелефона, 4);
					КонецЕсли;

				ИначеЕсли (ДлинаНомера + СтрДлина(СофтФонКодГорода)) = ПоследниеЦифрыТелефонногоНомера Тогда
					Если НЕ ЗначениеЗаполнено(КодСтраны) Тогда
						КодСтраны = СофтФонКодСтраны;
					КонецЕсли;
					Если НЕ ЗначениеЗаполнено(КодГорода) Тогда
						КодГорода = СофтФонКодГорода;
					КонецЕсли;					
				КонецЕсли;

			ИначеЕсли ДлинаНомера = ПоследниеЦифрыТелефонногоНомера Тогда
				КодГорода = Лев(НомерТелефона, 3);
				НомерТелефона = Сред(НомерТелефона, 4);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДлинаНомера < ПоследниеЦифрыТелефонногоНомера И СофтФонКодСтраны <> "+84" И СофтФонКодСтраны <> "84" Тогда
		СофтФонКодСтраны = "";
	КонецЕсли;
	
	СтруктураПолей = Новый Структура();
	СтруктураПолей.Вставить("КодСтраны", ?(ЗначениеЗаполнено(КодСтраны), КодСтраны, СофтФонКодСтраны));
	СтруктураПолей.Вставить("КодГорода", КодГорода);
	СтруктураПолей.Вставить("НомерТелефона", НомерТелефона);
	СтруктураПолей.Вставить("Добавочный", Добавочный);
	СтруктураПолей.Вставить("Комментарий", Комментарий);
	Возврат СтруктураПолей;

КонецФункции // сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон()

// Функция разбирает переданную структуру номера телефона и возвращает строку номера, которую будет набирать АТС.
//
// Параметры:
//	СтруктураНомера	- Структура	- Структура телефонного номера.
//
// Возвращаемое значение:
//	Строка	- Строка для набора АТС.
//
Функция сфпПреобразоватьНомерСУчетомПрефиксов(СтруктураНомера) Экспорт
	
	сфпПараметрыСервера	= сфпПараметрыСервера();
	Если ПустаяСтрока(СтруктураНомера.НомерТелефона) Тогда
		Если ПустаяСтрока(СтруктураНомера.Добавочный) Тогда
			// Номера нет.
			РезультатНомер = "";

		Иначе
			// Внутренний номер.
			РезультатНомер = СтруктураНомера.Добавочный;
		КонецЕсли;			
		
	// Проверяем длину номеру вместе с длиной кода города, т.к. в ряде компаний внутренний. 
	// Номер может быть достаточно длинным, до 9 символов.
	// У внутренних же номеров код города отсутствует.

	ИначеЕсли СтрДлина(СтруктураНомера.НомерТелефона) 
		+ СтрДлина(СтруктураНомера.КодГорода) > сфпПараметрыСервера.МаксимальнаяДлинаВнутреннихНомеров Тогда
		КодСтраныНомера	= СтруктураНомера.КодСтраны;
		КодСтраныНомера	= СтрЗаменить(КодСтраныНомера, "+", "");
		КодСтраныНомера	= СокрЛП(?(КодСтраныНомера = "8", "7", КодСтраныНомера));
		
		КодСтраныПользователя = сфпПараметрыСервера.КодСтраны;
		КодСтраныПользователя = СтрЗаменить(КодСтраныПользователя, "+", "");
		КодСтраныПользователя = СокрЛП(?(КодСтраныПользователя = "8", "7", КодСтраныПользователя));
		
		Если ПустаяСтрока(КодСтраныНомера) Тогда
			// Номер нашей страны.
			РезультатНомер = СтруктураНомера.КодГорода + СтруктураНомера.НомерТелефона;
		// ИначеЕсли ПустаяСтрока(КодСтраныПользователя) Тогда
		//	// Номер нашей страны.
		//	РезультатНомер = СтруктураНомера.КодГорода + СтруктураНомера.НомерТелефона;
		//ИначеЕсли (КодСтраныНомера	= КодСтраныПользователя) Тогда
		//	// Номер нашей страны.
		//	РезультатНомер = СтруктураНомера.КодГорода + СтруктураНомера.НомерТелефона;
		Иначе
			// Номер другой страны.
			РезультатНомер = СтруктураНомера.КодСтраны + СтруктураНомера.КодГорода + СтруктураНомера.НомерТелефона;
		КонецЕсли;

	Иначе
		// Внутренний номер.
		РезультатНомер = СтруктураНомера.НомерТелефона;
	КонецЕсли;	
	Возврат РезультатНомер;
КонецФункции // ПреобразоватьНомерСУчетомПрефиксов()

Функция ПроверитьНомерТелефонаКонтакта(НомерКонтакта) Экспорт
	
	НомерТелефона = сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерКонтакта);
	
	ПараметрыСервера = сфпСофтФонПроСерверПовтИсп.сфпПараметрыСервера();
	ДлинаВнутреннегоНомера = ПараметрыСервера.МаксимальнаяДлинаВнутреннихНомеров;
	ДлинаНомераТелефона = ПараметрыСервера.ПоследниеЦифрыТелефонногоНомера;
	КодГорода = ПараметрыСервера.КодГорода;
				
	Если СтрДлина(НомерТелефона) < ДлинаНомераТелефона
		 И СтрДлина(НомерТелефона) > ДлинаВнутреннегоНомера
		 И ЗначениеЗаполнено(КодГорода) Тогда
		Если СтрДлина(КодГорода + НомерТелефона) = ДлинаНомераТелефона Тогда
			НомерТелефона = "" + КодГорода + НомерТелефона;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НомерТелефона;
	
КонецФункции	//	ПроверитьНомерТелефонаКонтакта()

/////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С КОНТАКТНОЙ ИНФОРМАЦИЕЙ

// Функция разбивает номер телефона на части для полнотекстового поиска
//
// Параметры:
//  СтрокаТелефона 	- Строка - Разбиваемая строка номера телефона
//
// Возвращаемое значение:
//	СтрокаРазбивки	- Строка, состоящая из строк номера телефона различной длины, взятых справа
//
Функция сфпРазбитьНомерТелефонаДляПолнотекстовогоПоиска(СтрокаТелефона) Экспорт
	ДлинаНомера = СтрДлина(СтрокаТелефона);
	РезСтрока	= "";
	Для КоличествоСимволов = 4 По ДлинаНомера Цикл
		РезСтрока = РезСтрока + " " + Прав(СтрокаТелефона, КоличествоСимволов);
	КонецЦикла;	
	Возврат СокрЛП(РезСтрока);
КонецФункции // сфпРазбитьНомерТелефонаДляПолнотекстовогоПоиска()	

// Процедура добавляет записи в регистр поиска по номерам
//
// Параметры:
//	Объект			- СправочникСсылка	- Владелец телефона
//	ПроверкаНаКИ	- Булево			- Признак проверки наличия ТЧ "Контактная информация"
//
Процедура сфпЗаполнитьРегистрПоискаПоНомерам(Объект, ПроверкаНаКИ = Истина, ЗаписьМаршрутизации = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МетаданныеОбъекта = Объект.Метаданные();
	
	Если ПроверкаНаКИ Тогда
		Если МетаданныеОбъекта.ТабличныеЧасти.Найти("КонтактнаяИнформация") = Неопределено Тогда
			УстановитьПривилегированныйРежим(Ложь);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Очистим все записи по объекту
	НаборЗаписей = РегистрыСведений.сфпНомераТелефоновДляПоиска.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект.Ссылка);
	НаборЗаписей.Прочитать();
	
	СтарыйНабор = НаборЗаписей.Выгрузить(, "НомерТелефона,Представление,ВнутреннийНомер");
	
	// Сохраняем пользователя для переключения звонков
	Если НаборЗаписей.Количество() = 0 Тогда
		сфпПользователь	= Справочники.Пользователи.ПустаяСсылка();
		сфпВнутреннийНомер = "";

	Иначе	
		сфпПользователь	= НаборЗаписей[0].Пользователь;
		сфпВнутреннийНомер = сфпТекущийВнутреннийНомер(НаборЗаписей[0].Пользователь);
		
		ПользовательДляПереключенияЗвонков = Неопределено;
		
		Если МетаданныеОбъекта.Реквизиты.Найти("сфпПользовательДляПереключенияЗвонков") = Неопределено Тогда
			Если МетаданныеОбъекта.Имя = "Контрагенты" Тогда
				Если ЗначениеЗаполнено(Объект.Партнер) Тогда
					ПользовательДляПереключенияЗвонков = Объект.Партнер.сфпПользовательДляПереключенияЗвонков;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			ПользовательДляПереключенияЗвонков = Объект.сфпПользовательДляПереключенияЗвонков;
		КонецЕсли;
				
		Если ЗначениеЗаполнено(ПользовательДляПереключенияЗвонков) Тогда
			сфпВнутреннийНомер = сфпТекущийВнутреннийНомер(ПользовательДляПереключенияЗвонков, Истина);
			Если ЗначениеЗаполнено(сфпВнутреннийНомер) Тогда
				  сфпПользователь = ПользовательДляПереключенияЗвонков;
			Иначе сфпПользователь = Справочники.Пользователи.ПустаяСсылка();
			КонецЕсли;

		Иначе	
			сфпПользователь	= Справочники.Пользователи.ПустаяСсылка();
			сфпВнутреннийНомер = "";
        КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(сфпПользователь) Тогда
		сфпВнутреннийНомер = "";
	КонецЕсли;

	// Очищаем набор записей
	НаборЗаписей.Очистить();
	
	// Заполняем набор записей заново
	ПоследниеЦифрыТелефонногоНомера = Константы.сфпПоследниеЦифрыТелефонногоНомера.Получить();
	Если ПоследниеЦифрыТелефонногоНомера = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПервыйТелефон = "";
	ПервыйТелефонПредставление = "";
	
	ОсновнойТелефон = "";
	ОсновнойТелефонПредставление = "";
	
	ЕстьОсновнойТелефон =
		(Объект.Ссылка.Метаданные().ТабличныеЧасти.КонтактнаяИнформация.Реквизиты.Найти("CRM_ОсновнойДляСвязи") <> Неопределено);
	
	Для Каждого Контакт Из Объект.КонтактнаяИнформация Цикл
		Если (Контакт.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон)
			 ИЛИ (Контакт.Тип = Перечисления.ТипыКонтактнойИнформации.Факс) Тогда
			СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(Контакт.Представление);
			НомерТелефона = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
				+ СтруктураНомера.НомерТелефона,
				 ПоследниеЦифрыТелефонногоНомера);
			Если НЕ ЗначениеЗаполнено(НомерТелефона) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяЗапись	= НаборЗаписей.Добавить();
			НоваяЗапись.Объект = Объект.Ссылка;
			НоваяЗапись.Вид = Контакт.Вид;
			НоваяЗапись.ПорядковыйНомер = Контакт.НомерСтроки;
			НоваяЗапись.НомерТелефона = НомерТелефона;
			НоваяЗапись.Представление = Контакт.Представление;
			НоваяЗапись.НомерТелефонаДляПоиска = сфпРазбитьНомерТелефонаДляПолнотекстовогоПоиска(НомерТелефона);
			НоваяЗапись.Пользователь = сфпПользователь;
			НоваяЗапись.ВнутреннийНомер = сфпВнутреннийНомер;
			
			Если ЗначениеЗаполнено(НомерТелефона) И НЕ ЗначениеЗаполнено(ПервыйТелефон) Тогда
				ПервыйТелефон = НомерТелефона;
				ПервыйТелефонПредставление = Контакт.Представление;
			КонецЕсли;
			Если ЕстьОсновнойТелефон И Контакт.CRM_ОсновнойДляСвязи Тогда
				ОсновнойТелефон = НомерТелефона;
				ОсновнойТелефонПредставление = Контакт.Представление;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Попытка
		НаборЗаписей.Записать();
		НаборЗаписан = Истина;
	Исключение
		НаборЗаписан = Ложь;
	КонецПопытки;
	
	Если НаборЗаписан И ТипЗнч(Объект.Ссылка) <> Тип("СправочникСсылка.Партнеры")
		 И ТипЗнч(Объект.Ссылка) <> Тип("СправочникСсылка.Организации") Тогда
		ТелефонКонтакта = "";
		ТелефонКонтактаПредставление = "";
				
		Если ЗначениеЗаполнено(ОсновнойТелефон) Тогда
			ТелефонКонтакта = ОсновнойТелефон;
			ТелефонКонтактаПредставление = ОсновнойТелефонПредставление;

		ИначеЕсли ЗначениеЗаполнено(ПервыйТелефон) Тогда
			ТелефонКонтакта = ПервыйТелефон;
			ТелефонКонтактаПредставление = ПервыйТелефонПредставление;	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТелефонКонтакта) Тогда
			МенеджерЗаписи = РегистрыСведений.сфпКонтактыТелефонии.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Объект = Объект.Ссылка;
			МенеджерЗаписи.ВнутреннийНомерАТС = "";
			МенеджерЗаписи.Прочитать();
			Если НЕ МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Объект = Объект.Ссылка;
				МенеджерЗаписи.ВнутреннийНомерАТС = "";
			КонецЕсли;
			Если МенеджерЗаписи.ОсновнойНомерТелефона <> ТелефонКонтакта Тогда
				МенеджерЗаписи.ОсновнойНомерТелефона = ТелефонКонтакта;
				МенеджерЗаписи.ОсновнойНомерТелефонаПредставление = ТелефонКонтактаПредставление;
				МенеджерЗаписи.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь); 
	
	Если НЕ НаборЗаписан Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗаписьМаршрутизации И сфпИспользоватьМаршрутизацию() Тогда
		Если НЕ сфпЗаполненыНастройкиСервера() Тогда
			Возврат;
		КонецЕсли;
		
		НовыйНабор = НаборЗаписей.Выгрузить(, "НомерТелефона,Представление,ВнутреннийНомер");
		СписокМаршрутизации	= сфпСформироватьСписокМаршрутизации(СтарыйНабор, НовыйНабор);
		сфпИзменитьМаршрутизациюВАТС(СписокМаршрутизации);
	КонецЕсли;

КонецПроцедуры // сфпЗаполнитьРегистрПоискаПоНомерам()

// Процедура добавляет записи в регистр поиска по номерам
//
// Параметры:
//	ТипОбъекта	- Строка - Имя справочника
//
Процедура сфпЗаполнитьРегистрПоискаНомеровПоТипуОбъекта(ТипОбъекта) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПоследниеЦифрыТелефонногоНомера = Константы.сфпПоследниеЦифрыТелефонногоНомера.Получить();
	Если ПоследниеЦифрыТелефонногоНомера = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ПустойПользователь = Справочники.Пользователи.ПустаяСсылка();
	
	Выборка = Справочники[ТипОбъекта].Выбрать();
	Пока Выборка.Следующий() Цикл
		// Очистим все записи по объекту
		НаборЗаписей = РегистрыСведений.сфпНомераТелефоновДляПоиска.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Ссылка);
		НаборЗаписей.Прочитать();
		
		// Сохраняем пользователя для переключения звонков
		Если НаборЗаписей.Количество() = 0 Тогда
			сфпПользователь	= ПустойПользователь;
			сфпВнутреннийНомер = "";

		Иначе	
			сфпПользователь	= НаборЗаписей[0].Пользователь;
			сфпВнутреннийНомер = сфпТекущийВнутреннийНомер(НаборЗаписей[0].Пользователь);
			
			ПользовательДляПереключенияЗвонков = Неопределено;
			
			Если ТипОбъекта = "КонтактныеЛицаПартнеров" ИЛИ ТипОбъекта = "Партнеры" Тогда
				ПользовательДляПереключенияЗвонков = Выборка.сфпПользовательДляПереключенияЗвонков;

			ИначеЕсли ТипОбъекта = "Контрагенты" Тогда
				Если ЗначениеЗаполнено(Выборка.Партнер) Тогда
					ПользовательДляПереключенияЗвонков = Выборка.Партнер.сфпПользовательДляПереключенияЗвонков;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ПользовательДляПереключенияЗвонков) Тогда
				сфпВнутреннийНомер = сфпТекущийВнутреннийНомер(ПользовательДляПереключенияЗвонков, Истина);
				Если ЗначениеЗаполнено(сфпВнутреннийНомер) Тогда
					  сфпПользователь = ПользовательДляПереключенияЗвонков;
				Иначе сфпПользователь = Справочники.Пользователи.ПустаяСсылка();
				КонецЕсли;

			Иначе	
				сфпПользователь	= Справочники.Пользователи.ПустаяСсылка();
				сфпВнутреннийНомер = "";
	        КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(сфпПользователь) Тогда
			сфпВнутреннийНомер = "";
		КонецЕсли;
	
		// Очищаем набор записей
		НаборЗаписей.Очистить();
		
		ПервыйТелефон = "";
		ПервыйТелефонПредставление = "";
	
		ОсновнойТелефон = "";
		ОсновнойТелефонПредставление = "";
		
		ЕстьОсновнойТелефон =
			(Выборка.Ссылка.Метаданные().ТабличныеЧасти.КонтактнаяИнформация.Реквизиты.Найти("CRM_ОсновнойДляСвязи") <> Неопределено);
		
		Для Каждого Контакт Из Выборка.КонтактнаяИнформация Цикл
			Если (Контакт.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон)
				 ИЛИ (Контакт.Тип = Перечисления.ТипыКонтактнойИнформации.Факс) Тогда
				СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(Контакт.Представление);
				НомерТелефона = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
					+ СтруктураНомера.НомерТелефона,
					 ПоследниеЦифрыТелефонногоНомера);
				Если НЕ ЗначениеЗаполнено(НомерТелефона) Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяЗапись	= НаборЗаписей.Добавить();
				НоваяЗапись.Объект = Выборка.Ссылка;
				НоваяЗапись.Вид = Контакт.Вид;
				НоваяЗапись.ПорядковыйНомер = Контакт.НомерСтроки;
				НоваяЗапись.НомерТелефона = НомерТелефона;
				НоваяЗапись.Представление = Контакт.Представление;
				НоваяЗапись.НомерТелефонаДляПоиска = сфпРазбитьНомерТелефонаДляПолнотекстовогоПоиска(НомерТелефона);
				НоваяЗапись.Пользователь = сфпПользователь;
				НоваяЗапись.ВнутреннийНомер = сфпВнутреннийНомер;
				
				Если НЕ ЗначениеЗаполнено(ПервыйТелефон) Тогда
					ПервыйТелефон = НомерТелефона;
					ПервыйТелефонПредставление = Контакт.Представление;
				КонецЕсли;
				Если ЕстьОсновнойТелефон И Контакт.CRM_ОсновнойДляСвязи Тогда
					ОсновнойТелефон = НомерТелефона;
					ОсновнойТелефонПредставление = Контакт.Представление;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			НаборЗаписей.Записать();
			НаборЗаписан = Истина;
		Исключение
			НаборЗаписан = Ложь;
		КонецПопытки;
		
		Если НаборЗаписан И ТипЗнч(Выборка.Ссылка) <> Тип("СправочникСсылка.Партнеры")
			 И ТипЗнч(Выборка.Ссылка) <> Тип("СправочникСсылка.Организации") Тогда
			ТелефонКонтакта = "";
			ТелефонКонтактаПредставление = "";
					
			Если ЗначениеЗаполнено(ОсновнойТелефон) Тогда
				ТелефонКонтакта = ОсновнойТелефон;
				ТелефонКонтактаПредставление = ОсновнойТелефонПредставление;

			ИначеЕсли ЗначениеЗаполнено(ПервыйТелефон) Тогда
				ТелефонКонтакта = ПервыйТелефон;
				ТелефонКонтактаПредставление = ПервыйТелефонПредставление;	
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТелефонКонтакта) Тогда
				МенеджерЗаписи = РегистрыСведений.сфпКонтактыТелефонии.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.Объект = Выборка.Ссылка;
				МенеджерЗаписи.ВнутреннийНомерАТС = "";
				МенеджерЗаписи.Прочитать();
				Если НЕ МенеджерЗаписи.Выбран() Тогда
					МенеджерЗаписи.Объект = Выборка.Ссылка;
					МенеджерЗаписи.ВнутреннийНомерАТС = "";
				КонецЕсли;
				Если МенеджерЗаписи.ОсновнойНомерТелефона <> ТелефонКонтакта Тогда
					МенеджерЗаписи.ОсновнойНомерТелефона = ТелефонКонтакта;
					МенеджерЗаписи.ОсновнойНомерТелефонаПредставление = ТелефонКонтактаПредставление;
					МенеджерЗаписи.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	УстановитьПривилегированныйРежим(Ложь); 

КонецПроцедуры // сфпЗаполнитьРегистрПоискаНомеровПоТипуОбъекта()

// Процедура заново заполняет регистр поиска по номерам
//
// Параметры:
//	Нет.
//
Процедура сфпПерезаполнитьРегистрПоискаПоНомерам() Экспорт
	
	ПоследниеЦифрыТелефонногоНомера = Константы.сфпПоследниеЦифрыТелефонногоНомера.Получить();
	Если ПоследниеЦифрыТелефонногоНомера = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого ТипСправочника Из Метаданные.РегистрыСведений.сфпНомераТелефоновДляПоиска.Измерения.Объект.Тип.Типы() Цикл
		СправочникСсылка = Новый(ТипСправочника);
		Если СправочникСсылка.Метаданные().ТабличныеЧасти.Найти("КонтактнаяИнформация") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка = Справочники[СправочникСсылка.Метаданные().Имя].Выбрать();
		Пока Выборка.Следующий() Цикл
			сфпЗаполнитьРегистрПоискаПоНомерам(Выборка.Ссылка, Ложь);
		КонецЦикла;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь); 
	
	Если сфпИспользоватьМаршрутизацию() Тогда
		// Если включена маршрутизация, то заменяем таблицу маршрутизации
		СтарыйНабор = Новый Массив;
		НовыйНабор	= сфпПолучитьТаблицуМаршрутизации();
		СписокМаршрутизации = сфпСформироватьСписокМаршрутизации(СтарыйНабор, НовыйНабор);
		сфпЗаменитьМаршрутизациюВАТС(СписокМаршрутизации);
	КонецЕсли;

КонецПроцедуры // сфпПерезаполнитьРегистрПоискаПоНомерам()

// Функция вычисляет общее количество объектов для перезаполнения по номерам  
//
// Параметры:
//	Нет.
//
Функция сфпВычислитьОбщееКоличествоОбъектовДляПерезаполненияПоНомерам() Экспорт
	
	ПоследниеЦифрыТелефонногоНомера = Константы.сфпПоследниеЦифрыТелефонногоНомера.Получить();
	Если ПоследниеЦифрыТелефонногоНомера = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина); 
	
	ТекстЗапроса = "";
	
	Для Каждого ТипСправочника Из Метаданные.РегистрыСведений.сфпНомераТелефоновДляПоиска.Измерения.Объект.Тип.Типы() Цикл
		СправочникСсылка = Новый(ТипСправочника);
		Если СправочникСсылка.Метаданные().ТабличныеЧасти.Найти("КонтактнаяИнформация") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + ?(ТекстЗапроса = "", "", "
		|ОБЪЕДИНИТЬ ВСЕ") + "
		|ВЫБРАТЬ КОЛИЧЕСТВО(Ссылка) КАК КоличествоСсылок
		|ИЗ Справочник." + СправочникСсылка.Метаданные().Имя;
	КонецЦикла;
	
	ОбщееКоличество = 0;
	
	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ СУММА(КоличествоСсылок) КАК КоличествоСсылок
		|ИЗ
		|	(" + ТекстЗапроса + ") КАК Подзапрос");
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ОбщееКоличество = Выборка.КоличествоСсылок;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ОбщееКоличество;

КонецФункции // сфпВычислитьОбщееКоличествоОбъектовДляПерезаполненияПоНомерам()

// Функция вычисляет типы объектов для перезаполнения по номерам
//
// Параметры:
//	Нет.
//
Функция сфпПолучитьТипыОбъектовДляПерезаполненияПоНомерам() Экспорт
	
	МассивТипов = Новый Массив();
	
	ПоследниеЦифрыТелефонногоНомера = Константы.сфпПоследниеЦифрыТелефонногоНомера.Получить();
	Если ПоследниеЦифрыТелефонногоНомера = 0 Тогда
		Возврат МассивТипов;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина); 
	
	Для Каждого ТипСправочника Из Метаданные.РегистрыСведений.сфпНомераТелефоновДляПоиска.Измерения.Объект.Тип.Типы() Цикл
		СправочникСсылка = Новый(ТипСправочника);
		Если СправочникСсылка.Метаданные().ТабличныеЧасти.Найти("КонтактнаяИнформация") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		МассивТипов.Добавить(СправочникСсылка.Метаданные().Имя);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат МассивТипов;

КонецФункции // сфпПолучитьТипыОбъектовДляПерезаполненияПоНомерам()

// Функция формирует список объектов для перезаполнения по номерам
//
// Параметры:
//	Нет.
//
Функция сфпПолучитьСписокОбъектовДляПерезаполнения() Экспорт
	
	СписокОбъектов = Новый СписокЗначений();
	
	УстановитьПривилегированныйРежим(Истина); 
	
	Для Каждого ТипСправочника Из Метаданные.РегистрыСведений.сфпНомераТелефоновДляПоиска.Измерения.Объект.Тип.Типы() Цикл
		СправочникСсылка = Новый(ТипСправочника);
		Если СправочникСсылка.Метаданные().ТабличныеЧасти.Найти("КонтактнаяИнформация") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка = Справочники[СправочникСсылка.Метаданные().Имя].Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокОбъектов.Добавить(Выборка.Ссылка); 
		КонецЦикла;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь); 
	
	Возврат СписокОбъектов;

КонецФункции // сфпПолучитьСписокОбъектовДляПерезаполнения()

// Функция возвращает массив телефонов и факсов
//
// Параметры:
//	Ссылка	- СправочникСсылка	- Ссылка на владельца контактной информации
//
// Возвращаемое значение:
//	Массив	- Массив структур телефонов и факсов
//
Функция сфпПолучитьМассивТелефоновИФаксов(Ссылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина); 
	
	Если ЗначениеЗаполнено(Ссылка) И НЕ Метаданные.Справочники.Содержит(Ссылка.Метаданные()) Тогда
		Возврат Новый Массив();
	КонецЕсли;
	
	ИмяСправочника = сфпПолучитьИмяМетаданных(Ссылка);
	
	ПолеЗадержкиНабора = 5;
	Если Метаданные.Справочники[ИмяСправочника].ТабличныеЧасти.КонтактнаяИнформация.Реквизиты.Найти("сфпВремяЗадержкиНабораДобавочногоНомера") <> Неопределено Тогда
		ПолеЗадержкиНабора = "сфпВремяЗадержкиНабораДобавочногоНомера";
	КонецЕсли;	
	
	ПолеПрефиксНабора = """";
	Если Метаданные.Справочники[ИмяСправочника].ТабличныеЧасти.КонтактнаяИнформация.Реквизиты.Найти("сфпПрефиксНабора") <> Неопределено Тогда
		ПараметрПолеПрефиксНабора = "сфпПрефиксНабора.Наименование";
	Иначе
		ПараметрПолеПрефиксНабора = СтрЗаменить(ПолеПрефиксНабора, """", """""");
	КонецЕсли;	
		
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Ссылка,
	|	Вид,
	|	Представление,
	|	" + ПолеЗадержкиНабора + " КАК ЗадержкаДобавочногоНомера,
	|	" + ПараметрПолеПрефиксНабора + " КАК ПрефиксНабора
	|ИЗ
	|	Справочник." + ИмяСправочника + ".КонтактнаяИнформация
	|ГДЕ
	|	Ссылка = &Ссылка
	|	И Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Вид.РеквизитДопУпорядочивания");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	МассивТелефоновИФаксов = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураТелефона = Новый Структура();
		СтруктураТелефона.Вставить("Объект", Выборка.Ссылка);
		СтруктураТелефона.Вставить("Вид", Выборка.Вид);
		СтруктураТелефона.Вставить("Представление", Выборка.Представление);
		СтруктураТелефона.Вставить("ЗадержкаДобавочногоНомера", Выборка.ЗадержкаДобавочногоНомера);
		СтруктураТелефона.Вставить("ПрефиксНабора", Выборка.ПрефиксНабора);
		МассивТелефоновИФаксов.Добавить(СтруктураТелефона);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь); 
	
	Возврат МассивТелефоновИФаксов;
	
КонецФункции // сфпПолучитьМассивТелефоновИФаксов()

// Функция ищет объект в регистре по телефону
//
// Параметры:
//	НомерТелефона - Строка	- Номер телефона
//
// Возвращаемое значение:
//	Массив	- Массив найденных владельцев телефонного номера
//
Функция сфпНайтиОбъектВРегистреПоТелефону(НомерТелефона, МассивВладельцевКонтактов = Неопределено) Экспорт
	
	Если МассивВладельцевКонтактов = Неопределено Тогда
		МассивВладельцевКонтактов = Новый Массив();
	КонецЕсли;
	
	МассивОбъектов = Новый Массив;
	ОчищенныйНомерТелефона = сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона);
	
	Если СтрДлина(ОчищенныйНомерТелефона) > 0 Тогда
		УстановитьПривилегированныйРежим(Истина);
		
		ДлинаВнутреннихНомеров = Константы.сфпМаксимальнаяДлинаВнутреннихНомеров.Получить();
		КоличествоЦифрВНомере = Константы.сфпПоследниеЦифрыТелефонногоНомера.Получить();
		
		Если СтрДлина(ОчищенныйНомерТелефона) > ДлинаВнутреннихНомеров Тогда
			// Если внешний номер, то ищем в регистре номеров для поиска
			Запрос = Новый Запрос("
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	сфпНомераТелефоновДляПоиска.Объект КАК Объект,
			|	ВЫБОР
			|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.КонтактныеЛицаПартнеров)
			|			ТОГДА сфпНомераТелефоновДляПоиска.Объект.Владелец
			|		ИНАЧЕ НЕОПРЕДЕЛЕНО
			|	КОНЕЦ КАК Владелец,
			|	ВЫБОР
			|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Партнеры)
			|			ТОГДА ВЫБОР" 
			+ ?(сфпСофтФонПроСерверПовтИсп.ЭтоНеCRM(), "
				|					КОГДА сфпНомераТелефоновДляПоиска.Объект.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.КомпанияЧастноеЛицо.ЧастноеЛицо)
				|						ТОГДА 1
				|					ИНАЧЕ 4
				|				КОНЕЦ
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Контрагенты)
				|			ТОГДА 2
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.КонтактныеЛицаПартнеров)
				|			ТОГДА 3
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.ФизическиеЛица)
				|			ТОГДА 5
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Пользователи)
				|			ТОГДА 6
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.CRM_ПотенциальныеКлиенты)
				|			ТОГДА 7
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Организации)
				|			ТОГДА 8
				|	КОНЕЦ КАК Порядок,", "
				|					КОГДА сфпНомераТелефоновДляПоиска.Объект.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.КомпанияЧастноеЛицо.ЧастноеЛицо)
				|						ТОГДА 1
				|					ИНАЧЕ 3
				|				КОНЕЦ
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.КонтактныеЛицаПартнеров)
				|			ТОГДА 2
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.ФизическиеЛица)
				|			ТОГДА 4
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Пользователи)
				|			ТОГДА 5
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.CRM_ПотенциальныеКлиенты)
				|			ТОГДА 6
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Организации)
				|			ТОГДА 7
				|	КОНЕЦ КАК Порядок,") + "
			|	ВЫБОР
			|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.КонтактныеЛицаПартнеров)
			|			ТОГДА ВЫБОР
			|					КОГДА сфпНомераТелефоновДляПоиска.Объект.CRM_Состояние = ЗНАЧЕНИЕ(Перечисление.CRM_Состояние.Уволен)
			|						ТОГДА 2
			|					КОГДА сфпНомераТелефоновДляПоиска.Объект.CRM_Состояние = ЗНАЧЕНИЕ(Перечисление.CRM_Состояние.ВременноНеРаботает)
			|						ТОГДА 1
			|					ИНАЧЕ 0
			|				КОНЕЦ
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК СостояниеКонтакта
			|ИЗ
			|	РегистрСведений.сфпНомераТелефоновДляПоиска КАК сфпНомераТелефоновДляПоиска
			|ГДЕ
			|	сфпНомераТелефоновДляПоиска.НомерТелефона = &НомерТелефона
			|
			|УПОРЯДОЧИТЬ ПО
			|	Порядок,
			|	СостояниеКонтакта");
			Запрос.УстановитьПараметр("НомерТелефона", Прав(ОчищенныйНомерТелефона, КоличествоЦифрВНомере));
			
		Иначе
			// Если внутренний номер, то ищем в настройках пользователей
			Запрос = Новый Запрос("
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	Пользователь КАК Объект,
			|	5 КАК Порядок
			|ИЗ
			|	РегистрСведений.CRM_НастройкиПользователей
			|ГДЕ
			|	Настройка = &Настройка
			|	И Значение = &НомерТелефона
			|УПОРЯДОЧИТЬ ПО
			|	Порядок");
			Запрос.УстановитьПараметр("Настройка", ПланыВидовХарактеристик.CRM_НастройкиПользователей.сфпТекущийВнутреннийНомер);
			Запрос.УстановитьПараметр("НомерТелефона", ОчищенныйНомерТелефона);
			
			Результат = Запрос.Выполнить();
			Если Результат.Пустой() Тогда
				// Если внутренний номер не нашли, то ищем в регистре номеров для поиска
				Запрос = Новый Запрос("
				|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	сфпНомераТелефоновДляПоиска.Объект КАК Объект,
				|	ВЫБОР
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.КонтактныеЛицаПартнеров)
				|			ТОГДА сфпНомераТелефоновДляПоиска.Объект.Владелец
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.CRM_ПотенциальныеКлиенты)
				|			ТОГДА сфпНомераТелефоновДляПоиска.Объект.Партнер
				|		ИНАЧЕ НЕОПРЕДЕЛЕНО
				|	КОНЕЦ КАК Владелец,
				|	ВЫБОР
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Партнеры)
				|			ТОГДА ВЫБОР" 
				+ ?(сфпСофтФонПроСерверПовтИсп.ЭтоНеCRM(), "
				|					КОГДА сфпНомераТелефоновДляПоиска.Объект.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.КомпанияЧастноеЛицо.ЧастноеЛицо)
				|						ТОГДА 1
				|					ИНАЧЕ 4
				|				КОНЕЦ
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Контрагенты)
				|			ТОГДА 2
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.КонтактныеЛицаПартнеров)
				|			ТОГДА 3
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.ФизическиеЛица)
				|			ТОГДА 5
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Пользователи)
				|			ТОГДА 6
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.CRM_ПотенциальныеКлиенты)
				|			ТОГДА 7
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Организации)
				|			ТОГДА 8
				|	КОНЕЦ КАК Порядок,", "
				|					КОГДА сфпНомераТелефоновДляПоиска.Объект.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.КомпанияЧастноеЛицо.ЧастноеЛицо)
				|						ТОГДА 1
				|					ИНАЧЕ 3
				|				КОНЕЦ
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.КонтактныеЛицаПартнеров)
				|			ТОГДА 2
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.ФизическиеЛица)
				|			ТОГДА 4
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Пользователи)
				|			ТОГДА 5
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.CRM_ПотенциальныеКлиенты)
				|			ТОГДА 6
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.Организации)
				|			ТОГДА 7
				|	КОНЕЦ КАК Порядок,") + "
				|	ВЫБОР
				|		КОГДА ТИПЗНАЧЕНИЯ(сфпНомераТелефоновДляПоиска.Объект) = ТИП(Справочник.КонтактныеЛицаПартнеров)
				|			ТОГДА ВЫБОР
				|					КОГДА сфпНомераТелефоновДляПоиска.Объект.CRM_Состояние = ЗНАЧЕНИЕ(Перечисление.CRM_Состояние.Уволен)
				|						ТОГДА 2
				|					КОГДА сфпНомераТелефоновДляПоиска.Объект.CRM_Состояние = ЗНАЧЕНИЕ(Перечисление.CRM_Состояние.ВременноНеРаботает)
				|						ТОГДА 1
				|					ИНАЧЕ 0
				|				КОНЕЦ
				|		ИНАЧЕ 0
				|	КОНЕЦ КАК СостояниеКонтакта
				|ИЗ
				|	РегистрСведений.сфпНомераТелефоновДляПоиска КАК сфпНомераТелефоновДляПоиска
				|ГДЕ
				|	сфпНомераТелефоновДляПоиска.НомерТелефона = &НомерТелефона
				|
				|УПОРЯДОЧИТЬ ПО
				|	Порядок,
				|	СостояниеКонтакта");
				Запрос.УстановитьПараметр("НомерТелефона", ОчищенныйНомерТелефона);
			КонецЕсли;
		КонецЕсли;
		
		ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
		Если ТаблицаРезультата.Количество() > 0 Тогда
			МассивДобавленных = Новый Массив;
			Для Каждого СтрокаРезультата Из ТаблицаРезультата Цикл
				// Владельца КЛ добавляем в массив добавленных объектов (повторно в список поиска партнёра-владельца не берём).
				Если ТипЗнч(СтрокаРезультата.Объект) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
					Если МассивВладельцевКонтактов.Найти(СтрокаРезультата.Владелец) = Неопределено Тогда	
						МассивВладельцевКонтактов.Добавить(СтрокаРезультата.Владелец);
					КонецЕсли;
					Если МассивДобавленных.Найти(СтрокаРезультата.Владелец) = Неопределено Тогда
						МассивДобавленных.Добавить(СтрокаРезультата.Владелец);
					КонецЕсли;
					МассивОбъектов.Добавить(СтрокаРезультата.Объект);
					
				ИначеЕсли ТипЗнч(СтрокаРезультата.Объект) = Тип("СправочникСсылка.Партнеры") Тогда
					Если СтрокаРезультата.Порядок = 1 Тогда
						// Для случая когда партнёр является физическием лицом возможен вариант, 
						// что у него есть связанные клиенты/контакты.
						МассивСвязанныхОбъектов =
							РегистрыСведений.CRM_СвязиФизЛицСКонтактнымиЛицамиПартнеров.ПолучитьКонтактныеЛица(СтрокаРезультата.Объект);
						Если МассивСвязанныхОбъектов.Количество() > 0 Тогда
							Для Каждого ЭлементМассива Из МассивСвязанныхОбъектов Цикл
								ВладелецОбъекта = сфпПолучитьВладельцаКонтакта(ЭлементМассива);
								Если МассивДобавленных.Найти(ВладелецОбъекта) = Неопределено Тогда
									МассивДобавленных.Добавить(ВладелецОбъекта);
								КонецЕсли;
							КонецЦикла;
							
							Если МассивДобавленных.Найти(СтрокаРезультата.Объект) = Неопределено Тогда
								МассивДобавленных.Добавить(СтрокаРезультата.Объект);
								МассивОбъектов.Добавить(СтрокаРезультата.Объект);
							КонецЕсли;
							
						Иначе
							Если МассивДобавленных.Найти(СтрокаРезультата.Объект) = Неопределено Тогда	
								МассивДобавленных.Добавить(СтрокаРезультата.Объект);
								МассивОбъектов.Добавить(СтрокаРезультата.Объект);
							КонецЕсли;
						КонецЕсли;
						
					Иначе
						Если МассивДобавленных.Найти(СтрокаРезультата.Объект) = Неопределено Тогда	
							МассивДобавленных.Добавить(СтрокаРезультата.Объект);
							МассивОбъектов.Добавить(СтрокаРезультата.Объект);
						КонецЕсли;
					КонецЕсли;
				// +CRM3
				ИначеЕсли ТипЗнч(СтрокаРезультата.Объект) = Тип("СправочникСсылка.CRM_ПотенциальныеКлиенты") Тогда
					
					// Если в выборке существует партнер, соответствующий данному потенциальному клиенту,
					// потенциальный клиент не добавляется.
					Если ТаблицаРезультата.Найти(СтрокаРезультата.Объект.Партнер) <> Неопределено Тогда
						Продолжить;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаРезультата.Владелец) Тогда
						Если МассивВладельцевКонтактов.Найти(СтрокаРезультата.Владелец) = Неопределено Тогда
							МассивВладельцевКонтактов.Добавить(СтрокаРезультата.Владелец);
						КонецЕсли;
						
						Продолжить;
					КонецЕсли;
					
					Если МассивДобавленных.Найти(СтрокаРезультата.Объект) = Неопределено Тогда	
						МассивДобавленных.Добавить(СтрокаРезультата.Объект);
					КонецЕсли;
					МассивОбъектов.Добавить(СтрокаРезультата.Объект);
				// -CRM3
				Иначе
					МассивОбъектов.Добавить(СтрокаРезультата.Объект);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат МассивОбъектов;
	
КонецФункции // сфпНайтиОбъектВРегистреПоТелефону()

// Функция выполняет поиск объекта в базе данных по значению переданного GUID
//
// Параметры:
//	GUID	- Строка	- Уникальный идентификатор
//
// Возвращаемое значение:
//	СправочникСсылка	- Владелец GUID
//	
Функция сфпНайтиКонтактПоGUID(GUIDСоединенС) Экспорт
	Результат = Ложь;
	Попытка
		УникальныйИдентификаторДляПоиска = Новый УникальныйИдентификатор(GUIDСоединенС);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	УстановитьПривилегированныйРежим(Истина); 
	МассивТиповСправочников = Метаданные.РегистрыСведений.сфпНомераТелефоновДляПоиска.Измерения.Объект.Тип.Типы();
	Для Каждого ТипСправочника Из МассивТиповСправочников Цикл
		СправочникСсылка = Новый(ТипСправочника);
		ИмяСправочника = СправочникСсылка.Метаданные().Имя; 
		НайденныйОбъект = Справочники[ИмяСправочника].ПолучитьСсылку(УникальныйИдентификаторДляПоиска);
		// Ссылка создается в любом случае...Даже если объект не был найден
		//  но если объект найден не был, то в ссылке есть строка <Объект не найден>
		Если ОбщегоНазначения.СсылкаСуществует(НайденныйОбъект) Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь); 
	Возврат ?(Результат, НайденныйОбъект, Неопределено);
КонецФункции // сфпНайтиКонтактПоGUID()

// Функция возвращает владельца контакта
//
// Параметры:
//	Контакт	- СправочникСсылка	- Контакт
//
// Возвращаемое значение:
//	СправочникСсылка	- Владелец контакта
//
Функция сфпПолучитьВладельцаКонтакта(Знач Контакт, ПредставлениеВладельца = "") Экспорт

	Владелец = Неопределено;

	Если ЗначениеЗаполнено(Контакт) И Контакт.Метаданные().Имя = "CRM_Заявка" Тогда
		Контакт = Контакт.Контакт;
	КонецЕсли;
	
	ЧужойКонтакт = Ложь;
	Если СтрНайти(Строка(Контакт), НСтр("ru='<Объект не найден>';en='<Object not found>'")) > 0 Тогда
		// "Чужой" клиент
		ЧужойКонтакт = Истина;
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина); 

	Если ЧужойКонтакт И СтрНайти(Строка(Контакт), НСтр("ru='<Объект не найден>';en='<Object not found>'")) > 0 Тогда
		// А вдруг не чужой, а битая ссылка
		ЧужойКонтакт = Ложь;
	КонецЕсли;

	Если ЗначениеЗаполнено(Контакт) Тогда
		МетаданныеКонтакта = Контакт.Метаданные();
		ИмяМетаданных = МетаданныеКонтакта.Имя;

		Если МетаданныеКонтакта.Владельцы.Количество() > 0 Тогда
			Владелец = Контакт.Владелец;

		ИначеЕсли ИмяМетаданных = "CRM_ПотенциальныеКлиенты" Тогда
			Владелец = Контакт.Партнер;
			
		ИначеЕсли ИмяМетаданных = "КонтактныеЛица" Тогда
			Если Метаданные.РегистрыСведений.Найти("СвязиКонтрагентКонтакт") <> Неопределено Тогда
				// BSLLS:QueryToMissingMetadata-off 
				// Использкется в спарках
				
				Запрос = Новый Запрос("
				|ВЫБРАТЬ Контрагент
				|ИЗ РегистрСведений.СвязиКонтрагентКонтакт.СрезПоследних(, Контакт = &Контакт)
				|ГДЕ НЕ СвязьНедействительна");
				
				// BSLLS:QueryToMissingMetadata-on
				
				Запрос.УстановитьПараметр("Контакт", Контакт);
				
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					Владелец = Выборка.Контрагент;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		Если ЧужойКонтакт Тогда
			Попытка
				ПредставлениеВладельца = "Партнер группы: " + Владелец.ГруппаДоступа;
			Исключение
				ПредставлениеВладельца = "" + Владелец;
			КонецПопытки;
			
		Иначе	
			ДополнительнаяИнформация = ПолучитьДополнительнуюИнформациюКонтакта(Владелец);
			Если ЗначениеЗаполнено(ДополнительнаяИнформация) Тогда
				  ПредставлениеВладельца = "" + Владелец + ": " + ДополнительнаяИнформация;
			Иначе ПредставлениеВладельца = "" + Владелец;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь); 
	
	Возврат Владелец;

КонецФункции // сфпПолучитьВладельцаКонтакта()

// Функция получает наименование контакта
//
// Параметры:
//	Контакт	- СправочникСсылка	- Контакт
//
// Возвращаемое значение:
//	Строка	- Наименование контакта
//
Функция сфпПолучитьНаименованиеКонтакта(Контакт) Экспорт
	
	ЧужойКонтакт = Ложь;
	Если СтрНайти(Строка(Контакт), НСтр("ru='<Объект не найден>';en='<Object not found>'")) > 0 Тогда
		// "Чужой" клиент
		ЧужойКонтакт = Истина;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина); 
	
	Если ЧужойКонтакт И СтрНайти(Строка(Контакт), НСтр("ru='<Объект не найден>';en='<Object not found>'")) > 0 Тогда
		// А вдруг не чужой, а битая ссылка
		ЧужойКонтакт = Ложь;
	КонецЕсли;
			
	НаименованиеКонтакта = Контакт.Наименование;
				
	Если ЧужойКонтакт И Метаданные.Справочники.Партнеры.Реквизиты.Найти("ГруппаДоступа") <> Неопределено Тогда
		// Если клиент "чужой", то вместо наименования выводим группу доступа
		Если ТипЗнч(Контакт) = Тип("СправочникСсылка.Партнеры") Тогда
			НаименованиеКонтакта = "Контакт группы: " + Контакт.ГруппаДоступа;
			
		ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
			НаименованиеКонтакта = "Контакт группы: " + Контакт.Владелец.ГруппаДоступа;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь); 
	
	Возврат НаименованиеКонтакта;

КонецФункции // сфпПолучитьНаименованиеКонтакта()

// Функция получает представление контакта с дополнительной информацией
//
// Параметры:
//	Контакт	- СправочникСсылка	- Контакт
//
// Возвращаемое значение:
//	Строка	- Представление контакта
//
Функция сфпПолучитьПредставлениеКонтакта(Контакт) Экспорт
	
	ЧужойКонтакт = Ложь;
	Если СтрНайти(Строка(Контакт), НСтр("ru='<Объект не найден>';en='<Object not found>'")) > 0 Тогда
		// "Чужой" клиент
		ЧужойКонтакт = Истина;
	КонецЕсли;
		
	УстановитьПривилегированныйРежим(Истина); 
	
	Если ЧужойКонтакт И СтрНайти(Строка(Контакт), НСтр("ru='<Объект не найден>';en='<Object not found>'")) > 0 Тогда
		// А вдруг не чужой, а битая ссылка
		ЧужойКонтакт = Ложь;
	КонецЕсли;
	
	ПредставлениеКонтакта = "" + Контакт;
	
	Если ЧужойКонтакт Тогда
		Если Метаданные.Справочники.Партнеры.Реквизиты.Найти("ГруппаДоступа") <> Неопределено Тогда
			Если ТипЗнч(Контакт) = Тип("СправочникСсылка.Партнеры") Тогда
				ПредставлениеКонтакта = "Контакт группы: " + Контакт.ГруппаДоступа;
				
			ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
				ПредставлениеКонтакта = "Контакт группы: " + Контакт.Владелец.ГруппаДоступа;
			КонецЕсли;

		Иначе
			ПредставлениеКонтакта = "Нет доступа";
		КонецЕсли;
	КонецЕсли;
	
	ДополнительнаяИнформация = ПолучитьДополнительнуюИнформациюКонтакта(Контакт);
	Если ЗначениеЗаполнено(ДополнительнаяИнформация) Тогда
		ПредставлениеКонтакта = ПредставлениеКонтакта + ": " + ДополнительнаяИнформация;
	КонецЕсли;
		
	УстановитьПривилегированныйРежим(Ложь); 
	
	Возврат ПредставлениеКонтакта;

КонецФункции // сфпПолучитьПредставлениеКонтакта()

// Функция получает уникальный идентификатор контакта
//
// Параметры:
//	Контакт	- СправочникСсылка	- Контакт
//
// Возвращаемое значение:
//	Строка	- Уникальный идентификатор контакта
//
Функция сфпПолучитьИдентификаторКонтакта(Контакт) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторКонтакта = "";
	
	Если ЗначениеЗаполнено(Контакт) Тогда
		ИмяМетаданных = Контакт.Метаданные().Имя;
		Если ИмяМетаданных = "CRM_Заявка" Тогда
			Если ЗначениеЗаполнено(Контакт.Контакт) Тогда
				ИдентификаторКонтакта = Строка(Контакт.Контакт.УникальныйИдентификатор());
			КонецЕсли;
			
		Иначе
			ИдентификаторКонтакта = Строка(Контакт.УникальныйИдентификатор());
		КонецЕсли;	
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Ложь); 
	
	Возврат ИдентификаторКонтакта;

КонецФункции // сфпПолучитьИдентификаторКонтакта()

// Функция формирует строковое представление телефона
//
// Параметры:
//	КодСтраны		- Строка	- Код страны
//	КодГорода		- Строка	- Код города
//	НомерТелефона	- Строка	- Номер телефона
//
// Возвращаемое значение:
//	Строка	- Представление телефона
//
Функция сфпСформироватьПредставлениеТелефона(КодСтраны, КодГорода, НомерТелефона) Экспорт
	Возврат УправлениеКонтактнойИнформациейКлиентСервер.СформироватьПредставлениеТелефона(КодСтраны,
		 КодГорода, НомерТелефона, "",
		 "");
КонецФункции // сфпСформироватьПредставлениеТелефона()	

// Функция ищет в регистре представление номера телефона по контакту и его номеру телефона
//
// Параметры:
//	Контакт			- СправочникСсылка	- Контакт
//	НомерТелефона	- Строка			- Номер телефона
//
// Возвращаемое значение:
//	Строка	- Представление номера телефона
//
Функция сфпПолучитьПредставлениеНомераТелефона(Контакт, НомерТелефона) Экспорт
	ПредставлениеНомераТелефона = НомерТелефона;
	ОчищенныйНомерТелефона = сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона);
	Если ЗначениеЗаполнено(Контакт) Тогда
		Если СтрДлина(ОчищенныйНомерТелефона) > 0 Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Объект", Контакт);
			КоличествоЦифрВНомере = Константы.сфпПоследниеЦифрыТелефонногоНомера.Получить();
			Если СтрДлина(ОчищенныйНомерТелефона) > КоличествоЦифрВНомере Тогда
				Запрос.УстановитьПараметр("НомерТелефона", Прав(ОчищенныйНомерТелефона, КоличествоЦифрВНомере));
			Иначе
				сфпПараметрыСервера	= сфпПараметрыСервера();
				КодГорода 	= сфпПараметрыСервера.КодГорода;
				Если СтрДлина(КодГорода + ОчищенныйНомерТелефона) = 10 Тогда
					Запрос.УстановитьПараметр("НомерТелефона", Прав(КодГорода + ОчищенныйНомерТелефона, КоличествоЦифрВНомере));
				Иначе
					Запрос.УстановитьПараметр("НомерТелефона", ОчищенныйНомерТелефона);
				КонецЕсли;
			КонецЕсли;
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			               |	сфпНомераТелефоновДляПоиска.Представление КАК Представление
			               |ИЗ
			               |	РегистрСведений.сфпНомераТелефоновДляПоиска КАК сфпНомераТелефоновДляПоиска
			               |ГДЕ
			               |	сфпНомераТелефоновДляПоиска.НомерТелефона = &НомерТелефона
			               |	И сфпНомераТелефоновДляПоиска.Объект = &Объект";
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ПредставлениеНомераТелефона = Выборка.Представление;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ПараметрыСервера = сфпПараметрыСервера();
		// Получаем структуру полей по представлению
		СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(НомерТелефона);
		// Если не заполнены код страны или код города, то заполняем их по умолчанию
		Если НЕ ЗначениеЗаполнено(СтруктураНомера.КодСтраны) Тогда
			СтруктураНомера.КодСтраны = ПараметрыСервера.КодСтраны;
		КонецЕсли;	
		Если НЕ ЗначениеЗаполнено(СтруктураНомера.КодГорода) Тогда
			СтруктураНомера.КодГорода = ПараметрыСервера.КодГорода;
		КонецЕсли;	
		ПредставлениеНомераТелефона = сфпСформироватьПредставлениеТелефона(СтруктураНомера.КодСтраны,
			СтруктураНомера.КодГорода, СтруктураНомера.НомерТелефона);
	КонецЕсли;
	Возврат ПредставлениеНомераТелефона;
КонецФункции // сфпПолучитьПредставлениеНомераТелефона()

/////////////////////////////////////////////////
// МАРШРУТИЗАЦИЯ

 // Функция проверяет использование маршрутизации средствами сервера СофтФона
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Признак использования маршрутизации
//
Функция сфпИспользоватьМаршрутизацию() Экспорт
	Возврат Константы.сфпИспользоватьМаршрутизацию.Получить();
КонецФункции // сфпИспользоватьМаршрутизацию()

// Функция возвращает идентификатор таблицы маршрутизации
//
// Параметры:
//	Нет.
//
Функция сфпПолучитьИдентификаторМаршрутизации(СоздаватьНовый = Ложь) Экспорт
	ИдентификаторМаршрутизации = Константы.сфпИдентификаторМаршрутизации.Получить();
	Если ПустаяСтрока(ИдентификаторМаршрутизации) Тогда
		Если СоздаватьНовый Тогда
			УстановитьПривилегированныйРежим(Истина);
			ИдентификаторМаршрутизации = Строка(Новый УникальныйИдентификатор);
			Константы.сфпИдентификаторМаршрутизации.Установить(ИдентификаторМаршрутизации);
		КонецЕсли;	
	КонецЕсли;	
	Возврат ИдентификаторМаршрутизации;
КонецФункции // сфпПолучитьИдентификаторМаршрутизации()

// Функция возвращает внутренний номер текущего пользователя
//
// Параметры:
//	Пользователь			- СправочникСсылка	- Пользователь, для которого получается внутренний номер
//  НомерДляМаршрутизации   - Булево - Признак получения номера для нужд маршрутизации
//
// Возвращаемое значение:
//	Строка	- Внутренний номер пользователя
//
Функция сфпТекущийВнутреннийНомер(Пользователь = Неопределено, НомерДляМаршрутизации = Ложь) Экспорт
	
	ИспользуемаяВерсия = Константы.сфпИспользуемаяВерсияСофтФон.Получить();
	ИспользоватьОблачнуюТелефонию = Константы.сфпИспользоватьОблачнуюТелефонию.Получить();
	Если ИспользуемаяВерсия = Перечисления.сфпВерсииСофтФон.СофтФотPROSTO И ИспользоватьОблачнуюТелефонию Тогда
		ДанныеАбонента = сфпЛицензированиеЭкспортныеМетоды.ДанныеПользователяАТС(Пользователь);
		Возврат ДанныеАбонента.ВнутреннийНомер;

	Иначе	
		сфпВнутреннийНомер = сфпПолучитьЗначениеНастройкиПользователя("сфпТекущийВнутреннийНомер", Пользователь);
		Если НЕ НомерДляМаршрутизации Тогда
			Возврат сфпУбратьИзНомераТелефонаВсеПрефиксы(сфпВнутреннийНомер, Истина);
			
		Иначе
			Если НЕ ЗначениеЗаполнено(сфпВнутреннийНомер) Тогда
				сфпВнутреннийНомер = сфпПолучитьВнутреннийНомерПользователяИзКИ(Пользователь);
			КонецЕсли;			
			
			Возврат сфпВнутреннийНомер; 
		КонецЕсли;
	КонецЕсли;

КонецФункции // сфпТекущийВнутреннийНомер()

// Функция возвращает дополнительные внутренние номера текущего пользователя
//
// Параметры:
//	Пользователь	- СправочникСсылка	- Пользователь, для которого получаются внутренние номера
//
// Возвращаемое значение:
//	Массив	- Дополнительные внутренние номера пользователя
//
Функция сфпНомераЛинийПользователя(Пользователь = Неопределено) Экспорт
	
	ДополнительныеНомера = сфпПолучитьЗначениеНастройкиПользователя("сфпДополнительныеВнутренниеНомера", Пользователь);
	МассивНомеров = сфпОбщегоНазначенияКлиентСервер.сфпРазложитьСтрокуВМассивПодстрок(ДополнительныеНомера, ",");
	
	ВнутренниеНомера = Новый Массив();
	Для Каждого ТекНомер Из МассивНомеров Цикл
		ВнутренниеНомера.Добавить(сфпУбратьИзНомераТелефонаВсеПрефиксы(ТекНомер, Истина));
	КонецЦикла;	
	
	Возврат ВнутренниеНомера; 

КонецФункции // сфпНомераЛинийПользователя()

// Функция возращает внутренний номер телефона из контактной информации пользователя
//
// Параметры:
//	Пользователь			- СправочникСсылка	- Пользователь, для которого получается внутренний номер
//
// Возвращаемое значение:
//	Строка	- Внутренний номер пользователя из КИ
//
Функция сфпПолучитьВнутреннийНомерПользователяИзКИ(Пользователь = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	|	ПользователиКонтактнаяИнформация.Представление КАК НомерТелефона
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	|ГДЕ
	|	ПользователиКонтактнаяИнформация.Ссылка = &Пользователь
	|	И ПользователиКонтактнаяИнформация.Вид = &ВидКИ
	|";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("ВидКИ", Справочники.ВидыКонтактнойИнформации.CRM_ТелефонПользователяРабочий);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.НомерТелефона;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

// Функция изменяет внутренний номер пользователя
	// и возвращает массив пользователей, у которых был очищен внутрений номер
//
// Параметры:
//	НомерЛинии		- Строка			- Номер линии
//	Пользователь	- СправочникСсылка	- Пользователь
//
// Возвращаемое значение:
//	Массив	- Массив пользователей, внутренние номера которых были очищены
//
Функция сфпЗаписатьНомерПользователю(НомерЛинии, Пользователь) Экспорт
	МассивПользователей = Новый Массив;
	// Записываем новый номер только, если он не пустой
	Если НЕ ПустаяСтрока(НомерЛинии) Тогда
		НомерЗаписан = Ложь;
		НаборЗаписей = РегистрыСведений.CRM_НастройкиПользователей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Настройка.Установить(ПланыВидовХарактеристик.CRM_НастройкиПользователей.сфпТекущийВнутреннийНомер);
		НаборЗаписей.Прочитать();
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
			Если ЗаписьНабора.Пользователь = Пользователь Тогда
				ЗаписьНабора.Значение = НомерЛинии;
				НомерЗаписан = Истина;
			ИначеЕсли ЗаписьНабора.Значение = НомерЛинии Тогда
				ЗаписьНабора.Значение = "";
				МассивПользователей.Добавить(ЗаписьНабора.Пользователь);	
			КонецЕсли;	
		КонецЦикла;
		Если НЕ НомерЗаписан Тогда
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Пользователь	= Пользователь;
			ЗаписьНабора.Настройка		= ПланыВидовХарактеристик.CRM_НастройкиПользователей.сфпТекущийВнутреннийНомер;
			ЗаписьНабора.Значение		= НомерЛинии;
		КонецЕсли;	
		Попытка
			НаборЗаписей.Записать();
		Исключение
			МассивПользователей = Новый Массив;
		КонецПопытки;
	КонецЕсли;
	Возврат МассивПользователей;
КонецФункции // сфпЗаписатьНомерПользователю()

// Процедура изменяет дополнительные внутренние номера пользователя
//
// Параметры:
//	Пользователь	- СправочникСсылка	- Пользователь
//	НомераЛиний		- Строка			- Номера линий
//
Процедура сфпЗаписатьДополнительныеНомераПользователю(Пользователь, НомераЛиний) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.CRM_НастройкиПользователей.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь = Пользователь;
	МенеджерЗаписи.Настройка = ПланыВидовХарактеристик.CRM_НастройкиПользователей.сфпДополнительныеВнутренниеНомера;
	МенеджерЗаписи.Прочитать();
	Если НЕ МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Пользователь = Пользователь;
		МенеджерЗаписи.Настройка = ПланыВидовХарактеристик.CRM_НастройкиПользователей.сфпДополнительныеВнутренниеНомера;
	КонецЕсли;
	Если МенеджерЗаписи.Значение <> НомераЛиний Тогда
		МенеджерЗаписи.Значение = НомераЛиний;
		МенеджерЗаписи.Записать();
	КонецЕсли;

КонецПроцедуры // сфпЗаписатьДополнительныеНомераПользователю()

// Функция возвращает список маршрутизации
//
// Параметры:
//	Объект			- СправочникСсылка	- Владелец контактной информации
//	Пользователь	- СправочникСсылка	- Пользователь для переключения звонков
//
// Возвращаемое значение:
//	Строка	- Строка маршрутизации
//
Функция сфпПолучитьСписокМаршрутизации(Объект = Неопределено, Пользователь = Неопределено) Экспорт
	Если Пользователь = Неопределено Тогда
		Пользователь	= сфпТекущийПользователь();
	КонецЕсли;	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	НомераТелефоновДляПоиска.Объект КАК Контакт,
	               |	НомераТелефоновДляПоиска.Представление КАК Представление,
	               |	ЕСТЬNULL(НастройкиПользователей.Значение, """") КАК НомерВнТелефона
	               |ИЗ
	               |	РегистрСведений.сфпНомераТелефоновДляПоиска КАК НомераТелефоновДляПоиска
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			НастройкиПользователей.Пользователь КАК Пользователь,
	               |			НастройкиПользователей.Значение КАК Значение
	               |		ИЗ
	               |			РегистрСведений.CRM_НастройкиПользователей КАК НастройкиПользователей
	               |		ГДЕ
	               |			НастройкиПользователей.Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.CRM_НастройкиПользователей.сфпТекущийВнутреннийНомер)) КАК НастройкиПользователей
	               |		ПО НомераТелефоновДляПоиска.Пользователь = НастройкиПользователей.Пользователь";
	Если ЗначениеЗаполнено(Объект) Тогда				   
		Запрос.Текст = Запрос.Текст + "
	               |ГДЕ
	               |	НЕ НомераТелефоновДляПоиска.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	               |	И НомераТелефоновДляПоиска.Объект = &Объект";
	ИначеЕсли ЗначениеЗаполнено(Пользователь) Тогда				   
		Запрос.Текст = Запрос.Текст + "
	               |ГДЕ
	               |	НомераТелефоновДляПоиска.Пользователь = &Пользователь";
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	СписокСтрока = "";
	Пока Выборка.Следующий() Цикл
		Если ПустаяСтрока(Выборка.НомерВнТелефона) Тогда Продолжить; КонецЕсли;
		СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(Выборка.Представление);
		НомерТелефона = СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
			+ СтруктураНомера.НомерТелефона;
		Если ПустаяСтрока(НомерТелефона) Тогда Продолжить; КонецЕсли;
		СписокСтрока = СписокСтрока + Прав(НомерТелефона, 10) + "=" + СокрЛП(Выборка.НомерВнТелефона) + ";";
	КонецЦикла;
	Возврат СписокСтрока;
КонецФункции // сфпПолучитьСписокМаршрутизации()

// Процедура меняет пользователя для переключения у выбранного контакта
//
// Параметры:
//	Контакт	- СправочникСсылка	- Контакт, для которого ищется пользователь для переключения звонков
//
// Возвращаемое значение:
//	СправочникСсылка	- Пользователь для переключения звонков 
//
Функция сфпПолучитьПользователяДляПереключенияЗвонков(Контакт) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контакт", Контакт);
	Запрос.Текст = "ВЫБРАТЬ
	               |	сфпНомераТелефоновДляПоиска.Пользователь КАК Пользователь
	               |ИЗ
	               |	РегистрСведений.сфпНомераТелефоновДляПоиска КАК сфпНомераТелефоновДляПоиска
	               |ГДЕ
	               |	сфпНомераТелефоновДляПоиска.Объект = &Контакт";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Пользователь;
	Иначе
		Возврат Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;	
КонецФункции // сфпПолучитьПользователяДляПереключенияЗвонков()

// Функция записывает нового пользователя для переключения в регистр номеров телефонов
//
// Параметры:
//	Контакт						- СправочникСсылка	- Контакт
//	НовыйПользователь			- СправочникСсылка	- Новый пользователь для переключения
//  ИскатьНомерВКИПользователя 	- Булево - Признак поиска номера телефона в КИ пользователя
//
// Возвращаемое значение:
//	Булево	- Признак обновления регистра
//
Функция сфпЗаписатьНовогоПользователя(Контакт, НовыйПользователь, ИскатьНомерВКИПользователя = Ложь) Экспорт
	
	НаборЗаписей = РегистрыСведений.сфпНомераТелефоновДляПоиска.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Контакт);
	НаборЗаписей.Прочитать();
	
	ВнутреннийНомер	= сфпТекущийВнутреннийНомер(НовыйПользователь, ИскатьНомерВКИПользователя);
	
	Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
		ЗаписьНабора.Пользователь = НовыйПользователь;
		ЗаписьНабора.ВнутреннийНомер = ВнутреннийНомер;
	КонецЦикла;	
	
	Попытка
		НаборЗаписей.Записать(Истина);
		Возврат Истина;
	Исключение 
		ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка, , , 
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
	КонецПопытки;
	
	Возврат Ложь;

КонецФункции // сфпЗаписатьНовогоПользователя()	

// Процедура перезаписывает внутренний номер, на который осуществляется маршрутизация пользователя
//
// Параметры:
//	НовыйПользователь			- СправочникСсылка	- Новый пользователь для переключения
//  НомерЛинии 					- Строка - Новый внутренний нмоер пользователя
//
Процедура сфпПерезаписатьНомерЛинииТекущегоПользователяВРегистреПоиска(НовыйПользователь, НомерЛинии) Экспорт
	Запрос = Новый Запрос();
	Запрос.Текст =   "ВЫБРАТЬ
	|	сфпНомераТелефоновДляПоиска.Объект КАК Объект,
	|	сфпНомераТелефоновДляПоиска.Вид КАК Вид,
	|	сфпНомераТелефоновДляПоиска.ПорядковыйНомер КАК ПорядковыйНомер,
	|	сфпНомераТелефоновДляПоиска.НомерТелефона КАК НомерТелефона,
	|	сфпНомераТелефоновДляПоиска.Представление КАК Представление,
	|	сфпНомераТелефоновДляПоиска.Пользователь КАК Пользователь,
	|	сфпНомераТелефоновДляПоиска.ВнутреннийНомер КАК ВнутреннийНомер,
	|	сфпНомераТелефоновДляПоиска.НомерТелефонаДляПоиска КАК НомерТелефонаДляПоиска
	|ИЗ
	|	РегистрСведений.сфпНомераТелефоновДляПоиска КАК сфпНомераТелефоновДляПоиска
	|ГДЕ
	|	сфпНомераТелефоновДляПоиска.Пользователь = &НовыйПользователь";
	Запрос.УстановитьПараметр("НовыйПользователь", НовыйПользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	МассивКонтактов = Новый Массив();
	Пока Выборка.Следующий() Цикл
		// для каждой записи регистра изменяем номер линии на новый
		НайденныйКонтакт = Выборка.Объект;
		Если МассивКонтактов.Найти(НайденныйКонтакт) = Неопределено Тогда		 
			НаборЗаписей = РегистрыСведений.сфпНомераТелефоновДляПоиска.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(НайденныйКонтакт);
			НаборЗаписей.Прочитать();
			Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
				Если НЕ (ЗаписьНабора.ВнутреннийНомер = НомерЛинии) Тогда
					ЗаписьНабора.ВнутреннийНомер = НомерЛинии;
				КонецЕсли;			
			КонецЦикла;
			Попытка
				НаборЗаписей.Записать(Истина);
			Исключение
				ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
					УровеньЖурналаРегистрации.Ошибка, , , 
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			МассивКонтактов.Добавить(НайденныйКонтакт);
		КонецЕсли;
	КонецЦикла;		
КонецПроцедуры

// Функция возвращает признак использования маршрутизации по номеру из КИ пользователя
//
// Возвращаемое значение:
//	Булево	- Используем маршрутизацию по номеру СФ (Значение - Ложь) или по номеру из КИ (Истина)
//
Функция сфпИспользоватьМаршрутизациюПоНомеруИзКИПользователя() Экспорт
	Возврат Константы.сфпИспользоватьВнутреннийНомерИзКИПользователя.Получить();
КонецФункции

// Функция возвращает таблицу маршрутизации
//
// Параметры:
//	Контакт			- СправочникСсылка	- Контакт
//	Пользователь	- СправочникСсылка	- Пользователь
//
// Возвращаемое значение:
//	Массив	- Таблица маршрутизации
//
Функция сфпПолучитьТаблицуМаршрутизации(Контакт = Неопределено, Пользователь = Неопределено) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	сфпНомераТелефоновДляПоиска.НомерТелефона КАК НомерТелефона,
	               |	сфпНомераТелефоновДляПоиска.Представление КАК Представление,
	               |	сфпНомераТелефоновДляПоиска.ВнутреннийНомер КАК ВнутреннийНомер
	               |ИЗ
	               |	РегистрСведений.сфпНомераТелефоновДляПоиска КАК сфпНомераТелефоновДляПоиска";
	Если НЕ (Контакт = Неопределено) И ЗначениеЗаполнено(Контакт) Тогда
		Запрос.УстановитьПараметр("Объект", Контакт);
		Запрос.Текст = Запрос.Текст + "				   
	               |ГДЕ
	               |	сфпНомераТелефоновДляПоиска.Объект = &Объект";
		Если НЕ (Пользователь = Неопределено) И ЗначениеЗаполнено(Пользователь) Тогда
			Запрос.УстановитьПараметр("Пользователь", Пользователь);
			Запрос.Текст = Запрос.Текст + "				   
	               |	И сфпНомераТелефоновДляПоиска.Пользователь = &Пользователь";
		КонецЕсли;		   
	ИначеЕсли НЕ (Пользователь = Неопределено) И ЗначениеЗаполнено(Пользователь) Тогда
		Запрос.Текст = Запрос.Текст + "				   
	               |ГДЕ
	               |	сфпНомераТелефоновДляПоиска.Пользователь = &Пользователь";
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
	КонецЕсли;
	Выборка = Запрос.Выполнить().Выбрать();
	ТаблицаМаршрутизации = Новый Массив;
	Пока Выборка.Следующий() Цикл
		СтруктураТаблицы = Новый Структура();
		СтруктураТаблицы.Вставить("НомерТелефона",		Выборка.НомерТелефона);
		СтруктураТаблицы.Вставить("Представление",		Выборка.Представление);
		СтруктураТаблицы.Вставить("ВнутреннийНомер",	Выборка.ВнутреннийНомер);
		ТаблицаМаршрутизации.Добавить(СтруктураТаблицы);
	КонецЦикла;	
	Возврат ТаблицаМаршрутизации;
КонецФункции // сфпПолучитьТаблицуМаршрутизации()

// Функция возвращает список маршрутизации
//
// Параметры:
//	Объект			- СправочникСсылка	- Владелец контактной информации
//	Пользователь	- СправочникСсылка	- Пользователь для переключения звонков
//
// Возвращаемое значение:
//	Строка	- Строка маршрутизации
//
Функция сфпСформироватьСписокМаршрутизации(СтараяТаблица, НоваяТаблица) Экспорт
	СписокСтрока = "";
	Для Каждого СтрокаТаблицы Из СтараяТаблица Цикл
		Если ПустаяСтрока(СтрокаТаблицы.ВнутреннийНомер) Тогда Продолжить; КонецЕсли;
		НайденнаяСтрока = Неопределено;
		Для Каждого СтрокаМассива Из НоваяТаблица Цикл
			Если СтрокаМассива.НомерТелефона = СтрокаТаблицы.НомерТелефона Тогда
				НайденнаяСтрока = СтрокаМассива;
				Прервать;	
			КонецЕсли;	
		КонецЦикла;	
		Если НайденнаяСтрока = Неопределено Тогда
			СтруктураНомера	= сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(СтрокаТаблицы.Представление);
			НомерТелефона	= СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
				+ СтруктураНомера.НомерТелефона;
			СписокСтрока	= СписокСтрока + СокрЛП(НомерТелефона) + "=" + ";";
		ИначеЕсли НЕ (СтрокаТаблицы.ВнутреннийНомер = НайденнаяСтрока.ВнутреннийНомер) Тогда
			СтруктураНомера	= сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(СтрокаТаблицы.Представление);
			НомерТелефона	= СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
				+ СтруктураНомера.НомерТелефона;
			СписокСтрока	= СписокСтрока + СокрЛП(НомерТелефона) + "=" + СокрЛП(НайденнаяСтрока.ВнутреннийНомер) + ";";
		КонецЕсли;	
	КонецЦикла;
	Для Каждого СтрокаТаблицы Из НоваяТаблица Цикл
		Если ПустаяСтрока(СтрокаТаблицы.ВнутреннийНомер) Тогда Продолжить; КонецЕсли;
		НайденнаяСтрока = Неопределено;
		Для Каждого СтрокаМассива Из СтараяТаблица Цикл
			Если СтрокаМассива.НомерТелефона = СтрокаТаблицы.НомерТелефона Тогда
				НайденнаяСтрока = СтрокаМассива;
				Прервать;	
			КонецЕсли;	
		КонецЦикла;	
		Если НайденнаяСтрока = Неопределено Тогда
			СтруктураНомера	= сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(СтрокаТаблицы.Представление);
			НомерТелефона	= СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
				+ СтруктураНомера.НомерТелефона;
			СписокСтрока = СписокСтрока + СокрЛП(НомерТелефона) + "=" + СокрЛП(СтрокаТаблицы.ВнутреннийНомер) + ";";
		ИначеЕсли НЕ (СтрокаТаблицы.ВнутреннийНомер = НайденнаяСтрока.ВнутреннийНомер) Тогда
			СтруктураНомера	= сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(СтрокаТаблицы.Представление);
			НомерТелефона	= СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
				+ СтруктураНомера.НомерТелефона;
			СписокСтрока = СписокСтрока + СокрЛП(НомерТелефона) + "=" + СокрЛП(СтрокаТаблицы.ВнутреннийНомер) + ";";
		КонецЕсли;	
	КонецЦикла;
	Возврат СписокСтрока;
КонецФункции // сфпПолучитьСписокМаршрутизации()

// Процедура изменяет таблицу маршрутизации в АТС
//
// Параметры:
//	Объект	- СправочникСсылка	- Объект, информацию о маршрутизации которого следует изменить
//
Процедура сфпЗаменитьМаршрутизациюВАТС(СписокМаршрутизации) Экспорт
	Если ПустаяСтрока(СписокМаршрутизации) Тогда Возврат; КонецЕсли;
	#Если ВебКлиент Тогда
		Состояние(НСтр("ru='Работа СофтФона невозможна в web-клиенте!';
			|en='Operation of SoftPhone is impossible in a web client!'"));
	#Иначе
		ВебСервис 		= Неопределено;
		АдресСервера	= Константы.сфпИмяСервера.Получить();
		ПортСервера		= Константы.сфпПорт.Получить();
		Попытка
			
			Прокси = Неопределено;
			Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
				МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
				Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("http");
			КонецЕсли;
	
			Определения	= Новый WSОпределения("http://" + АдресСервера + ":" + Формат(Число(ПортСервера), "ЧГ=0") 
				+ "/wsdl/ISOAPSoftPhoneSrv", , , Прокси, 60);
				
			ВебСервис	= Новый WSПрокси(Определения, "http://tempuri.org/",
				"ISOAPSoftPhoneSrvservice", "ISOAPSoftPhoneSrvPort", Прокси, 60);
			
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru='Подключение к серверу СофтФон';
				|en='Connecting to the Softphone server'"), УровеньЖурналаРегистрации.Предупреждение, , ,
				 НСтр("ru='Не удалось подключиться к серверу СофтФон'"));
		КонецПопытки;	
		Если ВебСервис = Неопределено Тогда Возврат; КонецЕсли;
		ИдентификаторМаршрутизации = сфпПолучитьИдентификаторМаршрутизации(Истина);
		Попытка
			ВебСервис.SendStrData("", 1, "", ИдентификаторМаршрутизации);
			ВебСервис.SendStrData("", 0, СписокМаршрутизации, ИдентификаторМаршрутизации);
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru='Запись таблицы маршрутизации';
				|en='Recording a routing table'"), УровеньЖурналаРегистрации.Предупреждение, , ,
				 НСтр("ru='Не удалось записать таблицу маршрутизации'"));
		КонецПопытки;	
	#КонецЕсли
КонецПроцедуры // сфпЗаменитьМаршрутизациюВАТС()

// Процедура изменяет таблицу маршрутизации в АТС
//
// Параметры:
//	Объект	- СправочникСсылка	- Объект, информацию о маршрутизации которого следует изменить
//
Процедура сфпИзменитьМаршрутизациюВАТС(СписокМаршрутизации) Экспорт
	Если ПустаяСтрока(СписокМаршрутизации) Тогда Возврат; КонецЕсли;
	#Если ВебКлиент Тогда
		Состояние(НСтр("ru='Работа СофтФона невозможна в web-клиенте!';
			|en='Operation of SoftPhone is impossible in a web client!'"));
	#Иначе
		ВебСервис 		= Неопределено;
		АдресСервера	= Константы.сфпИмяСервера.Получить();
		ПортСервера		= Константы.сфпПорт.Получить();
		Попытка
			
			Прокси = Неопределено;
			Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
				МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
				Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("http");
			КонецЕсли;
			
			Определения	= Новый WSОпределения("http://" + АдресСервера + ":" + Формат(Число(ПортСервера), "ЧГ=0") 
				+ "/wsdl/ISOAPSoftPhoneSrv", , , Прокси, 60);
			
			ВебСервис	= Новый WSПрокси(Определения, "http://tempuri.org/",
				"ISOAPSoftPhoneSrvservice", "ISOAPSoftPhoneSrvPort", Прокси, 60);
			
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru='Подключение к серверу СофтФон';
				|en='Connecting to the Softphone server'"), УровеньЖурналаРегистрации.Предупреждение, , ,
				 НСтр("ru='Не удалось подключиться к серверу СофтФон'"));
		КонецПопытки;
		
		Если ВебСервис = Неопределено Тогда Возврат; КонецЕсли;
		ИдентификаторМаршрутизации	= сфпПолучитьИдентификаторМаршрутизации(Ложь);
		Если ПустаяСтрока(ИдентификаторМаршрутизации) Тогда
			// Если таблица маршрутизации еще ни разу не передавалась на сервер СофтФон
			СтарыйНабор = Новый Массив;
			НовыйНабор	= сфпПолучитьТаблицуМаршрутизации();
			СписокМаршрутизации = сфпСформироватьСписокМаршрутизации(СтарыйНабор, НовыйНабор);
			сфпЗаменитьМаршрутизациюВАТС(СписокМаршрутизации);
		Иначе	
			Попытка
				ВебСервис.SendStrData("", 2, СписокМаршрутизации, ИдентификаторМаршрутизации);
			Исключение
				ЗаписьЖурналаРегистрации(НСтр("ru='Запись таблицы маршрутизации';
					|en='Recording a routing table'"), УровеньЖурналаРегистрации.Предупреждение, , ,
					 НСтр("ru='Не удалось записать таблицу маршрутизации'"));
			КонецПопытки;	
		КонецЕсли;	
	#КонецЕсли
КонецПроцедуры // сфпИзменитьМаршрутизациюВАТС()

// Функция определяет требуется ли обновитьтаблицу маршрутизации после записи телефонного звонка
//
// Параметры:
//	НовыйЗвонок	- ДокументСсылка	- Новый телефонный звонок
//
// Возвращаемое значение:
//	Булево	- Необходимость обновления таблицы маршрутизации
//
Процедура сфпОбновитьТаблицуМаршрутизации(НовыйЗвонок) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КоличествоВзаимодействий = Константы.сфпКоличествоВзаимодействийДляМаршрутизации.Получить();
	ИспользоватьМаршрутизацию = Константы.сфпИспользоватьМаршрутизацию.Получить();
	ИспользоватьВнутреннийНомерИзКИПользователя = Константы.сфпИспользоватьВнутреннийНомерИзКИПользователя.Получить();
	Если КоличествоВзаимодействий > 0 И ИспользоватьМаршрутизацию Тогда
		Контакт = НовыйЗвонок.АбонентКонтакт;
		Если ЗначениеЗаполнено(Контакт) Тогда
			Запрос = Новый Запрос("
			|ВЫБРАТЬ Ответственный, КоличествоЗвонков, ДатаПоследнегоЗвонка
			|ИЗ (ВЫБРАТЬ Ответственный, КОЛИЧЕСТВО(Ссылка) КАК КоличествоЗвонков, МАКСИМУМ(Дата) КАК ДатаПоследнегоЗвонка
			|    ИЗ Документ.ТелефонныйЗвонок
			|    ГДЕ АбонентКонтакт = &Контакт И НЕ ПометкаУдаления И Ответственный <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
			|    СГРУППИРОВАТЬ ПО Ответственный) КАК Подзапрос
			|ГДЕ КоличествоЗвонков >= &КоличествоВзаимодействий
			|УПОРЯДОЧИТЬ ПО КоличествоЗвонков УБЫВ, ДатаПоследнегоЗвонка УБЫВ");
			Запрос.УстановитьПараметр("Контакт", Контакт);
			Запрос.УстановитьПараметр("КоличествоВзаимодействий", КоличествоВзаимодействий);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Если Контакт.Метаданные().Реквизиты.Найти("сфпПользовательДляПереключенияЗвонков") <> Неопределено Тогда
					КонтактОбъект = Контакт.ПолучитьОбъект();
					Если КонтактОбъект.сфпПользовательДляПереключенияЗвонков <> Выборка.Ответственный Тогда
						КонтактОбъект.сфпПользовательДляПереключенияЗвонков = Выборка.Ответственный;
						КонтактОбъект.Записать();
					КонецЕсли;
					
				Иначе
					ПользовательДляПереключения	= сфпПолучитьПользователяДляПереключенияЗвонков(Контакт);
					Если ПользовательДляПереключения <> Выборка.Ответственный Тогда
						СтарыйНабор	= сфпПолучитьТаблицуМаршрутизации(Контакт, ПользовательДляПереключения);
						Если сфпЗаписатьНовогоПользователя(Контакт, Выборка.Ответственный,
							 ИспользоватьВнутреннийНомерИзКИПользователя) Тогда
							НовыйНабор = сфпПолучитьТаблицуМаршрутизации(Контакт, Выборка.Ответственный);
							СписокМаршрутизации = сфпСформироватьСписокМаршрутизации(СтарыйНабор, НовыйНабор);
							сфпИзменитьМаршрутизациюВАТС(СписокМаршрутизации);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // сфпОбновитьТаблицуМаршрутизации()	

/////////////////////////////////////////////////
// ЗАПИСЬ ТЕЛЕФОННЫХ ПЕРЕГОВОРОВ

// Функция проверяет использование записи переговоров
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Признак использования СофтФона
//
Функция сфпИспользоватьЗаписьПереговоров() Экспорт
	Возврат Константы.сфпИспользоватьЗаписьПереговоров.Получить();
КонецФункции // ИспользоватьЗаписьПереговоров(

// Функция проверяет использование Спрут7
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Признак использования СофтФона
//
Функция сфпИспользоватьСпрут7() Экспорт
	Возврат Константы.сфпИспользоватьСпрут7.Получить();
КонецФункции // ИспользоватьСпрут7()

// Функция проверяет использование CLON
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Признак использования СофтФона
//
Функция сфпИспользоватьCLON() Экспорт
	Возврат Константы.сфпИспользоватьCLON.Получить();
КонецФункции // ИспользоватьCLON()

// Функция возвращает массив пользователей, телефонные переговоры которых могут прослушиваться переданным пользователем
//
// Параметры:
//	Пользователь	- СправочникСсылка	- Пользователь, который контролирует телефонные переговоры
//
// Возвращаемое значение:
//	Массив	- Массив прослушиваемых пользователей
//
Функция сфпПолучитьМассивПрослушиваемыхПользователей(Пользователь) Экспорт
	МассивПользователей = Новый Массив;
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		Возврат МассивПользователей;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	СписокПользователейГрупп = Новый СписокЗначений;
	ИспользоватьГруппыПользователей	= сфпИспользоватьГруппыПользователей();	
	Если ИспользоватьГруппыПользователей Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		               |	сфпДоступныеДляПрослушиванияПользователи.КонтролируемыйПользователь КАК Группа
		               |ИЗ
		               |	РегистрСведений.сфпДоступныеДляПрослушиванияПользователи КАК сфпДоступныеДляПрослушиванияПользователи
		               |ГДЕ
		               |	сфпДоступныеДляПрослушиванияПользователи.Пользователь = &Пользователь
		               |	И сфпДоступныеДляПрослушиванияПользователи.ЭтоГруппа";
		СписокГрупп = Новый СписокЗначений;
		СписокГрупп.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Группа"));
		Если СписокГрупп.НайтиПоЗначению(Справочники.ГруппыПользователей.ВсеПользователи) = Неопределено Тогда
			Запрос.УстановитьПараметр("СписокГрупп", СписокГрупп);
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			               |	ГруппыПользователейСостав.Пользователь КАК Пользователь
			               |ИЗ
			               |	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
			               |ГДЕ
			               |	ГруппыПользователейСостав.Ссылка В ИЕРАРХИИ(&СписокГрупп)";
			СписокПользователейГрупп.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Пользователь"));
		Иначе	
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			               |	Пользователи.Ссылка КАК Пользователь
			               |ИЗ
			               |	Справочник.Пользователи КАК Пользователи";
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
			   МассивПользователей.Добавить(Выборка.Пользователь);
		   	КонецЦикла;
			Если МассивПользователей.Найти(Пользователь) = Неопределено Тогда
				МассивПользователей.Добавить(Пользователь);
			КонецЕсли;				
			Возврат МассивПользователей;	
		КонецЕсли;
	КонецЕсли;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	               |	сфпДоступныеДляПрослушиванияПользователи.КонтролируемыйПользователь КАК Пользователь
	               |ИЗ
	               |	РегистрСведений.сфпДоступныеДляПрослушиванияПользователи КАК сфпДоступныеДляПрослушиванияПользователи
	               |ГДЕ
	               |	сфпДоступныеДляПрослушиванияПользователи.Пользователь = &Пользователь
	               |	И НЕ сфпДоступныеДляПрослушиванияПользователи.ЭтоГруппа";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
	   МассивПользователей.Добавить(Выборка.Пользователь);
	КонецЦикла;
	Если ИспользоватьГруппыПользователей Тогда
		Для Каждого ПользовательГруппы Из СписокПользователейГрупп Цикл
			Если МассивПользователей.Найти(ПользовательГруппы.Значение) = Неопределено Тогда
				МассивПользователей.Добавить(ПользовательГруппы.Значение);
			КонецЕсли;	
		КонецЦикла;	                
	КонецЕсли;	
	Если МассивПользователей.Найти(Пользователь) = Неопределено Тогда
		МассивПользователей.Добавить(Пользователь);
	КонецЕсли;
	Возврат МассивПользователей;	
КонецФункции // сфпПолучитьМассивПрослушиваемыхПользователей()	

/////////////////////////////////////////////////
// МАСТЕР НАСТРОЙКИ IP-АТС AGAT UX

// Функция возвращает массив пользователей, для использования в мастере настроек
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Массив	- Массив пользователей
//
Функция сфпПолучитьМассивПользователей() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Пользователи.Наименование КАК Наименование
	               |ИЗ
	               |	Справочник.Пользователи КАК Пользователи
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Наименование";
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Наименование");			   
КонецФункции // сфпПолучитьМассивПользователей()

// Функция возвращает адрес хранилища файла настроек мастера настройки
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Строка	- Адрес хранилища мастера настроек
//
Функция сфпВернутьХранилищеФайлаНастройкиМастера() Экспорт
	Макет = ПолучитьОбщийМакет("сфпФайлНастроекМастераAgatUX");
	Адрес = ПоместитьВоВременноеХранилище(Макет);
	Возврат Адрес;
КонецФункции // сфпВернутьХранилищеФайлаНастройкиМастера()

// Функция возвращает адрес хранилища мастера настроек
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Строка	- Адрес хранилища мастера настроек
//
Функция сфпВернутьХранилищеМастераНастройки() Экспорт
	Макет = ПолучитьОбщийМакет("сфпУстановщикМастераНастройкиAgatUX");
	Адрес = ПоместитьВоВременноеХранилище(Макет);
	Возврат Адрес;
КонецФункции // сфпВернутьХранилищеМастераНастройки()

/////////////////////////////////////////////////
// СИСТЕМА ЗАПИСИ ТЕЛЕФОННЫХ ПЕРЕГОВОРОВ CLON

// Функция убирает из IP-адреса пробелы и незначащие нули
//
// Параметры:
//	IPАдрес	- Строка IP-адреса
//
// Возвращаемое значение:
//	Строка	- Строка IP-адреса после очистки
//
Функция ОчиститьIPАдрес(IPАдрес)
	Попытка
		СтрокаIP		= IPАдрес;
		ПозицияТочки	= СтрНайти(СтрокаIP, ".");
		Строка1IP		= Лев(СтрокаIP, ПозицияТочки - 1);
		СтрокаIP		= Сред(СтрокаIP, ПозицияТочки + 1);
		ПозицияТочки	= СтрНайти(СтрокаIP, ".");
		Строка2IP		= Лев(СтрокаIP, ПозицияТочки - 1);
		СтрокаIP		= Сред(СтрокаIP, ПозицияТочки + 1);
		ПозицияТочки	= СтрНайти(СтрокаIP, ".");
		Строка3IP		= Лев(СтрокаIP, ПозицияТочки - 1);
		Строка4IP		= Сред(СтрокаIP, ПозицияТочки + 1);
		CLONServerIP 	= Строка(Число(СокрЛП(Строка1IP))) + "." + Строка(Число(СокрЛП(Строка2IP))) + "."
			+ Строка(Число(СокрЛП(Строка3IP))) + "." + Строка(Число(СокрЛП(Строка4IP)));
	Исключение
		CLONServerIP	= IPАдрес;
	КонецПопытки;
	Возврат CLONServerIP;
КонецФункции // ОчиститьIPАдрес()

// Функция возвращает структуру настроек для подключения к серверу CLON
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Структура	- Структура настроек
//
Функция сфпПолучитьНастройкиCLON() Экспорт
	НастройкиCLON = Новый Структура;
	CLONServerIP = ОчиститьIPАдрес(Константы.сфпCLONServerIP.Получить());
	НастройкиCLON.Вставить("CLONServerIP",			CLONServerIP);
	НастройкиCLON.Вставить("CLONServerPort",		Константы.сфпCLONServerPort.Получить());
	CLONType = Константы.сфпCLONType.Получить();
	Если CLONType = Перечисления.сфпCLONType.E1 Тогда
		НастройкиCLON.Вставить("CLONType",	0);
	ИначеЕсли CLONType = Перечисления.сфпCLONType.BRI Тогда
		НастройкиCLON.Вставить("CLONType",	2);
	ИначеЕсли CLONType = Перечисления.сфпCLONType.IP Тогда
		НастройкиCLON.Вставить("CLONType",	3);
	Иначе	
		НастройкиCLON.Вставить("CLONType",	1);
	КонецЕсли;	
	НастройкиCLON.Вставить("CLONServerLogin",		СокрЛП(Константы.сфпCLONServerLogin.Получить()));
	НастройкиCLON.Вставить("CLONServerPassword",	СокрЛП(Константы.сфпCLONServerPassword.Получить()));
	Возврат НастройкиCLON;
КонецФункции // сфпПолучитьНастройкиCLON()

/////////////////////////////////////////////////
// ОБЩИЕ ТЕЛЕФОННЫЕ КНИГИ СЕРВЕРА СОФТФОН

// Функция проверяет заполнение констант настроек сервера СофтФон
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Заполнение настроек
//
Функция сфпЗаполненыНастройкиСервера()
	ЕстьОшибки = Ложь;	
	Если ПустаяСтрока(Константы.сфпИмяСервера.Получить()) Тогда
		ЕстьОшибки = Истина;	
	КонецЕсли;	
	Если ПустаяСтрока(Константы.сфпПорт.Получить()) Тогда
		ЕстьОшибки = Истина;	
	КонецЕсли;
	Возврат НЕ ЕстьОшибки;
КонецФункции // сфпЗаполненыНастройкиСервера()	

// Функция возвращает расписание регламентного задания
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	РасписаниеРегламентногоЗадания	- Расписание регламентного задания
//
Функция сфпПолучитьРасписаниеРегламентноеЗадание() Экспорт
	РегламентноеЗадание = сфпПолучитьРегламентноеЗаданиеОбновленияТелефонныхКниг();
	Возврат РегламентноеЗадание.Расписание;
КонецФункции // сфпПолучитьРасписаниеРегламентноеЗадание();

// Функция возвращает регламентное задание ПроверкаСМС
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	РегламентноеЗадание	- Регламентное задание обновления телефонных книг
//
Функция сфпПолучитьРегламентноеЗаданиеОбновленияТелефонныхКниг() Экспорт
    Возврат РегламентныеЗадания.НайтиПредопределенное("сфпОбновлениеТелефонныхКниг");
КонецФункции // сфпПолучитьРегламентноеЗаданиеОбновленияТелефонныхКниг()

// Функция возвращает строку с описанием номера для дерева телефонной книги
//
// Параметры:
//	Контакт			- СправочникСсылка	- Контакт
//	ВидТелефона		- СправочникСсылка	- Вид телефонного номера
//	ПорядковыйНомер	- Число				- Порядковый номер телефона
//
Функция сфпПолучитьПредставлениеНомера(Контакт, ВидТелефона, ПорядковыйНомер) Экспорт
	ПредставлениеНомера = "";
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект",				Контакт);
	Запрос.УстановитьПараметр("Вид",				ВидТелефона);
	Запрос.УстановитьПараметр("ПорядковыйНомер",	ПорядковыйНомер);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	сфпНомераТелефоновДляПоиска.Представление КАК Представление
	               |ИЗ
	               |	РегистрСведений.сфпНомераТелефоновДляПоиска КАК сфпНомераТелефоновДляПоиска
	               |ГДЕ
	               |	сфпНомераТелефоновДляПоиска.Объект = &Объект
	               |	И сфпНомераТелефоновДляПоиска.Вид = &Вид";
	               //|	И сфпНомераТелефоновДляПоиска.ПорядковыйНомер = &ПорядковыйНомер";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПредставлениеНомера = Выборка.Представление;
	КонецЕсли;	
	Возврат ПредставлениеНомера;
КонецФункции // сфпПолучитьПредставлениеНомера()	

// Функция возвращает массив всех пользователей
//
// Параметры:
//	МассивВидов	- Массив	- Массив используемых  видов телефонов
//
// Возвращаемое значение:
//	Массив	- Массив всех пользователей
//
Функция сфпПолучитьМассивВсехПользователей(МассивВидов) Экспорт
	
	МассивВсехПользователей = Новый Массив;
	
	СписокВидов = Новый СписокЗначений;
	СписокВидов.ЗагрузитьЗначения(МассивВидов);
	
	Если Метаданные.Справочники.Пользователи.Реквизиты.Найти("ТекущееПодразделение") = Неопределено Тогда
		  ПолеПодразделение = ", """" КАК Подразделение";
	Иначе ПолеПодразделение = ", ТекущееПодразделение КАК Подразделение";
	КонецЕсли;				   
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Пользователи.Объект КАК Объект,
	|	Пользователи.Наименование КАК Наименование,
	|	Пользователи.Подразделение КАК Подразделение,
	|	ЕСТЬNULL(Телефоны.Вид, ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ПустаяСсылка)) КАК Вид,
	|	ЕСТЬNULL(Телефоны.НаименованиеВида, """") КАК НаименованиеВида,
	|	ЕСТЬNULL(Телефоны.ПорядковыйНомер, 0) КАК ПорядковыйНомер,
	|	ЕСТЬNULL(Телефоны.Представление, """") КАК Представление,
	|	ЕСТЬNULL(Настройки.Значение, """") КАК ВнутреннийНомер
	|ИЗ
	|	(ВЫБРАТЬ Ссылка КАК Объект, Наименование" + ПолеПодразделение 
		+ " ИЗ Справочник.Пользователи ГДЕ НЕ ПометкаУдаления И НЕ Недействителен) КАК Пользователи
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ Объект, Вид, ПорядковыйНомер, Представление, Вид.Наименование КАК НаименованиеВида
	|					  ИЗ РегистрСведений.сфпНомераТелефоновДляПоиска
	|					  ГДЕ Вид В(&СписокВидов)) КАК Телефоны
	|	ПО Пользователи.Объект = Телефоны.Объект
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ Пользователь, Значение
	|					  ИЗ РегистрСведений.CRM_НастройкиПользователей
	|					  ГДЕ Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.CRM_НастройкиПользователей.сфпТекущийВнутреннийНомер)) КАК Настройки
	|	ПО Пользователи.Объект = Настройки.Пользователь
	|
	|УПОРЯДОЧИТЬ ПО
	|	Пользователи.Наименование,
	|	ЕСТЬNULL(Телефоны.Вид, ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ПустаяСсылка)),
	|	ЕСТЬNULL(Телефоны.ПорядковыйНомер, 0)");
	Запрос.УстановитьПараметр("СписокВидов", СписокВидов);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураПользователя = Новый Структура("Объект,Наименование,ВнутреннийНомер,Подразделение,Вид,
			|НаименованиеВида,ПорядковыйНомер,
			|Представление");
		ЗаполнитьЗначенияСвойств(СтруктураПользователя, Выборка);
		МассивВсехПользователей.Добавить(СтруктураПользователя);
	КонецЦикла;
	
	Возврат МассивВсехПользователей;

КонецФункции // ПолучитьМассивВсехПользователей()	

// Процедура проверяет заполнение структуры строки телефонной книги и добавляет ее в массив строк
//
// Параметры:
//	ЭлементМассива			- Структура	- Структура строки телефонной книги
//	МассивТелефоннойКниги	- Массив	- Массив строк телефонной книги
//	Родитель				- Структура	- Строка-родитель текущей строки телефонной книги
//
Процедура сфпДобавитьВМассивТелефоннойКниги(ЭлементМассива, МассивТелефоннойКниги, Родитель)
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ЭлементМассива.Объект))
		 И НЕ ОбщегоНазначения.СсылкаСуществует(ЭлементМассива.Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭлементМассива.Объект) Тогда
		Если ЭлементМассива.Картинка = 2 Тогда
			Если ТипЗнч(ЭлементМассива.Объект) = Тип("СправочникСсылка.Пользователи") Тогда
				ВнутреннийНомер = сфпТекущийВнутреннийНомер(ЭлементМассива.Объект);
				Если ПустаяСтрока(ВнутреннийНомер) Тогда
					Возврат;
				КонецЕсли;
				
				ЭлементМассива.Наименование = НСтр("ru='Внутренний номер: ';en='Internal number: '") + ВнутреннийНомер;
				
			Иначе
				ПредставлениеНомера = сфпПолучитьПредставлениеНомера(Родитель.Объект, ЭлементМассива.Объект,
					 ЭлементМассива.ПорядковыйНомер);
				Если ПустаяСтрока(ПредставлениеНомера) Тогда
					Возврат;
				КонецЕсли;
				
				ЭлементМассива.Наименование = ЭлементМассива.Объект.Наименование + ": " + ПредставлениеНомера;
			КонецЕсли;
			
			МассивТелефоннойКниги.Добавить(ЭлементМассива);
			
		Иначе
			Если ЭлементМассива.Объект.ПометкаУдаления Тогда
				Возврат;
			КонецЕсли;
			
			ЭлементМассива.Наименование = ЭлементМассива.Объект.Наименование;
			
			ПодчиненныеСтроки = ЭлементМассива.Строки;
			ЭлементМассива.Строки = Новый Массив();
			Для Каждого ПодчиненныйЭлемент Из ПодчиненныеСтроки Цикл
				сфпДобавитьВМассивТелефоннойКниги(ПодчиненныйЭлемент, ЭлементМассива.Строки, ЭлементМассива);
			КонецЦикла;
			
			Если ЭлементМассива.Строки.Количество() > 0 Тогда
				МассивТелефоннойКниги.Добавить(ЭлементМассива);
			КонецЕсли;	
		КонецЕсли;
		
	Иначе
		ПодчиненныеСтроки = ЭлементМассива.Строки;
		ЭлементМассива.Строки = Новый Массив();
		Для Каждого ПодчиненныйЭлемент Из ПодчиненныеСтроки Цикл
			сфпДобавитьВМассивТелефоннойКниги(ПодчиненныйЭлемент, ЭлементМассива.Строки, ЭлементМассива);
		КонецЦикла;
		
		Если ЭлементМассива.Строки.Количество() > 0 Тогда
			МассивТелефоннойКниги.Добавить(ЭлементМассива);
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры // сфпДобавитьВМассивТелефоннойКниги()	

// Функция обновляет данные телефонной книги и возвращает ее структуру
//
// Параметры:
//	ТелефоннаяКнига	- СправочникСсылка	- Телефонная книга
//
Функция сфпОбновитьТелефоннуюКнигу(ТелефоннаяКнига)
	
	СтруктураКниги	= Новый Структура();
	СтруктураКниги.Вставить("Идентификатор", ТелефоннаяКнига.Идентификатор);
	СтруктураКниги.Вставить("ИмяКниги", ТелефоннаяКнига.Наименование);
	СтруктураКниги.Вставить("Порядок", ТелефоннаяКнига.РеквизитДопУпорядочивания);
	Если ТелефоннаяКнига.ПометкаУдаления Тогда
		СтруктураКниги.Вставить("ВидыТелефонов", Новый Массив());
		СтруктураКниги.Вставить("МассивКниги", Новый Массив());
		Возврат СтруктураКниги;
	КонецЕсли;	
	
	ТелефоннаяКнигаОбъект = ТелефоннаяКнига.ПолучитьОбъект();
	
	МассивВидовТелефонов = Новый Массив();
	МассивВидов = Новый Массив();
	Для Каждого СтрокаВида Из ТелефоннаяКнигаОбъект.ВидыТелефонов Цикл
		Если НЕ СтрокаВида.Пометка Тогда
			Продолжить;
			
		ИначеЕсли СтрокаВида.Вид = НСтр("ru='Внутренний номер';en='Internal number'") Тогда 	
			СтруктураВида = Новый Структура();
			СтруктураВида.Вставить("Идентификатор", СтрокаВида.Идентификатор);
			СтруктураВида.Вставить("Наименование", СтрокаВида.Наименование);
			СтруктураВида.Вставить("Псевдоним", СтрокаВида.Псевдоним);
			МассивВидовТелефонов.Добавить(СтруктураВида);
			МассивВидов.Добавить(СтрокаВида.Вид);
			
		ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаВида.Вид) Тогда
			СтрокаВида.Пометка = Ложь;
			
		ИначеЕсли НЕ ОбщегоНазначения.СсылкаСуществует(СтрокаВида.Вид) Тогда
			СтрокаВида.Вид = Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
			СтрокаВида.Пометка = Ложь;
			
		ИначеЕсли СтрокаВида.Вид.ПометкаУдаления Тогда
			СтрокаВида.Вид = Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
			СтрокаВида.Пометка = Ложь;
			
		Иначе
			СтруктураВида = Новый Структура();
			СтруктураВида.Вставить("Идентификатор",	СтрокаВида.Идентификатор);
			СтруктураВида.Вставить("Наименование", СтрокаВида.Наименование);
			СтруктураВида.Вставить("Псевдоним", СтрокаВида.Псевдоним);
			СтруктураВида.Вставить("Вид", СтрокаВида.Вид);
			
			МассивВидовТелефонов.Добавить(СтруктураВида);
			МассивВидов.Добавить(СтрокаВида.Вид);
		КонецЕсли;	
	КонецЦикла;
	
	//МассивВсехПользователей	= сфпПолучитьМассивВсехПользователей(МассивВидов);
	
	МассивКниги	= ТелефоннаяКнигаОбъект.МассивКниги.Получить();
	Если МассивКниги = Неопределено Тогда
		МассивКниги	= Новый Массив();
	КонецЕсли;	
	
	//МассивТелефоннойКниги = Новый Массив();
	// Для Каждого ЭлементМассива Из МассивКниги Цикл
	//	сфпДобавитьВМассивТелефоннойКниги(ЭлементМассива, МассивТелефоннойКниги, Неопределено);
	//КонецЦикла;	
	
	МассивТиповОбъектаКниги = Новый Массив();
	МассивТиповОбъектаКниги.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
	МассивТиповОбъектаКниги.Добавить(Тип("СправочникСсылка.Пользователи"));
	МассивТиповОбъектаКниги.Добавить(Тип("СправочникСсылка.ГруппыПользователей"));
	МассивТиповОбъектаКниги.Добавить(Тип("СправочникСсылка.ВидыКонтактнойИнформации"));
	
	ДеревоКниги = Новый ДеревоЗначений();
	ДеревоКниги.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("УникальныйИдентификатор"));
	ДеревоКниги.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ДеревоКниги.Колонки.Добавить("ЭтоГруппа", Новый ОписаниеТипов("Булево"));
	ДеревоКниги.Колонки.Добавить("Картинка", Новый ОписаниеТипов("Число"));
	ДеревоКниги.Колонки.Добавить("ПорядковыйНомер", Новый ОписаниеТипов("Число"));
	ДеревоКниги.Колонки.Добавить("ТипОбъекта", Новый ОписаниеТипов("Строка"));
	ДеревоКниги.Колонки.Добавить("Объект", Новый ОписаниеТипов(МассивТиповОбъектаКниги));

	ОбъектыКниги = Новый Массив();
	
	Для Каждого ЭлементМассива Из МассивКниги Цикл
		ДобавитьСтрокуСтруктурыВТелефоннуюКнигу(ЭлементМассива, ДеревоКниги, ТелефоннаяКнигаОбъект, ОбъектыКниги);
	КонецЦикла;
	
	СтруктураКниги.Вставить("Объекты", ОбъектыКниги);
	СтруктураКниги.Вставить("ВидыТелефонов", МассивВидовТелефонов);
	СтруктураКниги.Вставить("МассивКниги", МассивКниги);
	
	Возврат СтруктураКниги;

КонецФункции // сфпОбновитьТелефоннуюКнигу()	

Процедура ДобавитьСтрокуСтруктурыВТелефоннуюКнигу(СтрокаКниги, СтрокаДерева, КнигаОбъект, ОбъектыКниги)
	
	СтрокиДерева = СтрокаДерева.Строки;
	
	Если ЗначениеЗаполнено(СтрокаКниги.Объект) И НЕ СтрокаКниги.ЭтоГруппа Тогда
		Если НЕ ОбщегоНазначения.СсылкаСуществует(СтрокаКниги.Объект) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(СтрокаКниги.Объект) Тогда
		Если ТипЗнч(СтрокаКниги.Объект) = Тип("СправочникСсылка.Пользователи") ИЛИ
			 ТипЗнч(СтрокаКниги.Объект) = Тип("СправочникСсылка.ГруппыПользователей") ИЛИ
			 ТипЗнч(СтрокаКниги.Объект) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			ОбъектыКниги.Добавить(СтрокаКниги.Объект);
		КонецЕсли;
	
		Если СтрокаКниги.Картинка = 2 Тогда
			Если ТипЗнч(СтрокаКниги.Объект) = Тип("СправочникСсылка.Пользователи") Тогда
				ТекущийВнутреннийНомер = сфпСофтФонПроСервер.сфпТекущийВнутреннийНомер(СтрокаКниги.Объект);
				Если ПустаяСтрока(ТекущийВнутреннийНомер) Тогда
					Возврат;
				КонецЕсли;
				
				СтрокаГруппы = СтрокиДерева.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаГруппы, СтрокаКниги);
				СтрокаГруппы.Наименование = НСтр("ru='Внутренний номер: ';en='Internal number: '") + ТекущийВнутреннийНомер;
				
			Иначе
				НайденныеСтроки = КнигаОбъект.ВидыТелефонов.НайтиСтроки(Новый Структура("Вид", СтрокаКниги.Объект));
				Если НайденныеСтроки.Количество() = 0 Тогда
					Возврат;
				КонецЕсли;
				
				ПредставлениеНомера = сфпСофтФонПроСервер.сфпПолучитьПредставлениеНомера(СтрокаДерева.Объект,
					 СтрокаКниги.Объект,
					 СтрокаКниги.ПорядковыйНомер);
				Если ПустаяСтрока(ПредставлениеНомера) Тогда
					Возврат;
				КонецЕсли;
				
				СтрокаГруппы = СтрокиДерева.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаГруппы, СтрокаКниги);
				СтрокаГруппы.Наименование = НайденныеСтроки[0].Наименование + ": " + ПредставлениеНомера;
			КонецЕсли;

		Иначе
			Если сфпСофтФонПроСервер.сфпПолучитьЗначениеРеквизита(СтрокаКниги.Объект, "ПометкаУдаления") Тогда
				Возврат;
			КонецЕсли;	
			
			СтрокаГруппы = СтрокиДерева.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаГруппы, СтрокаКниги);
			СтрокаГруппы.Наименование = сфпСофтФонПроСервер.сфпПолучитьЗначениеРеквизита(СтрокаКниги.Объект, "Наименование");
			
			Для Каждого ПодчиненныйЭлемент Из СтрокаКниги.Строки Цикл
				ДобавитьСтрокуСтруктурыВТелефоннуюКнигу(ПодчиненныйЭлемент, СтрокаГруппы, КнигаОбъект, ОбъектыКниги);
			КонецЦикла;
		КонецЕсли;

	Иначе
		СтрокаГруппы = СтрокиДерева.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаГруппы, СтрокаКниги);
		
		Для Каждого ПодчиненныйЭлемент Из СтрокаКниги.Строки Цикл
			ДобавитьСтрокуСтруктурыВТелефоннуюКнигу(ПодчиненныйЭлемент, СтрокаГруппы, КнигаОбъект, ОбъектыКниги);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры // ДобавитьСтрокуСтруктурыВТелефоннуюКнигу()

Функция СоставПользователейГруппы(Группа)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ Пользователь
	|ИЗ Справочник.ГруппыПользователей.Состав
	|ГДЕ Ссылка В ИЕРАРХИИ (&Объект) И НЕ Ссылка.ПометкаУдаления И НЕ Пользователь.ПометкаУдаления И НЕ Пользователь.Недействителен");
	Запрос.УстановитьПараметр("Объект", Группа);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция СоставПользователейПодразделения(Подразделение)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ Ссылка КАК Пользователь
	|ИЗ Справочник.Пользователи
	|ГДЕ НЕ Недействителен И Подразделение В ИЕРАРХИИ (&Объект) И НЕ ПометкаУдаления");
	Запрос.УстановитьПараметр("Объект", Подразделение);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции	

// Процедура записывает описание контакта телефонной книги
//
// Параметры:
//	XMLОписание			- ЗаписьXML	- Описание телефонной книги
//	СтруктураКонтакта	- Структура	- Структура контакта
//
Процедура сфпЗаписатьОписаниеКонтакта(XMLОписание, СтруктураКонтакта, ВидыТелефонов, ТаблицаНомеров)
	
	Если СтруктураКонтакта.ЭтоГруппа Тогда
		XMLОписание.ЗаписатьНачалоЭлемента("group");
		XMLОписание.ЗаписатьАтрибут("id", XMLСтрока(СтруктураКонтакта.Идентификатор));
		XMLОписание.ЗаписатьАтрибут("name", XMLСтрока(СтруктураКонтакта.Наименование));
		XMLОписание.ЗаписатьАтрибут("deleted", XMLСтрока(0));
		
		Для Каждого СтрокаСтруктурыГруппы Из СтруктураКонтакта.Строки Цикл
			сфпЗаписатьОписаниеКонтакта(XMLОписание, СтрокаСтруктурыГруппы, ВидыТелефонов, ТаблицаНомеров);
		КонецЦикла;
		
		XMLОписание.ЗаписатьКонецЭлемента();

	Иначе
		Если ТипЗнч(СтруктураКонтакта.Объект) = Тип("СправочникСсылка.ГруппыПользователей") ИЛИ
			 ТипЗнч(СтруктураКонтакта.Объект) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			XMLОписание.ЗаписатьНачалоЭлемента("group");
			XMLОписание.ЗаписатьАтрибут("id", XMLСтрока(СтруктураКонтакта.Идентификатор));
			XMLОписание.ЗаписатьАтрибут("name", XMLСтрока(СтруктураКонтакта.Наименование));
			XMLОписание.ЗаписатьАтрибут("deleted", XMLСтрока(0));

			Если ТипЗнч(СтруктураКонтакта.Объект) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
				  ВыборкаСостава = СоставПользователейГруппы(СтруктураКонтакта.Объект);
			Иначе ВыборкаСостава = СоставПользователейПодразделения(СтруктураКонтакта.Объект);
			КонецЕсли;	

			Пока ВыборкаСостава.Следующий() Цикл
				Если ЗначениеЗаполнено(ВыборкаСостава.Пользователь) Тогда
					сфпВыбратьНомераКонтакта(XMLОписание, ТаблицаНомеров, ВидыТелефонов, ВыборкаСостава.Пользователь);
				КонецЕсли;
			КонецЦикла;

			XMLОписание.ЗаписатьКонецЭлемента();

		ИначеЕсли ТипЗнч(СтруктураКонтакта.Объект) = Тип("СправочникСсылка.Пользователи") Тогда
			сфпВыбратьНомераКонтакта(XMLОписание, ТаблицаНомеров, ВидыТелефонов, СтруктураКонтакта);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // сфпЗаписатьОписаниеКонтакта()

// Функция формирует XML-строку описания телефонной книги
//
// Параметры:
//	СтруктураКниги	- Структура	- Структура телефонной книги
//	УдалениеКниги	- Булево	- Признак формирования сокращенной структуры книги для ее удаления
//		
// Возвращаемое значение:
//	Строка	- XML-строка описания телефонной книги
//
Функция сфпСформироватьОписаниеТелефоннойКниги(СтруктураКниги, УдалениеКниги = Ложь) Экспорт
	
	// Версия книги - 2
	
	XMLОписание = Новый ЗаписьXML();
	XMLОписание.УстановитьСтроку();
	XMLОписание.ЗаписатьОбъявлениеXML();
	XMLОписание.ЗаписатьНачалоЭлемента("addressbook");
	XMLОписание.ЗаписатьАтрибут("id", XMLСтрока(СтруктураКниги.Идентификатор));
	XMLОписание.ЗаписатьАтрибут("name", XMLСтрока(СтруктураКниги.ИмяКниги));
	XMLОписание.ЗаписатьАтрибут("type", XMLСтрока(0));
	XMLОписание.ЗаписатьАтрибут("login", XMLСтрока(""));
	XMLОписание.ЗаписатьАтрибут("pass", XMLСтрока(""));
	XMLОписание.ЗаписатьАтрибут("timestamp", XMLСтрока(""));
	XMLОписание.ЗаписатьАтрибут("fullreload", XMLСтрока(1));
	
	ПорядокКниги = ?(СтруктураКниги.Свойство("Порядок"), СтруктураКниги.Порядок, 0);
	XMLОписание.ЗаписатьАтрибут("order", XMLСтрока(ПорядокКниги));
	
	Если УдалениеКниги Тогда
		XMLОписание.ЗаписатьАтрибут("deleted", XMLСтрока(1));

	Иначе
		XMLОписание.ЗаписатьАтрибут("deleted", XMLСтрока(0));
		
		ВидыКИ = Новый Массив();
		
		XMLОписание.ЗаписатьНачалоЭлемента("infotype");
		Для Каждого СтрокаВида Из СтруктураКниги.ВидыТелефонов Цикл
			XMLОписание.ЗаписатьНачалоЭлемента("info");
			XMLОписание.ЗаписатьАтрибут("infoid", XMLСтрока(СтрокаВида.Идентификатор));
			XMLОписание.ЗаписатьАтрибут("name", XMLСтрока(СтрокаВида.Наименование));
			XMLОписание.ЗаписатьАтрибут("shortname", XMLСтрока(СтрокаВида.Псевдоним));
			XMLОписание.ЗаписатьАтрибут("type", XMLСтрока(0));
			XMLОписание.ЗаписатьКонецЭлемента();
			
			ВидыКИ.Добавить(СтрокаВида.Вид);
		КонецЦикла;
		XMLОписание.ЗаписатьКонецЭлемента();
		
		// BSLLS:LogicalOrInTheWhereSectionOfQuery-off
		// Справочник из нескольких записей.
		Запрос = Новый Запрос("
		|ВЫБРАТЬ Объект, Вид, НомерТелефона
		|ИЗ РегистрСведений.сфпНомераТелефоновДляПоиска
		|ГДЕ Объект В (ВЫБРАТЬ Ссылка
		|			   ИЗ Справочник.Пользователи
		|			   ГДЕ НЕ Недействителен И (Подразделение В ИЕРАРХИИ (&Объекты) ИЛИ Ссылка В (&Объекты)) И НЕ ПометкаУдаления
		|
		|			   ОБЪЕДИНИТЬ ВСЕ
		|
		|			   ВЫБРАТЬ Пользователь
		|			   ИЗ Справочник.ГруппыПользователей.Состав
		|			   ГДЕ Ссылка В ИЕРАРХИИ (&Объекты) И НЕ Ссылка.ПометкаУдаления И НЕ Пользователь.ПометкаУдаления И НЕ Пользователь.Недействителен)
		|	 И Вид В (&Виды)");
		// BSLLS:LogicalOrInTheWhereSectionOfQuery-on
		Запрос.УстановитьПараметр("Виды", ВидыКИ);
		Запрос.УстановитьПараметр("Объекты", СтруктураКниги.Объекты);
		
		ТаблицаНомеров = Запрос.Выполнить().Выгрузить();
		
		XMLОписание.ЗаписатьНачалоЭлемента("group");
		XMLОписание.ЗаписатьАтрибут("id", XMLСтрока(СтруктураКниги.Идентификатор));
		XMLОписание.ЗаписатьАтрибут("name", XMLСтрока(СтруктураКниги.ИмяКниги));
		XMLОписание.ЗаписатьАтрибут("deleted", XMLСтрока(0));
		// Контакты
		Для Каждого ЭлементМассива Из СтруктураКниги.МассивКниги Цикл
			сфпЗаписатьОписаниеКонтакта(XMLОписание, ЭлементМассива, СтруктураКниги.ВидыТелефонов, ТаблицаНомеров);
		КонецЦикла;
		
		XMLОписание.ЗаписатьКонецЭлемента();
	КонецЕсли;
	
	XMLОписание.ЗаписатьКонецЭлемента();
	
	Возврат XMLОписание.Закрыть();
	
КонецФункции // сфпСформироватьОписаниеТелефоннойКниги()

Процедура сфпВыбратьНомераКонтакта(XMLОписание, ТаблицаНомеров, ВидыТелефонов, Контакт)
	
	Если ТипЗнч(Контакт) = Тип("Структура") Тогда
		ДанныеКонтакта = Контакт;
		
	Иначе
		ДанныеКонтакта = Новый Структура();
		ДанныеКонтакта.Вставить("Идентификатор", Контакт.УникальныйИдентификатор());
		ДанныеКонтакта.Вставить("Наименование", "" + Контакт);
		ДанныеКонтакта.Вставить("Объект", Контакт);
	КонецЕсли;	
	
	МассивНомеров = Новый Массив();
	
	Для Каждого СтрокаВида Из ВидыТелефонов Цикл
		СтруктураПоиска = Новый Структура("Объект,Вид", ДанныеКонтакта.Объект, СтрокаВида.Вид);
		НайденныеСтроки = ТаблицаНомеров.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 И ЗначениеЗаполнено(НайденныеСтроки[0].НомерТелефона) Тогда
			СтруктураНомера = Новый Структура("Идентификатор,Наименование", СтрокаВида.Идентификатор,
				 НайденныеСтроки[0].НомерТелефона);
			МассивНомеров.Добавить(СтруктураНомера);
		КонецЕсли;
	КонецЦикла;	
	
	Если МассивНомеров.Количество() > 0 Тогда
		XMLОписание.ЗаписатьНачалоЭлемента("contact");
		XMLОписание.ЗаписатьАтрибут("id", XMLСтрока(ДанныеКонтакта.Идентификатор));
		XMLОписание.ЗаписатьАтрибут("name", XMLСтрока(ДанныеКонтакта.Наименование));
		XMLОписание.ЗаписатьАтрибут("extcode", XMLСтрока(""));
		XMLОписание.ЗаписатьАтрибут("timestamp", XMLСтрока(""));
		XMLОписание.ЗаписатьАтрибут("deleted", XMLСтрока(0));
		
		Для Каждого ЭлементСписка Из МассивНомеров Цикл
			XMLОписание.ЗаписатьНачалоЭлемента("item");
			XMLОписание.ЗаписатьАтрибут("infoid", XMLСтрока(ЭлементСписка.Идентификатор));
			XMLОписание.ЗаписатьАтрибут("value", XMLСтрока(ЭлементСписка.Наименование));
			XMLОписание.ЗаписатьКонецЭлемента();
		КонецЦикла;
		
		АватарКонтакта = сфпПолучитьАватарКонтакта(ДанныеКонтакта.Объект, Ложь);
		Если НЕ ПустаяСтрока(АватарКонтакта) Тогда
			XMLОписание.ЗаписатьНачалоЭлемента("image");
			XMLОписание.ЗаписатьТекст(XMLСтрока(СтрЗаменить(АватарКонтакта, Символы.ПС, "")));
			XMLОписание.ЗаписатьКонецЭлемента();
		КонецЕсли;
		
		XMLОписание.ЗаписатьКонецЭлемента();
	КонецЕсли;

КонецПроцедуры	//	сфпВыбратьНомераКонтакта()
	
// Процедура - обработчик регламентного задания по обновлению общих телефонных книг
//
// Параметры:
//	Нет.
//
Процедура сфпОбновлениеТелефонныхКниг() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.сфпОбновлениеТелефонныхКниг);

	Если НЕ сфпИспользоватьСофтФон() Тогда
		Возврат;
	ИначеЕсли НЕ сфпИспользоватьРегламентноеЗаданиеТелефонныхКниг() Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ сфпЗаполненыНастройкиСервера() Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru='Подключение к серверу СофтФон';
			|en='Connecting to the Softphone server'"), УровеньЖурналаРегистрации.Предупреждение, , ,
			 НСтр("ru='Не заполнены настройки подключения к веб-сервису сервера СофтФон'"));
		Возврат;
	КонецЕсли;
	
	ВебСервис = Неопределено;
	АдресСервера = Константы.сфпИмяСервера.Получить();
	ПортСервера = Константы.сфпПорт.Получить();
	
	Попытка
		
		Прокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		
		Определения	= Новый WSОпределения("http://" + АдресСервера + ":" + Формат(Число(ПортСервера), "ЧГ=0") 
			+ "/wsdl/ISOAPSoftPhoneSrv", , , Прокси, 60);
			
		ВебСервис = Новый WSПрокси(Определения, "http://tempuri.org/",
			"ISOAPSoftPhoneSrvservice", "ISOAPSoftPhoneSrvPort", Прокси, 60);
		
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru='Подключение к серверу СофтФон';
			|en='Connecting to the Softphone server'"), УровеньЖурналаРегистрации.Предупреждение, , ,
			 НСтр("ru='Не удалось подключиться к веб-сервису серверу СофтФон'"));
	КонецПопытки;
	
	Если ВебСервис = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Выборка	= Справочники.сфпТелефонныеКниги.Выбрать();
	Пока Выборка.Следующий() Цикл
		// Если Выборка.УчаствуетВРегламентномЗадании Тогда
			СтруктураКниги = сфпОбновитьТелефоннуюКнигу(Выборка.Ссылка);
			ОписаниеТелефоннойКниги = сфпСформироватьОписаниеТелефоннойКниги(СтруктураКниги, Выборка.ПометкаУдаления);
			
			Попытка
				ВебСервис.PutAddressBookXML("", ОписаниеТелефоннойКниги); 
			Исключение
				ЗаписьЖурналаРегистрации(НСтр("ru='Запись телефонной книги';en='Phone Book Recording'"),
					 УровеньЖурналаРегистрации.Предупреждение, , , НСтр("ru='Не удалось записать телефонную книгу: '" 
					+ Выборка.Наименование));
			КонецПопытки;	
		//КонецЕсли;	
	КонецЦикла;

КонецПроцедуры // сфпОбновлениеТелефонныхКниг()	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С JSON

// Функция получает описание объекта из модели json. 
//
// Параметры:
//  СтрJSON - Строка - описание объекта в виде модели json
//
// Возвращаемое значение:
//  Значение    - Соответствие - соответствие свойств и значений объекта 
//
Функция UnJSON(СтрJSON) Экспорт 
	
	Перем Значение;
	Если ПолучитьЗначениеJSON(СтрJSON, Значение) = 0 Тогда
		Возврат Значение;
	КонецЕсли;
	Возврат Неопределено;
	
КонецФункции // UnJSON()

// Функция осуществляет непосредственный разбор json. 
//
//
Функция ПолучитьЗначениеJSON(СтрJSON, Значение, Позиция = 1, Ключ = "") Экспорт
	Перем Кавычка;

	ЗначениеВСтроке = "";
	Кавычек = 0;
	Комментарий = Ложь;
	Строка = Ложь;

	Пока Позиция <= СтрДлина(СтрJSON) Цикл
		ХХ = Сред(СтрJSON, Позиция, 2);
		Х_ОдинСлева = Лев(ХХ, 1);
		Позиция = Позиция + 1;

		Если Х_ОдинСлева > " " ИЛИ Строка Тогда // Отсекаем всякий хлам

			Если Комментарий Тогда
				// Это комментарий. Крутимся в цикле пока не встретится конец комментария.
				Если ХХ = "*/" Тогда
					// Комментарий закончился.
					Комментарий = Ложь;
					Позиция = Позиция + 1;
				КонецЕсли;

			ИначеЕсли Х_ОдинСлева = "\" Тогда
				Позиция = Позиция + 1;
				ХХ = ВРег(ХХ);
				Если ХХ = "\""" Тогда ЗначениеВСтроке = ЗначениеВСтроке + """";
				ИначеЕсли ХХ = "\\" Тогда ЗначениеВСтроке = ЗначениеВСтроке + "\";
				ИначеЕсли ХХ = "\/" Тогда ЗначениеВСтроке = ЗначениеВСтроке + "/";
				ИначеЕсли ХХ = "\B" Тогда ЗначениеВСтроке = ЗначениеВСтроке + Символ(8);
				ИначеЕсли ХХ = "\F" Тогда ЗначениеВСтроке = ЗначениеВСтроке + Символы.ПФ; // Перевод формы (страницы)
				ИначеЕсли ХХ = "\N" Тогда ЗначениеВСтроке = ЗначениеВСтроке + Символы.ПС; // Перевод строки
				ИначеЕсли ХХ = "\R" Тогда ЗначениеВСтроке = ЗначениеВСтроке + Символы.ВК; // Возврат каретки
				ИначеЕсли ХХ = "\T" Тогда ЗначениеВСтроке = ЗначениеВСтроке + Символы.ВТаб; // Символ вертикальной табуляции
				ИначеЕсли ХХ = "\U" Тогда
					ЗначениеВСтроке = ЗначениеВСтроке + Символ(Hex2Число(Сред(СтрJSON, Позиция, 4))); // Шестнадцатиричное число
					Позиция = Позиция + 4;
				КонецЕсли;

			ИначеЕсли Строка Тогда
				// Если строка не закончилась - то пропускаем управляющие символы.
				Если Х_ОдинСлева = Кавычка Тогда
					// Закончилась строка.
					Строка = Ложь;
					Кавычек = Кавычек + 1;
				Иначе
					ЗначениеВСтроке = ЗначениеВСтроке + Х_ОдинСлева;
				КонецЕсли;

			ИначеЕсли ХХ = "/*" Тогда
				// Начался комментарий.
				Комментарий = Истина;
				Позиция = Позиция + 1;

			ИначеЕсли СтрНайти("""'{}[]:,", Х_ОдинСлева) > 0 Тогда
				Если Х_ОдинСлева = """" ИЛИ Х_ОдинСлева = "'" Тогда
					// Началась строка
					// Строка - коллекция нуля или больше символов Unicode, заключенная в
					// двойные кавычки, используя "\" в качестве символа экранирования.
					// Символ представляется как односимвольная строка.
					// Похожий синтаксис используется в C и Java.
					Строка = Истина;
					Кавычка = Х_ОдинСлева;
					Кавычек = Кавычек + 1;

				ИначеЕсли Х_ОдинСлева = "{" Тогда
					// Начался объект
					// Объект - неупорядоченный набор пар ключ/значение.
					// Объект начинается с "{" и заканчивается "}".
					// Каждое имя сопровождается ":", пары ключ/значение разделяются ",".
					Объект = Новый Соответствие;
					// Объект=Новый Структура;
					Пока Истина Цикл
						// Получим ключ и значение
						Ключ = "";
						Режим = ПолучитьЗначениеJSON(СтрJSON, Значение, Позиция, Ключ);
						// 0 - есть значение и не конец объекта (запятая)
						// 1 - есть значение и конец объекта
						// 2 - нет значения и не конец объекта (запятая)
						// 3 - нет значения и конец объекта
						Если Режим = 0 Тогда
							Объект.Вставить(Ключ, Значение);
						ИначеЕсли Режим = 1 Тогда
							Объект.Вставить(Ключ, Значение);
							Прервать;
						ИначеЕсли Режим = 3 Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Значение = Объект;
					Возврат 0;

				ИначеЕсли Х_ОдинСлева = "[" Тогда
					// Начался массив
					// Массив - упорядоченная коллекция значений.
					// Массив начинается с "[" и заканчивается "]".
					// Значения разделены ",".
					Массив = Новый Массив;
					Пока Истина Цикл
						Режим = ПолучитьЗначениеJSON(СтрJSON, Значение, Позиция);
						// 0 - есть значение и не конец массива (запятая)
						// 1 - есть значение и конец массива
						// 2 - нет значения и не конец массива (запятая)
						// 3 - нет значения и конец массива
						Если Режим = 0 Тогда
							Массив.Добавить(Значение);
						ИначеЕсли Режим = 1 Тогда
							Массив.Добавить(Значение);
							Прервать;
						ИначеЕсли Режим = 3 Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Значение = Массив;
					Возврат 0;

				ИначеЕсли Х_ОдинСлева = "]" ИЛИ Х_ОдинСлева = "}" Тогда
					// Закончился массив/объект
					Если ЗначениеВСтроке = "" И Кавычек = 0 Тогда
						Возврат 3; // Нет значения и конец массива/объекта
					Иначе
						Значение = ПолучитьЗначениеИзСтроки(ЗначениеВСтроке, Кавычек);
						Возврат 1; // Есть значение и конец массива/объекта
					КонецЕсли;

				ИначеЕсли Х_ОдинСлева = ":" Тогда
					Ключ = ЗначениеВСтроке;
					Возврат ПолучитьЗначениеJSON(СтрJSON, Значение, Позиция);

				Иначе
					// Запятая
					Прервать;
				КонецЕсли;

			Иначе
				ЗначениеВСтроке = ЗначениеВСтроке + Х_ОдинСлева;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если Кавычек > 0 Тогда
		Значение = ЗначениеВКавычках(ЗначениеВСтроке);
	Иначе
		Если ЗначениеВСтроке = "" Тогда
			Возврат 2;
		Иначе
			Значение = ЗначениеБезКавычек(ЗначениеВСтроке);
		КонецЕсли;
	КонецЕсли;
	Возврат 0;
КонецФункции // ПолучитьЗначениеJSON()

// Вспомогательная функция 
//
//
Функция Hex2Число(Hex) 
	Стр = ВРег(СокрЛП(Hex));
	Dec = 0;
	Для Х = 1 По СтрДлина(Стр) Цикл
		Dec = Dec + СтрНайти("123456789ABCDEF", Сред(Стр, Х, 1)) * Pow(16, СтрДлина(Стр) - Х);
	КонецЦикла;
	Возврат Dec;
КонецФункции

// Вспомогательная функция 
//
//
Функция ПолучитьЗначениеИзСтроки(ЗначениеВСтроке, Кавычек)
	Если Кавычек > 0 Тогда
		Возврат ЗначениеВКавычках(ЗначениеВСтроке);
	ИначеЕсли ЗначениеВСтроке = "" Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат ЗначениеБезКавычек(ЗначениеВСтроке);
КонецФункции

// Вспомогательная функция 
//
//
Функция ЗначениеВКавычках(ЗначениеВСтроке)
	Возврат ЗначениеВСтроке;
КонецФункции

// Вспомогательная функция 
//
//
Функция ЗначениеБезКавычек(ЗначениеВСтроке)
	// Это число, булево или null.
	// Хотя здесь могут быть и строки. Например: {Code:123}
	Стр = ВРег(ЗначениеВСтроке);
	Если Стр = "TRUE" Тогда
		Возврат Истина;
	ИначеЕсли Стр = "FALSE" Тогда
		Возврат Ложь;
	ИначеЕсли Стр = "NULL" Тогда
		Возврат Неопределено;
	КонецЕсли;
	// Пробежимся по предполагаемому "числу"
	Экспонента	= Ложь;
	ХХ			= " ";
	Для Индекс = 1 По СтрДлина(Стр) Цикл
		Х_ОдинСимвол = Сред(Стр, Индекс, 1);
		Если СтрНайти("0123456789.+-", Х_ОдинСимвол) Тогда
			ХХ = ХХ + Х_ОдинСимвол;
		ИначеЕсли Х_ОдинСимвол = "E" Тогда
			#Если ВебКлиент Тогда
				Возврат ЗначениеВСтроке;
			#Иначе
				Экспонента = Истина;
				УУ = XMLЗначение(Тип("Число"), ХХ);
				ХХ = " ";
			#КонецЕсли
		Иначе
			// то точно не число, а строка
			Возврат ЗначениеВСтроке;
		КонецЕсли;
	КонецЦикла;
	#Если ВебКлиент Тогда
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ХХ) Тогда
			ХХ = Число(ХХ);
		КонецЕсли;	
	#Иначе
		ХХ = XMLЗначение(Тип("Число"), ХХ);
		Если Экспонента Тогда
			ХХ = УУ * Pow(10, ХХ);
		КонецЕсли;
	#КонецЕсли
	Возврат ХХ;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ СОЗДАНИЯ ДОКУМЕНТОВ

// Функция создает документ "Телефонный звонок"
//
// Параметры:
//	СтруктураЗвонка		- Структура	- Структура данных звонка
//	ДанныеЗаполнения	- Структура	- Структура данных для заполнения документа
//
// Возвращаемое значение:
//	ДокументСсылка	- Телефонный звонок
//
Функция сфпСоздатьТелефонныйЗвонок(СтруктураЗвонка, ДанныеЗаполнения, СозданЗвонок = Ложь) Экспорт
	
	НовыйЗвонок	= Неопределено;
	
	Если СтруктураЗвонка.Свойство("ДатаНачала") И ЗначениеЗаполнено(СтруктураЗвонка.ДатаНачала) Тогда
		  ДатаНачала = СтруктураЗвонка.ДатаНачала;
	Иначе ДатаНачала = сфпТекущаяДата();
	КонецЕсли;
	
	// Проверяем наличие уже созданного ранее документа телефонный звонок по данному звонку
	НовыйЗвонок = сфпНайтиЗвонокВРегистреПоДаннымЗвонка(СтруктураЗвонка.hCall,
		 СтруктураЗвонка.ВходящийЗвонок, ДатаНачала,
		 СтруктураЗвонка.НомерТелефона);
	Если НЕ ЗначениеЗаполнено(НовыйЗвонок) Тогда
		НовыйЗвонок	= сфпСофтФонПроСерверПереопределяемый.сфпСоздатьТелефонныйЗвонок(СтруктураЗвонка, ДанныеЗаполнения);
		СозданЗвонок = ЗначениеЗаполнено(НовыйЗвонок);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НовыйЗвонок) Тогда
		// Добавляем запись в регистр истории звонков
		МенеджерЗаписи = РегистрыСведений.сфпИсторияЗвонков.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ДатаНачала	 = НовыйЗвонок.Дата;
		МенеджерЗаписи.НомерТелефона = НовыйЗвонок.АбонентКакСвязаться;
	 	МенеджерЗаписи.Звонок 		 = НовыйЗвонок.Ссылка;
		МенеджерЗаписи.Прочитать();
		Если НЕ МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.ДатаНачала 			= НовыйЗвонок.Дата;
			МенеджерЗаписи.НомерТелефона 		= НовыйЗвонок.АбонентКакСвязаться;
	 		МенеджерЗаписи.Звонок 				= НовыйЗвонок.Ссылка;
		    МенеджерЗаписи.ДатаОтвета			= НовыйЗвонок.Дата;
			МенеджерЗаписи.ДатаОкончания		= НовыйЗвонок.Дата;	
			МенеджерЗаписи.Входящий				= НовыйЗвонок.Входящий;
			МенеджерЗаписи.ИдентификаторЗаписи	= НовыйЗвонок.сфпИдентификаторЗаписи;
			МенеджерЗаписи.ИдентификаторЗвонка	= НовыйЗвонок.сфпИдентификаторЗвонка;
			МенеджерЗаписи.Ответственный		= НовыйЗвонок.Ответственный;
			МенеджерЗаписи.НомерКомпании		= СтруктураЗвонка.Caller_Destination_Number;
			
			Если СтруктураЗвонка.Свойство("ВнутреннийНомер") И ЗначениеЗаполнено(СтруктураЗвонка.ВнутреннийНомер) Тогда
				  МенеджерЗаписи.ВнутреннийНомер = СтруктураЗвонка.ВнутреннийНомер;
			Иначе МенеджерЗаписи.ВнутреннийНомер = сфпТекущийВнутреннийНомер();
			КонецЕсли;
			
			ИмяРеквизитаАбонентКонтакт = сфпИмяРеквизитаАбонентКонтакт();
			Если НЕ ПустаяСтрока(ИмяРеквизитаАбонентКонтакт) Тогда
				МенеджерЗаписи.АбонентКонтакт = НовыйЗвонок[ИмяРеквизитаАбонентКонтакт];
			КонецЕсли;

			Если СтруктураЗвонка.Свойство("ДатаНачала") И ЗначениеЗаполнено(СтруктураЗвонка.ДатаНачала) Тогда
				МенеджерЗаписи.ДатаНачала = СтруктураЗвонка.ДатаНачала;
			КонецЕсли;
			Если СтруктураЗвонка.Свойство("ДатаОтвета") И ЗначениеЗаполнено(СтруктураЗвонка.ДатаОтвета) Тогда
				МенеджерЗаписи.ДатаОтвета = СтруктураЗвонка.ДатаОтвета;
			КонецЕсли;
			Если СтруктураЗвонка.Свойство("ДатаОкончания") И ЗначениеЗаполнено(СтруктураЗвонка.ДатаОкончания) Тогда
				МенеджерЗаписи.ДатаОкончания = СтруктураЗвонка.ДатаОкончания;
			КонецЕсли;
		
			МенеджерЗаписи.Записать();
		КонецЕсли;
	КонецЕсли;	
	
	Возврат НовыйЗвонок.Ссылка;

КонецФункции // сфпСоздатьТелефонныйЗвонок()

// Процедура устанавливает значение реквизита с проверкой изменения
//
// Параметры:
//	ОбъектУстановки 	- Произвольный	- Объект, для которого производится установка реквизита
//	ИмяРеквизита		- Строка		- Имя реквизита
//	ЗначениеРеквизита	- Произвольный	- Значение реквизита
//	ТребуетсяЗапись		- Булево		- Признак изменения объекта записи
//
Процедура УстановитьЗначениеРеквизита(ОбъектУстановки, ИмяРеквизита, ЗначениеРеквизита, ТребуетсяЗапись)
	
	Если ОбъектУстановки[ИмяРеквизита] <> ЗначениеРеквизита Тогда
		ОбъектУстановки[ИмяРеквизита] = ЗначениеРеквизита;
		
		ТребуетсяЗапись = Истина;
	КонецЕсли;
	
КонецПроцедуры
			
// Процедура загружает историю звонков из SQL-базы сервера СофтФон
//
// Параметры:
//	Нет.
//
Процедура сфпПолучитьИсториюЗвонков() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.сфпПолучитьИсториюЗвонков);

	Если НЕ сфпРолиДоступны("ПолныеПрава, сфпУправлениеМаршрутизацией") Тогда
		Возврат;
	КонецЕсли;
	Если НЕ сфпИсточникИсторииЗвонковПодключен() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина); 
	
	СтрокаПодключения	= Константы.сфпСтрокаПодключенияИстории.Получить();
	СтруктураПодключения	= сфпПолучитьСтруктуруПодключения(СтрокаПодключения);
	ТипПодключения = ?(СтруктураПодключения.ТипПодключения = "1", "Postgre", "MSSQL");
	
	НачалоЗагрузкиИстории = сфпТекущаяДата();
	
	ДатаНачалаВыборки = Константы.сфпДатаИсторииЗвонков.Получить();
	Если ДатаНачалаВыборки = Дата('00010101') Тогда
		ДатаНачалаВыборки = НачалоМесяца(НачалоЗагрузкиИстории);
	КонецЕсли;
	
	ЗапросВерсииТаблицы = Новый Запрос;
	ЗапросВерсииТаблицы.Текст = "ВЫБРАТЬ
	|	dbo_c_Sys.ParamName КАК ИмяПараметра,
	|	dbo_c_Sys.ParamValue КАК ЗначениеПараметра
	|ИЗ
	|	ВнешнийИсточникДанных.сфпВнешнийИсточникДанныхИсторииЗвонков.Таблица.dbo_c_Sys КАК dbo_c_Sys";
	Если ТипПодключения = "Postgre" Тогда
		ЗапросВерсииТаблицы.Текст = СтрЗаменить(ЗапросВерсииТаблицы.Текст, "ParamName", "param_name");
		ЗапросВерсииТаблицы.Текст = СтрЗаменить(ЗапросВерсииТаблицы.Текст, "ParamValue", "param_value");
		ЗапросВерсииТаблицы.Текст = СтрЗаменить(ЗапросВерсииТаблицы.Текст, "dbo_c_Sys", "public_params");
	КонецЕсли;
	ТаблицаЗапроса = ЗапросВерсииТаблицы.Выполнить().Выгрузить();
	СвежаяВерсияТаблицы = Ложь;
	Если ТаблицаЗапроса.Количество() > 0 Тогда
		ИскомаяСтрока = ТаблицаЗапроса[0];
		Если ИскомаяСтрока.ИмяПараметра = "VerDB" Тогда
			СвежаяВерсияТаблицы = (Число(ИскомаяСтрока.ЗначениеПараметра) >= 24);
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	dbo_Calls.hCall КАК hCall,
	|	dbo_Calls.StartTime КАК StartTime,
	|	ЕСТЬNULL(dbo_Calls.AnswerTime, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК AnswerTime,
	|	ЕСТЬNULL(dbo_Calls.DropTime, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК DropTime,
	|	dbo_Calls.CallerId КАК CallerId,
	|	dbo_Calls.CalledId КАК CalledId,
	|	dbo_Calls.Origin КАК Origin,
	|	dbo_Calls.RecordID КАК RecordID,
	|	dbo_Calls.Caller_DestinationId КАК Caller_DestinationId,
	|	// Имя_Параметра КАК NumberOnLine
	|ИЗ
	|	ВнешнийИсточникДанных.сфпВнешнийИсточникДанныхИсторииЗвонков.Таблица.dbo_Calls КАК dbo_Calls
	|ГДЕ
	|	dbo_Calls.StartTime > &ДатаНачалаВыборки
	// |	И dbo_Calls.StartTime < &ДатаОкончанияВыборки
	|	И dbo_Calls.Deleted = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	dbo_Calls.StartTime");
	Запрос.УстановитьПараметр("ДатаНачалаВыборки", ДатаНачалаВыборки);
	Запрос.УстановитьПараметр("ДатаОкончанияВыборки", (ТекущаяДатаСеанса() - 900));
	
	Если СвежаяВерсияТаблицы Тогда
		  Запрос.Текст = СтрЗаменить(Запрос.Текст, "// Имя_Параметра", "ЕСТЬNULL(dbo_Calls.NumberOnLine, """")");
	Иначе Запрос.Текст = СтрЗаменить(Запрос.Текст, "// Имя_Параметра", """""");
	КонецЕсли;
	
	Если ТипПодключения = "Postgre" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "dbo_Calls", "public_calls");
	КонецЕсли;
	
	ДлинаВнутреннихНомеров = Константы.сфпМаксимальнаяДлинаВнутреннихНомеров.Получить();
	ДействиеПриОшибке = Константы.сфпДействиеПриОшибкеЗагрузкиИсторииЗвонков.Получить();
	ПрерватьВыполнениеПриОшибке =
		(ДействиеПриОшибке = Перечисления.сфпДействияПриОшибкеЗагрузкиИсторииЗвонков.ПрерватьВыполнение);
	ПовторитьВыполнениеПриОшибке =
		(ДействиеПриОшибке = Перечисления.сфпДействияПриОшибкеЗагрузкиИсторииЗвонков.ПовторитьВыполнение);
	
	НеЗагружатьЗвонкиДлительностьюМенее = Константы.сфпНеЗагружатьЗвонкиДлительностьюМенее.Получить();
	ЗагружатьВнутренниеЗвонки = Константы.сфпЗагружатьВнутренниеЗвонки.Получить();
	
	ДатаНезавершенногоЗвонка = Дата('00010101');
	ДатаПоследнегоЗвонка = ДатаНачалаВыборки;
	
	ТекущийАвторизованныйПользователь = сфпТекущийПользователь();
	ПользовательДляОповещений = Константы.сфпПользовательДляОповещений.Получить();
	Если НЕ ЗначениеЗаполнено(ПользовательДляОповещений) Тогда
		ПользовательДляОповещений = ТекущийАвторизованныйПользователь;
	КонецЕсли;
	
	ВнутренниеНомераИБ = Константы.сфпВнутренниеНомераИнформационнойБазы.Получить();
	МассивВнутреннихНомеровИБ = Новый Массив();
	МассивНомеров = сфпОбщегоНазначенияКлиентСервер.сфпРазложитьСтрокуВМассивПодстрок(ВнутренниеНомераИБ, ",");
	Для Каждого ТекНомер Из МассивНомеров Цикл
		ПозицияРазделителя = СтрНайти(ТекНомер, "-");
		
		Попытка
			Если ПозицияРазделителя > 0 Тогда
				НачалоДиапазона = Число(СокрЛП(Лев(ТекНомер, ПозицияРазделителя - 1)));
				КонецДиапазона = Число(СокрЛП(Сред(ТекНомер, ПозицияРазделителя + 1)));
				Для к = НачалоДиапазона По КонецДиапазона Цикл
					МассивВнутреннихНомеровИБ.Добавить(Формат(к, "ЧГ="));
				КонецЦикла;
				
			Иначе
				МассивВнутреннихНомеровИБ.Добавить(Формат(Число(СокрЛП(ТекНомер)), "ЧГ="));
			КонецЕсли;
		Исключение
			ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка, , , 
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;	
	КонецЦикла;
	
	ЗапросРегистра = Новый Запрос("
	|ВЫБРАТЬ ДатаНачала, НомерТелефона, Звонок
	|ИЗ РегистрСведений.сфпИсторияЗвонков
	|ГДЕ ИдентификаторЗвонка = &ИдентификаторЗвонка И ДатаНачала МЕЖДУ &НачалоПериода И &КонецПериода");
	
	СчетчикЗаписей = 0;
	КоличествоЗаписейВПакете = 1000;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СчетчикЗаписей = СчетчикЗаписей + 1;
		
		Если СчетчикЗаписей = КоличествоЗаписейВПакете Тогда
			СчетчикЗаписей = 1;
			
			// Проверяем на незавершенные звонки
			Если ДатаНезавершенногоЗвонка = Дата('00010101') Тогда
				// Устанавливаем дату начала последнего звонка
				Константы.сфпДатаИсторииЗвонков.Установить(ДатаПоследнегоЗвонка);

			Иначе
				// Устанавливаем дату начала первого незавершенного звонка
				Константы.сфпДатаИсторииЗвонков.Установить(ДатаНезавершенногоЗвонка);
				ДатаНезавершенногоЗвонка = Дата('00010101');
			КонецЕсли;
		КонецЕсли;
		
		сфпCallerId	= Выборка.CallerId;
		сфпCallerId	= сфпУбратьИзНомераТелефонаВсеПрефиксы(сфпCallerId);
		
		сфпCalledId	= Выборка.CalledId;
		сфпCalledId	= сфпУбратьИзНомераТелефонаВсеПрефиксы(сфпCalledId);

		Если ПустаяСтрока(сфпCallerId) ИЛИ ПустаяСтрока(сфпCalledId) Тогда
			// Нет номера звонящего или вызываемого абонента
			Продолжить;

		ИначеЕсли (СтрДлина(сфпCalledId) <= ДлинаВнутреннихНомеров) И (СтрДлина(сфпCallerId) <= ДлинаВнутреннихНомеров)
			И НЕ ЗагружатьВнутренниеЗвонки Тогда
			// Внутренний звонок
			Продолжить;

		ИначеЕсли Выборка.StartTime >= (Выборка.DropTime - 3) Тогда
			// Звонок сразу переключен на другой внутренний номер
			Продолжить;

		ИначеЕсли Выборка.DropTime = Дата('00010101') Тогда
			// Если разговор не завершен
			Если НачалоДня(НачалоЗагрузкиИстории) = НачалоДня(Выборка.StartTime) Тогда
				Если ДатаНезавершенногоЗвонка = Дата('00010101') Тогда
					// Запоминаем дату первого незавершенного звонка
					ДатаНезавершенногоЗвонка = Выборка.StartTime - 1;
				КонецЕсли;
				
				Продолжить;
			КонецЕсли;

		ИначеЕсли СвежаяВерсияТаблицы И Выборка.NumberOnLine = "" Тогда
			// Попали на дозвон от шлюза, игнорируем
			Продолжить;	

		Иначе	
			// Добавляем запись в регистр
			ЭтоВходящийЗвонок = сфпОпределитьВходящийЗвонокПриЗагрузкеИстории(Выборка.Origin); 
			
			// Возможен случай, когда невозможно точно определить вид звонка по origin,
			// тогда используем "старый" алгоритм определения входящий/исходящий
			Если ЭтоВходящийЗвонок = Неопределено Тогда
				Если СтрДлина(сфпCallerId) <= ДлинаВнутреннихНомеров И СтрДлина(сфпCalledId) > ДлинаВнутреннихНомеров Тогда
					ЭтоВходящийЗвонок = Ложь;

				ИначеЕсли СтрДлина(сфпCalledId) <= ДлинаВнутреннихНомеров И СтрДлина(сфпCallerId) > ДлинаВнутреннихНомеров Тогда
					ЭтоВходящийЗвонок = Истина;

				Иначе
					ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
						 УровеньЖурналаРегистрации.Ошибка, , , НСтр("ru='Не удалось определить тип звонка'") + ": hCall = " 
						+ Выборка.hCall);
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если СвежаяВерсияТаблицы Тогда
				  НомерТелефонаВнутренний = Выборка.NumberOnLine;
			Иначе НомерТелефонаВнутренний = ?(ЭтоВходящийЗвонок, сфпCalledId, сфпCallerId);
			КонецЕсли;
			
			Если МассивВнутреннихНомеровИБ.Количество() > 0 Тогда
				Если МассивВнутреннихНомеровИБ.Найти(НомерТелефонаВнутренний) = Неопределено Тогда
					Продолжить;
				КонецЕсли;	
			КонецЕсли;
			
			НомерТелефона = ?(ЭтоВходящийЗвонок, сфпCallerId, сфпCalledId);
			ИдентификаторЗвонка = СтрЗаменить(Формат(Выборка.hCall, "ЧГ=3,0"), ",", Символы.НПП);
			
			ЗапросРегистра.УстановитьПараметр("ИдентификаторЗвонка", ИдентификаторЗвонка);
			ЗапросРегистра.УстановитьПараметр("НачалоПериода", Выборка.StartTime - 300);
			ЗапросРегистра.УстановитьПараметр("КонецПериода", Выборка.StartTime + 300);
			
			ВыборкаРегистра = CRM_ОбщегоНазначенияСервер.ВыполнитьЗапрос(ЗапросРегистра).Выбрать();
			Если ВыборкаРегистра.Следующий() Тогда
				ЗаписьРегистра = РегистрыСведений.сфпИсторияЗвонков.СоздатьМенеджерЗаписи();
				ЗаписьРегистра.ДатаНачала = ВыборкаРегистра.ДатаНачала;
				ЗаписьРегистра.НомерТелефона = ВыборкаРегистра.НомерТелефона;
				ЗаписьРегистра.Звонок = ВыборкаРегистра.Звонок;
				ЗаписьРегистра.Прочитать();

				ТребуетсяЗаписьРегистра = Ложь;

			Иначе
				Если сфпНайтиЗвонокВРегистреПоНомеруПриЗагрузкеИстории(Выборка.StartTime, сфпCallerId) Тогда
					// Звонок уже есть в регистре
					ДатаПоследнегоЗвонка = Выборка.StartTime;
					Продолжить;
					
				ИначеЕсли сфпНайтиЗвонокВРегистреПоНомеруПриЗагрузкеИстории(Выборка.StartTime, сфпCalledId) Тогда
					// Звонок уже есть в регистре
					ДатаПоследнегоЗвонка = Выборка.StartTime;
					Продолжить;
				КонецЕсли;
				
				ЗаписьРегистра = РегистрыСведений.сфпИсторияЗвонков.СоздатьМенеджерЗаписи();
				ЗаписьРегистра.ДатаНачала = Выборка.StartTime;
				ЗаписьРегистра.НомерТелефона = НомерТелефона;
				ЗаписьРегистра.Звонок = Документы.ТелефонныйЗвонок.ПустаяСсылка();
				
				ТребуетсяЗаписьРегистра = Истина;
			КонецЕсли;
			
			ДатаОкончанияЗвонка = ?(Выборка.DropTime = Дата('00010101'), МАКС(Выборка.StartTime,
				 Выборка.AnswerTime),
				 Выборка.DropTime);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ДатаОкончания", ДатаОкончанияЗвонка, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ДатаОтвета", Выборка.AnswerTime, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ДатаНачала", Выборка.StartTime, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "Входящий", ЭтоВходящийЗвонок, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ИдентификаторЗаписи", Выборка.RecordID, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ИдентификаторЗвонка", ИдентификаторЗвонка, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ВнутреннийНомер", НомерТелефонаВнутренний, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "НомерКомпании", Выборка.Caller_DestinationId, ТребуетсяЗаписьРегистра);
			
			Если НеЗагружатьЗвонкиДлительностьюМенее > 0
				 И ЗначениеЗаполнено(ЗаписьРегистра.ДатаОкончания) Тогда // фильтр установлен
				Если ЗначениеЗаполнено(ЗаписьРегистра.ДатаОтвета) Тогда
					ДлительностьРазговора = ЗаписьРегистра.ДатаОкончания - ЗаписьРегистра.ДатаОтвета;
				    Если ДлительностьРазговора < НеЗагружатьЗвонкиДлительностьюМенее Тогда
						Продолжить;
					КонецЕсли;
					
				ИначеЕсли ЗначениеЗаполнено(ЗаписьРегистра.ДатаНачала) Тогда
					ДлительностьРазговора = ЗаписьРегистра.ДатаОкончания - ЗаписьРегистра.ДатаНачала;
				    Если ДлительностьРазговора < НеЗагружатьЗвонкиДлительностьюМенее Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			// Проверяем наличие уже созданного ранее документа телефонный звонок по данному звонку
			Если ЗначениеЗаполнено(ЗаписьРегистра.Звонок) Тогда
				НайденныйЗвонок = ЗаписьРегистра.Звонок;
			
			Иначе	
				НайденныйЗвонок = сфпНайтиПоследнийЗвонокПоНомеру(ЗаписьРегистра.НомерТелефона, ЗаписьРегистра.ДатаНачала);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НайденныйЗвонок) И НайденныйЗвонок.сфпИдентификаторЗвонка = ИдентификаторЗвонка Тогда
				// Обновляем существующий звонок
				ЗвонокОбъект = НайденныйЗвонок.ПолучитьОбъект();
				ТребуетсяЗаписьЗвонка = Ложь;
				
			Иначе	
				// Создаем новый телефонный звонок
				ЗвонокОбъект = Документы.ТелефонныйЗвонок.СоздатьДокумент();
				ЗвонокОбъект.Дата = ЗаписьРегистра.ДатаНачала;
				ТребуетсяЗаписьЗвонка = Истина;
			КонецЕсли;
			
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "Входящий", ЗаписьРегистра.Входящий, ТребуетсяЗаписьЗвонка);
						
			Если НЕ ЗначениеЗаполнено(ЗвонокОбъект.Важность) Тогда
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "Важность",
					 Перечисления.ВариантыВажностиВзаимодействия.Обычная,
					 ТребуетсяЗаписьЗвонка);
			КонецЕсли;
			
			ТемаЗвонка = сфпЗаполнитьТемуТелефонногоЗвонка(ЗвонокОбъект.Входящий, ЗвонокОбъект.Дата);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "Тема", ТемаЗвонка, ТребуетсяЗаписьЗвонка);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "АбонентКакСвязаться", НомерТелефона, ТребуетсяЗаписьЗвонка);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "сфпИдентификаторЗвонка",
				 ЗаписьРегистра.ИдентификаторЗвонка,
				 ТребуетсяЗаписьЗвонка);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "сфпИдентификаторЗаписи",
				 ЗаписьРегистра.ИдентификаторЗаписи,
				 ТребуетсяЗаписьЗвонка);
			
			ДлительностьЗвонка = ЗаписьРегистра.ДатаОкончания - ЗаписьРегистра.ДатаНачала;
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "сфпДлительностьЗвонка", ДлительностьЗвонка, ТребуетсяЗаписьЗвонка);
			
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "Дата", ЗаписьРегистра.ДатаНачала, ТребуетсяЗаписьЗвонка);
			
			СостояниеЗвонка = ?(ЗаписьРегистра.ДатаОтвета = Дата('00010101'),
				 Перечисления.сфпСостоянияЗвонков.Пропущенный,
				 Перечисления.сфпСостоянияЗвонков.Отвеченный);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "сфпСостояниеЗвонка", СостояниеЗвонка, ТребуетсяЗаписьЗвонка);
						
			НайденныйОтветственный = сфпНайтиОтветственного(ЗаписьРегистра.ВнутреннийНомер);
			Если ЗначениеЗаполнено(НайденныйОтветственный) Тогда
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "Ответственный", НайденныйОтветственный, ТребуетсяЗаписьЗвонка);
			КонецЕсли;
						
			ОписаниеЗвонка = сфпЗаполнитьОписаниеТелефонногоЗвонка(ЗвонокОбъект.сфпДлительностьЗвонка);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "Описание", ОписаниеЗвонка, ТребуетсяЗаписьЗвонка);
						
			ОписаниеЗвонка = "";
				
			МассивЗвонящих = сфпНайтиОбъектВРегистреПоТелефону(ЗаписьРегистра.НомерТелефона);
			Если МассивЗвонящих.Количество() = 0 Тогда
				АбонентПредставление = НСтр("ru='!!!Не определен!!!';en='!!!Indefined!!!'");

			ИначеЕсли МассивЗвонящих.Количество() = 1 Тогда
				АбонентКонтакт = МассивЗвонящих[0];
				АбонентПредставление = сфпПолучитьНаименованиеКонтакта(АбонентКонтакт);
				
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "АбонентКонтакт", АбонентКонтакт, ТребуетсяЗаписьЗвонка);
				
				Если ЗаписьРегистра.Входящий Тогда
					Если НЕ ЗначениеЗаполнено(ЗвонокОбъект.Ответственный) Тогда
						Если ТипЗнч(АбонентКонтакт) = Тип("СправочникСсылка.Партнеры") Тогда
							УстановитьЗначениеРеквизита(ЗвонокОбъект, "Ответственный", АбонентКонтакт.ОсновнойМенеджер,
								 ТребуетсяЗаписьЗвонка);
							Если ЗначениеЗаполнено(ЗвонокОбъект.Ответственный) Тогда
								ТекстОписания = Нстр("ru='Ответственный определен по закреплению менеджера за клиентом';
									|en='The manager is responsible for assigning the manager to customer'");
								ОписаниеЗвонка = ОписаниеЗвонка + Символы.ПС + Символы.ПС + ТекстОписания;
							КонецЕсли;

						ИначеЕсли ТипЗнч(АбонентКонтакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
							ВладелецКонтакта = сфпПолучитьВладельцаКонтакта(АбонентКонтакт);
							УстановитьЗначениеРеквизита(ЗвонокОбъект, "Ответственный", ВладелецКонтакта.ОсновнойМенеджер,
								 ТребуетсяЗаписьЗвонка);
							Если ЗначениеЗаполнено(ЗвонокОбъект.Ответственный) Тогда
								ТекстОписания = Нстр("ru='Ответственный определен по закреплению менеджера за клиентом';
									|en='The manager is responsible for assigning the manager to customer'");
								ОписаниеЗвонка = ОписаниеЗвонка + Символы.ПС + Символы.ПС + ТекстОписания;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			
			Иначе
				//АбонентПредставление = НСтр("ru='Несколько совпадений номера: ';en='Multiple number matches: '") 
					//+ ЗаписьРегистра.НомерТелефона;
				
				АбонентКонтакт = МассивЗвонящих[0];
				АбонентПредставление = сфпПолучитьНаименованиеКонтакта(АбонентКонтакт);
				
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "АбонентКонтакт", АбонентКонтакт, ТребуетсяЗаписьЗвонка);
				
				СписокЗвонящих = сфпСформироватьСписокОбъектовДляВыбораПоМассивуЗвонящих(МассивЗвонящих);
				Если СписокЗвонящих.Количество() > 0 Тогда
					СтрокаДобавки = Нстр("ru='Совпадения по номеру ';en='Matches by number'") + ЗаписьРегистра.НомерТелефона 
						+ Нстр("ru=':'");
					ОписаниеЗвонка = ?(ЗначениеЗаполнено(ОписаниеЗвонка), ОписаниеЗвонка + Символы.ПС + Символы.ПС 
						+ СтрокаДобавки,
						 СтрокаДобавки);
				КонецЕсли;
				Для Каждого ЭлементСписка Из СписокЗвонящих Цикл
					СтрокаПредставления = ЭлементСписка.Представление;
					ОписаниеЗвонка = ОписаниеЗвонка + Символы.ПС + СтрокаПредставления;
				КонецЦикла;
			КонецЕсли;	
			
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "АбонентПредставление", АбонентПредставление, ТребуетсяЗаписьЗвонка);
			
			Если НЕ ПустаяСтрока(ОписаниеЗвонка) Тогда
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "Описание", ОписаниеЗвонка, ТребуетсяЗаписьЗвонка);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ЗвонокОбъект.Ответственный) Тогда
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "Ответственный", ПользовательДляОповещений, ТребуетсяЗаписьЗвонка);
			КонецЕсли;
			
			АвторЗвонка = ?(ЗначениеЗаполнено(ЗвонокОбъект.Ответственный), ЗвонокОбъект.Ответственный,
				 ТекущийАвторизованныйПользователь);
            УстановитьЗначениеРеквизита(ЗвонокОбъект, "Автор", АвторЗвонка, ТребуетсяЗаписьЗвонка);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "сфпНомерКомпании", ЗаписьРегистра.НомерКомпании, ТребуетсяЗаписьЗвонка);
			
			Попытка
				Если ТребуетсяЗаписьЗвонка Тогда
					ЗвонокОбъект.Записать();
				КонецЕсли;
			Исключение
				СтрОшибки = ОписаниеОшибки();
				СтрОшибки = НСтр("ru='Не удалось записать Телефонный звонок';en='Unable to record Phone call'") + ": " + СтрОшибки;
								
				Если ПрерватьВыполнениеПриОшибке Тогда
					ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
						 УровеньЖурналаРегистрации.Ошибка, , ,
						 СтрОшибки);
					ВызватьИсключение СтрОшибки;
					
				ИначеЕсли ПовторитьВыполнениеПриОшибке Тогда
					УспешнаяЗапись = Ложь;
					
					Для к = 1 По 3 Цикл
						Попытка
							ЗвонокОбъект.Записать();
							УспешнаяЗапись = Истина;
							Прервать;
						Исключение
							СтрОшибки = ОписаниеОшибки();
							СтрОшибки = НСтр("ru='Не удалось записать Телефонный звонок';en='Unable to record Phone call'") + ": " 
								+ СтрОшибки;
						КонецПопытки;	
					КонецЦикла;
					
					Если НЕ УспешнаяЗапись Тогда
						ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
							 УровеньЖурналаРегистрации.Ошибка, , ,
							 СтрОшибки);
						ВызватьИсключение СтрОшибки;
					КонецЕсли;
					
				Иначе
					ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
						 УровеньЖурналаРегистрации.Ошибка, , ,
						 СтрОшибки);
				КонецЕсли;	
			КонецПопытки;
			
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "Звонок", ЗвонокОбъект.Ссылка, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "Ответственный", ЗвонокОбъект.Ответственный, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "АбонентКонтакт", ЗвонокОбъект.АбонентКонтакт, ТребуетсяЗаписьРегистра);

			// Записываем новую запись регистра
			Попытка
				Если ТребуетсяЗаписьРегистра Тогда
					ЗаписьРегистра.Записать();
				КонецЕсли;
			Исключение
				СтрОшибки = ОписаниеОшибки();
				СтрОшибки = НСтр("ru='Не удалось сформировать запись регистра Истории звонков';en='Could not generate the History of History register'") 
					+ ": " 
					+ СтрОшибки;
								
				Если ПрерватьВыполнениеПриОшибке Тогда
					ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
						 УровеньЖурналаРегистрации.Ошибка, , ,
						 СтрОшибки);
					ВызватьИсключение СтрОшибки;
					
				ИначеЕсли ПовторитьВыполнениеПриОшибке Тогда
					УспешнаяЗапись = Ложь;
					
					Для к = 1 По 3 Цикл
						Попытка
							ЗвонокОбъект.Записать();
							УспешнаяЗапись = Истина;
							Прервать;
						Исключение
							СтрОшибки = ОписаниеОшибки();
							СтрОшибки = НСтр("ru='Не удалось записать Телефонный звонок';en='Unable to record Phone call'") + ": " 
								+ СтрОшибки;
						КонецПопытки;	
					КонецЦикла;
					
					Если НЕ УспешнаяЗапись Тогда
						ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
							 УровеньЖурналаРегистрации.Ошибка, , ,
							 СтрОшибки);
						ВызватьИсключение СтрОшибки;
					КонецЕсли;
					
				Иначе
					ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
						 УровеньЖурналаРегистрации.Ошибка, , ,
						 СтрОшибки);
				КонецЕсли;
			КонецПопытки;
			
			Если ЗвонокОбъект.сфпСостояниеЗвонка = Перечисления.сфпСостоянияЗвонков.Отвеченный Тогда
				// Отмечаем все неотвеченные звонки по данному номеру, как отвеченные
				сфпЗавершитьПропущенныеЗвонки(ЗаписьРегистра.НомерТелефона);
			КонецЕсли;
			
			// Сдвигаем границу начала выборки записей
			ДатаПоследнегоЗвонка = Выборка.StartTime;
		КонецЕсли;	
	КонецЦикла;
	
	// Проверяем на незавершенные звонки
	Если ДатаНезавершенногоЗвонка = Дата('00010101') Тогда
		// Устанавливаем дату начала последнего звонка
		Константы.сфпДатаИсторииЗвонков.Установить(ДатаПоследнегоЗвонка);

	Иначе
		// Устанавливаем дату начала первого незавершенного звонка
		Константы.сфпДатаИсторииЗвонков.Установить(ДатаНезавершенногоЗвонка);
	КонецЕсли;

КонецПроцедуры // сфпПолучитьИсториюЗвонков()

// Процедура записывает окончание звонка в регистр истории звонков
//
// Параметры:
//	Звонок				- ДокументСсылка	- Телефонный звонок
//	ИдентификаторЗвонка	- Число				- Идентификатор звонка
//
Процедура сфпЗаписатьОкончаниеЗвонкаВРегистр(Звонок, ИдентификаторЗвонка) Экспорт
	ДатаОкончания	= сфпТекущаяДата();
	НаборЗаписей	= РегистрыСведений.сфпИсторияЗвонков.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Звонок.Установить(Звонок);
	НаборЗаписей.Прочитать();
	Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
		Если ЗаписьНабора.ИдентификаторЗвонка = Строка(ИдентификаторЗвонка) Тогда
			ЗаписьНабора.ДатаОкончания	= ДатаОкончания;
			Если НЕ ЗначениеЗаполнено(ЗаписьНабора.АбонентКонтакт) Тогда
				ЗаписьНабора.АбонентКонтакт = Звонок.АбонентКонтакт;		
			КонецЕсли;	
			Попытка
				НаборЗаписей.Записать();
			Исключение
				ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка, , , 
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;	
			// Отмечаем все неотвеченные звонки по данному номеру, как отвеченные
			сфпЗавершитьПропущенныеЗвонки(ЗаписьНабора.НомерТелефона);	
			Прервать;
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры // сфпЗаписатьОкончаниеЗвонкаВРегистр()	

// Процедура записывает/изменяет абонента в регистре из документа "Телефонынй звонок"
//
// Параметры:
//	Звонок				- ДокументСсылка	- Телефонный звонок
//	ИдентификаторЗвонка	- Число				- Идентификатор звонка
//
Процедура сфпЗаписатьАбонентаЗвонкаВРегистрИсторииЗвонков(Звонок, ИдентификаторЗвонка)
	НаборЗаписей	= РегистрыСведений.сфпИсторияЗвонков.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Звонок.Установить(Звонок);
	НаборЗаписей.Прочитать();
	Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
		Если ЗаписьНабора.ИдентификаторЗвонка = ИдентификаторЗвонка Тогда
			ЗаписьНабора.АбонентКонтакт = Звонок.АбонентКонтакт;		
			Попытка
				НаборЗаписей.Записать();
			Исключение
				ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка, , , 
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;	
			Прервать;
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры // сфпЗаписатьАбонентаЗвонкаВРегистрИсторииЗвонков()

// Процедура записывает идентификатор записи в регистр истории звонков
//
// Параметры:
//	Звонок				- ДокументСсылка	- Телефонный звонок
//	ИдентификаторЗвонка	- Число				- Идентификатор звонка
//	ИдентификаторЗаписи	- Строка			- Идентификатор записи
//
Процедура сфпЗаписатьИдентификаторЗаписиВРегистр(Звонок, ИдентификаторЗвонка, ИдентификаторЗаписи) Экспорт
	
	Если Звонок = Неопределено Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ДатаНачала,
		|	НомерТелефона,
		|	Звонок,
		|	Звонок.сфпИдентификаторЗаписи КАК сфпИдентификаторЗаписи
		|ИЗ
		|	РегистрСведений.сфпИсторияЗвонков
		|ГДЕ
		|	ИдентификаторЗвонка = &ИдентификаторЗвонка
		|	И ПОДСТРОКА(ИдентификаторЗаписи, 1, 10) = """"
		|	И ДатаНачала > &ДатаНачала");
		Запрос.УстановитьПараметр("ИдентификаторЗвонка", "" + ИдентификаторЗвонка);
		Запрос.УстановитьПараметр("ДатаНачала", ТекущаяДатаСеанса() - 7200);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.сфпИсторияЗвонков.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
			МенеджерЗаписи.Прочитать();
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.ИдентификаторЗаписи = ИдентификаторЗаписи;
				
				Попытка
					МенеджерЗаписи.Записать();
				Исключение 
					ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
					УровеньЖурналаРегистрации.Ошибка, , , 
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
				КонецПопытки;
				
				Если Выборка.сфпИдентификаторЗаписи <> ИдентификаторЗаписи Тогда
					ЗвонокОбъект = Выборка.Звонок.ПолучитьОбъект();
					ЗвонокОбъект.сфпИдентификаторЗаписи = ИдентификаторЗаписи;
					
					Попытка
						ЗвонокОбъект.Записать();
					Исключение 
						ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
						УровеньЖурналаРегистрации.Ошибка, , , 
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
					КонецПопытки;
				КонецЕсли;
			КонецЕсли;		
		КонецЦикла;	
		
	Иначе
		НаборЗаписей = РегистрыСведений.сфпИсторияЗвонков.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Звонок.Установить(Звонок);
		НаборЗаписей.Прочитать();
		
		ЗаписатьНабор = Ложь;
		
		Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
			Если ЗаписьНабора.ИдентификаторЗвонка = Строка(ИдентификаторЗвонка) Тогда
				Если НЕ ЗначениеЗаполнено(ЗаписьНабора.ИдентификаторЗаписи) Тогда
					ЗаписьНабора.ИдентификаторЗаписи = ИдентификаторЗаписи;
					ЗаписатьНабор = Истина;
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(ЗаписьНабора.АбонентКонтакт) Тогда
					ЗаписьНабора.АбонентКонтакт = Звонок.АбонентКонтакт;		
					ЗаписатьНабор = Истина;
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;

		Если ЗаписатьНабор Тогда
			Попытка
				НаборЗаписей.Записать();
			Исключение 
				ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка, , , 
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
			КонецПопытки;	
		КонецЕсли;
	КонецЕсли;
			
КонецПроцедуры // сфпЗаписатьИдентификаторЗаписиВРегистр()

// Функция выполняет поиск звонка в регистре истории звонков по данным звонка
// в диапазоне +- 1 час от даты начала звонка
//
// Параметры:
//	hCall		- Строка	- Идентификатор звонка
//	StartDate	- Дата		- Дата начала звонка
//
// Возвращаемое значение:
//	Ссылка	- Ссылка на документ Телефонный звонок
//
Функция сфпНайтиЗвонокВРегистреПоДаннымЗвонка(hCall, ВходящийЗвонок, StartDate, НомерТелефона) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Звонок = Документы.ТелефонныйЗвонок.ПустаяСсылка();
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Звонок
	|ИЗ
	|	РегистрСведений.сфпИсторияЗвонков
	|ГДЕ
	|	ДатаНачала МЕЖДУ &НачалоПериода И &КонецПериода" + ?(СтрДлина(hCall) > 1, "", "
	|	И (НомерТелефона = &НомерТелефона
	|		ИЛИ НомерТелефона ПОДОБНО &НомерТелефонаШаблон
	|		ИЛИ НомерТелефона = &ИдентификаторЗвонка
	|		ИЛИ НомерТелефона = &ИдентификаторПреобразованный
	|		ИЛИ &НомерТелефона ПОДОБНО ""%""+НомерТелефона)") + "
	|	И (ИдентификаторЗвонка = &ИдентификаторЗвонка ИЛИ ИдентификаторЗвонка = &ИдентификаторПреобразованный)
	|	И Входящий = &Входящий
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНачала");
	Запрос.УстановитьПараметр("ИдентификаторЗвонка", "" + hCall);
	Запрос.УстановитьПараметр("Входящий", ВходящийЗвонок);
	Запрос.УстановитьПараметр("НомерТелефона", НомерТелефона);
	Запрос.УстановитьПараметр("НачалоПериода", ?(СтрДлина(hCall) > 1, StartDate - 3600, StartDate - 30));
	Запрос.УстановитьПараметр("КонецПериода", StartDate + 60);
	Запрос.УстановитьПараметр("НомерТелефонаШаблон", "%"+НомерТелефона);
	Запрос.УстановитьПараметр("ИдентификаторПреобразованный", СтрЗаменить(Формат(hCall, "ЧГ=3,0"), ",", Символы.НПП));

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Звонок = Выборка.Звонок;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Звонок;

КонецФункции // сфпНайтиЗвонокВРегистреПоДаннымЗвонка()

// Функция выполняет поиск звонка в регистре истории звонков по номеру телефона 
// в диапазоне +- 10 секунд от переданной даты звонка
//
// Параметры:
//	ДатаНачала			- Дата		- Дата начала звонка
//	НомерТелефона		- Строка	- очищенный номер телефона
//
// Возвращаемое значение:
//	Булево	- Наличие записи в регистре
//
Функция сфпНайтиЗвонокВРегистреПоНомеруПриЗагрузкеИстории(ДатаЗвонка, НомерТелефона)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",			ДатаЗвонка - 10);
	Запрос.УстановитьПараметр("КонецПериода",			ДатаЗвонка + 10);
	Запрос.УстановитьПараметр("НомерТелефона",			"%" + НомерТелефона + "%");
	// Получаем таблицу истории звонков
	Запрос.Текст = "ВЫБРАТЬ
	               |	сфпИсторияЗвонков.Звонок КАК Звонок
	               |ИЗ
	               |	РегистрСведений.сфпИсторияЗвонков КАК сфпИсторияЗвонков
	               |ГДЕ
	               |	сфпИсторияЗвонков.ДатаНачала МЕЖДУ &НачалоПериода И &КонецПериода
	               |	И сфпИсторияЗвонков.НомерТелефона ПОДОБНО &НомерТелефона";
	Выборка = Запрос.Выполнить();
	Возврат НЕ Выборка.Пустой(); 
КонецФункции // сфпНайтиЗвонокВРегистреПоНомеруПриЗагрузкеИстории()

// Функция находит звонок по номеру телефона и дате звонка в регистре истории звонков.
//
// Параметры:
//	НомерТелефона	- Строка	- Номер телефона
//	ДатаЗвонка		- Дата		- Дата и время звонка
//
// Возвращаемое значение:
//	ДокументСсылка	- Найденный звонок
//
Функция сфпНайтиПоследнийЗвонокПоНомеру(НомерТелефона, ДатаЗвонка)
	Звонок	= Документы.ТелефонныйЗвонок.ПустаяСсылка();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НомерТелефона",	НомерТелефона);
	Запрос.УстановитьПараметр("НачалоПериода",	ДатаЗвонка - 3600);
	Запрос.УстановитьПараметр("КонецПериода",	ДатаЗвонка + 1);
	Запрос.Текст = "ВЫБРАТЬ
	               |	сфпИсторияЗвонков.Звонок КАК Звонок,
	               |	МИНИМУМ(сфпИсторияЗвонков.ДатаНачала) КАК ДатаНачала,
	               |	МАКСИМУМ(сфпИсторияЗвонков.ДатаОкончания) КАК ДатаОкончания
	               |ИЗ
	               |	РегистрСведений.сфпИсторияЗвонков КАК сфпИсторияЗвонков
	               |ГДЕ
	               |	сфпИсторияЗвонков.НомерТелефона = &НомерТелефона
	               |	И сфпИсторияЗвонков.ДатаНачала МЕЖДУ &НачалоПериода И &КонецПериода
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	сфпИсторияЗвонков.Звонок
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	сфпИсторияЗвонков.Звонок.Дата";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ДатаЗвонка >= Выборка.ДатаНачала Тогда
			Если ДатаЗвонка < Выборка.ДатаОкончания Тогда
				// Новый звонок входит в текущий.
				Звонок	= Выборка.Звонок;
				Прервать;
			ИначеЕсли (ДатаЗвонка - Выборка.ДатаОкончания) < 3 Тогда
				// Новый звонок является продолжением текущего.
				Звонок	= Выборка.Звонок;
				Прервать;
			КонецЕсли;	
		ИначеЕсли (Выборка.ДатаНачала - ДатаЗвонка) < 60 Тогда
			// Новый звонок является началом текущего.
			Звонок	= Выборка.Звонок;
			Прервать;
		КонецЕсли;	
	КонецЦикла;	
	Возврат Звонок;
КонецФункции // сфпНайтиПоследнийЗвонокПоНомеру()	

// Функция находит ответственного по его внутреннему номеру
//
// Параметры:
//	ВнутреннийНомер	- Строка	- Внутренний номер
//
// Возвращаемое значение:
//	СправочникСсылка	- Ответственный
//
Функция сфпНайтиОтветственного(ВнутреннийНомер) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВложенныйЗапрос.Пользователь КАК Пользователь
	|ИЗ
	|	(ВЫБРАТЬ
	|	Пользователь
	|ИЗ
	|	РегистрСведений.CRM_НастройкиПользователей
	|ГДЕ
	|	(Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.CRM_НастройкиПользователей.сфпТекущийВнутреннийНомер) И Значение = &ВнутреннийНомерОсновной)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|	Пользователь
	|ИЗ
	|	РегистрСведений.CRM_НастройкиПользователей
	|ГДЕ
	|	(Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.CRM_НастройкиПользователей.сфпДополнительныеВнутренниеНомера) И Значение ПОДОБНО &ВнутреннийНомерДополнительный)
	|	) КАК ВложенныйЗапрос
	|");
	
	Запрос.УстановитьПараметр("ВнутреннийНомерОсновной", ВнутреннийНомер);
	Запрос.УстановитьПараметр("ВнутреннийНомерДополнительный", "%" + ВнутреннийНомер + "%");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Пользователь;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1 Ссылка
	|ИЗ Справочник.Пользователи.КонтактнаяИнформация
	|ГДЕ Тип = &ТипКИ И Представление = &ВнутреннийНомер");
	Запрос.УстановитьПараметр("ВнутреннийНомер", ВнутреннийНомер);
	Запрос.УстановитьПараметр("ТипКИ", Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;			
	
	Возврат Справочники.Пользователи.ПустаяСсылка();

КонецФункции // сфпНайтиОтветственного()	

// Функция находит ответственного по его номеру
//
// Параметры:
//	НомерТелефона	- Строка	- Номер телефона
//
// Возвращаемое значение:
//	СправочникСсылка	- Ответственный
//
Функция сфпНайтиОтветственногоПоТелефону(НомерТелефона)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1 Ссылка
	|ИЗ Справочник.Пользователи.КонтактнаяИнформация
	|ГДЕ Тип = &ТипКИ И НомерТелефона ПОДОБНО &НомерТелефона");
	Запрос.УстановитьПараметр("НомерТелефона", "%" + НомерТелефона + "%");
	Запрос.УстановитьПараметр("ТипКИ", Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.Пользователи.ПустаяСсылка();
	
КонецФункции

// Функция определяет внутренний или внешний звонок исходя
//  из длины переданного номера телефона, и максимальной длины внутреннего номера 
//  телефона установленного в системе. Если длина номера > 0 И <= макс, то это внутренний
//
// Параметры:
//	НомерТелефона						- Строка	- Номер телефона
//
// Возвращаемое значение:
//	Булево	- Признак внутреннего звонка
//
Функция сфпОпределитьВнутреннийЗвонокПоНомеруПриЗагрузке(НомерТелефона) Экспорт
	сфпПараметрыСервера = сфпПараметрыСервера();
	МаксимальнаяДлинаВнутреннегоНомера = сфпПараметрыСервера.МаксимальнаяДлинаВнутреннихНомеров;
	НомерТелефона = сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона);
	ДлинаНомераТелефона = СтрДлина(НомерТелефона);
	Возврат (ДлинаНомераТелефона <= МаксимальнаяДлинаВнутреннегоНомера);
КонецФункции // сфпОпределитьВнутреннийЗвонокПоНомеруПриЗагрузке()

// Функция формирует из строки подключения структуру параметров подключения
//
// Параметры:
//	СтрокаПодключения	- Строка	- Строка подключения
//
// Возвращаемое значение:
//	Структура	- Структура  параметров подключения
//
Функция сфпПолучитьСтруктуруПодключения(СтрокаПодключения) Экспорт
	СтруктураПодключения = Новый Структура;
	СтруктураПодключения.Вставить("Пользователь",	"");
	СтруктураПодключения.Вставить("Пароль",			"");
	СтруктураПодключения.Вставить("Сервер",			"");
	СтруктураПодключения.Вставить("Порт",			"");
	СтруктураПодключения.Вставить("БазаДанных",		"");
	СтруктураПодключения.Вставить("ТипПодключения",		"");
	Пока Истина Цикл
		ПозицияКонца	= СтрНайти(СтрокаПодключения, ";");
		ПозицияСередины	= СтрНайти(СтрокаПодключения, "=");
		Если ПозицияСередины = 0 Тогда Прервать; КонецЕсли;
		Ключ = Лев(СтрокаПодключения, ПозицияСередины - 1);
		Если ПозицияКонца = 0 Тогда
			Значение = Сред(СтрокаПодключения, ПозицияСередины + 1);
			СтрокаПодключения = "";
		Иначе	
			Значение = Сред(СтрокаПодключения, ПозицияСередины + 1, ПозицияКонца - ПозицияСередины - 1);	 
			СтрокаПодключения = Сред(СтрокаПодключения, ПозицияКонца + 1); 
		КонецЕсли;	
		Если Ключ = "Data Source" Тогда
			СтруктураПодключения.Сервер = Значение;
		ИначеЕсли Ключ = "Initial Catalog" Тогда
			СтруктураПодключения.БазаДанных = Значение;
		ИначеЕсли Ключ = "User ID" Тогда
			СтруктураПодключения.Пользователь = Значение;
		ИначеЕсли Ключ = "Password" Тогда
			СтруктураПодключения.Пароль = Значение;
		ИначеЕсли Ключ = "DBServerType" Тогда
			СтруктураПодключения.ТипПодключения = Значение;
		ИначеЕсли Ключ = "Port" Тогда
			СтруктураПодключения.Порт = Значение;
		КонецЕсли;	
	КонецЦикла;
	Возврат СтруктураПодключения;
КонецФункции // сфпПолучитьСтруктуруПодключения()	

// Процедура меняет статус у пропущенных звонков по номеру телефона на отвеченные
//
// Параметры:
//	НомерТелефона	- Строка	- Номер телефона
//
Процедура сфпЗавершитьПропущенныеЗвонки(НомерТелефона) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВерсияСофтФона = ИспользуемаяВерсияСофтФона();
	УдалятьНапоминания = (ВерсияСофтФона <> Перечисления.сфпВерсииСофтФон.СофтФотPROSTO);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ Звонок
	|ИЗ РегистрСведений.сфпИсторияЗвонков
	|ГДЕ НомерТелефона = &НомерТелефона И Звонок.сфпСостояниеЗвонка = ЗНАЧЕНИЕ(Перечисление.сфпСостоянияЗвонков.Пропущенный) И Входящий");
	Запрос.УстановитьПараметр("НомерТелефона", НомерТелефона);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗвонокОбъект = Выборка.Звонок.ПолучитьОбъект();
		ЗвонокОбъект.сфпСостояниеЗвонка	= Перечисления.сфпСостоянияЗвонков.Отвеченный;
		ЗвонокОбъект.сфпПерезвонили = Истина;
			
		Попытка
			ЗвонокОбъект.Записать();
		Исключение 
			ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка, , , 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
		КонецПопытки;
		
		Если УдалятьНапоминания Тогда
			// Удаляем напоминание о пропущенном звонке
			CRM_НапоминанияСервер.УдалитьНапоминание(Выборка.Звонок);
		КонецЕсли;
	КонецЦикла;

	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры // сфпЗавершитьПропущенныеЗвонки()	

// Процедура-обработчик события "ПриЗаписи" объектов СофтФона
//
// Параметры:
//	Источник 	- Объект	- Объект СофтФона, который записывается
//	Отказ		- Булево	- Признак отмены
//
Процедура сфпПриЗаписиОбъектаСофтФона(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	Если НЕ сфпИспользоватьСофтФон() Тогда
		Возврат;
	КонецЕсли;

	МетаданныеОбъекта = Источник.Ссылка.Метаданные();
	ИмяМетаданных = МетаданныеОбъекта.Имя;
	
	// Обновим регистр поиска по номерам
	Если Метаданные.ОпределяемыеТипы.сфпКонтактВзаимодействияОбъект.Тип.СодержитТип(ТипЗнч(Источник)) Тогда
		сфпЗаполнитьРегистрПоискаПоНомерам(Источник, Истина, Истина);
	КонецЕсли;
	
	// Заполним Бизнес-регион
	Если сфпИспользоватьАвтоопределениеБизнесРегиона() Тогда
		сфпЗаполнитьБизнесРегионПоНомеру(Источник);
	КонецЕсли;
	
	Если ИмяМетаданных = "CRM_Интерес" ИЛИ ИмяМетаданных = "CRM_СчетНаОплатуПокупателю" Тогда
		Если МетаданныеОбъекта.Реквизиты.Найти("Партнер") <> Неопределено Тогда
			ТелефонныйЗвонок = сфпВернутьОснованиеДокумента(Источник.Ссылка);
			
			Абонент = Источник.Партнер;
			Если МетаданныеОбъекта.Реквизиты.Найти("ПотенциальныйКлиент") <> Неопределено Тогда
				ЗаполнитьПотенциальнымКлиентом = НЕ ЗначениеЗаполнено(Источник.Партнер)
					 И ЗначениеЗаполнено(Источник.ПотенциальныйКлиент);
				Если ЗаполнитьПотенциальнымКлиентом Тогда
					Абонент = Источник.ПотенциальныйКлиент;
				КонецЕсли;
			КонецЕсли;
			
			Если (ТелефонныйЗвонок = Неопределено) И ИмяМетаданных = "CRM_Интерес" Тогда
				// Возможен вариант, когда интерес связан с телефонным звонком через поле "Предмет" в телефонном звонке.
				МассивЗвонков = сфпНайтиТелефонныйЗвонокПоРеквизитуПредмет(Источник.Ссылка);
				Если МассивЗвонков.КолИчество() = 0 Тогда
					Возврат;
				КонецЕсли;
				
				Для Каждого ЭлементМассива Из МассивЗвонков Цикл
					сфпСкорректироватьАбонентаУТелефонногоЗвонка(Источник.КонтактноеЛицо, Абонент, ЭлементМассива);
				КонецЦикла;

			Иначе
				сфпСкорректироватьАбонентаУТелефонногоЗвонка(Источник.КонтактноеЛицо, Абонент, ТелефонныйЗвонок);
			КонецЕсли;

		ИначеЕсли МетаданныеОбъекта.Реквизиты.Найти("Клиент") <> Неопределено Тогда
			ТелефонныйЗвонок = сфпВернутьОснованиеДокумента(Источник.Ссылка);
			сфпСкорректироватьАбонентаУТелефонногоЗвонка(Источник.КонтактноеЛицо, Источник.Клиент, ТелефонныйЗвонок);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура сфпПередЗаписьюОбъектаСофтФонаПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	Если НЕ сфпИспользоватьСофтФон() Тогда
		Возврат;
	КонецЕсли;

	МетаданныеОбъекта = Источник.Ссылка.Метаданные();
	ИмяМетаданных = МетаданныеОбъекта.Имя;
	
	Если ИмяМетаданных = "ТелефонныйЗвонок" Тогда
		Если НЕ ЗначениеЗаполнено(Источник.сфпИдентификаторЗаписи) Тогда
			Запрос = Новый Запрос("
			|ВЫБРАТЬ ДатаНачала, НомерТелефона, Звонок, ИдентификаторЗаписи
			|ИЗ РегистрСведений.сфпИсторияЗвонков
			|ГДЕ Звонок = &Звонок И ИдентификаторЗвонка = &ИдентификаторЗвонка И ПОДСТРОКА(ИдентификаторЗаписи, 1, 10) <> &ПустаяСтрока");
			Запрос.УстановитьПараметр("Звонок", Источник.Ссылка);
			Запрос.УстановитьПараметр("ИдентификаторЗвонка", Источник.сфпИдентификаторЗвонка);
			Запрос.УстановитьПараметр("ПустаяСтрока", "");
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.ИдентификаторЗаписи) Тогда
				Источник.сфпИдентификаторЗаписи = Выборка.ИдентификаторЗаписи;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

// Функция ищет среди документов "Телефонный звонок" те, в которых реквизит "Предмет" равен переданной ссылке
//
// Параметры:
//	ДокументСсылка	- ДокументСсылка - Предмет, по которому ищутся телефонные звонки
//
// Возвращаемое значение:
//	Массив	- Массив, состоящий из набора документов "Телефонный звонок"
//
Функция сфпНайтиТелефонныйЗвонокПоРеквизитуПредмет(ДокументСсылка)
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	|	ПредметыПапкиВзаимодействий.Взаимодействие.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
	|ГДЕ
	|	ПредметыПапкиВзаимодействий.Предмет = &Предмет
	|	И ТИПЗНАЧЕНИЯ(ПредметыПапкиВзаимодействий.Взаимодействие) = ТИП(Документ.ТелефонныйЗвонок)";
	
	Запрос.УстановитьПараметр("Предмет" , ДокументСсылка);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
КонецФункции	

Функция сфпРучнаяЗагрузкаЗвонковВФоне(ПараметрыПроцедуры) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ручная загрузка звонков'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне("сфпСофтФонПроСервер.сфпРучнаяЗагрузкаЗвонков",
		ПараметрыПроцедуры,	ПараметрыВыполнения);
	Возврат ДлительнаяОперация;
КонецФункции

Процедура сфпРучнаяЗагрузкаЗвонков(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	Результат = Новый Структура;
	
	ДатаНачалаВыгрузки = ПараметрыПроцедуры.ДатаНачалаВыгрузки;
	ДатаОкончанияВыгрузки = ПараметрыПроцедуры.ДатаОкончанияВыгрузки;
	Ресурс = ПараметрыПроцедуры.Ресурс;
	ApiKey = ПараметрыПроцедуры.ApiKey;
	ApiSalt = ПараметрыПроцедуры.ApiSalt;
	ТипВатс = ПараметрыПроцедуры.ТипВатс;
	Токен = ПараметрыПроцедуры.Токен;
	
	Если ТипВатс = 1 Тогда
		
		КлючОтчета = ПолучитьКлюч(ДатаНачалаВыгрузки, ДатаОкончанияВыгрузки, ApiKey, ApiSalt);
		Если КлючОтчета = "" Тогда
			Возврат;
		КонецЕсли;
			
		HTTPОтвет = ПолучитьОтветПоКлючу(КлючОтчета, ApiKey, ApiSalt);
		СостояниеОтвета = HTTPОтвет.КодСостояния;

		Если СостояниеОтвета <> 200 И СостояниеОтвета <> 204 Тогда
			ЗаписьЖурналаРегистрации("Ручная загрузка звонков", УровеньЖурналаРегистрации.Информация, , ,
				 "Не удалось получить ответ. Код Состояния = " 
				+ СостояниеОтвета);
			Возврат;
		КонецЕсли;
		Если СостояниеОтвета = 200 Тогда
			ОбработкаОтвета(HTTPОтвет);
			Возврат;
		КонецЕсли;
	ИначеЕсли ТипВатс = 2 Тогда
		
		Для СтраницаВыборки = 0 По 99 Цикл
			
			HTTPОтвет = ВыполнитьЗапросБилайн(Ресурс, ДатаНачалаВыгрузки, ДатаОкончанияВыгрузки, Токен, СтраницаВыборки);
			
			СостояниеОтвета = HTTPОтвет.КодСостояния;
			
			Если СостояниеОтвета <> 200 И СостояниеОтвета <> 204 Тогда
				ЗаписьЖурналаРегистрации("Ручная загрузка звонков", УровеньЖурналаРегистрации.Информация, , ,
					 "Не удалось получить ответ. Код Состояния = " 
					+ СостояниеОтвета);
				Возврат;
			КонецЕсли;
			Если СостояниеОтвета = 200 Тогда
				СоздатьДокументыБилайн(HTTPОтвет.ПолучитьТелоКакСтроку());
				Возврат;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипВатс = 3 Тогда
		
		АдресСервера = ПараметрыПроцедуры.АдресСервера;
		HTTPОтвет = ВыполнитьЗапросМегафон(Ресурс, АдресСервера, ДатаНачалаВыгрузки,
			 ДатаОкончанияВыгрузки, Токен,
			 СтраницаВыборки);
		
		СостояниеОтвета = HTTPОтвет.КодСостояния;
		
		Если СостояниеОтвета <> 200 И СостояниеОтвета <> 204 Тогда
			ЗаписьЖурналаРегистрации("Ручная загрузка звонков", УровеньЖурналаРегистрации.Информация, , ,
				 "Не удалось получить ответ. Код Состояния = " 
				+ СостояниеОтвета);
			Возврат;
		КонецЕсли;
		Если СостояниеОтвета = 200 Тогда
			СоздатьДокументыМегафон(HTTPОтвет.ПолучитьТелоКакСтроку());
			Возврат;
		КонецЕсли;
	
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

Функция ПолучитьКлюч(ДатаНачала, ДатаОкончания, ApiKey, ApiSalt)
	
	Ресурс = "stats/request";
	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("date_from", ДатуВTimestamp(ДатаНачала));
	ПараметрыJSON.Вставить("date_to", ДатуВTimestamp(ДатаОкончания));
	ПараметрыJSON.Вставить("fields", "records, start, finish, answer, from_extension, from_number,
		| to_extension, to_number, disconnect_reason, line_number, location, create,
		| entry_id");
	
	HTTPОтвет = ВыполнитьЗапросМанго(Ресурс, ПараметрыJSON, ApiKey, ApiSalt); 
	Если HTTPОтвет.КодСостояния <> 200 Тогда
		ЗаписьЖурналаРегистрации("Ручная загрузка звонков", УровеньЖурналаРегистрации.Информация, , ,
			 "Ошибка, ключ не получен. Код Состояния = " 
			+ HTTPОтвет.КодСостояния);
		Возврат "";
	КонецЕсли;
	
	ТекстОтвета = HTTPОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	Данные = Неопределено;
		
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстОтвета);
	Данные = ПрочитатьJSON(ЧтениеJSON, Истина);
	ЧтениеJSON.Закрыть();
	
	ПолученныйКлюч = Данные.Получить("key");
	Возврат ПолученныйКлюч;
	
КонецФункции

Функция ПолучитьОтветПоКлючу(Ключ, ApiKey, ApiSalt)
	
	Ресурс = "stats/result/";
	
	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("key", Ключ);
	
	HTTPОтвет = ВыполнитьЗапросМанго(Ресурс, ПараметрыJSON, ApiKey, ApiSalt);
	Возврат HTTPОтвет;
	
КонецФункции

Процедура ОбработкаОтвета(HTTPОтвет)
	
	РезультатТекст = HTTPОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	
	СоздатьДокументыМанго(РезультатТекст);
	
КонецПроцедуры

Функция ВыполнитьЗапросМанго(АдресРесурса, ПараметрыЗапроса, ApiKey, ApiSalt)

	Сервер = "app.mango-office.ru/vpbx/";

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	ЗаписатьJSON(ЗаписьJSON, ПараметрыЗапроса);
	СтрокаJSON = ЗаписьJSON.Закрыть();
	
	Sign = сфпСофтФонПроСервер.ПолучитьSign(ApiKey, СтрокаJSON, ApiSalt);
	
	ПараметрыТела = Новый Массив;
	ПараметрыТела.Добавить("vpbx_api_key=" + КодироватьСтроку(ApiKey, СпособКодированияСтроки.URLВКодировкеURL));
	ПараметрыТела.Добавить("sign=" + КодироватьСтроку(Sign, СпособКодированияСтроки.URLВКодировкеURL));
	ПараметрыТела.Добавить("json=" + КодироватьСтроку(СтрокаJSON, СпособКодированияСтроки.URLВКодировкеURL));
	
	СтрокаЗапроса = сфпОбщегоНазначения.сфпСтрСоединить(ПараметрыТела, "&");
	
	URL = Сервер + АдресРесурса;
	СтруктураURI = сфпОбщегоНазначенияКлиентСервер.СтруктураURI(URL);
	
	HTTPЗапрос = Новый HTTPЗапрос();
	HTTPЗапрос.АдресРесурса = АдресРесурса;
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаЗапроса,
		КодировкаТекста.UTF8,
		ИспользованиеByteOrderMark.НеИспользовать
	);
	
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(
		Сервер,
		СтруктураURI.Порт, , , Прокси, 60,
		Новый ЗащищенноеСоединениеOpenSSL);
	
	Попытка
		
		Ответ = HTTPСоединение.ВызватьHTTPМетод("POST", HTTPЗапрос);
		
	Исключение
		ЗаписьЖурналаРегистрации("Ручная загрузка звонков", УровеньЖурналаРегистрации.Информация, , ,
			 "Не удалось отправить запрос." 
			+ ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	Возврат Ответ;
	
КонецФункции

Процедура СоздатьДокументыМанго(Результат)
	
	МассивРезультатов = СтрРазделить(Результат, Символы.ПС, Ложь);
	СчетчикЗвонков = 0;
	
	Для Индекс = 0 По МассивРезультатов.Количество() - 1 Цикл
		Строка = МассивРезультатов[Индекс];
		МассивСтрок = СтрРазделить(Строка, ";", Истина);
		
		// RecordID - 0
		// DisconnectReason - 8
		// LineNumber - 9
		// Entry_ID - 11
		StartTime   = TimestampВДату(МассивСтрок[1]);
		DropTime  = TimestampВДату(МассивСтрок[2]);
		AnswerTime  = ?(МассивСтрок[3] = 0, Дата('00010101'), TimestampВДату(МассивСтрок[3]));
		from_extension =  МассивСтрок[4];
		fromnumber = МассивСтрок[5];
		to_extension = МассивСтрок[6];
		tonumber = МассивСтрок[7];
		location = МассивСтрок[10];
		
		call_Id = МассивСтрок[11] + МассивСтрок[1] + МассивСтрок[9];
		
		СчетчикЗвонков = СчетчикЗвонков + 1;
		
		Если НРег(location) = "abonent" Тогда
			Если Не fromnumber = "" И Не to_extension = "" Тогда
				ЭтоВходящийЗвонок = Истина;
				НомерТелефонаВнутренний = to_extension;
			ИначеЕсли  Не from_extension = "" И Не tonumber = "" Тогда
				ЭтоВходящийЗвонок = Ложь;
				НомерТелефонаВнутренний = from_extension;
			Иначе
				Продолжить;
			КонецЕсли;
			
			НомерТелефона = ?(ЭтоВходящийЗвонок, fromnumber, tonumber);
			ИдентификаторЗвонка = call_Id;
			
			СоздатьДокументЗагрузки( StartTime, AnswerTime, DropTime, НомерТелефона, НомерТелефонаВнутренний,
				 , ЭтоВходящийЗвонок,
				 ИдентификаторЗвонка);
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

Функция ВыполнитьЗапросБилайн(АдресРесурса, ДатаНачала, ДатаОкончания, Токен, СтраницаВыборки)
	
	Сервер = "cloudpbx.beeline.ru";
	
	ПараметрыТела = Новый Массив;
	ПараметрыТела.Добавить("?dateFrom=" + Формат(ДатаНачала, "ДФ=yyyy-MM-dd"));
	ПараметрыТела.Добавить("dateTo=" + Формат(ДатаОкончания, "ДФ=yyyy-MM-dd"));
	ПараметрыТела.Добавить("page=" + КодироватьСтроку(СтраницаВыборки, СпособКодированияСтроки.URLВКодировкеURL));
	ПараметрыТела.Добавить("pageSize=" + КодироватьСтроку("100", СпособКодированияСтроки.URLВКодировкеURL));
	
	СтрокаЗапроса = сфпОбщегоНазначения.сфпСтрСоединить(ПараметрыТела, "&");
	
	URL = Сервер + АдресРесурса;
	
	СтруктураURI = сфпОбщегоНазначенияКлиентСервер.СтруктураURI(URL);
	
	HTTPЗапрос = Новый HTTPЗапрос();
	HTTPЗапрос.АдресРесурса = "/apis/portal/statistics" + СтрокаЗапроса;
	HTTPЗапрос.Заголовки.Вставить("X-MPBX-API-AUTH-TOKEN", Токен);
	
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(
		Сервер,
		СтруктураURI.Порт, , , Прокси, 60,
		Новый ЗащищенноеСоединениеOpenSSL);
	
	Попытка
		
		Ответ = HTTPСоединение.ВызватьHTTPМетод("GET", HTTPЗапрос);
		
	Исключение
		ЗаписьЖурналаРегистрации("Ручная загрузка звонков", УровеньЖурналаРегистрации.Информация, , ,
			 "Не удалось отправить запрос." 
			+ ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	Возврат Ответ;
	
КонецФункции

Процедура СоздатьДокументыБилайн(Результат)
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(Результат);
	МассивОтвета = ПрочитатьJSON(ЧтениеОтвета, Ложь);
	
	СчетчикЗвонков = 0;
	
	Для каждого Структура Из МассивОтвета Цикл
		
		StartTime = TimeUnixВДату(Структура.startDate);
		Продолжительность = Окр(Структура.duration / 1000, 0);
		DropTime = StartTime + Продолжительность;
		Если Продолжительность > 0 Тогда
			AnswerTime = StartTime;
		Иначе
			AnswerTime = Дата('00010101');
		КонецЕсли;
		// Структура.abonent.lastname;
		extension = Структура.abonent.extension;
		phone = Структура.phone;
		direction = Структура.direction;
		
		call_Id = Структура.startDate + Структура.abonent.extension;
		
		Если direction = "INBOUND" Тогда
			ЭтоВходящийЗвонок = Истина;
		ИначеЕсли direction = "OUTBOUND" Тогда
			ЭтоВходящийЗвонок = Ложь;
		КонецЕсли;
		
		НомерТелефонаВнутренний = extension;
		НомерТелефона = phone;
		ИдентификаторЗвонка = call_Id;
		
		СчетчикЗвонков = СчетчикЗвонков + 1;
		
		СоздатьДокументЗагрузки( StartTime, AnswerTime, DropTime, НомерТелефона, НомерТелефонаВнутренний,
			 , ЭтоВходящийЗвонок,
			 ИдентификаторЗвонка);
		
	КонецЦикла;
		
КонецПроцедуры

Функция ВыполнитьЗапросМегафон(АдресРесурса, АдресСервера, ДатаНачала, ДатаОкончания, Токен, СтраницаВыборки)
	
	Данные = Новый Структура;
	
	Данные.Вставить("start", ЗаписатьДатуJSON(НачалоДня(ДатаНачала), ФорматДатыJSON.ISO) + "Z");
	Данные.Вставить("end", ЗаписатьДатуJSON(КонецДня(ДатаОкончания), ФорматДатыJSON.ISO) + "Z");
	Данные.Вставить("type", "all");
	
	ПараметрыТела = Новый Массив;
	ПараметрыТела.Добавить("?type=" + "all");
	ПараметрыТела.Добавить("start=" + ЗаписатьДатуJSON(НачалоДня(ДатаНачала), ФорматДатыJSON.ISO) + "Z");
	ПараметрыТела.Добавить("end=" + ЗаписатьДатуJSON(КонецДня(ДатаОкончания), ФорматДатыJSON.ISO) + "Z");
	
	СтрокаЗапроса = сфпОбщегоНазначения.сфпСтрСоединить(ПараметрыТела, "&");
	
	URL = АдресСервера + АдресРесурса;
	СтруктураURI = сфпОбщегоНазначенияКлиентСервер.СтруктураURI(URL);
	
	HTTPЗапрос = Новый HTTPЗапрос();
	HTTPЗапрос.АдресРесурса = "/crmapi/v1/history/json" + СтрокаЗапроса;
	HTTPЗапрос.Заголовки.Вставить("X-API-KEY", Токен);
	
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(
		АдресСервера,
		СтруктураURI.Порт, , , Прокси, 60,
		Новый ЗащищенноеСоединениеOpenSSL);
	
	Попытка
		
		Ответ = HTTPСоединение.ВызватьHTTPМетод("GET", HTTPЗапрос);
		
	Исключение
		ЗаписьЖурналаРегистрации("Ручная загрузка звонков", УровеньЖурналаРегистрации.Информация, , ,
			 "Не удалось отправить запрос." 
			+ ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	Возврат Ответ;
	
КонецФункции

Процедура СоздатьДокументыМегафон(Результат)
	
	Если Результат = "null" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Нет записей за указанный период!';
			|en='There are no entries for the specified period!'"));
		Возврат;
	КонецЕсли;
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(Результат);
	МассивОтвета = ПрочитатьJSON(ЧтениеОтвета, Ложь);
	
	СчетчикЗвонков = 0;
	
	Для каждого Структура Из МассивОтвета Цикл
		
		StartTime = ПрочитатьДатуJSON(Структура.start, ФорматДатыJSON.ISO);
		Если Не Структура.Свойство("duration") Тогда
			Продолжить;
		КонецЕсли;
		AnswerTime = StartTime + Структура.Wait;
		DropTime = StartTime + Структура.duration + Структура.Wait;
		phone = Структура.client;
		ТелефонПользователя = Структура.diversion;
		direction = Структура.type;
		ИдентификаторЗаписи = "";
		Если Структура.Свойство("record") Тогда
			ИдентификаторЗаписи = Структура.record;
		КонецЕсли;
		call_Id = Структура.Uid;
		
		СчетчикЗвонков = СчетчикЗвонков + 1;
		
		Если direction = "in" Тогда
			ЭтоВходящийЗвонок = Истина;
		ИначеЕсли direction = "out" Тогда
			ЭтоВходящийЗвонок = Ложь;
		КонецЕсли;
		
		НомерТелефонаВнутренний = "";
		НомерТелефона = phone;
		ИдентификаторЗвонка = call_Id;
		
		СоздатьДокументЗагрузки( StartTime, AnswerTime, DropTime, НомерТелефона, НомерТелефонаВнутренний,
			 ТелефонПользователя, ЭтоВходящийЗвонок, ИдентификаторЗвонка,
			 ИдентификаторЗаписи);
		
	КонецЦикла;
		
КонецПроцедуры

Процедура СоздатьДокументЗагрузки( ДатаНачала, ДатаОтвета, ДатаОкончанияЗвонка, НомерТелефона,
	 НомерТелефонаВнутренний, ТелефонПользователя = "", ЭтоВходящийЗвонок, ИдентификаторЗвонка,
	 ИдентификаторЗаписи = Неопределено)
	
	ДействиеПриОшибке = Константы.сфпДействиеПриОшибкеЗагрузкиИсторииЗвонков.Получить();
	ПрерватьВыполнениеПриОшибке =
		(ДействиеПриОшибке = Перечисления.сфпДействияПриОшибкеЗагрузкиИсторииЗвонков.ПрерватьВыполнение);
	ПовторитьВыполнениеПриОшибке =
		(ДействиеПриОшибке = Перечисления.сфпДействияПриОшибкеЗагрузкиИсторииЗвонков.ПовторитьВыполнение);
	
	НеЗагружатьЗвонкиДлительностьюМенее = Константы.сфпНеЗагружатьЗвонкиДлительностьюМенее.Получить();
	
	ВнутреннийЗвонок = сфпОпределитьВнутреннийЗвонокПоНомеруПриЗагрузке(НомерТелефона);
	Если ВнутреннийЗвонок Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийАвторизованныйПользователь = Пользователи.ТекущийПользователь();
	ПользовательДляОповещений = Константы.сфпПользовательДляОповещений.Получить();
	Если НЕ ЗначениеЗаполнено(ПользовательДляОповещений) Тогда
		ПользовательДляОповещений = ТекущийАвторизованныйПользователь;
	КонецЕсли;
	
	ВнутренниеНомераИБ = Константы.сфпВнутренниеНомераИнформационнойБазы.Получить();
	МассивВнутреннихНомеровИБ = Новый Массив();
	МассивНомеров = сфпОбщегоНазначенияКлиентСервер.сфпРазложитьСтрокуВМассивПодстрок(ВнутренниеНомераИБ, ",");
	Для Каждого ТекНомер Из МассивНомеров Цикл
		ПозицияРазделителя = СтрНайти(ТекНомер, "-");
		
		Попытка
			Если ПозицияРазделителя > 0 Тогда
				НачалоДиапазона = Число(СокрЛП(Лев(ТекНомер, ПозицияРазделителя - 1)));
				КонецДиапазона = Число(СокрЛП(Сред(ТекНомер, ПозицияРазделителя + 1)));
				Для к = НачалоДиапазона По КонецДиапазона Цикл
					МассивВнутреннихНомеровИБ.Добавить(Формат(к, "ЧГ="));
				КонецЦикла;
				
			Иначе
				МассивВнутреннихНомеровИБ.Добавить(Формат(Число(СокрЛП(ТекНомер)), "ЧГ="));
			КонецЕсли;
		Исключение
			ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка, , , 
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
		ЗапросРегистра = Новый Запрос("
		|ВЫБРАТЬ ДатаНачала, НомерТелефона, Звонок
		|ИЗ РегистрСведений.сфпИсторияЗвонков
		|ГДЕ ИдентификаторЗвонка = &ИдентификаторЗвонка И ДатаНачала МЕЖДУ &НачалоПериода И &КонецПериода");
		
		ИдентификаторЗвонка = СтрЗаменить(Формат(ИдентификаторЗвонка, "ЧГ="), ",", Символы.НПП);
		
		ЗапросРегистра.УстановитьПараметр("ИдентификаторЗвонка", ИдентификаторЗвонка);
		ЗапросРегистра.УстановитьПараметр("НачалоПериода", ДатаНачала - 300);
		ЗапросРегистра.УстановитьПараметр("КонецПериода", ДатаНачала + 300);
			
			ВыборкаРегистра = ЗапросРегистра.Выполнить().Выбрать();
			Если ВыборкаРегистра.Следующий() Тогда
				ЗаписьРегистра = РегистрыСведений.сфпИсторияЗвонков.СоздатьМенеджерЗаписи();
				ЗаписьРегистра.ДатаНачала = ВыборкаРегистра.ДатаНачала;
				ЗаписьРегистра.НомерТелефона = ВыборкаРегистра.НомерТелефона;
				ЗаписьРегистра.Звонок = ВыборкаРегистра.Звонок;
				ЗаписьРегистра.Прочитать();

				ТребуетсяЗаписьРегистра = Ложь;

			Иначе
				Если сфпНайтиЗвонокВРегистреПоНомеруПриЗагрузкеИстории(ДатаНачала, НомерТелефона) Тогда
					// Звонок уже есть в регистре
					Возврат;
				КонецЕсли;
				
				ЗаписьРегистра = РегистрыСведений.сфпИсторияЗвонков.СоздатьМенеджерЗаписи();
				ЗаписьРегистра.ДатаНачала = ДатаНачала;
				ЗаписьРегистра.НомерТелефона = НомерТелефона;
				ЗаписьРегистра.Звонок = Документы.ТелефонныйЗвонок.ПустаяСсылка();
				
				ТребуетсяЗаписьРегистра = Истина;
			КонецЕсли;
			
			ДатаОкончанияЗвонка = ?(ДатаОкончанияЗвонка = Дата('00010101'), ДатаНачала, ДатаОкончанияЗвонка);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ДатаОкончания", ДатаОкончанияЗвонка, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ДатаОтвета", ДатаОтвета, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "Входящий", ЭтоВходящийЗвонок, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ИдентификаторЗвонка", ИдентификаторЗвонка, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ВнутреннийНомер", НомерТелефонаВнутренний, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "ИдентификаторЗаписи", ИдентификаторЗаписи, ТребуетсяЗаписьРегистра);
			
			Если НеЗагружатьЗвонкиДлительностьюМенее > 0
				 И ЗначениеЗаполнено(ЗаписьРегистра.ДатаОкончания) Тогда // фильтр установлен
				Если ЗначениеЗаполнено(ЗаписьРегистра.ДатаОтвета) И Не ЗаписьРегистра.ДатаОтвета = Дата('00010101') Тогда
					ДлительностьРазговора = ЗаписьРегистра.ДатаОкончания - ЗаписьРегистра.ДатаОтвета;
				    Если ДлительностьРазговора < НеЗагружатьЗвонкиДлительностьюМенее Тогда
						Возврат;
					КонецЕсли;
					
				ИначеЕсли ЗначениеЗаполнено(ЗаписьРегистра.ДатаНачала) Тогда
					ДлительностьРазговора = ЗаписьРегистра.ДатаОкончания - ЗаписьРегистра.ДатаНачала;
				    Если ДлительностьРазговора < НеЗагружатьЗвонкиДлительностьюМенее Тогда
						Возврат;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			// Проверяем наличие уже созданного ранее документа телефонный звонок по данному звонку
			Если ЗначениеЗаполнено(ЗаписьРегистра.Звонок) Тогда
				НайденныйЗвонок = ЗаписьРегистра.Звонок;
			Иначе	
				НайденныйЗвонок = сфпНайтиПоследнийЗвонокПоНомеру(ЗаписьРегистра.НомерТелефона, ЗаписьРегистра.ДатаНачала);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НайденныйЗвонок) Тогда
				// Обновляем существующий звонок
				ЗвонокОбъект = НайденныйЗвонок.ПолучитьОбъект();
				ТребуетсяЗаписьЗвонка = Ложь;
				
			Иначе	
				// Создаем новый телефонный звонок
				ЗвонокОбъект = Документы.ТелефонныйЗвонок.СоздатьДокумент();
				ЗвонокОбъект.Дата = ЗаписьРегистра.ДатаНачала;
				ТребуетсяЗаписьЗвонка = Истина;
			КонецЕсли;
			
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "Входящий", ЗаписьРегистра.Входящий, ТребуетсяЗаписьЗвонка);
						
			Если НЕ ЗначениеЗаполнено(ЗвонокОбъект.Важность) Тогда
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "Важность",
					 Перечисления.ВариантыВажностиВзаимодействия.Обычная,
					 ТребуетсяЗаписьЗвонка);
			КонецЕсли;
			
			ТемаЗвонка = сфпЗаполнитьТемуТелефонногоЗвонка(ЗвонокОбъект.Входящий, ЗвонокОбъект.Дата);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "Тема", ТемаЗвонка, ТребуетсяЗаписьЗвонка);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "АбонентКакСвязаться", НомерТелефона, ТребуетсяЗаписьЗвонка);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "сфпИдентификаторЗвонка",
				 ЗаписьРегистра.ИдентификаторЗвонка,
				 ТребуетсяЗаписьЗвонка);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "сфпИдентификаторЗаписи",
				 ЗаписьРегистра.ИдентификаторЗаписи,
				 ТребуетсяЗаписьЗвонка);
			
			ДлительностьЗвонка = ЗаписьРегистра.ДатаОкончания - ЗаписьРегистра.ДатаНачала;
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "сфпДлительностьЗвонка", ДлительностьЗвонка, ТребуетсяЗаписьЗвонка);
			
			СостояниеЗвонка = ?(ЗаписьРегистра.ДатаОтвета = Дата('00010101'),
				 Перечисления.сфпСостоянияЗвонков.Пропущенный,
				 Перечисления.сфпСостоянияЗвонков.Отвеченный);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "сфпСостояниеЗвонка", СостояниеЗвонка, ТребуетсяЗаписьЗвонка);
			
			Если ЗначениеЗаполнено(НомерТелефонаВнутренний) Тогда
				НайденныйОтветственный = сфпНайтиОтветственного(ЗаписьРегистра.ВнутреннийНомер);
			Иначе
				НайденныйОтветственный = сфпНайтиОтветственногоПоТелефону(ТелефонПользователя);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НайденныйОтветственный) Тогда
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "Ответственный", НайденныйОтветственный, ТребуетсяЗаписьЗвонка);
			КонецЕсли;
						
			ОписаниеЗвонка = сфпЗаполнитьОписаниеТелефонногоЗвонка(ЗвонокОбъект.сфпДлительностьЗвонка);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "Описание", ОписаниеЗвонка, ТребуетсяЗаписьЗвонка);
						
			ОписаниеЗвонка = "";
				
			МассивЗвонящих = сфпСофтФонПроСервер.сфпНайтиОбъектВРегистреПоТелефону(ЗаписьРегистра.НомерТелефона);
			Если МассивЗвонящих.Количество() = 0 Тогда
				АбонентПредставление = НСтр("ru='!!!Не определен!!!';en='!!!Indefined!!!'");

			ИначеЕсли МассивЗвонящих.Количество() = 1 Тогда
				АбонентКонтакт = МассивЗвонящих[0];
				АбонентПредставление = сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(АбонентКонтакт);
				
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "АбонентКонтакт", АбонентКонтакт, ТребуетсяЗаписьЗвонка);
				
				Если ЗаписьРегистра.Входящий Тогда
					Если НЕ ЗначениеЗаполнено(ЗвонокОбъект.Ответственный) Тогда
						Если ТипЗнч(АбонентКонтакт) = Тип("СправочникСсылка.Партнеры") Тогда
							УстановитьЗначениеРеквизита(ЗвонокОбъект, "Ответственный", АбонентКонтакт.ОсновнойМенеджер,
								 ТребуетсяЗаписьЗвонка);
							Если ЗначениеЗаполнено(ЗвонокОбъект.Ответственный) Тогда
								ТекстОписания = Нстр("ru='Ответственный определен по закреплению менеджера за клиентом';
									|en='The manager is responsible for assigning the manager to customer'");
								ОписаниеЗвонка = ОписаниеЗвонка + Символы.ПС + Символы.ПС + ТекстОписания;
							КонецЕсли;

						ИначеЕсли ТипЗнч(АбонентКонтакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
							ВладелецКонтакта = сфпСофтФонПроСервер.сфпПолучитьВладельцаКонтакта(АбонентКонтакт);
							УстановитьЗначениеРеквизита(ЗвонокОбъект, "Ответственный", ВладелецКонтакта.ОсновнойМенеджер,
								 ТребуетсяЗаписьЗвонка);
							Если ЗначениеЗаполнено(ЗвонокОбъект.Ответственный) Тогда
								ТекстОписания = Нстр("ru='Ответственный определен по закреплению менеджера за клиентом';
									|en='The manager is responsible for assigning the manager to customer'");
								ОписаниеЗвонка = ОписаниеЗвонка + Символы.ПС + Символы.ПС + ТекстОписания;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			
			Иначе
				
				АбонентКонтакт = МассивЗвонящих[0];
				АбонентПредставление = сфпСофтФонПроСервер.сфпПолучитьНаименованиеКонтакта(АбонентКонтакт);
				
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "АбонентКонтакт", АбонентКонтакт, ТребуетсяЗаписьЗвонка);
				
				СписокЗвонящих = сфпСофтФонПроСервер.сфпСформироватьСписокОбъектовДляВыбораПоМассивуЗвонящих(МассивЗвонящих);
				Если СписокЗвонящих.Количество() > 0 Тогда
					СтрокаДобавки = Нстр("ru='Совпадения по номеру ';en='Matches by number'") + ЗаписьРегистра.НомерТелефона 
						+ Нстр("ru=':'");
					ОписаниеЗвонка = ?(ЗначениеЗаполнено(ОписаниеЗвонка), ОписаниеЗвонка + Символы.ПС + Символы.ПС 
						+ СтрокаДобавки,
						 СтрокаДобавки);
				КонецЕсли;
				Для Каждого ЭлементСписка Из СписокЗвонящих Цикл
					СтрокаПредставления = ЭлементСписка.Представление;
					ОписаниеЗвонка = ОписаниеЗвонка + Символы.ПС + СтрокаПредставления;
				КонецЦикла;
			КонецЕсли;	
			
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "АбонентПредставление", АбонентПредставление, ТребуетсяЗаписьЗвонка);
			
			Если НЕ ПустаяСтрока(ОписаниеЗвонка) Тогда
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "Описание", ОписаниеЗвонка, ТребуетсяЗаписьЗвонка);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ЗвонокОбъект.Ответственный) Тогда
				УстановитьЗначениеРеквизита(ЗвонокОбъект, "Ответственный", ПользовательДляОповещений, ТребуетсяЗаписьЗвонка);
			КонецЕсли;
			
			АвторЗвонка = ?(ЗначениеЗаполнено(ЗвонокОбъект.Ответственный), ЗвонокОбъект.Ответственный,
				 ТекущийАвторизованныйПользователь);
			УстановитьЗначениеРеквизита(ЗвонокОбъект, "Автор", АвторЗвонка, ТребуетсяЗаписьЗвонка);
			
			Попытка
				Если ТребуетсяЗаписьЗвонка Тогда
					ЗвонокОбъект.Записать();
				КонецЕсли;
			Исключение
				СтрОшибки = ОписаниеОшибки();
				СтрОшибки = НСтр("ru='Не удалось записать Телефонный звонок';en='Unable to record Phone call'") + ": " + СтрОшибки;
								
				Если ПрерватьВыполнениеПриОшибке Тогда
					ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
						 УровеньЖурналаРегистрации.Ошибка, , ,
						 СтрОшибки);
					ВызватьИсключение СтрОшибки;
					
				ИначеЕсли ПовторитьВыполнениеПриОшибке Тогда
					УспешнаяЗапись = Ложь;
					
					Для к = 1 По 3 Цикл
						Попытка
							ЗвонокОбъект.Записать();
							УспешнаяЗапись = Истина;
							Прервать;
						Исключение
							СтрОшибки = ОписаниеОшибки();
							СтрОшибки = НСтр("ru='Не удалось записать Телефонный звонок';en='Unable to record Phone call'") + ": " 
								+ СтрОшибки;
						КонецПопытки;	
					КонецЦикла;
					
					Если НЕ УспешнаяЗапись Тогда
						ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
							 УровеньЖурналаРегистрации.Ошибка, , ,
							 СтрОшибки);
						ВызватьИсключение СтрОшибки;
					КонецЕсли;
					
				Иначе
					ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
						 УровеньЖурналаРегистрации.Ошибка, , ,
						 СтрОшибки);
				КонецЕсли;	
			КонецПопытки;
			
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "Звонок", ЗвонокОбъект.Ссылка, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "Ответственный", ЗвонокОбъект.Ответственный, ТребуетсяЗаписьРегистра);
			УстановитьЗначениеРеквизита(ЗаписьРегистра, "АбонентКонтакт", ЗвонокОбъект.АбонентКонтакт, ТребуетсяЗаписьРегистра);

			// Записываем новую запись регистра
			Попытка
				Если ТребуетсяЗаписьРегистра Тогда
					ЗаписьРегистра.Записать();
				КонецЕсли;
			Исключение
				СтрОшибки = ОписаниеОшибки();
				СтрОшибки = НСтр("ru='Не удалось сформировать запись регистра Истории звонков';en='Could not generate the History of History register'") 
					+ ": " 
					+ СтрОшибки;
								
				Если ПрерватьВыполнениеПриОшибке Тогда
					ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
						 УровеньЖурналаРегистрации.Ошибка, , ,
						 СтрОшибки);
					ВызватьИсключение СтрОшибки;
					
				ИначеЕсли ПовторитьВыполнениеПриОшибке Тогда
					УспешнаяЗапись = Ложь;
					
					Для к = 1 По 3 Цикл
						Попытка
							ЗвонокОбъект.Записать();
							УспешнаяЗапись = Истина;
							Прервать;
						Исключение
							СтрОшибки = ОписаниеОшибки();
							СтрОшибки = НСтр("ru='Не удалось записать Телефонный звонок';en='Unable to record Phone call'") + ": " 
								+ СтрОшибки;
						КонецПопытки;	
					КонецЦикла;
					
					Если НЕ УспешнаяЗапись Тогда
						ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
							 УровеньЖурналаРегистрации.Ошибка, , ,
							 СтрОшибки);
						ВызватьИсключение СтрОшибки;
					КонецЕсли;
					
				Иначе
					ЗаписьЖурналаРегистрации(НСтр("ru='Получение истории звонков';en='Receiving call history'"),
						 УровеньЖурналаРегистрации.Ошибка, , ,
						 СтрОшибки);
				КонецЕсли;
			КонецПопытки;
			
			Если ЗвонокОбъект.сфпСостояниеЗвонка = Перечисления.сфпСостоянияЗвонков.Отвеченный Тогда
				// Отмечаем все неотвеченные звонки по данному номеру, как отвеченные
				сфпЗавершитьПропущенныеЗвонки(ЗаписьРегистра.НомерТелефона);
			КонецЕсли;
			
КонецПроцедуры // СоздатьДокументЗагрузки()

////////////////////////////////////////////////////////////////////////////////
// КОНТЕКСТНАЯ РЕКЛАМА COMAGIC

// Функция проверяет использование CoMagic
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Признак использования Comagic
//
Функция сфпИспользоватьCoMagic() Экспорт
	Возврат Константы.сфпИспользоватьCoMagic.Получить();
КонецФункции // сфпИспользоватьCoMagic()

// Функция проверяет наличие маркетингового мероприятия, и если его нет, то его создает
//
// Параметры:
//	Сontext	- Строка - Название маркетинговой компании
//
// Возвращаемое значение:
//	СправочникСсылка	- Маркетинговое мероприятие
//
Функция сфпНайтиМаркетинговоеМероприятие(Сontext) Экспорт
	
	МаркетинговоеМероприятиеСсылка = Справочники.МаркетинговыеМероприятия.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(Сontext) Тогда
		// +CRM
		//МаркетинговоеМероприятиеСсылка = Справочники.МаркетинговыеМероприятия.НайтиПоНаименованию(Сontext, Истина);
		
		// Ищем Маркетинговое мероприятие с ID (по сквозной аналитике)
		ПереданнаяКампания = СокрЛП(Сontext);
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	МаркетинговыеМероприятияДополнительныеРеквизиты.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.МаркетинговыеМероприятия.ДополнительныеРеквизиты КАК МаркетинговыеМероприятияДополнительныеРеквизиты
		               |ГДЕ
		               |	МаркетинговыеМероприятияДополнительныеРеквизиты.Свойство = &Свойство
		               |	И МаркетинговыеМероприятияДополнительныеРеквизиты.Ссылка.Наименование = &Наименование
		               |	И НЕ МаркетинговыеМероприятияДополнительныеРеквизиты.Ссылка.ПометкаУдаления
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	МаркетинговыеМероприятияДополнительныеРеквизиты.Ссылка.ДатаОкончания УБЫВ";
		
		Запрос.УстановитьПараметр("Наименование", ПереданнаяКампания);
		ПВХ = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ИДМаркетинговоеМероприятие");
		Запрос.УстановитьПараметр("Свойство", ПВХ);
		
		Результат = Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			МаркетинговоеМероприятиеСсылка = Результат.Ссылка;
		Иначе
			// Не нашли мероприятие с ID, ищем без ID самое последнее по дате завершения
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			               |	МаркетинговыеМероприятия.Ссылка КАК Ссылка
			               |ИЗ
			               |	Справочник.МаркетинговыеМероприятия КАК МаркетинговыеМероприятия
			               |ГДЕ
			               |	МаркетинговыеМероприятия.Ссылка.Наименование = &Наименование
			               |	И НЕ МаркетинговыеМероприятия.Ссылка.ПометкаУдаления
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	МаркетинговыеМероприятия.Ссылка.ДатаОкончания УБЫВ";
			
			Запрос.УстановитьПараметр("Наименование", ПереданнаяКампания);
			Запрос.УстановитьПараметр("Свойство", ПВХ);
			
			Результат = Запрос.Выполнить().Выбрать();
			
			Если Результат.Следующий() Тогда
				МаркетинговоеМероприятиеСсылка = Результат.Ссылка;
			КонецЕсли;
			
		КонецЕсли;
		// -CRM
		
		Если НЕ ЗначениеЗаполнено(МаркетинговоеМероприятиеСсылка) Тогда
			// Создаем новое мероприятие
			МаркетинговоеМероприятие = Справочники.МаркетинговыеМероприятия.СоздатьЭлемент();
			// +CRM
			МаркетинговоеМероприятие.Наименование			= ПереданнаяКампания;
			// -CRM
			МаркетинговоеМероприятие.CRM_Автор				= сфпТекущийПользователь();
			МаркетинговоеМероприятие.Ответственный			= МаркетинговоеМероприятие.CRM_Автор;
			МаркетинговоеМероприятие.ДатаНачала				= сфпТекущаяДата();
			МаркетинговоеМероприятие.ДатаОкончания			= КонецГода(МаркетинговоеМероприятие.ДатаНачала);
			МаркетинговоеМероприятие.CRM_ДатаАктуальности   = МаркетинговоеМероприятие.ДатаОкончания;
			МаркетинговоеМероприятие.CRM_ПериодАктуальности	=
				(МаркетинговоеМероприятие.ДатаОкончания - МаркетинговоеМероприятие.ДатаНачала) / 86400;
			Попытка
				МаркетинговоеМероприятие.Записать();
				МаркетинговоеМероприятиеСсылка = МаркетинговоеМероприятие.Ссылка;
			Исключение 
				ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка, , , 
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат МаркетинговоеМероприятиеСсылка;

КонецФункции // сфпНайтиМаркетинговоеМероприятие()

// Процедура записывает ключевые слова контекстной рекламы в регистр
//
// Параметры:
//	Событие			- ДокументСсылка	- Событие
//	СтруктураДанных	- Структура			- Структура с данными для записи в регистр
//
Процедура сфпЗаписатьКлючевыеСловаСобытия(Событие, СтруктураДанных) Экспорт
	НаборЗаписей = РегистрыСведений.сфпКлючевыеСловаКонтекстнойРекламы.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Событие.Установить(Событие);
	НаборЗаписей.Отбор.Событие.Использование = Истина;
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	НоваяЗапись								= НаборЗаписей.Добавить();
	НоваяЗапись.Событие						= Событие;
	НоваяЗапись.КаналПервичногоИнтереса		= СтруктураДанных.Канал;
	НоваяЗапись.ИсточникПервичногоИнтереса	= СтруктураДанных.Источник;
	НоваяЗапись.Контакт						= СтруктураДанных.Контакт;
	НоваяЗапись.ID							= СтруктураДанных.ИД;
	НоваяЗапись.КлючевыеСлова				= НРег(СокрЛП(СтруктураДанных.КлючевыеСлова));
	Попытка
		НаборЗаписей.Записать();
	Исключение
	КонецПопытки;	
КонецПроцедуры // сфпЗаписатьКлючевыеСловаСобытия()	

// Функция ищет в справочниках "Партнёры" и "Контактные лица партнёров" элемент по переданному ID
//
// Параметры: 
//  ID	- Строка	- Идентификатор контакта в системе CoMagic
//
// Возвращаемое значение: 
//  СправочникСсылка	- Найденный контакт
//
Функция сфпНайтиКонтактПоIDИзCoMagic(ID) Экспорт
	Если ПустаяСтрока(ID) Тогда
		Возврат Справочники.Партнеры.ПустаяСсылка();
	КонецЕсли;	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ID", ID);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	КонтактныеЛицаПартнеров.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	               |ГДЕ
	               |	КонтактныеЛицаПартнеров.сфпCoMagicID = &ID";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	Партнеры.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Партнеры КАК Партнеры
	               |ГДЕ
	               |	Партнеры.сфпCoMagicID = &ID";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	Возврат Справочники.Партнеры.ПустаяСсылка();
КонецФункции // сфпНайтиКонтактПоIDИзCoMagic()

// Процедура записывает ID клиента системы Comagic
//
// Параметры: 
//	Контакт	- СправочникСсылка	- Контакт	
//  ID		- Строка 			- Идентификатор контакта
//
Процедура сфпЗаписатьIDCoMagic(Контакт, ID) Экспорт 
	КонтактОбъект = Контакт.ПолучитьОбъект();
	КонтактОбъект.сфпCoMagicID = ID;
	Попытка
		КонтактОбъект.Записать();	
	Исключение 
		ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка, , , 
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;			
КонецПроцедуры // ЗаписатьIDCoMagic()	

// Функция возвращает ключ сесси CoMagic
//
// Параметры:
//   Нет.
//
// Возвращаемое значение:
//  Строка - Ключ сессии получен
//
Функция сфпПолучитьКлючСессииCoMagic() Экспорт
	
	ПараметрыОператораКоллтрекинга = сфпСофтФонПроСервер.сфпПараметрыОператораКоллтрекинга();
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	
	Соединение 			= Новый HTTPСоединение(ПараметрыОператораКоллтрекинга.ДоменAPI, , , , Прокси, 60,
		Новый ЗащищенноеСоединениеOpenSSL(Неопределено, Неопределено));
	Запрос 				= Новый HTTPЗапрос;
	Запрос.АдресРесурса = "/api/login/?login=" + СокрЛП(Константы.сфпЛогинCoMagic.Получить())
		+ "&password=" + СокрЛП(Константы.сфпПарольCoMagic.Получить()); 
	ФайлРезультата		= ПолучитьИмяВременногоФайла("txt");
	Попытка
		Ответ = Соединение.ОтправитьДляОбработки(Запрос, ФайлРезультата);
		Если Ответ.КодСостояния = 200 Тогда
			ТекстовыйФайл = Новый ТекстовыйДокумент;
			ТекстовыйФайл.Прочитать(ФайлРезультата, "UTF-8");
			РезультатСоответствие =
				UnJSON(ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыXML(ТекстовыйФайл.ПолучитьТекст()));
			КлючСессии = РезультатСоответствие.Получить("data").Получить("session_key");
		Иначе
			КлючСессии	= "";
		КонецЕсли;
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не удалось получить ключ сессии: ';en='Failed to obtain session key:'") 
			+ Символы.ПС 
			+ ОписаниеОшибки());
		КлючСессии	= "";
	КонецПопытки;
	Соединение = Неопределено;
	Возврат КлючСессии;
КонецФункции // сфпПолучитьКлючСессииCoMagic()

// Процедура записывает в документ структуру CoMagic
//
// Параметры:
//	НовыйЗвонок			- ДокументСсылка	- Ссылка на документ
//	СтруктураCoMagic	- Структура			- Структура CoMagic
//
Процедура сфпЗаписатьСтруктуруCoMagic(НовыйЗвонок, СтруктураCoMagic) Экспорт
	
	ДокументОбъект = НовыйЗвонок.ПолучитьОбъект();
	ДокументОбъект.сфпCoMagicID	= СтруктураCoMagic.comagic_context.visitor_id;
	ДокументОбъект.Комментарий = ДокументОбъект.Комментарий + ?(ПустаяСтрока(ДокументОбъект.Комментарий), "", Символы.ПС) 
		+ НСтр("ru='Кампания: ';en='Campaign: '") + СтруктураCoMagic.comagic_context.campaign + Символы.ПС 
		+ НСтр("ru='Сайт: ';en='Website: '") + СтруктураCoMagic.comagic_context.site + Символы.ПС
		+ НСтр("ru='Ключевые слова: ';en='Keywords: '") + СтруктураCoMagic.comagic_context.search_query; 
	Попытка
		ДокументОбъект.Записать();
	Исключение 
		ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка, , , 
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
	КонецПопытки;
	
	ИмяПредопределенногоЗначения = "ПланВидовХарактеристик.КаналыРекламныхВоздействий.CRM_CoMagic";	
	КаналПервичногоИнтереса = ПредопределенноеЗначение(ИмяПредопределенногоЗначения);
	ИсточникПервичногоИнтереса = сфпНайтиМаркетинговоеМероприятие(СтруктураCoMagic.comagic_context.campaign);

	Если ЗначениеЗаполнено(КаналПервичногоИнтереса) ИЛИ ЗначениеЗаполнено(ИсточникПервичногоИнтереса) Тогда
		// Зафиксируем актуальный источник привлечения
		НаборРегистра = РегистрыСведений.ИсточникиПервичногоИнтереса.СоздатьНаборЗаписей();
		НаборРегистра.Отбор.Сделка.Установить(ДокументОбъект.Ссылка);
	
		Партнер = Справочники.Партнеры.ПустаяСсылка();
		Если ЗначениеЗаполнено(ДокументОбъект.АбонентКонтакт) Тогда
			Если ТипЗнч(ДокументОбъект.АбонентКонтакт) = Тип("СправочникСсылка.Партнеры") Тогда
				Партнер = ДокументОбъект.АбонентКонтакт;
				
			ИначеЕсли ТипЗнч(ДокументОбъект.АбонентКонтакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда	
				Партнер = ДокументОбъект.АбонентКонтакт.Владелец;
			КонецЕсли;
		КонецЕсли;	
		
		Запись = НаборРегистра.Добавить();
		Запись.Период = ?(ЗначениеЗаполнено(ДокументОбъект.Дата), ДокументОбъект.Дата,
			 CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса());
		Запись.Партнер = Партнер;
		Запись.Сделка = ДокументОбъект.Ссылка;
		Запись.ИсточникПервичногоИнтереса = ИсточникПервичногоИнтереса;
		Запись.КаналПервичногоИнтереса = КаналПервичногоИнтереса;
		
		НаборРегистра.Записать();
	КонецЕсли;

КонецПроцедуры // сфпЗаписатьСтруктуруCoMagic()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ИСТОРИЕЙ ЗВОНКОВ

// Функция проверяет подключение внешнего источника данных звонков.
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Признак подключения источника.
//
Функция сфпИсточникИсторииЗвонковПодключен() Экспорт
	СтрокаПодключения	= Константы.сфпСтрокаПодключенияИстории.Получить();
	Если ПустаяСтрока(СтрокаПодключения) Тогда Возврат Ложь; КонецЕсли;	
	СтруктураПодключения	= сфпПолучитьСтруктуруПодключения(СтрокаПодключения);
	СостояниеПодключения	= ВнешниеИсточникиДанных.сфпВнешнийИсточникДанныхИсторииЗвонков.ПолучитьСостояние();
	Если СостояниеПодключения = СостояниеВнешнегоИсточникаДанных.Отключен Тогда
		ПараметрыСоединения = Новый ПараметрыСоединенияВнешнегоИсточникаДанных;
		ПараметрыСоединения.АутентификацияСтандартная	= Истина;
		ПараметрыСоединения.ИмяПользователя				= СтруктураПодключения.Пользователь;
		ПараметрыСоединения.Пароль						= СтруктураПодключения.Пароль;
		СтрокаСоединенияДрайвер = "DRIVER={PostgreSQL Unicode};SERVER=";
		Если СтруктураПодключения.ТипПодключения = "1" Тогда
			ПараметрыСоединения.СтрокаСоединения			= СтрокаСоединенияДрайвер 
				+ СтруктураПодключения.Сервер
				+ ";PORT="
				+ СтруктураПодключения.Порт
				+ ";UID=" 
				+ СтруктураПодключения.Пользователь 
				+ ";PWD=" + СтруктураПодключения.Пароль 
				+ ";DATABASE="
				+ СтруктураПодключения.БазаДанных;
			ПараметрыСоединения.СУБД						= "PostgreSQL";
		Иначе
			ПараметрыСоединения.СтрокаСоединения			= "DRIVER={SQL Server};SERVER=" + СтруктураПодключения.Сервер + ";UID=" 
				+ СтруктураПодключения.Пользователь + ";PWD=" + СтруктураПодключения.Пароль + ";DATABASE="
				+ СтруктураПодключения.БазаДанных;
			ПараметрыСоединения.СУБД						= "MSSQLServer";
		КонецЕсли;
		Попытка
			ВнешниеИсточникиДанных.сфпВнешнийИсточникДанныхИсторииЗвонков.УстановитьОбщиеПараметрыСоединения(ПараметрыСоединения);
			ВнешниеИсточникиДанных.сфпВнешнийИсточникДанныхИсторииЗвонков.УстановитьПараметрыСоединенияСеанса(ПараметрыСоединения);
			ВнешниеИсточникиДанных.сфпВнешнийИсточникДанныхИсторииЗвонков.УстановитьСоединение();
		Исключение
			Если ОбщегоНазначения.ЭтоLinuxКлиент() Тогда
				СтрокаСоединенияДрайвер = "DRIVER={PostgreSQL};SERVER=";
			Иначе
				СтрокаСоединенияДрайвер = "DRIVER={PostgreSQL ODBC Driver(UNICODE)};SERVER=";
			КонецЕсли;	
			ПараметрыСоединения.СтрокаСоединения			= СтрокаСоединенияДрайвер 
				+ СтруктураПодключения.Сервер
				+ ";PORT="
				+ СтруктураПодключения.Порт
				+ ";UID=" 
				+ СтруктураПодключения.Пользователь 
				+ ";PWD=" + СтруктураПодключения.Пароль 
				+ ";DATABASE="
				+ СтруктураПодключения.БазаДанных;
			ВнешниеИсточникиДанных.сфпВнешнийИсточникДанныхИсторииЗвонков.УстановитьОбщиеПараметрыСоединения(ПараметрыСоединения);
			ВнешниеИсточникиДанных.сфпВнешнийИсточникДанныхИсторииЗвонков.УстановитьПараметрыСоединенияСеанса(ПараметрыСоединения);
			ВнешниеИсточникиДанных.сфпВнешнийИсточникДанныхИсторииЗвонков.УстановитьСоединение();
		КонецПопытки;
		СостояниеПодключения = ВнешниеИсточникиДанных.сфпВнешнийИсточникДанныхИсторииЗвонков.ПолучитьСостояние();
		Если СостояниеПодключения = СостояниеВнешнегоИсточникаДанных.Отключен Тогда Возврат Ложь; КонецЕсли;
	КонецЕсли;
	Возврат Истина;
КонецФункции // сфпИсточникИсторииЗвонковПодключен()	

////////////////////////////////////////////////////////////////////////////////
// ПЛАН НУМЕРАЦИИ ФАС (РОССВЯЗЬ)

// Функция возвращает состав файлов реестра плана нумерации
//
// Параметры:
//  Разделитель	- Строка	- Разделитель файлов. Значение по умолчанию: ";"
//
// Возвращаемое значение:
//  Массив - Состав файлов реестра
//
Функция сфпПолучитьФайлыРеестра(Разделитель = ",")
	
	ИнтернетАдресРеестра = Константы.сфпПланНумерацииАдресРеестра.Получить();
	Если ПустаяСтрока(ИнтернетАдресРеестра) Тогда
		ИнтернетАдресРеестра = "https://opendata.digital.gov.ru/downloads/";
	КонецЕсли;
	
	ФайлыРеестраНумерации = Константы.сфпПланНумерацииФайлыРеестра.Получить();
	Если ПустаяСтрока(ФайлыРеестраНумерации) Тогда
		ФайлыРеестраНумерации = "ABC-3xx.csv,ABC-4xx.csv,ABC-8xx.csv,DEF-9xx.csv";
	КонецЕсли;
	ФайлыРеестра = сфпРазложитьСтрокуВМассивПодстрок(ФайлыРеестраНумерации, Разделитель);
	
	ПолныеИменаФайловРеестра = Новый Массив();
	
	Для Каждого ФайлРеестра Из ФайлыРеестра Цикл
		ПолныеИменаФайловРеестра.Добавить(ИнтернетАдресРеестра + ФайлРеестра);
	КонецЦикла;

	Возврат ПолныеИменаФайловРеестра;

КонецФункции // сфпПолучитьФайлыРеестра()

// Функция создает Бизнес-регион по полному имени региона
//
// Параметры:
//  ПолноеИмяРегиона	- Строка	- Полное имя Бизнес-региона 
//
// Возвращаемое значение:
//  СправочникСсылка - Бизнес-регион
//
Функция сфпСоздатьБизнесРегион(ПолноеИмяРегиона)
	
	МассивРегионов = Новый Массив();
	
	ОстатокИмени = ПолноеИмяРегиона;
	РазделительИмени = "|";
	Позиция = СтрНайти(ПолноеИмяРегиона, РазделительИмени);
	Если Позиция = 0 Тогда
		РазделительИмени = "*";
		Позиция = СтрНайти(ПолноеИмяРегиона, РазделительИмени);
	КонецЕсли;
	КоличествоВхождений = 0;
	Пока Позиция > 0 Цикл
		КоличествоВхождений = КоличествоВхождений + 1;
		Если КоличествоВхождений = 2 Тогда
			Прервать;
		КонецЕсли;
		ТекРегион = СокрЛП(Лев(ОстатокИмени, Позиция - 1));
		МассивРегионов.Добавить(ТекРегион);
	
		ОстатокИмени = Сред(ОстатокИмени, Позиция + 1);
		Позиция = СтрНайти(ОстатокИмени, РазделительИмени);
	КонецЦикла;
		
	Если ЗначениеЗаполнено(ОстатокИмени) Тогда
		МассивРегионов.Добавить(ОстатокИмени);
	КонецЕсли;

	ТекРодитель = Справочники.БизнесРегионы.ПустаяСсылка();
	
	НайденныйРегион = "";
	
	Сч = МассивРегионов.Количество() - 1;
	Пока Сч >= 0 Цикл
		ТекИмяРегиона = МассивРегионов[Сч];
		
		НайденныйРегион = Справочники.БизнесРегионы.НайтиПоНаименованию(ТекИмяРегиона, Истина, ТекРодитель);
		Если НайденныйРегион.Пустая() Тогда
			НайденныйРегион = Справочники.БизнесРегионы.СоздатьЭлемент();
			НайденныйРегион.Родитель = ТекРодитель;
			НайденныйРегион.Наименование = ТекИмяРегиона;
			НайденныйРегион.Записать();
			
			НайденныйРегион = НайденныйРегион.Ссылка;
		КонецЕсли;
		
		ТекРодитель = НайденныйРегион;
		
		Сч = Сч - 1;
	КонецЦикла;	
	
	Возврат НайденныйРегион;

КонецФункции // сфпСоздатьБизнесРегион()

// Процедура создает запись регистра Плана нумерации
//
// Параметры:
//	Код				- Строка			- Код оператора
//	НачалоДиапазона	- Строка			- Начало диапазона нумерации
//	КонецДиапазона	- Строка			- Конец диапазона нумерации
//	Емкость			- Число				- Количество номеров в диапазоне
//	Оператор		- Строка			- Наименование оператора
//	Регион			- СправочникСсылка	- Ссылка на Бизнес-регион
//	КэшРегионов		- ТаблицаЗначений	- Кэш регионов в разрезе полных наименований регионов
//
Процедура сфпОбновитьРегистрНумерации(Код, НачалоДиапазона, КонецДиапазона, Емкость, Оператор, Регион)
	
	МенеджерЗаписи = РегистрыСведений.сфпПланНумерации.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Код = Код;
	МенеджерЗаписи.НачалоДиапазона = НачалоДиапазона;
	МенеджерЗаписи.КонецДиапазона = КонецДиапазона;
	МенеджерЗаписи.Емкость = Емкость;
	МенеджерЗаписи.Оператор = Оператор;
	МенеджерЗаписи.НаименованиеРегиона = Регион;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры // сфпОбновитьРегистрНумерации()

// Процедура - обработчик регламентного задания загрузки плана нумерации
//
// Параметры:
//	Нет.
//
Процедура сфпЗагрузитьПланНумерацииИзСети() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.сфпЗагрузкаПланаНумерации);

	ФайлыРеестра = сфпПолучитьФайлыРеестра();
	Если ФайлыРеестра.Количество() = 0 Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru='Загрузка Плана нумерации';en='Loading number plan'"),
			 УровеньЖурналаРегистрации.Предупреждение, , ,
			 НСтр("ru='Файлы не найдены!'"));
		Возврат;
	КонецЕсли;	
	
	НаборЗаписей = РегистрыСведений.сфпПланНумерации.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	Для Каждого АдресФайла Из ФайлыРеестра Цикл
		ИмяФайлаРеестра = СкачатьФайлРеестра(АдресФайла);
		Если ЗначениеЗаполнено(ИмяФайлаРеестра) Тогда
			ТекстФайла = Новый ЧтениеТекста(ИмяФайлаРеестра, КодировкаТекста.UTF8);
			ТекСтрока = ТекстФайла.ПрочитатьСтроку();
			Пока ТекСтрока <> Неопределено Цикл
				ЗначенияСтроки = сфпРазложитьСтрокуВМассивПодстрок(ТекСтрока, ";");
					
				Код = СокрЛП(ЗначенияСтроки[0]);
							
				Попытка
					// BSLLS:UnusedLocalVariable-off
					КодЧислом = Число(Код);
					// BSLLS:UnusedLocalVariable-on
				Исключение
					ТекСтрока = ТекстФайла.ПрочитатьСтроку();
					Продолжить;
				КонецПопытки;

				НачалоДиапазона = СокрЛП(ЗначенияСтроки[1]);
				КонецДиапазона = СокрЛП(ЗначенияСтроки[2]);
				Емкость = СокрЛП(ЗначенияСтроки[3]);
				Оператор = СокрЛП(ЗначенияСтроки[4]);
				Регион = СокрЛП(ЗначенияСтроки[5]);
				
				сфпОбновитьРегистрНумерации(Код, НачалоДиапазона, КонецДиапазона, Емкость, Оператор, Регион);
				
				ТекСтрока = ТекстФайла.ПрочитатьСтроку();
			КонецЦикла;
		КонецЕсли;	
	КонецЦикла;

КонецПроцедуры // сфпЗагрузитьПланНумерацииИзСети()

// Функция возвращает путь к загруженному файлу реестра плана нумерации
//
// Параметры:
//  АдресФайла	- Строка	- url файла реестра на сайте ФАС Россвязь
//
// Возвращаемое значение:
//  Строка - Путь к загруженному файлу
//
Функция СкачатьФайлРеестра(АдресФайла)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("csv");
		
	СтруктураАдреса = сфпОбщегоНазначенияКлиентСервер.СтруктураURI(АдресФайла);
	
	HTTPЗапрос = Новый HTTPЗапрос();
	HTTPЗапрос.АдресРесурса = СтруктураАдреса.ПутьНаСервере;
	
	ИспользоватьЗащищенноеСоединение = (ВРЕГ(Лев(АдресФайла, 5)) = ВРЕГ("HTTPS"));
	ЗащищенноеСоединениеOpenSSL = Неопределено;
	Если ИспользоватьЗащищенноеСоединение Тогда
		ЗащищенноеСоединениеOpenSSL = Новый ЗащищенноеСоединениеOpenSSL;
	КонецЕсли;
	
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Протокол = ?(ИспользоватьЗащищенноеСоединение, "https", "http");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси(Протокол);
	КонецЕсли;
	
	HTTPСоединение = Новый HTTPСоединение(СтруктураАдреса.Хост, СтруктураАдреса.Порт, , , Прокси, 60,
		ЗащищенноеСоединениеOpenSSL);
	
	HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос, ИмяВременногоФайла);
	
	Если HTTPОтвет.КодСостояния = 301 Тогда
		НовыйАдресФайла = HTTPОтвет.Заголовки.Получить("location");
		Если ЗначениеЗаполнено(НовыйАдресФайла) Тогда
			Возврат СкачатьФайлРеестра(НовыйАдресФайла);
		КонецЕсли;
		
	Иначе
		Возврат ИмяВременногоФайла;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции	//	СкачатьФайлРеестра()

// Функция возвращает Бизнес-регион, определенный по номеру телефона
//
// Параметры:
//  НомерТелефона	- Строка	- Номер телефона, по которому необходимо определить Бизнес-регион
//
// Возвращаемое значение:
//  СправочникСсылка - Бизнес-регион
//
Функция сфпПолучитьБизнесРегионПоНомеруТелефона(НомерТелефона) Экспорт
	
	Если НЕ сфпИспользоватьСофтФон() Тогда
		Возврат Неопределено;
	
	ИначеЕсли НЕ сфпИспользоватьАвтоопределениеБизнесРегиона() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КодОператораПоиска = Лев(НомерТелефона, 3);
	НомерТелефонаПоиска = Сред(НомерТелефона, 4);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ НаименованиеРегиона
	|ИЗ РегистрСведений.сфпПланНумерации
	|ГДЕ Код = &Код И &Номер МЕЖДУ НачалоДиапазона И КонецДиапазона");
	Запрос.УстановитьПараметр("Код", КодОператораПоиска);
	Запрос.УстановитьПараметр("Номер", НомерТелефонаПоиска);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат сфпСоздатьБизнесРегион(Выборка.НаименованиеРегиона);
	КонецЕсли;
	
	Возврат Справочники.БизнесРегионы.ПустаяСсылка();

КонецФункции // сфпПолучитьБизнесРегионПоНомеруТелефона()

Процедура сфпЗаполнитьБизнесРегионПоНомеру(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПоследниеЦифрыТелефонногоНомера = Константы.сфпПоследниеЦифрыТелефонногоНомера.Получить();
	Если ПоследниеЦифрыТелефонногоНомера = 0 Тогда Возврат; КонецЕсли;
	
	Если Объект.Метаданные().Имя = "CRM_Телемаркетинг" Тогда
		Для Каждого СтрокаУчастника Из Объект.Участники Цикл
			Если ЗначениеЗаполнено(СтрокаУчастника.Партнер)
				 И ТипЗнч(СтрокаУчастника.Партнер) = Тип("СправочникСсылка.Партнеры") Тогда
				ПартнерОбъект = СтрокаУчастника.Партнер.ПолучитьОбъект();
				
				Если НЕ ЗначениеЗаполнено(ПартнерОбъект.БизнесРегион) Тогда
					Если ЗначениеЗаполнено(СтрокаУчастника.Телефон) Тогда
						СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(СтрокаУчастника.Телефон);
						НомерТелефона = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
							+ СтруктураНомера.НомерТелефона,
							 ПоследниеЦифрыТелефонногоНомера);
					
						БизнесРегион = сфпПолучитьБизнесРегионПоНомеруТелефона(НомерТелефона);
						Если ЗначениеЗаполнено(БизнесРегион) Тогда
							ПартнерОбъект.БизнесРегион = БизнесРегион;
						КонецЕсли;
					КонецЕсли;
					
					ПартнерОбъект.Записать();
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;	

	ИначеЕсли Объект.Метаданные().Имя = "ТелефонныйЗвонок" Тогда
		Если ЗначениеЗаполнено(Объект.АбонентКонтакт) Тогда 
			Если ТипЗнч(Объект.АбонентКонтакт) = Тип("СправочникСсылка.Партнеры") Тогда
				ПартнерОбъект = Объект.АбонентКонтакт.ПолучитьОбъект();
				
			ИначеЕсли ТипЗнч(Объект.АбонентКонтакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
				ПартнерОбъект = Объект.АбонентКонтакт.Владелец.ПолучитьОбъект();
				
			Иначе
				УстановитьПривилегированныйРежим(Ложь);
				Возврат;
			КонецЕсли;	
			
			Если НЕ ЗначениеЗаполнено(ПартнерОбъект.БизнесРегион) Тогда
				Если ЗначениеЗаполнено(Объект.АбонентКакСвязаться) Тогда
					СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(Объект.АбонентКакСвязаться);
					НомерТелефона = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
						+ СтруктураНомера.НомерТелефона,
						 ПоследниеЦифрыТелефонногоНомера);
										
					БизнесРегион = сфпПолучитьБизнесРегионПоНомеруТелефона(НомерТелефона);
					Если ЗначениеЗаполнено(БизнесРегион) Тогда
						ПартнерОбъект.БизнесРегион = БизнесРегион;
					КонецЕсли;
				КонецЕсли;
								
				ПартнерОбъект.Записать();
			КонецЕсли;	
		КонецЕсли;

	ИначеЕсли Объект.Метаданные().Имя = "Партнеры" Тогда
		Если НЕ ЗначениеЗаполнено(Объект.БизнесРегион) Тогда
			Для Каждого Контакт Из Объект.КонтактнаяИнформация Цикл
				Если (Контакт.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон)
					 ИЛИ (Контакт.Тип = Перечисления.ТипыКонтактнойИнформации.Факс) Тогда
					СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(Контакт.Представление);
					НомерТелефона = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
						+ СтруктураНомера.НомерТелефона,
						 ПоследниеЦифрыТелефонногоНомера);
					Если НЕ ЗначениеЗаполнено(НомерТелефона) Тогда Продолжить; КонецЕсли;
					
					БизнесРегион = сфпПолучитьБизнесРегионПоНомеруТелефона(НомерТелефона);
					Если ЗначениеЗаполнено(БизнесРегион) Тогда
						Объект.БизнесРегион = БизнесРегион;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;	
	КонецЕсли;

	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры // сфпЗаполнитьБизнесРегионПоНомеру()

Функция сфпБизнесРегионКонтакта(Контакт) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ БизнесРегион.Ссылка КАК БизнесРегион, БизнесРегион.CRM_ВремяПоГринвичу_GMT КАК ВремяПоГринвичу
	|ИЗ Справочник.Партнеры
	|ГДЕ Ссылка = &Ссылка И БизнесРегион <> ЗНАЧЕНИЕ(Справочник.БизнесРегионы.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ Владелец.БизнесРегион.Ссылка КАК БизнесРегион, Владелец.БизнесРегион.CRM_ВремяПоГринвичу_GMT КАК ВремяПоГринвичу
	|ИЗ Справочник.КонтактныеЛицаПартнеров
	|ГДЕ Ссылка = &Ссылка И Владелец.БизнесРегион <> ЗНАЧЕНИЕ(Справочник.БизнесРегионы.ПустаяСсылка)");
	Запрос.УстановитьПараметр("Ссылка", Контакт);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Новый Структура("БизнесРегион,ВремяПоГринвичу", Выборка.БизнесРегион, Выборка.ВремяПоГринвичу);
	КонецЕсли;	

	Возврат Новый Структура("БизнесРегион,ВремяПоГринвичу", Справочники.БизнесРегионы.ПустаяСсылка(), 0);
	
КонецФункции

Функция сфпРеквизитСуществует(Ссылка, ИмяРеквизита) Экспорт
	Возврат (Ссылка.Метаданные().Реквизиты.Найти(ИмяРеквизита) <> Неопределено);
КонецФункции

Функция сфпТабличнаяЧастьСуществует(Ссылка, ИмяТабличнойЧасти) Экспорт
	Возврат (Ссылка.Метаданные().ТабличныеЧасти.Найти(ИмяТабличнойЧасти) <> Неопределено);
КонецФункции

Функция сфпПолучитьИмяМетаданных(Ссылка) Экспорт
	
	Попытка    Возврат Ссылка.Метаданные().Имя;
	Исключение Возврат "";
	КонецПопытки;

КонецФункции

#Область ИнтеграцияСистемаВзаимодействия

Функция ИдентификаторОбсужденияЖурналЗвонков() Экспорт
	
	ОбсуждениеЖурналИдентификатор = Неопределено;
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	Если сфпОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.11.0") < 0 Тогда
		Возврат ОбсуждениеЖурналИдентификатор;
	КонецЕсли;
	
	#Если НЕ ВнешнееСоединение Тогда
	Ключ = КлючСлужебногоОбсужденияСистемыВзаимодействия();
	
	Если НЕ СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Возврат ОбсуждениеЖурналИдентификатор;
	КонецЕсли;
	
	Обсуждение = Неопределено;
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(Ключ);
		Если Обсуждение = Неопределено Тогда
			  ОбсуждениеЖурналИдентификатор = СоздатьОбсуждениеЖурналЗвонков();
		Иначе ОбсуждениеЖурналИдентификатор = Обсуждение.Идентификатор;
        КонецЕсли;
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru='Поиск обсуждения Системы взаимодействия по ключу';
			|en='Search discuss of Interaction System by key'"), УровеньЖурналаРегистрации.Ошибка, , Ключ,
			 ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
	
	#КонецЕсли
		
	Возврат ОбсуждениеЖурналИдентификатор;
	
КонецФункции

Функция КлючОбсужденияЖурналЗвонков() Экспорт
	
	ОбсуждениеЖурналИдентификатор = Неопределено;
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	Если сфпОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.11.0") < 0 Тогда
		Возврат ОбсуждениеЖурналИдентификатор;
	КонецЕсли;
	
	Обсуждение = Неопределено;
	Ключ = КлючСлужебногоОбсужденияСистемыВзаимодействия();
	
	#Если НЕ ВнешнееСоединение Тогда
	Если НЕ СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Возврат ОбсуждениеЖурналИдентификатор;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(Ключ);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru='Поиск обсуждения Системы взаимодействия по ключу';
			|en='Search discuss of Interaction System by key'"), УровеньЖурналаРегистрации.Ошибка, , Ключ,
			 ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
	#КонецЕсли
	
	Если Обсуждение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Ключ;
	
КонецФункции

Функция СоздатьОбсуждениеЖурналЗвонков() Экспорт
	
	Ключ = Неопределено;
	ОбсуждениеЖурналИдентификатор = "";
	
	#Если НЕ ВнешнееСоединение Тогда
	Ключ = КлючСлужебногоОбсужденияСистемыВзаимодействия();
	
	УстановитьПривилегированныйРежим(Истина);
	ОтборОбсуждений = Новый ОтборОбсужденийСистемыВзаимодействия();
	ОтборОбсуждений.Ключ = Ключ;
	Попытка
		СообщенияТелефонии = СистемаВзаимодействия.ПолучитьОбсуждения(ОтборОбсуждений);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru='Создание служебного обсуждения Системы взаимодействия';
			|en='Create service discussing the System of interaction'"), УровеньЖурналаРегистрации.Ошибка, ,
			 Ключ,
			 ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
	
	Если СообщенияТелефонии.Количество() = 0 Тогда
		ОбсуждениеЖурнал = СистемаВзаимодействия.СоздатьОбсуждение();
		ОбсуждениеЖурнал.Ключ = Ключ;
		ОбсуждениеЖурнал.Отображаемое = Ложь;
		ОбсуждениеЖурнал.Заголовок = НСтр("ru='Журнал звонков';en='Call log'");
		//ОбсуждениеЖурнал.Участники.Добавить(СистемаВзаимодействия.СтандартныеПользователи.ВсеПользователиПриложения);
		
		УстановитьПривилегированныйРежим(Истина);
		Попытка
			ОбсуждениеЖурнал.Записать();
			
			ВсеПользователи = СистемаВзаимодействия.СтандартныеПользователи.ВсеПользователиПриложения;
			Если НЕ ОбсуждениеЖурнал.Участники.Содержит(ВсеПользователи) Тогда
			   ОбсуждениеЖурнал.Участники.Добавить(ВсеПользователи);        
			   ОбсуждениеЖурнал.Записать();
			КонецЕсли;
			
			ОбсуждениеЖурналИдентификатор = ОбсуждениеЖурнал.Идентификатор;
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru='Создание служебного обсуждения Системы взаимодействия';
				|en='Create service discussing the System of interaction'"), УровеньЖурналаРегистрации.Ошибка, ,
				 Ключ,
				 ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбсуждениеЖурналИдентификатор = "";
		КонецПопытки;
		УстановитьПривилегированныйРежим(Ложь);
		
	Иначе
		ОбсуждениеЖурналИдентификатор = СообщенияТелефонии[0].Идентификатор;	
	КонецЕсли;
	#КонецЕсли
	
	Возврат ОбсуждениеЖурналИдентификатор;
	
КонецФункции

#КонецОбласти

#Область СистемаВзаимодействияПрочее

// Процедура - обработчик регламентного задания по очистке сообщений журнала звонков
//
// Параметры:
//	Нет.
//
Процедура ОчисткаСообщенийЖурналаЗвонков() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.сфпОчисткаСообщенийЖурналаЗвонков);
	
	Если Не ПолучитьФункциональнуюОпцию("сфпИспользоватьРегламентноеЗаданиеОчисткаСообщенийЖурналаЗвонков") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		ТекстКомментария = НСтр("ru = 'База не зарегистрирована в Системе взаимодействий'");
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Очистка сообщений журнала звонков (СофтФон)'"),
			 УровеньЖурналаРегистрации.Предупреждение, , ,
			 ТекстКомментария);
		Возврат;
	КонецЕсли;
	
	// Получение обсуждения
	ИдентификаторОбсужденияЖурналЗвонков = сфпСофтФонПроСервер.ИдентификаторОбсужденияЖурналЗвонков();
	Если ИдентификаторОбсужденияЖурналЗвонков = Неопределено Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Очистка сообщений журнала звонков (СофтФон)'"),
			 УровеньЖурналаРегистрации.Предупреждение, , ,
			 НСтр("ru = 'Не удалось получить журнал звонков'"));
		Возврат;
	КонецЕсли;
	
	сфпСрокЖизниСообщенияЖурналаЗвонков = Константы.сфпСрокЖизниСообщенияЖурналаЗвонков.Получить();
	Если НЕ ЗначениеЗаполнено(сфпСрокЖизниСообщенияЖурналаЗвонков) Тогда
		ТекстКомментария = НСтр("ru = 'Не задан срок жизни сообщения журнала звонков'");
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Очистка сообщений журнала звонков (СофтФон)'"),
			 УровеньЖурналаРегистрации.Предупреждение, , ,
			 ТекстКомментария);
		Возврат;
	КонецЕсли;
	
	ОчиститьРанееДаты = ТекущаяДатаСеанса() - сфпСрокЖизниСообщенияЖурналаЗвонков * 24 * 60 * 60;
	
	ЖурналОчищен = Ложь;
	
	КоличествоОшибок = 0;
	КоличествоУдаленныхСообщений = 0;
	Пока НЕ (ЖурналОчищен ИЛИ КоличествоОшибок > 3) Цикл
		
		ЖурналОчищен = ВыполнитьОчисткуСообщенийЖурналаЗвонков(ИдентификаторОбсужденияЖурналЗвонков,
			 ОчиститьРанееДаты, КоличествоУдаленныхСообщений,
			 КоличествоОшибок);
		
	КонецЦикла;
	
	ТекстКомментария = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Работа обработки завершена. Всего удалено %1 сообщений. Количество ошибок: %2'"),
						КоличествоУдаленныхСообщений, КоличествоОшибок);
						
	ЗаписьЖурналаРегистрации(НСтр("ru = 'Очистка сообщений журнала звонков (СофтФон)'"),
		 УровеньЖурналаРегистрации.Предупреждение, , ,
		 ТекстКомментария);
	
КонецПроцедуры

Функция ВыполнитьОчисткуСообщенийЖурналаЗвонков(ИдентификаторОбсужденияЖурналЗвонков,
	 ОчиститьРанееДаты, КоличествоУдаленныхСообщений,
	 КоличествоОшибок)
	
	УдаленоВДаннойИтерации = 0;
	
	УстановитьПривилегированныйРежим(Истина);
		
	Попытка
		
		// Удаление сообщений обсуждения
		ОтборСообщений = Новый ОтборСообщенийСистемыВзаимодействия();
		ОтборСообщений.Количество = 200;
		ОтборСообщений.Обсуждение = ИдентификаторОбсужденияЖурналЗвонков;
		ОтборСообщений.НаправлениеСортировки = НаправлениеСортировки.Возр;
		
		Сообщения = СистемаВзаимодействия.ПолучитьСообщения(ОтборСообщений);
		
		Для Каждого Сообщение Из Сообщения Цикл
			Если Сообщение.Дата < ОчиститьРанееДаты Тогда
				СистемаВзаимодействия.УдалитьСообщение(Сообщение.Идентификатор);
				УдаленоВДаннойИтерации = УдаленоВДаннойИтерации + 1;
			КонецЕсли;
		КонецЦикла;
		
		Если УдаленоВДаннойИтерации = 0 Тогда // все возможные сообщения удалены
			Возврат Истина;
		КонецЕсли;
		
	Исключение
		КоличествоОшибок = КоличествоОшибок + 1;
		ТекстКомментария = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Очистка сообщений журнала звонков (СофтФон)'"),
			 УровеньЖурналаРегистрации.Предупреждение, , ,
			 ТекстКомментария);
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	КоличествоУдаленныхСообщений = КоличествоУдаленныхСообщений + УдаленоВДаннойИтерации;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ИнтеграцияТехнологияСервиса

Процедура ПриЗагрузкеПользователяИнформационнойБазы(ПользовательИБ) Экспорт
	
	Если ПользовательИБ.Имя <> СлужебныйПользовательТелефонииЛогин() Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательИБ.ПоказыватьВСпискеВыбора = Ложь;
	
	Если НЕ ПользовательИБ.ПарольУстановлен Тогда
		ПользовательИБ.Пароль = СлужебныйПользовательТелефонииПароль();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиТелефонии

Функция КорневойАдресАТС(УправлениеЗвонками = Ложь) Экспорт
	
	ИспользуемаяАТС = Константы.сфпИспользуемаяАТС.Получить();
	Если ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.MangoOffice Тогда
		Возврат "https://app.mango-office.ru/vpbx/";

	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Яндекс Тогда
		Возврат "https://api.yandex.mightycall.ru/";
		
	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Билайн Тогда
		Возврат "https://cloudpbx.beeline.ru/";
		
	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Ростелеком Тогда
		Возврат "https://api.cloudpbx.rt.ru/";

	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.UIS Тогда
		Если УправлениеЗвонками Тогда
			Возврат "https://callapi.uiscom.ru/v4.0";

		Иначе	
			Возврат "https://dataapi.uiscom.ru/v2.0";
		КонецЕсли;

	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.СофтФонWebModule
		ИЛИ сфпСофтФонПроСерверПереопределяемый.ЭтоПлатформаITooLabs(ИспользуемаяАТС) Тогда
		
		АдресОблачнойАТС = ПолучитьНастройкиТелефонии().АдресОблачнойАТС;
		Если Прав(АдресОблачнойАТС, 1) <> "/" Тогда
			АдресОблачнойАТС = АдресОблачнойАТС + "/";
		КонецЕсли;	
		
		Возврат АдресОблачнойАТС;

	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьНастройкиТелефонии() Экспорт
	
	Результат = НастройкиТелефонииПоУмолчанию();
	
	СохраненныеНастройки = Константы.сфпНастройкиТелефонии.Получить().Получить();
	Если СохраненныеНастройки <> Неопределено Тогда
		Для Каждого КлючИЗначение Из Результат Цикл
			Если СохраненныеНастройки.Свойство(КлючИЗначение.Ключ) Тогда
				Результат[КлючИЗначение.Ключ] = СохраненныеНастройки[КлючИЗначение.Ключ];
			КонецЕсли;
		КонецЦикла;
		
		Если СохраненныеНастройки.Свойство("КлючДляАвторизацииВУНФ") Тогда
			Результат.КлючДляАвторизацииВИБ = СохраненныеНастройки.КлючДляАвторизацииВУНФ;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьНастройкуТелефонии(Ключ, Значение) Экспорт
	
	НастройкиТелефонии = ПолучитьНастройкиТелефонии();
	НастройкиТелефонии.Вставить(Ключ, Значение);
	
	Константы.сфпНастройкиТелефонии.Установить(Новый ХранилищеЗначения(НастройкиТелефонии));
	
КонецПроцедуры

Функция ПерсональноеМобильноеУстройство() Экспорт
	
	// СофтФон
	Возврат Неопределено;
	//Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиТелефонии", "УзелОбмена", Неопределено);
	
КонецФункции

Функция АдресОбратногоВызоваОблачнаяТелефония(ИспользуемаяАТС = Неопределено, АдресВебхуковКоллтрекинга = "") Экспорт
	Перем НавигационнаяСсылка;
	
	Если ИспользуемаяАТС = Неопределено Тогда
		ИспользуемаяАТС = Константы.сфпИспользуемаяАТС.Получить();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИспользуемаяАТС) Тогда
		Возврат НавигационнаяСсылка;
	КонецЕсли;
	
	ОбщиеНастройкиТелефонии = Неопределено;
	
	ПараметрыПодключения = Новый Структура;
	КонстантаОбщиеНастройкиТелефонии = Константы.сфпОбщиеНастройкиТелефонии.Получить();
	Если ЗначениеЗаполнено(КонстантаОбщиеНастройкиТелефонии)
		 И ТипЗнч(КонстантаОбщиеНастройкиТелефонии) = Тип("Строка") Тогда
		ОбщиеНастройкиТелефонии = ЗначениеИзСтрокиВнутр(КонстантаОбщиеНастройкиТелефонии);
	КонецЕсли;	
	
	АдресСервераОсновнойПубликации = Неопределено;
	АдресРесурсаОсновнойПубликации = Неопределено;
	АдресВебхуковКоллтрекинга = "";
	
	Если ТипЗнч(ОбщиеНастройкиТелефонии) = Тип("Структура") Тогда
		ОбщиеНастройкиТелефонии.Свойство("АдресСервераОсновнойПубликации", АдресСервераОсновнойПубликации);
		ОбщиеНастройкиТелефонии.Свойство("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликации);
		
		Если ЗначениеЗаполнено(АдресСервераОсновнойПубликации) Тогда
			ПараметрыПодключения.Вставить("АдресСервераОсновнойПубликации", АдресСервераОсновнойПубликации);
		КонецЕсли;
		
		КорневойURLСервиса = КорневойURLСервисаОсновнойПубликации(ИспользуемаяАТС);
		
		АдресВебхуковКоллтрекинга = АдресСервераОсновнойПубликации + "/" + АдресРесурсаОсновнойПубликации 
			+ "/hs/telephony-api/calltracking";
		АдресРесурсаОсновнойПубликации = АдресСервераОсновнойПубликации + "/" 
			+ АдресРесурсаОсновнойПубликации + "/hs/" 
			+ КорневойURLСервиса;
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ОбластьДанных = Формат(РаботаВМоделиСервиса.ЗначениеРазделителяСеанса(), "ЧГ=0");
		НавигационнаяСсылка = СтрЗаменить(АдресРесурсаОсновнойПубликации, "/hs/", "/" + ОбластьДанных + "/hs/");
		АдресВебхуковКоллтрекинга = СтрЗаменить(АдресВебхуковКоллтрекинга, "/hs/", "/" + ОбластьДанных + "/hs/");

	Иначе
		НавигационнаяСсылка = АдресРесурсаОсновнойПубликации;
	КонецЕсли;
	
	АдресВебхуковКоллтрекинга = СтрЗаменить(НавигационнаяСсылка, "telephony-api/mango", "telephony-api/calltracking");
	
	НавигационнаяСсылка = СтрЗаменить(НавигационнаяСсылка, "://", "@&?");
	НавигационнаяСсылка = СтрЗаменить(НавигационнаяСсылка, "//", "/");	
	НавигационнаяСсылка = СтрЗаменить(НавигационнаяСсылка, "@&?", "://");
		
	Возврат НавигационнаяСсылка;
	
КонецФункции

Функция АдресВебхуковКоллтрекинга(ИспользуемаяАТС = Неопределено) Экспорт
	Перем АдресВебхуковКоллтрекинга;
	
	ОбщиеНастройкиТелефонии = Неопределено;
	
	ПараметрыПодключения = Новый Структура;
	КонстантаОбщиеНастройкиТелефонии = Константы.сфпОбщиеНастройкиТелефонии.Получить();
	Если ЗначениеЗаполнено(КонстантаОбщиеНастройкиТелефонии)
		 И ТипЗнч(КонстантаОбщиеНастройкиТелефонии) = Тип("Строка") Тогда
		ОбщиеНастройкиТелефонии = ЗначениеИзСтрокиВнутр(КонстантаОбщиеНастройкиТелефонии);
	КонецЕсли;	
	
	АдресСервераОсновнойПубликации = Неопределено;
	АдресРесурсаОсновнойПубликации = Неопределено;
	АдресВебхуковКоллтрекинга = "";
	
	Если ТипЗнч(ОбщиеНастройкиТелефонии) = Тип("Структура") Тогда
		ОбщиеНастройкиТелефонии.Свойство("АдресСервераОсновнойПубликации", АдресСервераОсновнойПубликации);
		ОбщиеНастройкиТелефонии.Свойство("АдресРесурсаОсновнойПубликации", АдресРесурсаОсновнойПубликации);
		
		Если ЗначениеЗаполнено(АдресСервераОсновнойПубликации) Тогда
			ПараметрыПодключения.Вставить("АдресСервераОсновнойПубликации", АдресСервераОсновнойПубликации);
		КонецЕсли;
		
		КорневойURLСервиса = КорневойURLСервисаОсновнойПубликации(ИспользуемаяАТС);
		
		АдресВебхуковКоллтрекинга = АдресСервераОсновнойПубликации + "/" + АдресРесурсаОсновнойПубликации 
			+ "/hs/telephony-api/calltracking";
		АдресРесурсаОсновнойПубликации = АдресСервераОсновнойПубликации + "/" 
			+ АдресРесурсаОсновнойПубликации + "/hs/" 
			+ КорневойURLСервиса;
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ОбластьДанных = Формат(РаботаВМоделиСервиса.ЗначениеРазделителяСеанса(), "ЧГ=0");
		АдресВебхуковКоллтрекинга = СтрЗаменить(АдресВебхуковКоллтрекинга, "/hs/", "/" + ОбластьДанных + "/hs/");
	КонецЕсли;
	
	Возврат АдресВебхуковКоллтрекинга;
	
КонецФункции

Функция КорневойURLСервисаОсновнойПубликации(ИспользуемаяАТС) Экспорт
	
	КорневойСервис = Метаданные.HTTPСервисы.softphoneTelephonyApi.КорневойURL;
	
	Если ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.СофтФонWebModule Тогда
		URLСервиса = КорневойСервис + "/webmodule";

	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.MangoOffice Тогда
		URLСервиса = КорневойСервис + "/mango";

	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Яндекс Тогда
		URLСервиса = КорневойСервис + Метаданные.HTTPСервисы.softphoneTelephonyApi.ШаблоныURL.yandexEvent.Шаблон;
		
	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Билайн Тогда
		URLСервиса = КорневойСервис + Метаданные.HTTPСервисы.softphoneTelephonyApi.ШаблоныURL.beelineEvent.Шаблон;
		
	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.МТТ Тогда
		URLСервиса = КорневойСервис + Метаданные.HTTPСервисы.softphoneTelephonyApi.ШаблоныURL.mttEvent.Шаблон;
		
	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.UIS Тогда
		URLСервиса = КорневойСервис + Метаданные.HTTPСервисы.softphoneTelephonyApi.ШаблоныURL.uisEvent.Шаблон;	
		
	ИначеЕсли ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Ростелеком Тогда
		URLСервиса = КорневойСервис + "/rt";
		
	ИначеЕсли сфпСофтФонПроСерверПереопределяемый.ЭтоПлатформаITooLabs(ИспользуемаяАТС) Тогда
		URLСервиса = КорневойСервис + Метаданные.HTTPСервисы.softphoneTelephonyApi.ШаблоныURL.itoolabsEvent.Шаблон;	

	Иначе
		Возврат "";
	КонецЕсли;
	
	Возврат СтрЗаменить(URLСервиса, "/*", "");
	
КонецФункции

#КонецОбласти

#Область ОблачнаяТелефония

Функция ПолучитьПользователяОтветственногоЗаАбонента(АбонентОтКого) Экспорт
	
	Если Не ЗначениеЗаполнено(АбонентОтКого) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если АбонентОтКого.Метаданные().Реквизиты.Найти("Ответственный") = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// СофтФон
	Возврат Справочники.Пользователи.ПустаяСсылка();
	//Ответственный = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(АбонентОтКого, "Ответственный");
	//Если ЗначениеЗаполнено(Ответственный) И ТипЗнч(Ответственный) = Тип("СправочникСсылка.Сотрудники") Тогда
	//	Ответственный = РегистрыСведений.СотрудникиПользователя.ПолучитьПользователяПоСотруднику(Ответственный);
	//КонецЕсли;
	//
	// Возврат Ответственный;
	
КонецФункции

Функция ОбработатьВходящийЗвонок(НомерКонтакта, Пользователь, ДатаНачалаРазговора = Неопределено,
	 ИдентификаторЗвонкаВАТС = Неопределено) Экспорт
	
	Если НЕ БылаИнициализацияЗвонка(ИдентификаторЗвонкаВАТС, Пользователь) Тогда
		Возврат сфпЛицензированиеЭкспортныеМетоды.ОбработатьВходящийЗвонок(НомерКонтакта, Пользователь,
			 ДатаНачалаРазговора,
			 ИдентификаторЗвонкаВАТС);
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

Функция ОбработатьИсходящийЗвонок(ДатаСобытия, Пользователь, Контрагент = Неопределено,
	 НомерКонтакта = Неопределено, ИдентификаторЗвонкаВАТС = Неопределено,
	 ДатаНачалаРазговора = Неопределено) Экспорт
	Возврат сфпЛицензированиеЭкспортныеМетоды.ОбработатьИсходящийЗвонок(ДатаСобытия, Пользователь,
		 Контрагент, НомерКонтакта, ИдентификаторЗвонкаВАТС,
		 ДатаНачалаРазговора);
КонецФункции

Функция ОбработатьИзменениеЗвонка(ИдентификаторЗвонкаВАТС = Неопределено, ДатаНачалаРазговора,
	 Пользователь = Неопределено, ВходящееИсходящее = Неопределено,
	 НомерКонтакта = Неопределено) Экспорт
	
	Если НЕ БылаИнициализацияЗвонка(ИдентификаторЗвонкаВАТС, Пользователь) Тогда
		Если ВходящееИсходящее = Истина Тогда
			сфпЛицензированиеЭкспортныеМетоды.ОбработатьВходящийЗвонок(НомерКонтакта, Пользователь,
				 ДатаНачалаРазговора,
				 ИдентификаторЗвонкаВАТС);
			
		Иначе	
			сфпЛицензированиеЭкспортныеМетоды.ОбработатьИсходящийЗвонок(ДатаНачалаРазговора, Пользователь, ,
				 НомерКонтакта, ИдентификаторЗвонкаВАТС,
				 ДатаНачалаРазговора);
        КонецЕсли;
	КонецЕсли;
	
	Возврат сфпЛицензированиеЭкспортныеМетоды.ОбработатьИзменениеЗвонка(ИдентификаторЗвонкаВАТС,
		 ДатаНачалаРазговора, Пользователь, ВходящееИсходящее,
		 НомерКонтакта);

КонецФункции

Функция ОбработатьУдержаниеЗвонка(ИдентификаторЗвонкаВАТС, Пользователь) Экспорт
	Возврат сфпЛицензированиеЭкспортныеМетоды.ОбработатьУдержаниеЗвонка(ИдентификаторЗвонкаВАТС, Пользователь);
КонецФункции

Функция ОбработатьЗавершениеЗвонка(Входящий = Неопределено, НомерКонтакта = Неопределено,
	 ДатаНачалаВызова = Неопределено, ДатаЗавершенияВызова = Неопределено,
	 ДлительностьРазговора = Неопределено,
		Неотвеченный = Неопределено, СсылкаНаЗаписьРазговора = Неопределено, ТрубетсяЗапроситьЗаписьРазговора = Ложь, ИдентификаторЗвонкаВАТС = Неопределено, ОпределятьНеотвеченный = Истина, Пользователь = Неопределено) Экспорт
	Возврат сфпЛицензированиеЭкспортныеМетоды.ОбработатьЗавершениеЗвонка(Входящий, НомерКонтакта,
		 ДатаНачалаВызова, ДатаЗавершенияВызова, ДлительностьРазговора, Неотвеченный,
		 СсылкаНаЗаписьРазговора, ТрубетсяЗапроситьЗаписьРазговора, ИдентификаторЗвонкаВАТС,
		 ОпределятьНеотвеченный,
		 Пользователь);
КонецФункции

Функция ОбработатьЗаписьЗвонка(ИдентификаторЗвонкаВАТС, СсылкаНаЗаписьРазговора) Экспорт
	Возврат сфпЛицензированиеЭкспортныеМетоды.ОбработатьЗаписьЗвонка(ИдентификаторЗвонкаВАТС, СсылкаНаЗаписьРазговора);
КонецФункции

Функция ПолучитьДанныеКлиента(НомерКонтакта) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Абонент = ПолучитьАбонентаПоНомеруТелефона(НомерКонтакта);
	
	Если НЕ ЗначениеЗаполнено(Абонент) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Ответственный = ПолучитьПользователяОтветственногоЗаАбонента(Абонент);
	ВнутреннийНомерОтветственного = Неопределено;
	Если ЗначениеЗаполнено(Ответственный) Тогда
		ДанныеПользователя = ДанныеПользователяАТС(Ответственный);
		ВнутреннийНомерОтветственного = ДанныеПользователя.ВнутреннийНомер;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Представление", Строка(Абонент));
	Если ЗначениеЗаполнено(ВнутреннийНомерОтветственного) Тогда
		Результат.Вставить("ВнутреннийНомерОтветственного", ВнутреннийНомерОтветственного);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция БылаИнициализацияЗвонка(ИдентификаторЗвонкаВАТС, Пользователь = "")
	
	ДатаНачала = (ТекущаяДатаСеанса() - 3600);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ Звонок
	|ИЗ РегистрСведений.сфпИсторияЗвонков
	|ГДЕ ИдентификаторЗвонка = &ИдентификаторЗвонка И ДатаНачала > &ДатаНачала");
	Запрос.УстановитьПараметр("ИдентификаторЗвонка", ИдентификаторЗвонкаВАТС);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	
	Если ЗначениеЗаполнено(Пользователь) И ТипЗнч(Пользователь) = Тип("Строка") Тогда
		Запрос.Текст = Запрос.Текст + " И ВнутреннийНомер = &ВнутреннийНомер";
		Запрос.УстановитьПараметр("ВнутреннийНомер", Пользователь);
	КонецЕсли;

	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область МобильнаяТелефония

Функция НайтиТелефонныйЗвонокПоДаннымЗвонка(Входящий, Знач Номер = Неопределено,
	 ИдентификаторЗвонкаВАТС = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Звонок
	|ИЗ
	|	РегистрСведений.сфпИсторияЗвонков
	|ГДЕ
	|	ВЫБОР КОГДА &ЭтоЗвонокОблачнойАТС ТОГДА
	|		ИдентификаторЗвонка = &ИдентификаторЗвонкаВАТС И ДатаНачала > &ДатаНачала
	|	ИНАЧЕ Входящий = &Входящий
	|		  И НомерТелефона ПОДОБНО &ПоисковоеВыражение
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНачала УБЫВ");
	
	ПоисковоеВыражение = СтрЗаменить(Номер, "+", "") + "%";
	Если сфпОбщегоНазначения.сфпСтрНачинаетсяС(ПоисковоеВыражение, "7")
		 Или сфпОбщегоНазначения.сфпСтрНачинаетсяС(ПоисковоеВыражение,
		 "8") Тогда
		ПоисковоеВыражение = "[78]" + Сред(ПоисковоеВыражение, 2);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ЭтоЗвонокОблачнойАТС", ИдентификаторЗвонкаВАТС <> Неопределено);
	Запрос.УстановитьПараметр("ИдентификаторЗвонкаВАТС", ИдентификаторЗвонкаВАТС);
	Запрос.УстановитьПараметр("Входящий", Входящий);
	Запрос.УстановитьПараметр("ПоисковоеВыражение", ПоисковоеВыражение);
	Запрос.УстановитьПараметр("ДатаНачала", (ТекущаяДатаСеанса() - 3600));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Звонок;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область РечеваяАналитика
Процедура сфпОбновлениеРечевойАналитики() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.сфпОбновлениеРечевойАналитики);

	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ Константы.сфпИспользоватьРечевуюАналитику.Получить() Тогда
		Возврат;
	КонецЕсли;	
	
	ДатаВыборки = Константы.сфпДатаРечевойАналитики.Получить();
	Если НЕ ЗначениеЗаполнено(ДатаВыборки) Тогда
		ДатаВыборки = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли;	
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ Звонок, ИдентификаторЗаписи, ДатаНачала, НомерТелефона, ВнутреннийНомер
	|ИЗ РегистрСведений.сфпИсторияЗвонков
	|ГДЕ ДатаНачала >= &ДатаВыборки И ПОДСТРОКА(ИдентификаторЗаписи, 1, 10) <> """"
	|УПОРЯДОЧИТЬ ПО ДатаНачала
	|АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("ДатаВыборки", ДатаВыборки);
	
	КоличествоПорцииОбработки = 100;
	ИдентификаторыЗвонков = Новый Соответствие();
	
	ДатаПоследнегоЗвонка = Неопределено;
	
	ТаблицаИстории = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаИстории Из ТаблицаИстории Цикл
		Если ИдентификаторыЗвонков.Количество() = КоличествоПорцииОбработки Тогда
			ЗаполнитьРасшифровкуЗвонков(ИдентификаторыЗвонков);

			ИдентификаторыЗвонков.Очистить();
		КонецЕсли;
		
		ПараметрыЗвонка = Новый Структура("Звонок,ВнутреннийНомер,НомерТелефона", СтрокаИстории.Звонок,
			 СтрокаИстории.ВнутреннийНомер,
			 СтрокаИстории.НомерТелефона);
		ИдентификаторыЗвонков.Вставить(СтрокаИстории.ИдентификаторЗаписи, ПараметрыЗвонка);
		
		РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПолучитьРезультатыРаспознованияТематики",
			 СтрокаИстории.ИдентификаторЗаписи);
		Если РезультатЗапроса.Успешно И ЗначениеЗаполнено(РезультатЗапроса.Результат) Тогда
			json = Новый ЧтениеJSON();
			json.УстановитьСтроку(РезультатЗапроса.Результат);
			jsonСтруктура = ПрочитатьJSON(json);
			json.Закрыть();
			
			Если jsonСтруктура.result = 1000 Тогда
				Для Каждого СтрокаДанных Из jsonСтруктура.data Цикл
					Если СтрокаДанных.recording_id = СтрокаИстории.ИдентификаторЗаписи Тогда
						Для Каждого СтрокаКатегории Из СтрокаДанных.categories Цикл
							Тематика = ПолучитьТематикуРечевойАналитики(Перечисления.сфпДоступныеАТС.MangoOffice,
								 СтрокаКатегории.id,
								 СтрокаКатегории.name);
							
							СтопСлова = "";
							Количество = 0;
														
							Для Каждого СтрокаСлов Из СтрокаКатегории.terms Цикл
								СтопСлово = СтрЗаменить(СтрокаСлов.value, """", "");
								СтопСлова = СтопСлова + ?(СтопСлова = "", "", ", ") + СтопСлово;
								Количество = Количество + СтрокаСлов.count;
							КонецЦикла;
							
							МенеджерЗаписи = РегистрыСведений.сфпРечеваяАналитика.СоздатьМенеджерЗаписи();
							МенеджерЗаписи.Звонок = СтрокаИстории.Звонок;
							МенеджерЗаписи.Тематика = Тематика;
							МенеджерЗаписи.Количество = Количество;
							МенеджерЗаписи.СтопСлова = СтопСлова;
							МенеджерЗаписи.Записать();
							
							ДатаПоследнегоЗвонка = СтрокаИстории.ДатаНачала;
						КонецЦикла;
						
						Прервать;
					КонецЕсли;
				КонецЦикла;	
			КонецЕсли;	

		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;	

	ЗаполнитьРасшифровкуЗвонков(ИдентификаторыЗвонков);

	Если ДатаПоследнегоЗвонка <> Неопределено Тогда
		Константы.сфпДатаРечевойАналитики.Установить(ДатаПоследнегоЗвонка);
	КонецЕсли;	

КонецПроцедуры

Процедура ЗаполнитьРасшифровкуЗвонков(ДанныеЗвонков)
	
	СтрОшибки = "";
	
	Если ДанныеЗвонков.Количество() > 0 Тогда
		РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПолучитьРезультатыРасшифровкиРазговоров",
			 ДанныеЗвонков);
		Если РезультатЗапроса.Успешно И ЗначениеЗаполнено(РезультатЗапроса.Результат) Тогда
			json = Новый ЧтениеJSON();
			json.УстановитьСтроку(РезультатЗапроса.Результат);
			jsonСтруктура = ПрочитатьJSON(json);
			json.Закрыть();
			
			Если jsonСтруктура.result = 1000 Тогда
				Для Каждого СтрокаДанных Из jsonСтруктура.data Цикл
					ДанныеЗвонка = ДанныеЗвонков.Получить(СтрокаДанных.recording_id);
					Если ЗначениеЗаполнено(ДанныеЗвонка.Звонок) Тогда
						ТекстРазговора = "";
						Для Каждого СтрокаФраз Из СтрокаДанных.phrases Цикл
							СтрокаРасшифровки = СтрокаФраз[0] + ": " + СтрокаФраз[1];
							ТекстРазговора = ТекстРазговора + ?(ТекстРазговора = "", "", "
							|
							|") + СтрокаРасшифровки;
						КонецЦикла;	
						
						Если ЗначениеЗаполнено(ТекстРазговора) Тогда
							ТекстРазговора = СтрЗаменить(ТекстРазговора, "operator", ДанныеЗвонка.ВнутреннийНомер);
							ТекстРазговора = СтрЗаменить(ТекстРазговора, "client", ДанныеЗвонка.НомерТелефона);
														
							ЗвонокОбъект = ДанныеЗвонка.Звонок.ПолучитьОбъект();
							ЗвонокОбъект.сфпРасшифровкаРазговора = ТекстРазговора;
							ЗвонокОбъект.ОбменДанными.Загрузка = Истина;
							ЗвонокОбъект.Записать();
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			Иначе
				СтрОшибки = РезультатЗапроса.Результат;
			КонецЕсли;	
			
		Иначе
			СтрОшибки = РезультатЗапроса.Ошибка;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрОшибки) Тогда
		ВызватьИсключение "При получении расшифровки звонков произошла ошибка: " + СтрОшибки;
	КонецЕсли;

КонецПроцедуры	//	ЗаполнитьРасшифровкуЗвонков()

Функция ПолучитьТематикуРечевойАналитики(Оператор, Идентификатор, Наименование)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ Ссылка, Наименование
	|ИЗ Справочник.сфпТематикиРечевойАналитики
	|ГДЕ Оператор = &Оператор И Идентификатор = &Идентификатор");
	Запрос.УстановитьПараметр("Оператор", Оператор);
	Запрос.УстановитьПараметр("Идентификатор", "" + Идентификатор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.Наименование <> Наименование Тогда
			ТекОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ТекОбъект.Наименование = Наименование;
			ТекОбъект.Записать();
		КонецЕсли;
		
		Возврат Выборка.Ссылка;
	КонецЕсли;	
	
	НовыйЭлемент = Справочники.сфпТематикиРечевойАналитики.СоздатьЭлемент();
	НовыйЭлемент.Оператор = Оператор;
	НовыйЭлемент.Идентификатор = Идентификатор;
	НовыйЭлемент.Наименование = Наименование;
	НовыйЭлемент.Записать();
	
	Возврат НовыйЭлемент.Ссылка;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает Истина, если вход в сеанс выполнил внешний пользователь
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Истина, если вход в сеанс выполнил внешний пользователь, иначе Ложь
//
Функция сфпЭтоСеансВнешнегоПользователя() Экспорт
	Возврат ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя();
КонецФункции // сфпЭтоСеансВнешнегоПользователя()
 
Процедура сфпЗаписьЖурналаРегистрации(ИмяСобытия, ТекстЗаписи, Ошибка = Ложь) Экспорт
	
	Если Ошибка Тогда
		  Уровень = УровеньЖурналаРегистрации.Ошибка;
	Иначе Уровень = УровеньЖурналаРегистрации.Информация;
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации("Телефония." + ИмяСобытия, Уровень, , , ТекстЗаписи);

КонецПроцедуры

 // Функция проверяет использование СофтФона
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Признак использования СофтФона
//
Функция сфпИспользоватьСофтФон() Экспорт
	Если ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя() Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Константы.сфпИспользоватьСофтФон.Получить();
КонецФункции // сфпИспользоватьСофтФон()

// Функция проверяет использование автоматического определения Бизнес-региона
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Признак использования автоматического определения Бизнес-региона
//
Функция сфпИспользоватьАвтоопределениеБизнесРегиона() Экспорт
	Возврат Константы.сфпПланНумерацииАвтоопределениеБизнесРегиона.Получить();
КонецФункции // сфпИспользоватьАвтоопределениеБизнесРегиона()

// Функция возвращает значение настройки для переданного пользователя
//
// Параметры:
//	ИмяНастройки	- Строка			- Имя настройки в ПВХ НастройкиПользователей
//	Пользователь	- СправочникСсылка	- Пользователь, для которого получается настройка
//
// Возвращаемое значение:
//	Произвольный	- Значение настройки.
//
Функция сфпПолучитьЗначениеНастройкиПользователя(ИмяНастройки, Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		Пользователь = сфпТекущийПользователь();
	КонецЕсли;
	
	НастройкаПВХ = ПланыВидовХарактеристик.CRM_НастройкиПользователей[ИмяНастройки];
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ Значение
	|ИЗ РегистрСведений.CRM_НастройкиПользователей
	|ГДЕ Пользователь = &Пользователь И Настройка = &Настройка");
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Настройка", НастройкаПВХ);	
	
	ЗначениеНастройки = НастройкаПВХ.ТипЗначения.ПривестиЗначение();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.Значение) Тогда
			ЗначениеНастройки = Выборка.Значение;
		КонецЕсли;
	КонецЕсли;

	ВерсияСофтфон = сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпИспользуемаяВерсияСофтФон");
	Если ВерсияСофтфон = Перечисления.сфпВерсииСофтФон.СофтФотPROSTO И НЕ ЗначениеЗаполнено(ЗначениеНастройки) Тогда
		Если (ИмяНастройки = "сфпДействиеПриВходящемЗвонке" ИЛИ ИмяНастройки = "сфпДействиеПриИсходящемЗвонке") Тогда
			НастройкиТелефонии = ПолучитьНастройкиТелефонии();
			
			Если ИмяНастройки = "сфпДействиеПриВходящемЗвонке" Тогда
				ЗначениеНастройки = НастройкиТелефонии.ДействиеВходящегоЗвонка;
				
			ИначеЕсли ИмяНастройки = "сфпДействиеПриИсходящемЗвонке" Тогда
				ЗначениеНастройки = НастройкиТелефонии.ДействиеИсходящегоЗвонка;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначениеНастройки;

КонецФункции // сфпПолучитьЗначениеНастройкиПользователя() 

// Функция возвращает структуру параметров сервера
//
// Параметры:
//	Нет.
//
Функция сфпПараметрыСервера() Экспорт
	Возврат сфпСофтФонПроСерверПовтИсп.сфпПараметрыСервера();
КонецФункции // сфпПараметрыСервера()

// Процедура сохраняет в константах префиксы и настройки, полученные от сервера СофтФона
//
// Параметры:
//	СтруктураНастроек	- Соответствие	- Префиксы и настройки, полученные от сервера СофтФона
//
Процедура сфпЗаписатьПараметрыСервераНативнаяКомпонента(СтруктураНастроек) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если СтруктураНастроек.Свойство("PrefCity") И НЕ (СтруктураНастроек["PrefCity"] = Неопределено) Тогда
		Константы.сфпПрефиксВыходаВГород.Установить(СтруктураНастроек["PrefCity"]);
	КонецЕсли;		
	Если СтруктураНастроек.Свойство("PrefContry") И НЕ (СтруктураНастроек["PrefContry"] = Неопределено) Тогда
		Константы.сфпПрефиксВыходаВМежгород.Установить(СтруктураНастроек["PrefContry"]);
	КонецЕсли;		
	Если СтруктураНастроек.Свойство("PrefWorld") И НЕ (СтруктураНастроек["PrefWorld"] = Неопределено) Тогда
		Константы.сфпПрефиксВыходаНаМеждународную.Установить(СтруктураНастроек["PrefWorld"]);
	КонецЕсли;		
	Если СтруктураНастроек.Свойство("InternalNumMaxLen") И НЕ (СтруктураНастроек["InternalNumMaxLen"] = Неопределено) Тогда
		Константы.сфпМаксимальнаяДлинаВнутреннихНомеров.Установить(СтруктураНастроек["InternalNumMaxLen"]);
	КонецЕсли;		
	Если СтруктураНастроек.Свойство("InternalNumMinLen") И НЕ (СтруктураНастроек["InternalNumMinLen"] = Неопределено) Тогда
		Константы.сфпМинимальнаяДлинаВнутреннихНомеров.Установить(СтруктураНастроек["InternalNumMinLen"]);
	КонецЕсли;		
	Если СтруктураНастроек.Свойство("LastNumberCount") И НЕ (СтруктураНастроек["LastNumberCount"] = Неопределено) Тогда
		Константы.сфпПоследниеЦифрыТелефонногоНомера.Установить(СтруктураНастроек["LastNumberCount"]);
	КонецЕсли;		
	Если СтруктураНастроек.Свойство("DBServerType") И НЕ (СтруктураНастроек["DBServerType"] = Неопределено) Тогда
		Если СтруктураНастроек["DBServerType"] = 0 Тогда
			Если СтруктураНастроек.Свойство("SQLConnectionString")
				 И НЕ (СтруктураНастроек["SQLConnectionString"] = Неопределено) Тогда
				Константы.сфпСтрокаПодключенияИстории.Установить(СтруктураНастроек["SQLConnectionString"]);
			КонецЕсли;
		Иначе
			Если СтруктураНастроек.Свойство("PGServerName") И НЕ (СтруктураНастроек["PGServerName"] = Неопределено) Тогда
				pgСтруктураНастроек = "Provider=" + " " + ";" 
				+ "Password=" + СтруктураНастроек["PGPass"] + ";" 
				+ "Persist Security Info=True" + ";"
				+ "User ID=" + СтруктураНастроек["PGLogin"] + ";" 
				+ "Initial Catalog=" + СтруктураНастроек["PGDBName"] + ";"
				+ "Data Source=" + СтруктураНастроек["PGServerName"] + ";" 
				+ "Port=" + СтруктураНастроек["PGServerPort"] + ";" 
				+ "DBServerType=" + СтруктураНастроек["DBServerType"];
				Константы.сфпСтрокаПодключенияИстории.Установить(pgСтруктураНастроек);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если СтруктураНастроек.Свойство("SQLConnectionString")
			 И НЕ (СтруктураНастроек["SQLConnectionString"] = Неопределено) Тогда
			Константы.сфпСтрокаПодключенияИстории.Установить(СтруктураНастроек["SQLConnectionString"]);
		КонецЕсли;
	КонецЕсли;
	Если СтруктураНастроек.Свойство("UseRouter") И НЕ (СтруктураНастроек["UseRouter"] = Неопределено) Тогда
		Константы.сфпИспользоватьМаршрутизацию.Установить(СтруктураНастроек["UseRouter"]);	
	КонецЕсли;	
	Если СтруктураНастроек.Свойство("HistoryOn") И НЕ (СтруктураНастроек["HistoryOn"] = Неопределено) Тогда
		Константы.сфпИспользоватьИсториюЗвонков.Установить(СтруктураНастроек["HistoryOn"]);	
	КонецЕсли;		
	Если СтруктураНастроек.Свойство("UseHistory") И НЕ (СтруктураНастроек["UseHistory"] = Неопределено) Тогда
		Константы.сфпИспользоватьИсториюЗвонков.Установить(СтруктураНастроек["UseHistory"]);	
	КонецЕсли;		
	Если СтруктураНастроек.Свойство("UseSMS") И НЕ (СтруктураНастроек.Свойство("UseSMS") = Неопределено) Тогда
		Константы.сфпИспользоватьSMS.Установить(СтруктураНастроек["UseSMS"]);	
	КонецЕсли;	
	Если СтруктураНастроек.Свойство("ContryCode") И НЕ (СтруктураНастроек["ContryCode"] = Неопределено) Тогда
		Константы.сфпКодСтраны.Установить(СтруктураНастроек["ContryCode"]);	
	КонецЕсли;	
	Если СтруктураНастроек.Свойство("CityCode") И НЕ (СтруктураНастроек["CityCode"] = Неопределено) Тогда
		Константы.сфпКодГорода.Установить(СтруктураНастроек["CityCode"]);	
	КонецЕсли;		
	Если СтруктураНастроек.Свойство("WSDL") И НЕ (СтруктураНастроек["WSDL"] = Неопределено) Тогда
		СтрокаWSDL			= СтруктураНастроек["WSDL"];
		ПозицияРазделителя	= СтрНайти(СтрокаWSDL, "/wsdl/");
		СтрокаПути			= Лев(СтрокаWSDL, ПозицияРазделителя - 1);
		СтрокаПути			= СтрЗаменить(СтрокаПути, "http://", "");
		ПозицияРазделителя	= СтрНайти(СтрокаПути, ":");
		сфпИмяСервера		= Лев(СтрокаПути, ПозицияРазделителя - 1);
		сфпПорт				= Сред(СтрокаПути, ПозицияРазделителя + 1);
		Константы.сфпИмяСервера.Установить(сфпИмяСервера);	
		Константы.сфпПорт.Установить(сфпПорт);	
	КонецЕсли;

	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры // сфпЗаписатьПараметрыСервераНативнаяКомпонента()

// Функция возвращает текущего пользователя
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	СправочникСсылка	- Текущий пользователь
//
Функция сфпТекущийПользователь() Экспорт
	Возврат Пользователи.АвторизованныйПользователь();
КонецФункции // сфпТекущийПользователь()

// Функция возвращает текущее время на сервере
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Дата	- Текущая дата и время
//
Функция сфпТекущаяДата() Экспорт
	Возврат ТекущаяДатаСеанса();	
КонецФункции // сфпТекущаяДата()

// Функция возвращает значение реквизита, прочитанного из информационной базы по ссылке на объект.
// 
// Параметры:
//  Ссылка       - Ссылка на объект, - элемент справочника, документ, ...
//  ИмяРеквизита - Строка, например, "Код".
// 
// Возвращаемое значение:
//  Произвольный    - зависит от типа значения прочитанного реквизита.
// 
Функция сфпПолучитьЗначениеРеквизита(Ссылка, ИмяРеквизита) Экспорт
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
КонецФункции // сфпПолучитьЗначениеРеквизита()

// Функция получает картинку контакта
//
// Параметры:
//	Контакт		- СправочникСсылка	- Контакт
//	ПоУмолчанию	- Булево			- Признак возвращения аватара по умолчанию, в случае его отсутствия
//
// Возвращаемое значение:
//	Строка	- Двоичное представление картинки
//
Функция сфпПолучитьАватарКонтакта(Контакт, ПоУмолчанию = Истина) Экспорт
	АватарНайден	= Ложь;
	Строка64		= "";
	Попытка
		Если НЕ (Контакт.Метаданные().Реквизиты.Найти("CRM_Фотография") = Неопределено)
			 И ЗначениеЗаполнено(Контакт.CRM_Фотография) Тогда
			ДвоичныеДанные =
				ПолучитьИзВременногоХранилища(РаботаСФайлами.ДанныеФайла(Контакт.CRM_Фотография).СсылкаНаДвоичныеДанныеФайла);
			Если (ДвоичныеДанные.Размер() > 0) И (ДвоичныеДанные.Размер() < 2000000) Тогда
				Строка64		= Base64Строка(ДвоичныеДанные);
				АватарНайден	= Истина;
			КонецЕсли;	
		КонецЕсли;
	Исключение
		АватарНайден = Ложь;
	КонецПопытки;
	Если НЕ АватарНайден И ПоУмолчанию Тогда
		Строка64 = Base64Строка(БиблиотекаКартинок.сфпАватарПоУмолчанию.ПолучитьДвоичныеДанные());
	КонецЕсли;
	Возврат Строка64;	
КонецФункции // сфпПолучитьАватарКонтакта()

// Функция возвращает доступность хотя бы одной из указанных ролей или полноправность
// пользователя (текущего или указанного).
//
// Параметры:
//  ИменаРолей   - Строка - имена ролей, разделенные запятыми, доступность которых проверяется.
//
//  Пользователь - Неопределено - проверяется текущий пользователь ИБ.
//               - СправочникСсылка.Пользователи, СправочникСсылка.ВнешниеПользователи - осуществляется
//                    поиск пользователя ИБ по уникальному идентификатору, заданному в реквизите
//                    ИдентификаторПользователяИБ. Если пользователь ИБ не найден, возвращается Ложь.
//               - ПользовательИнформационнойБазы - проверяется указанный пользователь ИБ.
//
// Возвращаемое значение:
//  Булево - Истина, если хотя бы одна из указанных ролей доступна,
//           или функция ЭтоПолноправныйПользователь(Пользователь) возвращает Истина.
//
Функция сфпРолиДоступны(ИменаРолей, Пользователь = Неопределено) Экспорт
	Если Пользователь = Неопределено Тогда
		Пользователь	= сфпТекущийПользователь();
	КонецЕсли;	
	Возврат Пользователи.РолиДоступны(ИменаРолей, Пользователь);
КонецФункции // сфпРолиДоступны()

// Функция проверяет использование регламентного задания обновления телефонных книг
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Признак использования регламентного задания обновления телефонных книг
//
Функция сфпИспользоватьРегламентноеЗаданиеТелефонныхКниг()
	Возврат Константы.сфпИспользоватьРегламентноеЗаданиеТелефонныхКниг.Получить();
КонецФункции // сфпИспользоватьРегламентноеЗаданиеТелефонныхКниг()

// Функция возвращает признак использования подразделений пользователей
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Использование подразделений пользователей
//
Функция сфпИспользоватьПодразделенияПользователей() Экспорт
	Если Метаданные.Справочники.Найти("СтруктураПредприятия") = Неопределено Тогда
		Возврат Ложь;
	ИначеЕсли Не CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		ИмяОпции = "CRM_Модуль_ИспользоватьПодразделения";
		Возврат ПолучитьФункциональнуюОпцию(ИмяОпции);
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции // сфпИспользоватьПодразделенияПользователей()	

// Функция возвращает признак использования групп пользователей
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Использование групп пользователей
//
Функция сфпИспользоватьГруппыПользователей() Экспорт
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГруппыПользователей") = Истина Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;	
КонецФункции // сфпИспользоватьГруппыПользователей()	

// Процедура записывает окончание разговора в документ "Телефонный звонок"
//
// Параметры:
//	НовыйЗвонок	- ДокументСсылка	- Ссылка на документ
//  НовыйОтветственный 	- СправочникСсылка 	- Новый ответственный звонка
//
Процедура сфпЗаписатьОкончаниеЗвонка(НовыйЗвонок, НовыйОтветственный = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);	

	ДокументОбъект = НовыйЗвонок.ПолучитьОбъект();
	ДокументОбъект.сфпДлительностьЗвонка = (сфпТекущаяДата() - НовыйЗвонок.Дата);
	ДокументОбъект.Описание	= сфпЗаполнитьОписаниеТелефонногоЗвонка(ДокументОбъект.сфпДлительностьЗвонка);
	ДокументОбъект.сфпСостояниеЗвонка = Перечисления.сфпСостоянияЗвонков.Отвеченный;
	Если ЗначениеЗаполнено(НовыйОтветственный) Тогда
		ДокументОбъект.Ответственный = НовыйОтветственный;
	КонецЕсли;

	Попытка
		ДокументОбъект.Записать();
	Исключение 
		ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка, , , 
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
	КонецПопытки;

	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры // сфпЗаписатьОкончаниеЗвонка()

// Процедура записывает окончание разговора в документ "Телефонный звонок"
//
// Параметры:
//	Звонок			- ДокументСсылка	- Ссылка на документ
//  ДанныеЗвонка	- Структура			- Обновляемые параметры
//
Функция сфпОбновитьДанныеЗвонка(Звонок, ДанныеЗвонка, ЗавершатьПропущенныеЗвонки = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);	
	
	ЗвонокОбъект = Звонок.ПолучитьОбъект();
	
	ЗаписатьОбъект = Ложь;
	
	Если ДанныеЗвонка.Свойство("СостояниеЗвонка") И ЗначениеЗаполнено(ДанныеЗвонка.СостояниеЗвонка)
		 И (ЗвонокОбъект.сфпСостояниеЗвонка <> ДанныеЗвонка.СостояниеЗвонка) Тогда
		ЗвонокОбъект.сфпСостояниеЗвонка = ДанныеЗвонка.СостояниеЗвонка;
		ЗаписатьОбъект = Истина;
	КонецЕсли;
	
	Если ДанныеЗвонка.Свойство("ДлительностьЗвонка")
		 И ЗначениеЗаполнено(ДанныеЗвонка.ДлительностьЗвонка)
		 И (ЗвонокОбъект.сфпДлительностьЗвонка <> ДанныеЗвонка.ДлительностьЗвонка) Тогда
		ЗвонокОбъект.сфпДлительностьЗвонка = ДанныеЗвонка.ДлительностьЗвонка;
		ЗаписатьОбъект = Истина;
	КонецЕсли;	
	
	Если ДанныеЗвонка.Свойство("ИдентификаторЗаписи")
		 И ЗначениеЗаполнено(ДанныеЗвонка.ИдентификаторЗаписи)
		 И НЕ ЗначениеЗаполнено(ЗвонокОбъект.сфпИдентификаторЗаписи) Тогда
		ЗвонокОбъект.сфпИдентификаторЗаписи = ДанныеЗвонка.ИдентификаторЗаписи;
		ЗаписатьОбъект = Истина;
	КонецЕсли;	
	
	Описание = сфпЗаполнитьОписаниеТелефонногоЗвонка(ЗвонокОбъект.сфпДлительностьЗвонка);
	Если ЗвонокОбъект.Описание <> Описание Тогда
		ЗвонокОбъект.Описание = Описание;
		ЗаписатьОбъект = Истина;
	КонецЕсли;
	
	Справочники.CRM_ПравилаОбработкиОбращений.ПрименитьПравило(ЗвонокОбъект);
	
	Результат = Ложь;
	
	Попытка
		Если ЗаписатьОбъект Тогда
			ЗвонокОбъект.Записать();
			Результат = Истина;
		КонецЕсли;
	Исключение 
		ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка, , , 
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
	КонецПопытки;	
	
	Если ЗвонокОбъект.сфпСостояниеЗвонка = Перечисления.сфпСостоянияЗвонков.Отвеченный
		 И ЗавершатьПропущенныеЗвонки = Истина Тогда
		// Отмечаем все неотвеченные звонки по данному номеру, как отвеченные
		сфпЗавершитьПропущенныеЗвонки(ЗвонокОбъект.АбонентКакСвязаться);
	КонецЕсли;
	
	Справочники.CRM_ПравилаОбработкиОбращений.ОбработатьОбращение(ЗвонокОбъект);
	
	Возврат Результат;
	
КонецФункции // сфпЗаписатьОкончаниеЗвонка()

// Функция преобразовывает строковые данные оповещения в объекты
//
// Параметры:
//  ДанныеОповещения	- Структура	- Данные оповещения
//
Функция сфпПреобразоватьДанныеОповещения(ДанныеОповещения) Экспорт

	УстановитьПривилегированныйРежим(Истина);	

	ПреобразованныеДанные = ДанныеОповещения;

	Если ДанныеОповещения.Свойство("СостояниеЗвонка") И ЗначениеЗаполнено(ДанныеОповещения.СостояниеЗвонка) Тогда
		ПреобразованныеДанные.Вставить("СостояниеЗвонка", Перечисления.сфпСостоянияЗвонков[ДанныеОповещения.СостояниеЗвонка]);
	КонецЕсли;

	Если ДанныеОповещения.Свойство("ДлительностьЗвонка") И ЗначениеЗаполнено(ДанныеОповещения.ДлительностьЗвонка) Тогда
		ПреобразованныеДанные.Вставить("ДлительностьЗвонка", Число(ДанныеОповещения.ДлительностьЗвонка));
	КонецЕсли;

	Если ДанныеОповещения.Свойство("Звонок") И ЗначениеЗаполнено(ДанныеОповещения.Звонок) Тогда
		ПреобразованныеДанные.Вставить("Звонок",
			 Документы.ТелефонныйЗвонок.ПолучитьСсылку(Новый УникальныйИдентификатор(ДанныеОповещения.Звонок)));
	КонецЕсли;

	УстановитьПривилегированныйРежим(Ложь);

	Возврат ПреобразованныеДанные;

КонецФункции // сфпЗаписатьОкончаниеЗвонка()

// Функция проверяет существует ли данный справочник в конфигурации
//
// Параметры:
//	ИмяСправочника	- Строка	- Имя справочника в конфигурации
//
// Возвращаемое значение:
//	Булево	- Признак наличия справочника
//
Функция сфпСправочникСуществует(ИмяСправочника) Экспорт
	ОбъектМетаданных = Метаданные.Справочники.Найти(ИмяСправочника);
	Если ОбъектМетаданных = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;	
КонецФункции	

// Функция формирует список объектов для выбора пользователем среди найденного массива номеров
//
// Параметры:
//	МассивЗвонящих	- Массив - Массив найденных объектов по номеру телефона
//  ТолькоПартнеры  - Булево - В список добавлять только партнёров и КЛ
//
// Возвращаемое значение:
//	СписокКонтактов - СписокЗначений - Список значений для выбора
//
Функция сфпСформироватьСписокОбъектовДляВыбораПоМассивуЗвонящих(МассивЗвонящих, ТолькоПартнеры = Ложь) Экспорт
	СписокКонтактов = Новый СписокЗначений;	
	Для Каждого Контакт Из МассивЗвонящих Цикл
		Если СтрНайти(Строка(Контакт), НСтр("ru='<Объект не найден>';en='<Object not found>'")) > 0 Тогда
			// Работаем под неполными правами, контакт нам не доступен, в список выбора не подставляем.
			Продолжить;
		КонецЕсли;					
		Если Тип("СправочникСсылка.КонтактныеЛицаПартнеров") = ТипЗнч(Контакт) Тогда
			СписокКонтактов.Добавить(Контакт, Строка(Контакт) + " <" + Строка(сфпПолучитьВладельцаКонтакта(Контакт)) + ">" 
				+ Нстр("ru=' (контактное лицо партнера)';en=' (contact person of partner)'"));
		ИначеЕсли Тип("СправочникСсылка.ФизическиеЛица") = ТипЗнч(Контакт) И НЕ ТолькоПартнеры Тогда
			СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru=' (физическое лицо)';en=' (individual)'"));
		ИначеЕсли Тип("СправочникСсылка.Пользователи") = ТипЗнч(Контакт) И НЕ ТолькоПартнеры Тогда
			СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru=' (пользователь)';en=' (user)'"));
		ИначеЕсли Тип("СправочникСсылка.Партнеры") = ТипЗнч(Контакт) Тогда
			Если Контакт.ЮрФизЛицо = Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо Тогда
				СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru=' (партнер, физ. лицо)';en=' (partner, individual)'"));
			Иначе
				СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru=' (партнер,
					| юр. лицо)';en=' (Partner,
					| Legal Entity)'"));
			КонецЕсли;
		// +CRM3		
		ИначеЕсли Тип("СправочникСсылка.CRM_ПотенциальныеКлиенты") = ТипЗнч(Контакт) Тогда
			СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru=' (потенциальный клиент)';en=' (potential customer)'"));
		// -CRM3
		ИначеЕсли Тип("СправочникСсылка.Организации") = ТипЗнч(Контакт) И НЕ ТолькоПартнеры Тогда
			СписокКонтактов.Добавить(Контакт, Строка(Контакт) + Нстр("ru=' (организация)';en=' (organization)'"));
		Иначе
			// в список найденных не добавляем
		КонецЕсли;	
	КонецЦикла;	
	Возврат СписокКонтактов;
КонецФункции	

// Функция преобразовывает входящий массив в список значений
//
// Параметры:
//	МассивЗвонящих	- Массив - Массив звонящих
//
// Возвращаемое значение:
//	СписокЗвонящих - Список значений - Преобразованынй список значений
//
Функция сфпПреобразоватьМассивЗвонящихВСписокЗначений(МассивЗвонящих) Экспорт
	СписокЗвонящих = Новый СписокЗначений;
	Для Каждого ЭлементМассива Из МассивЗвонящих Цикл
		СписокЗвонящих.Добавить(ЭлементМассива);
	КонецЦикла;		
	Возврат СписокЗвонящих;
КонецФункции	

// Процедура корректирует в случае необходимости значение абонента в документе "Телефонный звонок"
//
// Параметры:
//	КонтактноеЛицо		- СправочникСсылка	- Ссылка на Контактное Лицо
//	Абонент				- СправочникСсылка	- Ссылка на Партнера
//	ТелефонныйЗвонок	- ДокументСсылка	- Ссылка на документ
//
Процедура сфпСкорректироватьАбонентаУТелефонногоЗвонка(КонтактноеЛицо = Неопределено, Абонент, ТелефонныйЗвонок) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Если НЕ ЗначениеЗаполнено(ТелефонныйЗвонок) Тогда Возврат; КонецЕсли;
	Если НЕ (ТипЗнч(ТелефонныйЗвонок) = Тип("ДокументСсылка.ТелефонныйЗвонок")) Тогда
		Возврат;
	КонецЕсли;
	Если КонтактноеЛицо = ТелефонныйЗвонок.АбонентКонтакт Тогда Возврат; КонецЕсли;
	Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		АбонентНовый = КонтактноеЛицо;
	Иначе
		АбонентНовый = Абонент;
	КонецЕсли;		
	Если АбонентНовый <> ТелефонныйЗвонок.АбонентКонтакт Тогда
		ДокументОбъект = ТелефонныйЗвонок.ПолучитьОбъект();
		ДокументОбъект.АбонентКонтакт = АбонентНовый;
		ДокументОбъект.АбонентПредставление = Строка(ДокументОбъект.АбонентКонтакт);
		Попытка  
			ДокументОбъект.Записать();
			Записали = Истина;
		Исключение
			Записали = Ложь;
		КонецПопытки;
		Если Записали Тогда
			сфпЗаписатьАбонентаЗвонкаВРегистрИсторииЗвонков(ТелефонныйЗвонок, ТелефонныйЗвонок.сфпИдентификаторЗвонка);
		КонецЕсли;			
	КонецЕсли;	
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры	

// Функция возвращает ссылку на документ-основание переданного документа
//
// Параметры:
//	ДокументСсылка	- ДокументСсылка - Ссылка на документ
//
// Возвращаемое значение:
//	ДокументОснование - ДокументСсылка - Ссылка на документ-основание
//
Функция сфпВернутьОснованиеДокумента(ДокументСсылка) Экспорт
	МетаданныеДокумента = ДокументСсылка.Метаданные(); 
	Если CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("ДокументОснование", МетаданныеДокумента) Тогда
		Возврат ДокументСсылка.ДокументОснование;
	ИначеЕсли CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("Основание", МетаданныеДокумента) Тогда
		Возврат ДокументСсылка.Основание;
	Иначе
		Возврат Неопределено;
	КонецЕсли;		
КонецФункции	

// Функция возвращает тему для заполнения переданного звонка
//
// Параметры:
//	НаправлениеЗвонка	- Булево 			 - Истина - входящий, Ложь - исходящий звонок
//	ДатаЗвонка			- Дата				 - Дата звонка
//
// Возвращаемое значение:
//	ТемаЗвонка - Строка - Тема звонка
//
Функция сфпЗаполнитьТемуТелефонногоЗвонка(НаправлениеЗвонка, ДатаЗвонка) Экспорт
	// тема звонка = Направление звонка (Входящий/Исходящий) + Время звонка с секундами
	ТемаЗвонка = "";
	Если НаправлениеЗвонка Тогда
		ТемаЗвонка = ТемаЗвонка + Нстр("ru='Входящий: ';en='Incoming: '") + Формат(ДатаЗвонка, "ДЛФ=В");
	Иначе
		ТемаЗвонка = ТемаЗвонка + Нстр("ru='Исходящий: ';en='Outgoing: '") + Формат(ДатаЗвонка, "ДЛФ=В");		
	КонецЕсли;
	Возврат ТемаЗвонка;
КонецФункции

// Функция возвращает описание для заполнения переданного звонка
//
// Параметры:
//	ДлительностьЗвонка	- Дата - Продолжительность звонка
//
// Возвращаемое значение:
//	ОписаниеЗвонка - Строка - Описание звонка
//
Функция сфпЗаполнитьОписаниеТелефонногоЗвонка(ДлительностьЗвонка) Экспорт
	// Описание: Длительность - [длительность звонка]
	ОписаниеЗвонка = Нстр("ru='Длительность - ';en='Duration '") + Формат(Дата('00010101') 
		+ ДлительностьЗвонка,
		 "ДЛФ=T; ДП=0:00:00");
	Возврат ОписаниеЗвонка;
КонецФункции

// Функция возвращает значение ФО "Фиксировать первичный интерес"
//
// Возвращаемое значение:
//	Булево - Значение опции
//
Функция сфпПолучитьЗначениеКонстантыФиксироватьПервичныйИнтерес() Экспорт
	Возврат ПолучитьФункциональнуюОпцию("ФиксироватьПервичныйИнтерес");
КонецФункции	

// Функция вычисляет необходимость появления кнопки "Добавить телефон"
//
//	 Параметры:
//	  Абонент		 		- СправочникСсылка - Проверяяемый абонент на наличие номера телефона
//    АбонентКакСвязаться	 - Строка - Номер телефона
//
//   Возвращаемое значение:
//    Булево - Необходимость показа кнопки "Добавить телефон"
//
Функция сфпПроверитьНаличиеНомераТелефонаУАбонента(Абонент, АбонентКакСвязаться) Экспорт

	Если НЕ ЗначениеЗаполнено(Абонент) Тогда 
		Возврат Ложь;
	КонецЕсли;
	ПоследниеЦифрыТелефонногоНомера = Константы.сфпПоследниеЦифрыТелефонногоНомера.Получить();
	Если ПоследниеЦифрыТелефонногоНомера = 0 Тогда Возврат Ложь; КонецЕсли;		
	СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(АбонентКакСвязаться);
	ПредставлениеКакСвязаться = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
		+ СтруктураНомера.НомерТелефона,
		 ПоследниеЦифрыТелефонногоНомера);
	Если ТипЗнч(Абонент) = Тип("СправочникСсылка.Партнеры") Тогда
		ТаблицаКИ = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюОбъекта(Абонент, Неопределено,
			 Перечисления.ТипыКонтактнойИнформации.Телефон);
		Для Каждого СтрокаКИ Из ТаблицаКИ Цикл
			СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(СтрокаКИ.Представление);	
			НомерТелефона = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
				+ СтруктураНомера.НомерТелефона,
				 ПоследниеЦифрыТелефонногоНомера);
			Если НомерТелефона = ПредставлениеКакСвязаться Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Абонент) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
		ТаблицаКИ = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюОбъекта(Неопределено, Абонент,
			 Перечисления.ТипыКонтактнойИнформации.Телефон);
		Для Каждого СтрокаКИ Из ТаблицаКИ Цикл
			СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(СтрокаКИ.Представление);	
			НомерТелефона = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
				+ СтруктураНомера.НомерТелефона,
				 ПоследниеЦифрыТелефонногоНомера);
			Если НомерТелефона = ПредставлениеКакСвязаться Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Абонент) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		ТаблицаКИ = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюФизЛиц(Абонент,
			 Перечисления.ТипыКонтактнойИнформации.Телефон);
		Для Каждого СтрокаКИ Из ТаблицаКИ Цикл
			СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(СтрокаКИ.Представление);	
			НомерТелефона = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
				+ СтруктураНомера.НомерТелефона,
				 ПоследниеЦифрыТелефонногоНомера);
			Если НомерТелефона = ПредставлениеКакСвязаться Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Абонент) = Тип("СправочникСсылка.Пользователи") Тогда
		ТаблицаКИ = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюПользователей(Абонент,
			 Перечисления.ТипыКонтактнойИнформации.Телефон);
		Для Каждого СтрокаКИ Из ТаблицаКИ Цикл
			СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(СтрокаКИ.Представление);	
			НомерТелефона = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
				+ СтруктураНомера.НомерТелефона,
				 ПоследниеЦифрыТелефонногоНомера);
			Если НомерТелефона = ПредставлениеКакСвязаться Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Абонент) = Тип("СправочникСсылка.CRM_ПотенциальныеКлиенты") Тогда
		ТаблицаКИ = CRM_ОбщегоНазначенияСервер.ПолучитьКонтактнуюИнформациюПоТипуКИ(Абонент,
			 Перечисления.ТипыКонтактнойИнформации.Телефон);
		Для Каждого СтрокаКИ Из ТаблицаКИ Цикл
			СтруктураНомера = сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(СтрокаКИ.Представление);	
			НомерТелефона = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
				+ СтруктураНомера.НомерТелефона,
				 ПоследниеЦифрыТелефонногоНомера);
			Если НомерТелефона = ПредставлениеКакСвязаться Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции // сфпПроверитьНаличиеНомераТелефонаУАбонента()

// Функция возвращает значения реквизитов "Офис" и "Подразделение" переданного пользователя
//
// Параметры:
//	Пользователь - СправочникСсылка - Ссылка на пользователя
//
// Возвращаемое значение:
//	Структура - Структура из значений офиса и подразделения пользователя
//
Функция сфпПолучитьЗначенияРеквизитовПользователя(Пользователь) Экспорт
	Возврат Новый Структура("Подразделение, Офис", Пользователь.Подразделение, Пользователь.Подразделение.CRM_Офис);
КонецФункции // сфпПолучитьЗначенияРеквизитовПользователя()

// Функция возвращает всех контактых лиц переданного партнера
//
// Параметры:
//	Партнер - СправочникСсылка - Ссылка на партнера
//
// Возвращаемое значение:
//	МассивКЛ - - Массив - Массив полученных контактных лиц
//
Функция сфпПолучитьВсехКонтактныхЛицПартнера(Партнер) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =   "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	КонтактныеЛицаПартнеров.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
	|ГДЕ
	|	КонтактныеЛицаПартнеров.Владелец = &Партнер";
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции // сфпПолучитьВсехКонтактныхЛицПартнера()

// Функция формирует дату из даты и времени.
//
// Параметры:
//	ПараметрДата	- Дата	- Дата
//	ПараметрВремя	- Дата	- Время
//
// Возвращаемое значение:
//	Дата	- Дата
//
Функция сфпСформироватьДатуИзДатыИВремени(ПараметрДата, ПараметрВремя) Экспорт
	Возврат НачалоДня(ПараметрДата) + (ПараметрВремя - Дата("00010101"));
КонецФункции // сфпСформироватьДатуИзДатыИВремени()

// Функция разделяет дату на дату и время, возвращает структуру с полями "Дата", "Время".
//
// Параметры:
//	ПараметрДата	- Дата	- Дата
//
// Возвращаемое значение:
//	Структура	- Структура с датой м временем.
//
Функция сфпРазделитьДатаНаДатуИВремя(ПараметрДата) Экспорт
	Возврат Новый Структура("Дата,Время", НачалоДня(ПараметрДата), Дата("00010101") 
		+ (ПараметрДата - НачалоДня(ПараметрДата)));
КонецФункции // сфпРазделитьДатаНаДатуИВремя()

// Функция определяет входящий или исходящий звонок
//
// Параметры:
//	НомерСобытия	- Число	- Состояние звонка
//
// Возвращаемое значение:
//	Булево	- Признак входящего звонка
//
Функция сфпОпределитьВходящийЗвонокПриЗагрузкеИстории(НаправлениеЗвонка) 
	Если НаправлениеЗвонка = 1 Тогда
		Возврат Ложь;
	ИначеЕсли НаправлениеЗвонка = 2 
		ИЛИ НаправлениеЗвонка = 3
		ИЛИ НаправлениеЗвонка = 4 
		ИЛИ НаправлениеЗвонка = 6  Тогда
		Возврат Истина;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции // сфпОпределитьВходящийЗвонок()

// Функция возвращает ссылку на первого найденного контрагента
// Используется только в тех объединенных решениях, где есть справочник "Контрагенты", связанный с "Партнерами"
//
// Параметры:
//	Партнер	- СправочникСсылка - Ссылка на партнера
//
// Возвращаемое значение:
//	Контрагент - СправочникСсылка - Ссылка контрагента
//
Функция сфпПолучитьКонтрагентаПартнера(Партнер) Экспорт
	
	// BSLLS:QueryToMissingMetadata-off
	// Используется в спарках
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Партнер = &Партнер";
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники["Контрагенты"].ПустаяСсылка();
	КонецЕсли; 
	
	// BSLLS:QueryToMissingMetadata-on
КонецФункции	

// Функция возвращает ссылку на первого найденного партнера
// Используется только в тех объединенных решениях, где есть справочник "Партнеры", связанный с "Контрагентами"
//
// Параметры:
//	Контрагент	- СправочникСсылка - Ссылка на контрагента
//
// Возвращаемое значение:
//	Партнер - СправочникСсылка - Ссылка партнера
//
Функция сфпПолучитьПартнераКонтрагента(Контрагент) Экспорт
	
	// BSLLS:QueryToMissingMetadata-off
	// Используется в спарках
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Контрагенты.Партнер КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &Контрагент";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники["Партнеры"].ПустаяСсылка();
	КонецЕсли; 
	
	// BSLLS:QueryToMissingMetadata-on
КонецФункции

// Функция проверяет является ли передаваемая ссылка контрагентом
// Используется только в тех объединенных решениях, где есть справочник "Контрагенты"
//
// Параметры:
//	Результат	- СправочникСсылка - Проверяемая ссылка
//
// Возвращаемое значение:
//	Булево - признак что контрагент
//
Функция сфпЯвляетсяКонтрагентом(Результат) Экспорт
	
	// BSLLS:QueryToMissingMetadata-off
	// Используется в спарках
	Если сфпСофтФонПроСерверПовтИсп.ЭтоНеCRM() Тогда
		ТипСпарка = "СправочникСсылка.Контрагенты";
		Возврат ТипЗнч(Результат) = Тип(ТипСпарка);
	КонецЕсли;
	
	// BSLLS:QueryToMissingMetadata-on
КонецФункции

// Функция проверяет используется ли в конфигурации ограничение по телефонным звонкам
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Булево	- Признак использования ограничения
//
Функция сфпИспользоватьОграничениеПоказаТелефонныхЗвонков() Экспорт
	Возврат Константы.сфпОграничениеНаПросмотрТелефонныхЗвонков.Получить();
КонецФункции

// Разбивает строку на несколько строк по разделителю. Разделитель может иметь любую длину.
//
// Параметры:
//  Строка                 - Строка - текст с разделителями;
//  Разделитель            - Строка - разделитель строк текста, минимум 1 символ;
//  ПропускатьПустыеСтроки - Булево - признак необходимости включения в результат пустых строк.
//    Если параметр не задан, то функция работает в режиме совместимости со своей предыдущей версией:
//     - для разделителя-пробела пустые строки не включаются в результат, для остальных разделителей пустые строки
//       включаются в результат.
//     - если параметр Строка не содержит значащих символов или не содержит ни одного символа (пустая строка), то в
//       случае разделителя-пробела результатом функции будет массив, содержащий одно значение "" (пустая строка), а
//       при других разделителях результатом функции будет пустой массив.
//
//
// Возвращаемое значение:
//  Массив - массив строк.
//
// Примеры:
//  РазложитьСтрокуВМассивПодстрок(",один,,два,", ",
	//|") - возвратит массив из 5 элементов, три из которых  - пустые строки;
//  РазложитьСтрокуВМассивПодстрок(",один,,два,", ",", Истина) - возвратит массив из двух элементов;
//  РазложитьСтрокуВМассивПодстрок(" один   два  ", " ") - возвратит массив из двух элементов;
//  РазложитьСтрокуВМассивПодстрок("") - возвратит пустой массив;
//  РазложитьСтрокуВМассивПодстрок("",,Ложь) - возвратит массив с одним элементом "" (пустой строкой);
//  РазложитьСтрокуВМассивПодстрок("", " ") - возвратит массив с одним элементом "" (пустой строкой);
//
Функция сфпРазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",",
	 Знач ПропускатьПустыеСтроки = Неопределено) Экспорт
	
	Результат = Новый Массив;
	
	// для обеспечения обратной совместимости
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = СтрНайти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Результат.Добавить(Подстрока);
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = СтрНайти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Результат.Добавить(Строка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция определяет имя документа "Телефонный звонок", как оно задано в Конфигураторе
// в зависимости от используемой конфигурации
//
// Параметры:
//  КакСсылка	- Булево	- признак возврата имени документа с учетом ссылочного типа
//
// Возвращаемое значение:
//  Строка - имя документа "Телефонный звонок", как оно задано в Конфигураторе.
// 
Функция сфпИмяДокументаТелефонныйЗвонок(КакСсылка = Ложь) Экспорт
	
	Если Метаданные.Документы.Найти("ТелефонныйЗвонок") <> Неопределено Тогда
		ИмяДокумента = ?(КакСсылка, "ДокументСсылка.ТелефонныйЗвонок", "Документ.ТелефонныйЗвонок");
		
	ИначеЕсли Метаданные.Документы.Найти("CRM_ТелефонныйЗвонок") <> Неопределено Тогда
		ИмяДокумента = ?(КакСсылка, "ДокументСсылка.CRM_ТелефонныйЗвонок", "Документ.CRM_ТелефонныйЗвонок");
		
	Иначе
		ИмяДокумента = "";
	КонецЕсли;
	
	Возврат ИмяДокумента;

КонецФункции

// Функция определяет имя формы документа "Телефонный звонок", как оно задано в Конфигураторе
// в зависимости от используемой конфигурации
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - имя формы документа "Телефонный звонок", как оно задано в Конфигураторе.
// 
Функция сфпИмяФормыДокументаТелефонныйЗвонок() Экспорт
	
	Если Метаданные.Документы.Найти("ТелефонныйЗвонок") <> Неопределено Тогда
		Если Метаданные.Документы["ТелефонныйЗвонок"].Формы.Найти("сфпФормаДокумента") <> Неопределено Тогда
			  ИмяФормаЗвонка = "Документ.ТелефонныйЗвонок.Форма.сфпФормаДокумента";
		Иначе ИмяФормаЗвонка = "Документ.ТелефонныйЗвонок.ФормаОбъекта";
		КонецЕсли;
		
	ИначеЕсли Метаданные.Документы.Найти("CRM_ТелефонныйЗвонок") <> Неопределено Тогда
		Если Метаданные.Документы["CRM_ТелефонныйЗвонок"].Формы.Найти("сфпФормаДокумента") <> Неопределено Тогда
			  ИмяФормаЗвонка = "Документ.CRM_ТелефонныйЗвонок.Форма.сфпФормаДокумента";
		Иначе ИмяФормаЗвонка = "Документ.CRM_ТелефонныйЗвонок.ФормаОбъекта";
		КонецЕсли;

	Иначе
		ИмяФормаЗвонка = "";
	КонецЕсли;
	
	Возврат ИмяФормаЗвонка;

КонецФункции

// Функция определяет имя формы списка документов "Телефонный звонок", как оно задано в Конфигураторе
// в зависимости от используемой конфигурации
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - имя формы списка документов "Телефонный звонок", как оно задано в Конфигураторе.
//
Функция сфпИмяФормыСпискаДокументовТелефонныйЗвонок() Экспорт
	
	Если Метаданные.Документы.Найти("ТелефонныйЗвонок") <> Неопределено Тогда
		ИмяФормаЗвонка = "Документ.ТелефонныйЗвонок.ФормаСписка";
		
	ИначеЕсли Метаданные.Документы.Найти("CRM_ТелефонныйЗвонок") <> Неопределено Тогда
		ИмяФормаЗвонка = "Документ.CRM_ТелефонныйЗвонок.ФормаСписка";
		
	Иначе
		ИмяФормаЗвонка = "";
	КонецЕсли;
	
	Возврат ИмяФормаЗвонка;

КонецФункции

// Функция определяет имя ПВХ "Настройки пользователей", как оно задано в Конфигураторе
// в зависимости от используемой конфигурации
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - имя ПВХ "Настройки пользователей", как оно задано в Конфигураторе.
//
Функция сфпИмяПВХ_СофтфонНастройкиПользователей() Экспорт
	
	Если Метаданные.ПланыВидовХарактеристик.Найти("сфпНастройкиПользователей") <> Неопределено Тогда
		ИмяПВХ_СофтфонНастройкиПользователей = "сфпНастройкиПользователей";
		
	ИначеЕсли Метаданные.ПланыВидовХарактеристик.Найти("CRM_НастройкиПользователей") <> Неопределено Тогда
		ИмяПВХ_СофтфонНастройкиПользователей = "CRM_НастройкиПользователей";
		
	ИначеЕсли Метаданные.ПланыВидовХарактеристик.Найти("НастройкиПользователей") <> Неопределено Тогда
		ИмяПВХ_СофтфонНастройкиПользователей = "НастройкиПользователей";
		
	Иначе
		ИмяПВХ_СофтфонНастройкиПользователей = "";
	КонецЕсли;
	
	Возврат ИмяПВХ_СофтфонНастройкиПользователей;

КонецФункции

// Функция определяет имя регистра сведений "Настройки пользователей", как оно задано в Конфигураторе
// в зависимости от используемой конфигурации
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - имя регистра сведений "Настройки пользователей", как оно задано в Конфигураторе.
//
Функция сфпИмяРегистраСофтфонНастройкиПользователей() Экспорт
	
	Если Метаданные.РегистрыСведений.Найти("сфпНастройкиПользователей") <> Неопределено Тогда
		ИмяРегистраСофтфонНастройкиПользователей = "сфпНастройкиПользователей";
		
	ИначеЕсли Метаданные.РегистрыСведений.Найти("CRM_НастройкиПользователей") <> Неопределено Тогда
		ИмяРегистраСофтфонНастройкиПользователей = "CRM_НастройкиПользователей";
		
	ИначеЕсли Метаданные.РегистрыСведений.Найти("НастройкиПользователей") <> Неопределено Тогда
		ИмяРегистраСофтфонНастройкиПользователей = "НастройкиПользователей";

	Иначе
		ИмяРегистраСофтфонНастройкиПользователей = "";
	КонецЕсли;
	
	Возврат ИмяРегистраСофтфонНастройкиПользователей;

КонецФункции

// Функция определяет имя реквизита АбонентКонтакт документа "Телефонный звонок", как оно задано в Конфигураторе
// в зависимости от используемой конфигурации
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - имя реквизита АбонентКонтакт документа "Телефонный звонок", как оно задано в Конфигураторе.
//
Функция сфпИмяРеквизитаАбонентКонтакт() Экспорт
	
	ИмяДокумента = СтрЗаменить(сфпИмяДокументаТелефонныйЗвонок(), "Документ.", "");
	Если НЕ ПустаяСтрока(ИмяДокумента) Тогда
		РеквизитыДокумента = Метаданные.Документы[ИмяДокумента].Реквизиты;
		
		ИмяРеквизита = "АбонентКонтакт";
		Если РеквизитыДокумента.Найти(ИмяРеквизита) <> Неопределено Тогда
			Возврат ИмяРеквизита;
		КонецЕсли;
		
		ИмяРеквизита = "Абонент";
		Если РеквизитыДокумента.Найти(ИмяРеквизита) <> Неопределено Тогда
			Возврат ИмяРеквизита;
		КонецЕсли;
	КонецЕсли;
	
	Возврат "";

КонецФункции

Функция сфпТребуетсяНастройкаТелефонии(ВерсияСофтФон = Неопределено) Экспорт
	
	ТребуетсяНастройка = Истина;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВерсияСофтФон = Константы.сфпИспользуемаяВерсияСофтФон.Получить();
	Если ВерсияСофтФон = Перечисления.сфпВерсииСофтФон.СофтФотPROSTO Тогда
		ТребуетсяНастройка = сфпТребуетсяНастройкаОблачнойТелефонии();
		
	ИначеЕсли ВерсияСофтФон = Перечисления.сфпВерсииСофтФон.СофтФотПроф Тогда
		ТребуетсяНастройка = сфпТребуетсяНастройкаОбычнойТелефонии();
	КонецЕсли;

	Возврат ТребуетсяНастройка;

КонецФункции

Функция сфпТребуетсяНастройкаОблачнойТелефонии() Экспорт
	
	ТребуетсяНастройка = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Метаданные.Константы.Найти("ИспользоватьРазделениеПоОбластямДанных") <> Неопределено Тогда
		  ИспользуетсяРазделениеПоОбластямДанных = Константы["ИспользоватьРазделениеПоОбластямДанных"].Получить();
	Иначе ИспользуетсяРазделениеПоОбластямДанных = Ложь;
	КонецЕсли;
	
	ИспользуемаяАТС = Неопределено;
	Если Константы.сфпИспользоватьОблачнуюТелефонию.Получить() Тогда
		ИспользуемаяАТС = Константы.сфпИспользуемаяАТС.Получить();
	
	ИначеЕсли Константы.сфпИспользоватьМобильнуюТелефонию.Получить() Тогда
		ИспользуемаяАТС = "МобильнаяТелефония";
		
	Иначе
		ТребуетсяНастройка = Истина;
	КонецЕсли;
	
	АдресСервераПубликацииСервисовТелефонии = "";
	ИмяПубликацииСервисовТелефонии = "";
	
	Если НЕ ТребуетсяНастройка И НЕ ИспользуетсяРазделениеПоОбластямДанных Тогда
		ОбщиеНастройкиТелефонии = Константы.сфпОбщиеНастройкиТелефонии.Получить();
		Если ЗначениеЗаполнено(ОбщиеНастройкиТелефонии) И ТипЗнч(ОбщиеНастройкиТелефонии) = Тип("Строка") Тогда
			ОбщиеНастройкиТелефонии = ЗначениеИзСтрокиВнутр(ОбщиеНастройкиТелефонии);
			Если ТипЗнч(ОбщиеНастройкиТелефонии) = Тип("Структура") Тогда
				ОбщиеНастройкиТелефонии.Свойство("АдресСервераОсновнойПубликации", АдресСервераПубликацииСервисовТелефонии);
				ОбщиеНастройкиТелефонии.Свойство("АдресРесурсаОсновнойПубликации", ИмяПубликацииСервисовТелефонии);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ Константы.сфпИспользоватьСофтФон.Получить() Тогда
			ТребуетсяНастройка = Истина;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(АдресСервераПубликацииСервисовТелефонии)
			 ИЛИ НЕ ЗначениеЗаполнено(ИмяПубликацииСервисовТелефонии) Тогда
			ТребуетсяНастройка = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ТребуетсяНастройка Тогда
		НастройкиТелефонии = ПолучитьНастройкиТелефонии();
		
		Если ИспользуемаяАТС = ПредопределенноеЗначение("Перечисление.сфпДоступныеАТС.MangoOffice") Тогда
			Если НЕ ЗначениеЗаполнено(НастройкиТелефонии.vpbx_api_key)
				 ИЛИ НЕ ЗначениеЗаполнено(НастройкиТелефонии.vpbx_api_salt) Тогда
				ТребуетсяНастройка = Истина;
			КонецЕсли;

		ИначеЕсли ИспользуемаяАТС = ПредопределенноеЗначение("Перечисление.сфпДоступныеАТС.Билайн") Тогда
			Если НЕ ЗначениеЗаполнено(НастройкиТелефонии.КлючДляАвторизацииВОблачнойАТС)
				 ИЛИ НЕ ЗначениеЗаполнено(НастройкиТелефонии.КлючПодпискиНаСобытия) Тогда
				ТребуетсяНастройка = Истина;
			КонецЕсли;

		ИначеЕсли ИспользуемаяАТС = ПредопределенноеЗначение("Перечисление.сфпДоступныеАТС.МТТ") Тогда
			Если НЕ ЗначениеЗаполнено(НастройкиТелефонии.АдресОблачнойАТС)
				 ИЛИ НЕ ЗначениеЗаполнено(НастройкиТелефонии.КлючДляАвторизацииВОблачнойАТС)
				 ИЛИ НЕ ЗначениеЗаполнено(НастройкиТелефонии.КлючДляАвторизацииВИБ) Тогда
				ТребуетсяНастройка = Истина;
			КонецЕсли;

		ИначеЕсли ИспользуемаяАТС = ПредопределенноеЗначение("Перечисление.сфпДоступныеАТС.Яндекс") Тогда
			Если НЕ ЗначениеЗаполнено(НастройкиТелефонии.КлючДляАвторизацииАТСЯндекс) Тогда
				ТребуетсяНастройка = Истина;
			КонецЕсли;

		ИначеЕсли ИспользуемаяАТС = ПредопределенноеЗначение("Перечисление.сфпДоступныеАТС.СофтФонWebModule") Тогда
			Если НЕ ЗначениеЗаполнено(НастройкиТелефонии.АдресОблачнойАТС)
				 ИЛИ НЕ ЗначениеЗаполнено(НастройкиТелефонии.vpbx_api_key)
				 ИЛИ НЕ ЗначениеЗаполнено(НастройкиТелефонии.vpbx_api_salt) Тогда
				ТребуетсяНастройка = Истина;
			КонецЕсли;

		ИначеЕсли ИспользуемаяАТС = ПредопределенноеЗначение("Перечисление.сфпДоступныеАТС.Ростелеком") Тогда
			Если НЕ ЗначениеЗаполнено(НастройкиТелефонии.vpbx_api_salt) ИЛИ
				 НЕ ЗначениеЗаполнено(НастройкиТелефонии.vpbx_api_key) ИЛИ
				 НЕ ЗначениеЗаполнено(НастройкиТелефонии.Domain) Тогда
				ТребуетсяНастройка = Истина;
			КонецЕсли;

		ИначеЕсли ИспользуемаяАТС = ПредопределенноеЗначение("Перечисление.сфпДоступныеАТС.UIS") Тогда
			Если НЕ ЗначениеЗаполнено(НастройкиТелефонии.КлючДляАвторизацииАТСUIS) Тогда
				ТребуетсяНастройка = Истина;
			КонецЕсли;

		Иначе	
			Если НЕ ЗначениеЗаполнено(НастройкиТелефонии.АдресОблачнойАТС)
				 ИЛИ НЕ ЗначениеЗаполнено(НастройкиТелефонии.КлючДляАвторизацииВОблачнойАТС)
				 ИЛИ НЕ ЗначениеЗаполнено(НастройкиТелефонии.КлючДляАвторизацииВИБ) Тогда
				ТребуетсяНастройка = Истина;
			КонецЕсли;
		КонецЕсли;	
		
		Если НЕ ТребуетсяНастройка Тогда
		   	ТребуетсяНастройка = НЕ сфпПроверитьПодпискуНаСобытия();
			Если ТребуетсяНастройка Тогда
				РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПроверкаСтатуса");
				ТребуетсяНастройка = НЕ РезультатЗапроса.Успешно;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	#Если НЕ ВнешнееСоединение Тогда
	Если НЕ ТребуетсяНастройка Тогда
		Если НЕ СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
			ТребуетсяНастройка = Истина;
		КонецЕсли;
	КонецЕсли;
	#КонецЕсли

	Возврат ТребуетсяНастройка;

КонецФункции

Функция сфпТребуетсяНастройкаОбычнойТелефонии() Экспорт
	
	ТребуетсяНастройка = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Метаданные.Константы.Найти("ИспользоватьРазделениеПоОбластямДанных") <> Неопределено Тогда
		  ИспользуетсяРазделениеПоОбластямДанных = Константы["ИспользоватьРазделениеПоОбластямДанных"].Получить();
	Иначе ИспользуетсяРазделениеПоОбластямДанных = Ложь;
	КонецЕсли;
	
	Если НЕ ИспользуетсяРазделениеПоОбластямДанных Тогда
		Если НЕ Константы.сфпИспользоватьСофтФон.Получить() Тогда
			ТребуетсяНастройка = Истина;
		КонецЕсли;
	КонецЕсли;

	Возврат ТребуетсяНастройка;

КонецФункции

Функция сфпПроверитьПодпискуНаСобытия()
	
	ИспользуемаяАТС = Неопределено;
	Если Константы.сфпИспользоватьОблачнуюТелефонию.Получить() Тогда
		ИспользуемаяАТС = Константы.сфпИспользуемаяАТС.Получить();
	
	ИначеЕсли Константы.сфпИспользоватьМобильнуюТелефонию.Получить() Тогда
		ИспользуемаяАТС = "МобильнаяТелефония";
	КонецЕсли;
	
	Если ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Билайн Тогда
		НастройкиТелефонии = ПолучитьНастройкиТелефонии();
		КлючПодпискиНаСобытия = НастройкиТелефонии.КлючПодпискиНаСобытия;
	    		
		Если ЗначениеЗаполнено(КлючПодпискиНаСобытия) Тогда
			РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПолучитьДанныеПодпискиНаСобытия");
			Если РезультатЗапроса.Успешно Тогда
				json = Новый ЧтениеJSON();
				json.УстановитьСтроку(РезультатЗапроса.Результат);
				jsonСтруктура = ПрочитатьJSON(json);
				json.Закрыть();
				
				Если jsonСтруктура.Свойство("errorCode") Тогда
					сфпЗаписатьЗначениеНастройкиИнтеграцииАТС("КлючПодпискиНаСобытия", "");
					
				ИначеЕсли jsonСтруктура.Свойство("expires") Тогда
					Если jsonСтруктура.expires = 0 Тогда
						сфпЗаписатьЗначениеНастройкиИнтеграцииАТС("КлючПодпискиНаСобытия", "");
					КонецЕсли;	
				КонецЕсли;				
			КонецЕсли;
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(КлючПодпискиНаСобытия) Тогда
			РезультатЗапроса = сфпЛицензированиеЭкспортныеМетоды.ВыполнитьЗапросОблачнойАТС("ПодпискаНаСобытия");
			Если РезультатЗапроса.Успешно Тогда
				json = Новый ЧтениеJSON();
				json.УстановитьСтроку(РезультатЗапроса.Результат);
				jsonСтруктура = ПрочитатьJSON(json);
				json.Закрыть();
				
				Если jsonСтруктура.Свойство("subscriptionId") Тогда
					сфпЗаписатьЗначениеНастройкиИнтеграцииАТС("КлючПодпискиНаСобытия", jsonСтруктура.subscriptionId);
					
					ПодключитьОбновлениеПодпискиНаСобытияОблачнойАТС();
				КонецЕсли;				
			КонецЕсли;
			
		Иначе
			ПодключитьОбновлениеПодпискиНаСобытияОблачнойАТС();
		КонецЕсли;	
		
		Возврат ЗначениеЗаполнено(КлючПодпискиНаСобытия);
		
	Иначе
		Возврат Истина;
	КонецЕсли;	
	
КонецФункции

Процедура сфпЗаписатьЗначениеНастройкиИнтеграцииАТС(КлючНастройки, ЗначениеНастройки)
	
	сфпУдалитьСлужебныеСимволы(ЗначениеНастройки);
	УстановитьНастройкуТелефонии(КлючНастройки, ЗначениеНастройки);
	
КонецПроцедуры

Процедура сфпУдалитьСлужебныеСимволы(Строка) Экспорт
	
	Строка = СтрЗаменить(Строка, Символы.ПС, "");
	Строка = СокрЛП(Строка);
	Строка = СтрЗаменить(Строка, "¶", "");
	
КонецПроцедуры

// Функция возвращает Регламентное задание
//
// Параметры:
//	ИмяЗадания	- Строка	- Имя регламентного задания
//
// Возвращаемое значение:
//	РегламентноеЗадание	- Регламентное задание
//
Функция ПолучитьРегламентноеЗадание(ИмяЗадания) Экспорт
	
	Задание = РегламентныеЗадания.НайтиПредопределенное(ИмяЗадания);
	Если Задание = Неопределено Тогда
		ТекМетаданные = Метаданные.РегламентныеЗадания[ИмяЗадания];
		СтруктураОтбора = Новый Структура("Метаданные", ТекМетаданные);
		НайденныеЗадания = РегламентныеЗадания.ПолучитьРегламентныеЗадания(СтруктураОтбора);
		Если НайденныеЗадания.Количество() > 0 Тогда
			Задание = НайденныеЗадания[0];
			
		Иначе
			Задание = РегламентныеЗадания.СоздатьРегламентноеЗадание(ТекМетаданные);
			Задание.Использование = Истина;
			Задание.Ключ = Новый УникальныйИдентификатор();
			Задание.Наименование = ТекМетаданные.Представление();
			Задание.ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
			Задание.Записать();
		КонецЕсли;
	КонецЕсли;

	Возврат Задание;

КонецФункции // ПолучитьРегламентноеЗадание()

// Функция возвращает расписание регламентного задания
//
// Параметры:
//	ИмяЗадания	- Строка	- Имя регламентного задания
//
// Возвращаемое значение:
//	Расписание	- Расписание регламентного задания
//
Функция ПолучитьРасписаниеРегламентногоЗадания(ИмяЗадания) Экспорт
	
	Задание = ПолучитьРегламентноеЗадание(ИмяЗадания);
	Если Задание <> Неопределено Тогда
		Возврат Задание.Расписание;
	КонецЕсли;
		
	Возврат Неопределено;

КонецФункции // ПолучитьРасписаниеРегламентногоЗадания()

// Процедура устанавливает расписание регламентного задания
//
// Параметры:
//	Расписание	- РасписаниеРегламентногоЗадания	- Расписание регламентного задания
//	ИмяЗадания	- Строка							- Имя регламентного задания
//
Процедура УстановитьРасписаниеРегламентногоЗадания(Расписание, ИмяЗадания) Экспорт
	
	Задание = ПолучитьРегламентноеЗадание(ИмяЗадания);
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Задание.Расписание = Расписание;
		Задание.Записать();
	Исключение 
		ЗаписьЖурналаРегистрации(сфпСофтФонПроСерверПереопределяемый.СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка, , , 
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
	КонецПопытки;

КонецПроцедуры	//	УстановитьРасписаниеРегламентногоЗадания()

// Процедура устанавливает признак использования регламентного задания
//
// Параметры:
//	ИмяЗадания		- Строка	- Имя регламентного задания
//	Использование	- Булево	- Признак использования регламентного задания
//
Процедура УстановитьИспользованиеРегламентногоЗадания(ИмяЗадания, Использование) Экспорт

	РегламентныеЗаданияСервер.УстановитьПараметрыРегламентногоЗадания(
		Метаданные.РегламентныеЗадания[ИмяЗадания], Новый Структура("Использование", Использование));

КонецПроцедуры	//	УстановитьРасписаниеРегламентногоЗадания()

// Функция возвращает доступность команд API
//
// Параметры:
//	Нет
//
// Возвращаемое значение:
//	СтруктураКоманд	-	Структура доступности команд API
//
Функция ПолучитьДоступностьКомандAPI() Экспорт
	
	СтруктураКоманд = Новый Структура("Ответ, Завершить, Пауза, Перевод", Ложь, Истина, Истина, Истина);
	
	ИспользуемаяАТС = Константы.сфпИспользуемаяАТС.Получить();
	Если сфпСофтФонПроСерверПереопределяемый.ЭтоПлатформаITooLabs(ИспользуемаяАТС) ИЛИ
		 ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.UIS ИЛИ
		 ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.Ростелеком Тогда
		СтруктураКоманд = Новый Структура("Ответ, Завершить, Пауза, Перевод", Ложь, Ложь, Ложь, Ложь);
	КонецЕсли;
	
	Возврат СтруктураКоманд;

КонецФункции

// Функция возвращает значение дополнительной информации контакта
//
// Параметры:
//	Контакт	- СправочникСсылка	- Контакт-ссылка
//
// Возвращаемое значение:
//	ДополнительнаяИнформация	- Значение дополнительной информации контакта
//
Функция ПолучитьДополнительнуюИнформациюКонтакта(Контакт)
	
	ДополнительнаяИнформацияКонтактов = Константы.сфпДополнительнаяИнформацияКонтактов.Получить();
	
	Если НЕ ЗначениеЗаполнено(Контакт) ИЛИ НЕ ЗначениеЗаполнено(ДополнительнаяИнформацияКонтактов) Тогда
		Возврат "";
	КонецЕсли;
	
	ИмяРеквизитаКонтакта = "";
	ИмяМетаданныхКонтакта = Контакт.Метаданные().Имя;
	
	МассивДополнительнойИнформации = сфпОбщегоНазначения.сфпРазложитьСтрокуВМассивПодстрок(ДополнительнаяИнформацияКонтактов,
		 ";");
	Для Каждого СтрокаИнформации Из МассивДополнительнойИнформации Цикл
		Позиция = СтрНайти(СтрокаИнформации, ".");
		Если Позиция > 0 Тогда
			ИмяМетаданныхСтроки = Лев(СтрокаИнформации, Позиция - 1);
			Если ИмяМетаданныхСтроки = ИмяМетаданныхКонтакта Тогда
				ИмяРеквизитаКонтакта = Сред(СтрокаИнформации, Позиция + 1);
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	
	Если НЕ ЗначениеЗаполнено(ИмяРеквизитаКонтакта) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СКД = Новый СхемаКомпоновкиДанных();
	
	Источник = СКД.ИсточникиДанных.Добавить();
	Источник.Имя = "ИсточникДанных";
	Источник.СтрокаСоединения = "";
	Источник.ТипИсточникаДанных = "local";
	
	НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Запрос = "ВЫБРАТЬ Ссылка КАК Контакт ИЗ Справочник." + Контакт.Метаданные().Имя;
	НаборДанных.Имя = "Запрос";
	НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
	НаборДанных.ИсточникДанных = "ИсточникДанных";
	
	НастройкиСКД = СКД.НастройкиПоУмолчанию;
	
	ДетальнаяГруппировка = НастройкиСКД.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ДетальнаяГруппировка.Использование = Истина;
	
	ВыбранноеАвтоПоле = НастройкиСКД.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеАвтоПоле.Использование = Истина;
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных();
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СКД));

	ВыбранныеПоляДетальнаяГруппировка = ДетальнаяГруппировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранныеПоляДетальнаяГруппировка.Поле = Новый ПолеКомпоновкиДанных("Контакт." + ИмяРеквизитаКонтакта);

	ОтборКонтакта = НастройкиСКД.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборКонтакта.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Контакт.Ссылка");
	ОтборКонтакта.Использование = Истина;
	ОтборКонтакта.ПравоеЗначение = Контакт;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	
	Попытка
		МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СКД, НастройкиСКД, , ,
			 Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	Исключение
		Возврат "";
	КонецПопытки;
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
			
	ТаблицаРезультат = Новый ТаблицаЗначений();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений();
			
	ПроцессорВывода.УстановитьОбъект(ТаблицаРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДополнительнаяИнформация = "";
	
	Если ТаблицаРезультат.Количество() > 0 Тогда
		ДополнительнаяИнформация = ТаблицаРезультат[0][0];
	КонецЕсли;

	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДополнительнаяИнформация;
	
КонецФункции

Функция ПолучитьПредставлениеВыбраннойТелефонии() Экспорт
	
	Представление = "";
	
	ИспользоватьСофтФон = сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпИспользоватьСофтФон");
	Если ИспользоватьСофтФон Тогда
		ИспользуемаяВерсияСофтФон = сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпИспользуемаяВерсияСофтФон");
		Если ИспользуемаяВерсияСофтФон = Перечисления.сфпВерсииСофтФон.СофтФотPROSTO Тогда
			ИспользуемаяАТС = сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпИспользуемаяАТС");
			ИспользуемаяАТСДополнительно =
				сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпИспользуемаяАТСДополнительно");
			
			Если ЗначениеЗаполнено(ИспользуемаяАТС) Тогда
				Если ИспользуемаяАТС = Перечисления.сфпДоступныеАТС.УниверсальныйItoolabs Тогда
					УниверсальныеАТС = Перечисления.сфпДоступныеАТС.ДоступныеАТСУниверсальногоВиджетаItoolabs();
					
					СтруктураПоиска = Новый Структура("Идентификатор", ИспользуемаяАТСДополнительно);
					НайденныеСтроки = УниверсальныеАТС.НайтиСтроки(СтруктураПоиска);
					Если НайденныеСтроки.Количество() > 0 Тогда
						Представление = НайденныеСтроки[0].Представление;
					КонецЕсли;
				Иначе
					ТекПеречисление =
						Метаданные.Перечисления.сфпДоступныеАТС.ЗначенияПеречисления.Получить(Перечисления.сфпДоступныеАТС.Индекс(ИспользуемаяАТС));
					Представление = ТекПеречисление.Синоним;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			Представление = "Софтфон";
		КонецЕсли;
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

Функция ЭтоСсылкаСлужебногоПользователя(НавигационнаяСсылка) Экспорт
	
	Если СтрНайти(НавигационнаяСсылка, "Справочник.Пользователи") = 0 Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Логин = СлужебныйПользовательТелефонииЛогин();
	ПользовательИзСправочника = сфпОбщегоНазначения.НайтиПользователяПоИмени(Логин);
	Если ЗначениеЗаполнено(ПользовательИзСправочника) Тогда
		ТекСсылка = ПолучитьНавигационнуюСсылку(ПользовательИзСправочника);
		Если СтрНайти(НавигационнаяСсылка, ТекСсылка) > 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Ложь;
	
КонецФункции

Функция сфпОпределитьКаналИИсточник(Звонок, ДанныеЗаполнения) Экспорт
	
	КаналИИсточник = Новый Структура("Канал,Источник", "", "");
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) И ДанныеЗаполнения.Свойство("сфпСтруктураВнешнихДанных") Тогда
		сфпСтруктураВнешнихДанных = ДанныеЗаполнения.сфпСтруктураВнешнихДанных;
		ИмяПредопределенногоЗначения = "ПланВидовХарактеристик.КаналыРекламныхВоздействий.CRM_CoMagic";
		КаналИИсточник.Вставить("Канал", ПредопределенноеЗначение(ИмяПредопределенногоЗначения));
		КаналИИсточник.Вставить("Источник",
			 сфпНайтиМаркетинговоеМероприятие(сфпСтруктураВнешнихДанных.comagic_context.campaign));
			
	Иначе
		НаборЗаписейИсточники = РегистрыСведений.ИсточникиПервичногоИнтереса.СоздатьНаборЗаписей();
		НаборЗаписейИсточники.Отбор.Сделка.Установить(Звонок);
		НаборЗаписейИсточники.Прочитать();
		Если НаборЗаписейИсточники.Количество() > 0 Тогда
			КаналИИсточник.Вставить("Канал", НаборЗаписейИсточники[0].КаналПервичногоИнтереса);
			КаналИИсточник.Вставить("Источник", НаборЗаписейИсточники[0].ИсточникПервичногоИнтереса);
		КонецЕсли;
	КонецЕсли;
	
	Возврат КаналИИсточник;
	
КонецФункции	

Функция сфпПараметрыПереадресации(НомерТелефона) Экспорт
	
	УстановитьПривилегированныйРежим(Истина); 
	
	ПараметрыПереадресации = Новый Структура("ВнутреннийНомер,НаименованиеКонтакта", "", "");
	
	ИспользоватьМаршрутизацию = сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпИспользоватьМаршрутизацию");
	Если ИспользоватьМаршрутизацию Тогда
		ОчищенныйНомерТелефона = сфпУбратьИзНомераТелефонаВсеПрефиксы(НомерТелефона);
		Если СтрДлина(ОчищенныйНомерТелефона) > 0 Тогда
			ДлинаВнутреннихНомеров =
				сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпМаксимальнаяДлинаВнутреннихНомеров");
			КоличествоЦифрВНомере = сфпОбщегоНазначенияПовтИсп.ПолучитьЗначениеКонстанты("сфпПоследниеЦифрыТелефонногоНомера");
			
			Если СтрДлина(ОчищенныйНомерТелефона) > ДлинаВнутреннихНомеров Тогда
				// Если внешний номер, то ищем в регистре номеров для поиска
				// BSLLS:LogicalOrInTheWhereSectionOfQuery-off
				// В дополнительном условии оператор ИЛИ можно использовать без ограничений.
				Запрос = Новый Запрос("
				|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	Номера.Объект.Наименование КАК НаименованиеКонтакта,
				|   ЕСТЬNULL(АбонентыАТС.ВнутреннийНомерАТС, """") КАК ВнутреннийНомер,
				|   ЕСТЬNULL(АбонентыАТС.ЛогинАТС, """") КАК Логин
				|ИЗ
				|	РегистрСведений.сфпНомераТелефоновДляПоиска КАК Номера
				|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.сфпКонтактыТелефонии КАК АбонентыАТС
				|	ПО АбонентыАТС.Объект = Номера.Пользователь
				|ГДЕ
				|	Номера.НомерТелефона = &НомерТелефона
				|	И Номера.Пользователь <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
				|	И (ЕСТЬNULL(АбонентыАТС.ВнутреннийНомерАТС, """") <> """"
				|		ИЛИ ЕСТЬNULL(АбонентыАТС.ЛогинАТС, """") <> """")");
				// BSLLS:LogicalOrInTheWhereSectionOfQuery-on
				Запрос.УстановитьПараметр("НомерТелефона", Прав(ОчищенныйНомерТелефона, КоличествоЦифрВНомере));
				
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					ПараметрыПереадресации.Вставить("ВнутреннийНомер", Выборка.ВнутреннийНомер);
					ПараметрыПереадресации.Вставить("Логин", Выборка.Логин);
					ПараметрыПереадресации.Вставить("НаименованиеКонтакта", Выборка.НаименованиеКонтакта);
				КонецЕсли;
	 		КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат ПараметрыПереадресации;

КонецФункции // сфпПараметрыПереадресации()

Функция сфпПараметрыОператораКоллтрекинга(Оператор = Неопределено) Экспорт
	
	Перем ТекущийОператор;
	
	СтруктураПараметров = Новый Структура;
	
	СтруктураПараметров.Вставить("Протокол", "");
	СтруктураПараметров.Вставить("ДоменAPP", "");
	СтруктураПараметров.Вставить("ДоменAPI", "");
	СтруктураПараметров.Вставить("ДоменCallAPI", "");
	СтруктураПараметров.Вставить("ОтносительныйURLCallAPI", "");
	СтруктураПараметров.Вставить("ДоменDataAPI", "");
	СтруктураПараметров.Вставить("ОтносительныйURLDataAPI", "");
	
	Если ТипЗнч(Оператор) = Тип("ПеречислениеСсылка.CRM_CallTrakingСценарии") Тогда
		ТекущийОператор = Оператор;
	Иначе
		ТекущийОператор = Константы.сфпОператорКоллтрекинга.Получить();
	КонецЕсли;
	
	Если ТекущийОператор = Перечисления.CRM_CallTrakingСценарии.Comagic Тогда
		
		СтруктураПараметров.Протокол = "https://";
		СтруктураПараметров.ДоменAPP = "app.comagic.ru";
		СтруктураПараметров.ДоменAPI = "api.comagic.ru";
		СтруктураПараметров.ДоменCallAPI = "callapi.comagic.ru";
		СтруктураПараметров.ОтносительныйURLCallAPI = "/v4.0";
		СтруктураПараметров.ДоменDataAPI = "dataapi.comagic.ru";
		СтруктураПараметров.ОтносительныйURLDataAPI = "/v2.0";
		
	ИначеЕсли ТекущийОператор = Перечисления.CRM_CallTrakingСценарии.UIS Тогда
		
		СтруктураПараметров.Протокол = "https://";
		СтруктураПараметров.ДоменAPP = "app.uiscom.ru";
		СтруктураПараметров.ДоменAPI = "api.uiscom.ru";
		СтруктураПараметров.ДоменCallAPI = "callapi.uiscom.ru";
		СтруктураПараметров.ОтносительныйURLCallAPI = "/v4.0";
		СтруктураПараметров.ДоменDataAPI = "dataapi.uiscom.ru";
		СтруктураПараметров.ОтносительныйURLDataAPI = "/v2.0";
		
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция Телефония_ПолучитьКоличествоЗвонков(НачалоПериода, КонецПериода,
	 Пользователь = Неопределено, Подразделения = Неопределено,
	 КонтролируемыеПользователи = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстУсловия = "";
	Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(КонецПериода) Тогда
		ТекстУсловия = ТекстУсловия + " И Дата МЕЖДУ &НачалоПериода И &КонецПериода";
		
	ИначеЕсли ЗначениеЗаполнено(НачалоПериода) Тогда
		ТекстУсловия = ТекстУсловия + " И Дата >= &НачалоПериода";

	ИначеЕсли ЗначениеЗаполнено(КонецПериода) Тогда
		ТекстУсловия = ТекстУсловия + " И Дата <= КонецПериода";
	КонецЕсли;
	
	ТекстУсловияПринадлежностиЗвонки = "";
	Если ЗначениеЗаполнено(Пользователь) Тогда
		ТекстУсловияПринадлежностиЗвонки = ТекстУсловияПринадлежностиЗвонки 
			+ ?(ТекстУсловияПринадлежностиЗвонки = "", "", " ИЛИ ") 
				+ " (Автор = &Пользователь ИЛИ Ответственный = &Пользователь)";	
	КонецЕсли;
	Если ЗначениеЗаполнено(Подразделения) И ТипЗнч(Подразделения) = Тип("Массив") Тогда
		ТекстУсловияПринадлежностиЗвонки = ТекстУсловияПринадлежностиЗвонки 
			+ ?(ТекстУсловияПринадлежностиЗвонки = "", "", " ИЛИ ") 
				+ " (Автор.Подразделение В ИЕРАРХИИ(&Подразделения) ИЛИ Ответственный.Подразделение В ИЕРАРХИИ(&Подразделения))";
	КонецЕсли;
	Если ЗначениеЗаполнено(КонтролируемыеПользователи) И ТипЗнч(КонтролируемыеПользователи) = Тип("Массив") Тогда
		ТекстУсловияПринадлежностиЗвонки = ТекстУсловияПринадлежностиЗвонки 
			+ ?(ТекстУсловияПринадлежностиЗвонки = "", "", " ИЛИ ") 
				+ " (Автор В (&КонтролируемыеПользователи) ИЛИ Ответственный В (&КонтролируемыеПользователи))";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстУсловияПринадлежностиЗвонки) Тогда
		ТекстУсловия = ТекстУсловия + " И (" + ТекстУсловияПринадлежностиЗвонки + ")";
	КонецЕсли;

	ТекущийПользовательАдминистратор = РольДоступна("ПолныеПрава") ИЛИ РольДоступна("сфпУправлениеМаршрутизацией");
	Если НЕ ТекущийПользовательАдминистратор Тогда
		ТекстУсловия = ТекстУсловия + " И НЕ сфпНецелевойЗвонок";
	КонецЕсли;

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СУММА(ВсеЗвонки) КАК ВсеЗвонки,
	|   СУММА(Входящие) КАК Входящие,
	|	СУММА(Исходящие) КАК Исходящие,
	|	СУММА(Пропущенные) КАК Пропущенные
	|ИЗ
	|	(ВЫБРАТЬ
	|		1 КАК ВсеЗвонки,
	|		ВЫБОР КОГДА Входящий И сфпСостояниеЗвонка <> ЗНАЧЕНИЕ(Перечисление.сфпСостоянияЗвонков.Пропущенный) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК Входящие,
	|		ВЫБОР КОГДА Входящий ТОГДА 0 ИНАЧЕ 1 КОНЕЦ КАК Исходящие,
	|		ВЫБОР КОГДА Входящий И сфпСостояниеЗвонка = ЗНАЧЕНИЕ(Перечисление.сфпСостоянияЗвонков.Пропущенный) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ КАК Пропущенные
	|	ИЗ
	|		Документ.ТелефонныйЗвонок
	|	ГДЕ
	|		НЕ ПометкаУдаления" + ТекстУсловия + ") КАК Подзапрос");
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Подразделения", Подразделения);
	Запрос.УстановитьПараметр("КонтролируемыеПользователи", КонтролируемыеПользователи);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВсегоЗвонков = ?(Выборка.ВсеЗвонки = Null, 0, Выборка.ВсеЗвонки);
		ВсегоВходящих = ?(Выборка.Входящие = Null, 0, Выборка.Входящие);
		ВсегоИсходящих = ?(Выборка.Исходящие = Null, 0, Выборка.Исходящие);
		ВсегоПропущенных = ?(Выборка.Пропущенные = Null, 0, Выборка.Пропущенные);
		
	Иначе
		ВсегоЗвонков = 0;
		ВсегоВходящих = 0;
		ВсегоИсходящих = 0;
		ВсегоПропущенных = 0;
	КонецЕсли;	
	
	Возврат Новый Структура("Все,Входящие,Исходящие,Пропущенные", ВсегоЗвонков, ВсегоВходящих,
		 ВсегоИсходящих,
		 ВсегоПропущенных);

КонецФункции

Процедура СохранитьКонтактЗвонка(Телефон, ВнутреннийНомер, Контакт, ОбъектОснование = Неопределено) Экспорт
	
	ТелефонКонтакта = СтрЗаменить(Телефон, "+", "");
	
	МенеджерЗаписи = РегистрыСведений.сфпТекущиеЗвонки.СоздатьМенеджерЗаписи();
	//МенеджерЗаписи.Пользователь = сфпСофтФонПроСервер.сфпТекущийПользователь();
	МенеджерЗаписи.ИдентификаторЗвонка = "Исходящий_" + Прав(ТелефонКонтакта, 7) + "_" + ВнутреннийНомер;
	МенеджерЗаписи.АбонентКонтакт = Контакт;
	МенеджерЗаписи.ДатаЗвонка = ТекущаяДатаСеанса();
	МенеджерЗаписи.НомерТелефона = ТелефонКонтакта;
	МенеджерЗаписи.Звонок = ОбъектОснование;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция ЗаполнитьМаршрутизациюПоМенеджерам() Экспорт
	
	Результат = "";
	
	ВерсияСофтФон = Константы.сфпИспользуемаяВерсияСофтФон.Получить();
	
	Если ВерсияСофтФон = Перечисления.сфпВерсииСофтФон.СофтФотPROSTO Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Объект КАК Пользователь,
		|	ВнутреннийНомерАТС КАК Номер,
		|	ВЫБОР КОГДА ВнутреннийНомерАТС = ОсновнойНомерТелефона ТОГДА 1 ИНАЧЕ 2 КОНЕЦ КАК Порядок
		|ИЗ
		|	РегистрСведений.сфпКонтактыТелефонии
		|ГДЕ
		|	Объект ССЫЛКА Справочник.Пользователи
		|	И Объект <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|	И ВнутреннийНомерАТС <> """"
		|УПОРЯДОЧИТЬ ПО
		|	Объект,
		|	Порядок");

	Иначе
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Пользователь,
		|	Значение КАК Номер
		|ИЗ
		|	РегистрСведений.CRM_НастройкиПользователей
		|ГДЕ
		|	Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.CRM_НастройкиПользователей.сфпТекущийВнутреннийНомер)");
	КонецЕсли;
	
	ВнутренниеНомера = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекТип Из Метаданные.ОпределяемыеТипы.сфпКонтактВзаимодействия.Тип.Типы() Цикл
		МассивТипов = Новый Массив();
		МассивТипов.Добавить(ТекТип);
		ТекОписаниеТипа = Новый ОписаниеТипов(МассивТипов);
		ТекСсылка = ТекОписаниеТипа.ПривестиЗначение(Неопределено);
		ТекИмя = ТекСсылка.Метаданные().Имя;
		ТекРеквизиты = Метаданные.Справочники[ТекИмя].Реквизиты;
		
		Если ТекРеквизиты.Найти("сфпПользовательДляПереключенияЗвонков") <> Неопределено Тогда
			Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	Ссылка,
			|	сфпПользовательДляПереключенияЗвонков," + ?(ТекИмя = "КонтактныеЛицаПартнеров", "
			|	Владелец.ОсновнойМенеджер", "
			|	ОсновнойМенеджер") + " КАК ОсновнойМенеджер
			|ИЗ
			|	Справочник." + ТекИмя + "
			|ГДЕ
			|	НЕ ПометкаУдаления
			|	И сфпПользовательДляПереключенияЗвонков = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)" 
				+ ?(ТекИмя = "КонтактныеЛицаПартнеров", "
			|	И Владелец.ОсновнойМенеджер", "
			|	И ОсновнойМенеджер") + " <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
			|ИТОГИ ПО
			|	ОсновнойМенеджер");
			
			Сч = 0;
			
			ВыборкаМенеджеров = CRM_ОбщегоНазначенияСервер.ВыполнитьЗапрос(Запрос).Выбрать(
				ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаМенеджеров.Следующий() Цикл
				СтруктураПоиска = Новый Структура("Пользователь", ВыборкаМенеджеров.ОсновнойМенеджер);
				НайденныеСтроки = ВнутренниеНомера.НайтиСтроки(СтруктураПоиска);
				Если НайденныеСтроки.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ВнутреннийНомер = НайденныеСтроки[0].Номер;
				
				Выборка = ВыборкаМенеджеров.Выбрать();
				Пока Выборка.Следующий() Цикл
					ТекОбъект = Выборка.Ссылка.ПолучитьОбъект();
					ТекОбъект.сфпПользовательДляПереключенияЗвонков = ВыборкаМенеджеров.ОсновнойМенеджер;
					ТекОбъект.ОбменДанными.Загрузка = Истина;
					ТекОбъект.Записать();
					
					НаборЗаписей = РегистрыСведений.сфпНомераТелефоновДляПоиска.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Объект.Установить(Выборка.Ссылка);
					НаборЗаписей.Прочитать();
					
					ЗаписатьНабор = Ложь;
					
					Для Каждого ТекЗапись Из НаборЗаписей Цикл
						Если ТекЗапись.Пользователь <> ВыборкаМенеджеров.ОсновнойМенеджер Тогда
							ТекЗапись.Пользователь = ВыборкаМенеджеров.ОсновнойМенеджер;
							ЗаписатьНабор = Истина;
						КонецЕсли;
						Если ТекЗапись.ВнутреннийНомер <> ВнутреннийНомер Тогда
							ТекЗапись.ВнутреннийНомер = ВнутреннийНомер;
							ЗаписатьНабор = Истина;
						КонецЕсли;
					КонецЦикла;	
					
					Если ЗаписатьНабор Тогда
						НаборЗаписей.Записать();
					КонецЕсли;
					
					Сч = Сч + 1;
				КонецЦикла;
			КонецЦикла;
			
			Результат = Результат + ?(Результат = "", "", "
			|") + "Обработано " + Сч + " эл. справочника " + ТекИмя;
		КонецЕсли;	
	КонецЦикла;	
	
	Если ВерсияСофтФон = Перечисления.сфпВерсииСофтФон.СофтФотПроф Тогда
		Если Константы.сфпИспользоватьМаршрутизацию.Получить() Тогда
			СтарыйНабор = Новый Массив();
			НовыйНабор = сфпСофтФонПроСервер.сфпПолучитьТаблицуМаршрутизации();
			СписокМаршрутизации = сфпСофтФонПроСервер.сфпСформироватьСписокМаршрутизации(СтарыйНабор, НовыйНабор);
			сфпЗаменитьМаршрутизациюВАТС(СписокМаршрутизации);
		КонецЕсли;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция ОпределитьНецелевойЗвонокПоНомеру(НомерТелефона) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ НомерТелефона
	|ИЗ РегистрСведений.сфпРеестрНецелевыхНомеров
	|ГДЕ НомерТелефона ПОДОБНО &НомерТелефона");
	Запрос.УстановитьПараметр("НомерТелефона", "%" + Прав(НомерТелефона, 10));
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции	

Функция ПользовательАдмнистратор() Экспорт
	
	Возврат (РольДоступна("ПолныеПрава") ИЛИ РольДоступна("сфпУправлениеМаршрутизацией"));
	
КонецФункции	

// Возвращает структуру, содержащую значения реквизитов, прочитанные из информационной базы по ссылке на объект.
// Рекомендуется использовать вместо обращения к реквизитам объекта через точку от ссылки на объект
// для быстрого чтения отдельных реквизитов объекта из базы данных.
//
// Если необходимо зачитать реквизит независимо от прав текущего пользователя,
// то следует использовать предварительный переход в привилегированный режим.
//
// Параметры:
//  Ссылка    - ЛюбаяСсылка - объект, значения реквизитов которого необходимо получить.
//            - Строка      - полное имя предопределенного элемента, значения реквизитов которого необходимо получить.
//  Реквизиты - Строка - имена реквизитов, перечисленные через запятую, в формате
//                       требований к свойствам структуры.
//                       Например, "Код, Наименование, Родитель".
//            - Структура
//            - ФиксированнаяСтруктура - в качестве ключа передается
//                       псевдоним поля для возвращаемой структуры с результатом, а в качестве
//                       значения (опционально) фактическое имя поля в таблице.
//                       Если ключ задан, а значение не определено, то имя поля берется из ключа.
//                       Допускается указание имени поля через точку, но при этом параметр КодЯзыка для такого поля
//                       учитываться не будет.
//            - Массив из Строка
//            - ФиксированныйМассив из Строка - имена реквизитов в формате требований к свойствам структуры.
//  ВыбратьРазрешенные - Булево - если Истина, то запрос к объекту выполняется с учетом прав пользователя;
//                                если есть ограничение на уровне записей, то все реквизиты вернутся со 
//                                значением Неопределено; если нет прав для работы с таблицей, то возникнет исключение;
//                                если Ложь, то возникнет исключение при отсутствии прав на таблицу 
//                                или любой из реквизитов.
//  КодЯзыка - Строка - код языка для мультиязычного реквизита. Значение по умолчанию - основной язык конфигурации.
//
// Возвращаемое значение:
//  Структура - содержит имена (ключи) и значения затребованных реквизитов.
//              Если в параметр Реквизиты передана пустая строка, то возвращается пустая структура.
//              Если в параметр Ссылка передана пустая ссылка, то возвращается структура, 
//              соответствующая именам реквизитов со значениями Неопределено.
//              Если в параметр Ссылка передана ссылка несуществующего объекта (битая ссылка), 
//              то все реквизиты вернутся со значением Неопределено.
//
Функция ЗначенияРеквизитовОбъекта(Ссылка, Знач Реквизиты, ВыбратьРазрешенные = Ложь,
	 Знач КодЯзыка = Неопределено) Экспорт
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты, ВыбратьРазрешенные, КодЯзыка);
КонецФункции	

// Проверяет состояние звонка и определяет нужно ли выполнять автоматичские действия
Функция НеВыполнятьАвтоматическоеДействие(Ссылка) Экспорт
	
	ТекСостояние = РегистрыСведений.CRM_СостоянияЛидов.ПолучитьТекущееСостояниеЛида(Ссылка);
	Если Не ТекСостояние = Неопределено И ТекСостояние.Свойство("Состояние") Тогда 
		Если ТекСостояние.Состояние = Справочники.CRM_СостоянияЛидов.Отклонен Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	Возврат Ложь;
	
КонецФункции

// Процедура добавляет настройку телефонии на рабочий стол, если ее там нет.
Процедура ДобавитьНастройкуТелефонииНаРабочийСтол() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	CRM_НастройкиРабочегоСтола.Наименование КАК Наименование
	               |ИЗ
	               |	РегистрСведений.CRM_НастройкиРабочегоСтола КАК CRM_НастройкиРабочегоСтола
	               |ГДЕ
	               |	CRM_НастройкиРабочегоСтола.Объект = ""Общие настройки""
	               |	И CRM_НастройкиРабочегоСтола.ИмяФормы = ""Обработка.сфпАРМ_Телефония.Форма.Форма""";
	
	Если Запрос.Выполнить().Пустой() Тогда
		ЗаписьНастройки = РегистрыСведений.CRM_НастройкиРабочегоСтола.СоздатьМенеджерЗаписи();
		ЗаписьНастройки.ИмяФормы = "Обработка.сфпАРМ_Телефония.Форма.Форма";
		ЗаписьНастройки.Объект = "Общие настройки";
		ЗаписьНастройки.Идентификатор = Строка(Новый УникальныйИдентификатор);
		ЗаписьНастройки.Наименование = НСтр("ru='Телефония';en='Telephony'");
		ЗаписьНастройки.Порядок = 91;
		ЗаписьНастройки.Записать(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
