// @strict-types

#Область СлужебныеПроцедурыИФункции

#Область АвторизацияВСервисеЭДО

// Параметры:
//  ИдентификаторыУчетныхЗаписейЭДО - Массив из Строка
//  ОтпечаткиПоКонтекстам - см. КриптографияБЭДКлиентСервер.НовыеРезультатыПолученияОтпечатков
//  ВыбранныйСертификат - Неопределено,СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция АвторизоватьсяВСервисеЭДОВФоне(Знач ИдентификаторыУчетныхЗаписейЭДО, Знач ОтпечаткиПоКонтекстам,
	Знач ВыбранныйСертификат = Неопределено) Экспорт
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"УчетныеЗаписиЭДОИнтеграцияОблака.АвторизоватьсяВСервисеЭДО", // См. УчетныеЗаписиЭДОИнтеграцияОблака.АвторизоватьсяВСервисеЭДО
		ИдентификаторыУчетныхЗаписейЭДО, ОтпечаткиПоКонтекстам, ВыбранныйСертификат);
	
КонецФункции

// Параметры:
//  ДлительнаяОперация - Неопределено
//                     - См. ДлительныеОперации.ВыполнитьФункцию
// 
// Возвращаемое значение:
//  См. УчетныеЗаписиЭДОИнтеграцияОблака.НовыйРезультатАвторизацииВСервисеЭДО
Функция РезультатАвторизацииВСервисеЭДОВФоне(Знач ДлительнаяОперация) Экспорт
	
	Результат = УчетныеЗаписиЭДОИнтеграцияОблака.НовыйРезультатАвторизацииВСервисеЭДО();
	
	Если ДлительнаяОперация <> Неопределено
		И ДлительнаяОперация.Статус = "Выполнено" Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата); // См. УчетныеЗаписиЭДОИнтеграцияОблака.НовыйРезультатАвторизацииВСервисеЭДО
		УдалитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Если ТипЗнч(РезультатОперации) = Тип("Структура") Тогда
			Результат = РезультатОперации;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти // АвторизацияВСервисеЭДО

// Параметры:
//  ПараметрыРегистрации - см. УчетныеЗаписиЭДОИнтеграцияОблакаКлиент.НовыеПараметрыРегистрацииСертификатовВЭДО
//  ИдентификаторФормы - Неопределено,УникальныйИдентификатор
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция ПодготовитьДанныеДляПодписанияНаРегистрациюВЭДОВФоне(Знач ПараметрыРегистрации, Знач ИдентификаторФормы) Экспорт
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	// См. УчетныеЗаписиЭДОИнтеграцияОблака.ПодготовитьДанныеДляПодписанияНаРегистрациюВЭДО
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"УчетныеЗаписиЭДОИнтеграцияОблака.ПодготовитьДанныеДляПодписанияНаРегистрациюВЭДО", ПараметрыРегистрации);
	
КонецФункции

// Параметры:
//  УчетнаяЗаписьОблачногоЭДО - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  ИдентификаторЗаявки - Строка
//  Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 
// Возвращаемое значение:
//  См. УчетныеЗаписиЭДОИнтеграцияОблака.ЗаполнитьДанныеДляПодписанияНаРегистрациюВЭДОПоСертификату
Функция ЗаполнитьДанныеДляПодписанияНаРегистрациюВЭДОПоСертификату(Знач УчетнаяЗаписьОблачногоЭДО, Знач ИдентификаторЗаявки, Знач Сертификат) Экспорт
	Возврат УчетныеЗаписиЭДОИнтеграцияОблака.ЗаполнитьДанныеДляПодписанияНаРегистрациюВЭДОПоСертификату(
		УчетнаяЗаписьОблачногоЭДО, ИдентификаторЗаявки, Сертификат);
КонецФункции

// Параметры:
//  УчетнаяЗаписьОблачногоЭДО - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  ИдентификаторЗаявки - Строка
//  Подписи - Структура:
//  * ПодписьСоглашения - ДвоичныеДанные
//  * ПодписьДанныхДляРегистрации - ДвоичныеДанные
//  * Доверенность - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  ИдентификаторФормы - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция ОтправитьРегистрационныйПакетЭДОВФоне(Знач УчетнаяЗаписьОблачногоЭДО, Знач ИдентификаторЗаявки, Знач Подписи, Знач ИдентификаторФормы) Экспорт
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"УчетныеЗаписиЭДОИнтеграцияОблака.ОтправитьРегистрационныйПакетЭДО", // См. УчетныеЗаписиЭДОИнтеграцияОблака.ОтправитьРегистрационныйПакетЭДО
		УчетнаяЗаписьОблачногоЭДО, ИдентификаторЗаявки, Подписи);
	
КонецФункции

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
//  ИдентификаторыЗаявокПоУчетнымЗаписямОблачногоЭДО - Соответствие из КлючИЗначение:
//  * Ключ - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  * Значение - Массив из Строка
//  ИдентификаторФормы - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция ПроверитьОбработкуРегистрационныхПакетовЭДОВФоне(Знач ИдентификаторыЗаявокПоУчетнымЗаписямОблачногоЭДО, Знач ИдентификаторФормы) Экспорт
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"УчетныеЗаписиЭДОИнтеграцияОблака.ПроверитьОбработкуРегистрационныхПакетовЭДО", // См. УчетныеЗаписиЭДОИнтеграцияОблака.ПроверитьОбработкуРегистрационныхПакетовЭДО
		ИдентификаторыЗаявокПоУчетнымЗаписямОблачногоЭДО);
	
КонецФункции

// Параметры:
//  ИнформацияОбУчетнойЗаписи - см. УчетныеЗаписиЭДОИнтеграцияОблака.ИнформацияОбУчетнойЗаписи
//  ИдентификаторФормы - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция ОбновитьИнформациюОбУчетнойЗаписиЭДОВФоне(Знач ИнформацияОбУчетнойЗаписи, Знач ИдентификаторФормы) Экспорт
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"УчетныеЗаписиЭДОИнтеграцияОблака.ОбновитьИнформациюОбУчетнойЗаписиЭДО", // См. УчетныеЗаписиЭДОИнтеграцияОблака.ОбновитьИнформациюОбУчетнойЗаписиЭДО
		ИнформацияОбУчетнойЗаписи);
	
КонецФункции

// Параметры:
//  ИдентификаторУчетнойЗаписиЭДО - см. УчетныеЗаписиЭДОИнтеграцияОблака.ОбновитьНастройкиУведомленийУчетнойЗаписиЭДО.ИдентификаторУчетнойЗаписиЭДО
//  ПараметрыУведомлений - см. УчетныеЗаписиЭДОИнтеграцияОблака.ОбновитьНастройкиУведомленийУчетнойЗаписиЭДО.ПараметрыУведомлений
//  РасшифрованныеМаркеры - см. УчетныеЗаписиЭДОИнтеграцияОблака.ОбновитьНастройкиУведомленийУчетнойЗаписиЭДО.РасшифрованныеМаркеры
//  ИдентификаторФормы - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция ОбновитьНастройкиУведомленийУчетнойЗаписиЭДОВФоне(Знач ИдентификаторУчетнойЗаписиЭДО, Знач ПараметрыУведомлений, Знач РасшифрованныеМаркеры, Знач ИдентификаторФормы) Экспорт
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"УчетныеЗаписиЭДОИнтеграцияОблака.ОбновитьНастройкиУведомленийУчетнойЗаписиЭДО", // См. УчетныеЗаписиЭДОИнтеграцияОблака.ОбновитьНастройкиУведомленийУчетнойЗаписиЭДО
		ИдентификаторУчетнойЗаписиЭДО, ПараметрыУведомлений, РасшифрованныеМаркеры);
	
КонецФункции

// Параметры:
//  ИдентификаторУчетнойЗаписиЭДО - см. УчетныеЗаписиЭДОИнтеграцияОблака.НастройкиУведомленийУчетнойЗаписиЭДО.ИдентификаторУчетнойЗаписиЭДО
//  РасшифрованныеМаркеры - см. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
//  ИдентификаторФормы - Неопределено,УникальныйИдентификатор
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция НастройкиУведомленийУчетнойЗаписиЭДОВФоне(Знач ИдентификаторУчетнойЗаписиЭДО, Знач РасшифрованныеМаркеры, Знач ИдентификаторФормы = Неопределено) Экспорт
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"УчетныеЗаписиЭДОИнтеграцияОблака.НастройкиУведомленийУчетнойЗаписиЭДО", // См. УчетныеЗаписиЭДОИнтеграцияОблака.НастройкиУведомленийУчетнойЗаписиЭДО
		ИдентификаторУчетнойЗаписиЭДО, РасшифрованныеМаркеры);
	
КонецФункции

#КонецОбласти
