
////////////////////////////////////////////////////////////////////////////////
// Напоминания клиент
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура для вывода активных напоминаний.
//
Процедура ВывестиАктивныеНапоминания() Экспорт
	
	ПоказыватьВЦентреОповещений = CRM_НапоминанияСервер.ПоказыватьНапоминанияВЦентреОповещений();
	МассивНапоминаний = CRM_НапоминанияСервер.ПолучитьНапоминания();
	
	Если ПоказыватьВЦентреОповещений Тогда
		
		ИдентификаторыНапоминаний = CRM_НапоминанияСервер.ПолучитьМассивИдентификаторовВыведенныхНапоминаний();
		НовыеНапоминания = Новый Массив;
		
		Для Каждого Элемент Из МассивНапоминаний Цикл
			
			Если Элемент.Просмотрено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ИдентификаторыНапоминаний.Найти(Элемент.Идентификатор) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрыОткрытияФормы = Новый Структура;
			ПараметрыОткрытияФормы.Вставить("Пользователь", Элемент.Пользователь);
			ПараметрыОткрытияФормы.Вставить("Предмет", Элемент.Предмет);
			ПараметрыОткрытияФормы.Вставить("ДатаНачала", Элемент.ДатаНачала);
			ПараметрыОткрытияФормы.Вставить("ДатаОповещения", Элемент.ДатаОповещения);
			ПараметрыОткрытияФормы.Вставить("Идентификатор", Элемент.Идентификатор);
			ДействиеПриНажатии = Новый ОписаниеОповещения("ОткрытьФормуНапоминанияИзЦентраОповещений", 
				CRM_НапоминанияКлиент, ПараметрыОткрытияФормы);
			
			Важность = Элемент.Важность;
			ВажностьВысокая = 2;
			ВажностьНизкая = 0;
			Если Важность = ВажностьВысокая Тогда
				Картинка = БиблиотекаКартинок.CRM_Важно;
			ИначеЕсли Важность = ВажностьНизкая Тогда
				Картинка = БиблиотекаКартинок.CRM_ВажностьНизкая;
			Иначе 
				Картинка = Неопределено;
			КонецЕсли;
			
			Текст = Элемент.Содержание 
				+ ?(ЗначениеЗаполнено(Элемент.Партнер), НСтр("ru=' партнер: '") + Элемент.Партнер, "") 
				+ ?(ЗначениеЗаполнено(Элемент.КонтактноеЛицо), НСтр("ru=' контактное лицо: '") + Элемент.КонтактноеЛицо, "");
				
			ПоказатьОповещениеПользователя(
				Элемент.СрокИсполнения,
				ДействиеПриНажатии,
				Текст,
				Картинка,
				СтатусОповещенияПользователя.Важное,
				Элемент.КлючУникальности);
				
			НовыеНапоминания.Добавить(Элемент.Идентификатор);
			
		КонецЦикла;
		
		Если НовыеНапоминания.Количество() > 0 Тогда
			CRM_НапоминанияСервер.ДобавитьИдентификаторыВМассивВыведенныхНапоминаний(НовыеНапоминания);
		КонецЕсли;
		
	Иначе
#Если ВебКлиент Тогда
		ГлФормаНапоминанийCRM = ПолучитьФорму("ОбщаяФорма.CRM_ФормаНапоминаний", , , "Уникум");
#Иначе	
		Если ГлФормаНапоминанийCRM = Неопределено Тогда
			CRM_ЦентрМониторингаКлиент.НачатьЗамерОперацииБизнесСтатистики(
				"CRM_Статистика.Напоминания.Напоминания.ДлительностьСценариев.ВремяОткрытияФормы", Истина);
			ГлФормаНапоминанийCRM = ПолучитьФорму("ОбщаяФорма.CRM_ФормаНапоминаний", , , "Уникум");
		КонецЕсли;
#КонецЕсли
		
		Если (МассивНапоминаний.Количество() = 0) И НЕ ГлФормаНапоминанийCRM.Открыта() Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураНовыхНапоминаний = Новый Структура("ПрочиеНапоминания");
		СтруктураНовыхНапоминаний.Вставить("МассивНапоминаний", МассивНапоминаний);
		
		ГлФормаНапоминанийCRM.ОбновитьТаблицуНапоминаний(СтруктураНовыхНапоминаний);
	КонецЕсли;
	
КонецПроцедуры // ВывестиАктивныеНапоминания()

// Функция формирования ключа записи по строке.
//
// Параметры:
//	Строка	- Строка	- Строка с ключом записи.
//
// Возвращаемое значение:
//	Структура	- Ключ записи
//
Функция СформироватьКлючЗаписиПоСтроке(Строка) Экспорт
	Ключ = Новый Структура();
	Ключ.Вставить("Пользователь",	Строка.Пользователь);
	Ключ.Вставить("Предмет",		Строка.Предмет);
	Если ТипЗнч(Строка.Предмет) = Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты")
		 И ЗначениеЗаполнено(Строка.ВидОповещения) Тогда
		Ключ.Вставить("Содержание",		Строка.Содержание);
	Иначе	
		Ключ.Вставить("ДатаНачала",		Строка.ДатаНачала);
		Ключ.Вставить("ДатаОповещения",	Строка.ДатаОповещения);
		Ключ.Вставить("Идентификатор",	Строка.Идентификатор);
	КонецЕсли;
	Возврат Ключ;
КонецФункции // СформироватьКлючЗаписиПоСтроке()

// подключает обработчик оповещения о входящих письмах.
//
// Параметры:
//  ЗапуститьСразу	 - Булево - Запустить сразу. 
//
Процедура ПодключитьОбработчикОповещенияВходящиеПисьма(ЗапуститьСразу = Ложь) Экспорт
	
	// Получим период обновления напоминаний из настроек параметров учета.
	ВидОповещения = 
		ПредопределенноеЗначение("Справочник.CRM_ВидыОповещений.ОповещатьОНовыхВходящихПисьмах");
	ВходящиеПисьмаПериодОбновления = 
		CRM_НапоминанияСервер.ПолучитьПериодОбновленияНапоминанийВСекундах(ВидОповещения);
	
	Если ВходящиеПисьмаПериодОбновления > 0 Тогда
		
		Если ЗапуститьСразу Тогда
		
			ВывестиАктивныеНапоминанияОВходящихПисьмахСообщениях("НапоминанияОВходящихПисьмах");
		
		КонецЕсли;		
		
		ПодключитьОбработчикОжидания("CRM_ВывестиАктивныеНапоминанияОВходящихПисьмах", ВходящиеПисьмаПериодОбновления);
		
	КонецЕсли;	
	
КонецПроцедуры// ПодключитьОбработчикОжиданияВходящиеПисьма()
	
//  Подключает обработчик оповещения о входящих письмах.
//
Процедура ОтключитьОбработчикОповещенияВходящиеПисьма() Экспорт
	
	ОтключитьОбработчикОжидания("CRM_ВывестиАктивныеНапоминанияОВходящихПисьмах");
		
КонецПроцедуры// ПодключитьОбработчикОжиданияВходящиеПисьма()

// Подключает обработчик оповещения о сообщениях мессенджеров.
//
// Параметры:
//  ЗапуститьСразу	 - Булево - Запустить сразу. 
//
Процедура ПодключитьОбработчикОповещенияСообщенияМессенджеров(ЗапуститьСразу = Ложь) Экспорт
	
	// Получим период обновления напоминаний из настроек параметров учета.
	ВидОповещения = 
		ПредопределенноеЗначение("Справочник.CRM_ВидыОповещений.ОповещатьОНовыхСообщенияхМессенджеров");
	СообщенияМессенджеровПериодОбновления = 
		CRM_НапоминанияСервер.ПолучитьПериодОбновленияНапоминанийВСекундах(ВидОповещения);
	
	Если СообщенияМессенджеровПериодОбновления > 0 Тогда
		
		Если ЗапуститьСразу Тогда
		
			ВывестиАктивныеНапоминанияОВходящихПисьмахСообщениях("НапоминанияОСообщенияхМессенджеров");
		
		КонецЕсли;		
		
		ПодключитьОбработчикОжидания("CRM_ВывестиАктивныеНапоминанияОСообщенияхМессенджеров", 
			СообщенияМессенджеровПериодОбновления);
		
	КонецЕсли;	
	
КонецПроцедуры
	
// Отключает обработчик оповещения о сообщениях мессенджеров.
//
Процедура ОтключитьОбработчикОповещенияСообщенияМессенджеров() Экспорт
	
	ОтключитьОбработчикОжидания("CRM_ВывестиАктивныеНапоминанияОСообщенияхМессенджеров");
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик события "ПриНачалеРаботыСистемы".
//
// Параметры:
//	Нет.
//
Процедура ПриНачалеРаботыСистемы() Экспорт
	
	Если Не CRM_НапоминанияСервер.ИспользуютсяНапоминания() Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиАктивныеНапоминания();
	
	ПериодОбновления = CRM_НапоминанияСервер.ПолучитьПериодОбновленияНапоминаний();
	ПодключитьОбработчикОжидания("CRM_ВывестиАктивныеНапоминания", ПериодОбновления);
	ПодключитьОбработчикОповещенияВходящиеПисьма(Истина);
	
	Если CRM_ОбщегоНазначенияСервер.ФункциональнаяОпция("CRM_ИспользоватьИнтеграциюСМессенджерами") Тогда
		ПодключитьОбработчикОповещенияСообщенияМессенджеров(Истина);
	КонецЕсли;
	
КонецПроцедуры

// Процедура для вывода активных напоминаний.
//
// Параметры:
//	Нет.
//
Процедура ВывестиАктивныеНапоминанияОВходящихПисьмахСообщениях(ВидОповещений) Экспорт
	
	ПоказыватьВЦентреОповещений = CRM_НапоминанияСервер.ПоказыватьНапоминанияВЦентреОповещений();
	МассивНапоминаний = CRM_НапоминанияСервер.ПолучитьНапоминания(ВидОповещений);
	
	Если ПоказыватьВЦентреОповещений Тогда
		
		ИдентификаторыНапоминаний = CRM_НапоминанияСервер.ПолучитьМассивИдентификаторовВыведенныхНапоминаний();
		НовыеНапоминания = Новый Массив;
		
		Для Каждого Элемент Из МассивНапоминаний Цикл
			
			Если Элемент.Просмотрено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ИдентификаторыНапоминаний.Найти(Элемент.Идентификатор) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрыОткрытияФормы = Новый Структура;
			ПараметрыОткрытияФормы.Вставить("Пользователь", Элемент.Пользователь);
			ПараметрыОткрытияФормы.Вставить("Предмет", Элемент.Предмет);
			ПараметрыОткрытияФормы.Вставить("ДатаНачала", Элемент.ДатаНачала);
			ПараметрыОткрытияФормы.Вставить("ДатаОповещения", Элемент.ДатаОповещения);
			ПараметрыОткрытияФормы.Вставить("Идентификатор", Элемент.Идентификатор);
			ДействиеПриНажатии = Новый ОписаниеОповещения("ОткрытьФормуНапоминанияИзЦентраОповещений", 
				CRM_НапоминанияКлиент, ПараметрыОткрытияФормы);
			
			Важность = Элемент.Важность;
			ВажностьВысокая = 2;
			ВажностьНизкая = 0;
			Если Важность = ВажностьВысокая Тогда
				Картинка = БиблиотекаКартинок.CRM_Важно;
			ИначеЕсли Важность = ВажностьНизкая Тогда
				Картинка = БиблиотекаКартинок.CRM_ВажностьНизкая;
			Иначе 
				Картинка = Неопределено;
			КонецЕсли;
			
			Текст = Элемент.Содержание;
			Если Элемент.Свойство("Партнер") И Элемент.Свойство("КонтактноеЛицо") Тогда
				Текст = Текст 
					+ ?(ЗначениеЗаполнено(Элемент.Партнер), НСтр("ru=' партнер: '") + Элемент.Партнер, "") 
					+ ?(ЗначениеЗаполнено(Элемент.КонтактноеЛицо), НСтр("ru=' контактное лицо: '") + Элемент.КонтактноеЛицо, "");
			КонецЕсли;
				
			ПоказатьОповещениеПользователя(
				Элемент.СрокИсполнения,
				ДействиеПриНажатии,
				Текст,
				Картинка,
				СтатусОповещенияПользователя.Важное,
				Элемент.КлючУникальности);
				
			НовыеНапоминания.Добавить(Элемент.Идентификатор);
			
		КонецЦикла;
		
		Если НовыеНапоминания.Количество() > 0 Тогда
			CRM_НапоминанияСервер.ДобавитьИдентификаторыВМассивВыведенныхНапоминаний(НовыеНапоминания);
		КонецЕсли;
		
	Иначе
		
	#Если ВебКлиент Тогда
		ГлФормаНапоминанийCRM = ПолучитьФорму("ОбщаяФорма.CRM_ФормаНапоминаний", , , "Уникум");
	#Иначе	
		Если ГлФормаНапоминанийCRM = Неопределено Тогда
			CRM_ЦентрМониторингаКлиент.НачатьЗамерОперацииБизнесСтатистики(
				"CRM_Статистика.Напоминания.Напоминания.ДлительностьСценариев.ВремяОткрытияФормы", Истина);
			ГлФормаНапоминанийCRM = ПолучитьФорму("ОбщаяФорма.CRM_ФормаНапоминаний", , , "Уникум");
		КонецЕсли;
	#КонецЕсли	
		Если (МассивНапоминаний.Количество() = 0) И НЕ ГлФормаНапоминанийCRM.Открыта() Тогда
			Возврат;
		КонецЕсли;
		СтруктураНовыхНапоминаний = Новый Структура(ВидОповещений);
		СтруктураНовыхНапоминаний.Вставить("МассивНапоминаний", МассивНапоминаний);
		ГлФормаНапоминанийCRM.ОбновитьТаблицуНапоминаний(СтруктураНовыхНапоминаний);
		
	КонецЕсли;
	
КонецПроцедуры // ВывестиАктивныеНапоминания()

#Область ОбработчикиКоманд

Процедура ОткрытьФормуНапоминания(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Содержание", ПараметрКоманды);
	ПараметрыФормы.Вставить("Предмет", ПараметрКоманды);
	ОткрытьФорму("РегистрСведений.CRM_Напоминания.ФормаЗаписи", ПараметрыФормы,
		 ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность,
		 ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры// ОткрытьФормуНапоминаний() Экспорт

Процедура ОткрытьФормуНапоминанияИзЦентраОповещений(ДанныеЗаписи) Экспорт
	
	CRM_НапоминанияСервер.ИзменитьПризнакПросмотра(ДанныеЗаписи, Истина);
	
	Если ТипЗнч(ДанныеЗаписи.Предмет) = Тип("СправочникСсылка.CRM_УчетныеЗаписиМессенджеров") Тогда
		ОткрытьФорму("Обработка.CRM_Мессенджер.Форма.ФормаМессенджера");
	ИначеЕсли ТипЗнч(ДанныеЗаписи.Предмет) = Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты") Тогда
		ОткрытьМенеджерПочты(ДанныеЗаписи.Предмет);
	Иначе
		ПараметрыСоздания = Новый Массив;
		ПараметрыСоздания.Добавить(ДанныеЗаписи);
		КлючЗаписи = Новый(Тип("РегистрСведенийКлючЗаписи.CRM_Напоминания"), ПараметрыСоздания);
		ПараметрыОткрытия = Новый Структура("Ключ", КлючЗаписи);
		Попытка
			ОткрытьФорму("РегистрСведений.CRM_Напоминания.ФормаЗаписи", ПараметрыОткрытия);
		Исключение
			ПоказатьПредупреждение(, НСтр("ru = 'Данное оповещение больше не актуально.'"), 120);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуНапоминанияИзПодключаемойКоманды(Ссылка, Параметры) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Содержание", Ссылка);
	ПараметрыФормы.Вставить("Предмет", Ссылка);
	ОткрытьФорму("РегистрСведений.CRM_Напоминания.ФормаЗаписи", ПараметрыФормы,
		Параметры.Форма);
	
КонецПроцедуры

Процедура ОткрытьМенеджерПочты(УчетнаяЗапись)
	
	ФормаПочтовогоМенеджера = ПолучитьФорму("Обработка.CRM_МенеджерПочты.Форма");
	Если ФормаПочтовогоМенеджера = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ФормаПочтовогоМенеджера.Открыта() Тогда
		ФормаПочтовогоМенеджера.УстановитьПапкуУчетнойЗаписи(УчетнаяЗапись, "Входящие");
		ФормаПочтовогоМенеджера.Активизировать();
	Иначе
		ФормаПочтовогоМенеджера.УчетнаяЗаписьВыбранная	= УчетнаяЗапись;
		ФормаПочтовогоМенеджера.ВидПапкиВыбранный		= "Входящие";
		ФормаПочтовогоМенеджера.Открыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
