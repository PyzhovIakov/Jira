////////////////////////////////////////////////////////////////////////////////
// Подсистема "Объекты УТ11, КА2, УП2".
// ОбщийМодуль.КоммерческиеПредложенияУТ.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс


// см. КоммерческиеПредложенияПереопределяемый.ПодобратьИзображенияДляПубликацииВЗКП
//
Процедура ПодобратьИзображенияДляПубликацииВЗКП(ПараметрыПодбора, ТаблицаПодобранных) Экспорт
	
	Если Не ЗначениеЗаполнено(ПараметрыПодбора.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
		
	ВсеФайлыНоменклатуры = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(ПараметрыПодбора.Номенклатура, ВсеФайлыНоменклатуры);
	
	Если ВсеФайлыНоменклатуры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РежимПодбораОдиночный = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПодбора, "РежимПодбораОдиночный", Ложь);
	
	Если РежимПодбораОдиночный Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	НоменклатураПрисоединенныеФайлы.Ссылка КАК Изображение,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(НоменклатураПрисоединенныеФайлы.ВладелецФайла) КАК ПредставлениеВладельца,
			|	НоменклатураПрисоединенныеФайлы.ВладелецФайла КАК Владелец,
			|	Не Номенклатура.Ссылка ЕСТЬ NULL КАК ФайлКартинки
			|ИЗ
			|	Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
			|		ПО НоменклатураПрисоединенныеФайлы.ВладелецФайла = Номенклатура.Ссылка
			|		И НоменклатураПрисоединенныеФайлы.Ссылка = Номенклатура.ФайлКартинки
			|ГДЕ
			|	НоменклатураПрисоединенныеФайлы.ВладелецФайла = &Номенклатура
			|	И НоменклатураПрисоединенныеФайлы.Расширение В (&РасширенияИзображений)
			|	И НоменклатураПрисоединенныеФайлы.ПубликуетсяВСервисах
			|	И Не НоменклатураПрисоединенныеФайлы.ПометкаУдаления
			|
			|УПОРЯДОЧИТЬ ПО
			|	ФайлКартинки УБЫВ";
			
		Запрос.УстановитьПараметр("Номенклатура", ПараметрыПодбора.Номенклатура);
		Запрос.УстановитьПараметр("РасширенияИзображений", КоммерческиеПредложенияСлужебный.РасширенияИзображений());
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Счетчик = 1;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрокаТаблицыПодобранных = ТаблицаПодобранных.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицыПодобранных, Выборка);
			НоваяСтрокаТаблицыПодобранных.ПорядокСортировки = Счетчик;
			
			Прервать;
			
		КонецЦикла;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	НоменклатураПрисоединенныеФайлы.Ссылка КАК Изображение,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(НоменклатураПрисоединенныеФайлы.ВладелецФайла) КАК ПредставлениеВладельца,
			|	НоменклатураПрисоединенныеФайлы.ВладелецФайла КАК Владелец,
			|	НоменклатураПрисоединенныеФайлы.ДатаСоздания КАК ДатаСоздания
			|ИЗ
			|	Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
			|ГДЕ
			|	НоменклатураПрисоединенныеФайлы.ВладелецФайла = &Номенклатура
			|	И НоменклатураПрисоединенныеФайлы.Расширение В (&РасширенияИзображений)
			|	И Не НоменклатураПрисоединенныеФайлы.ПометкаУдаления
			|
			|УПОРЯДОЧИТЬ ПО
			|	ДатаСоздания УБЫВ";
			
		Запрос.УстановитьПараметр("Номенклатура", ПараметрыПодбора.Номенклатура);
		Запрос.УстановитьПараметр("РасширенияИзображений", КоммерческиеПредложенияСлужебный.РасширенияИзображений());
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Счетчик = 1;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрокаТаблицыПодобранных = ТаблицаПодобранных.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицыПодобранных, Выборка);
			НоваяСтрокаТаблицыПодобранных.ПорядокСортировки = Счетчик;
			
			Счетчик = Счетчик + 1;
			
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

// см. КоммерческиеПредложенияПереопределяемый.ПриДобавленииИзображенияИзФайловойСистемы
//
Процедура ПриДобавленииИзображенияИзФайловойСистемы(ДанныеДляДобавленияФайла, Изображение, Отказ, Ошибки) Экспорт
	
	ПараметрыДобавленияФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
	ПараметрыДобавленияФайла.ВладелецФайлов = ДанныеДляДобавленияФайла.Номенклатура;
	ПараметрыДобавленияФайла.ИмяБезРасширения = ДанныеДляДобавленияФайла.ИмяБезРасширения;
	ПараметрыДобавленияФайла.РасширениеБезТочки = ДанныеДляДобавленияФайла.РасширениеБезТочки;

	Изображение = 
		РаботаСФайлами.ДобавитьФайл(ПараметрыДобавленияФайла, ДанныеДляДобавленияФайла.АдресДвоичныхДанныхФайла);

КонецПроцедуры

#КонецОбласти

