#Область ПрограммныйИнтерфейс

// Процедура устанавливает для CRM уникальные первоначальные настройки интерфейса "Такси"
//  Не используется в объединенных решениях!
//
// Параметры:
//  НачальныеНастройки	 - Структура	 - Начальные настройки.
//
Процедура ПервоначальнаяНастройкаИнтерфейсаТакси(НачальныеНастройки) Экспорт
	
	НеРазделенныйРежим = НЕ ОбщегоНазначения.РазделениеВключено();
	
	НачальныеНастройки.НастройкиИнтерфейса.ОтображениеПанелиРазделов = ОтображениеПанелиРазделов.КартинкаСверхуИТекст;
	НастройкиСостава = Новый НастройкиСоставаИнтерфейсаКлиентскогоПриложения;
	
	ГруппаСлева = Новый ГруппаНастройкиСоставаИнтерфейсаКлиентскогоПриложения;
	Если НеРазделенныйРежим Тогда
		ГруппаСлева.Добавить(Новый ЭлементНастройкиСоставаИнтерфейсаКлиентскогоПриложения("ПанельРазделов"));
	КонецЕсли;
	
	ГруппаСверху = Новый ГруппаНастройкиСоставаИнтерфейсаКлиентскогоПриложения;
	Если НеРазделенныйРежим Тогда
		ГруппаСверху.Добавить(Новый ЭлементНастройкиСоставаИнтерфейсаКлиентскогоПриложения("ПанельФункцийТекущегоРаздела"));
	КонецЕсли;
	
	ГруппаСнизу = Новый ГруппаНастройкиСоставаИнтерфейсаКлиентскогоПриложения;
	ГруппаСнизу.Добавить(Новый ЭлементНастройкиСоставаИнтерфейсаКлиентскогоПриложения("ПанельОткрытых"));
	
	НастройкиСостава.Лево.Добавить(ГруппаСлева);
	НастройкиСостава.Верх.Добавить(ГруппаСверху);
	НастройкиСостава.Низ.Добавить(ГруппаСнизу);
	
	НачальныеНастройки.НастройкиТакси = Новый НастройкиИнтерфейсаКлиентскогоПриложения;
	НачальныеНастройки.НастройкиТакси.УстановитьСостав(НастройкиСостава);
	
	Если НеРазделенныйРежим Тогда
		НастройкиНачальнойСтраницы = Новый НастройкиНачальнойСтраницы;
		СоставФорм = Новый СоставФормНачальнойСтраницы;
		СоставФорм.ЛеваяКолонка.Добавить("Обработка.CRM_АРМ_МоиДела.Форма.Форма");
		НастройкиНачальнойСтраницы.УстановитьСоставФорм(СоставФорм);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

