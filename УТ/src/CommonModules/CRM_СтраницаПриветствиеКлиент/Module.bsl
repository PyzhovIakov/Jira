
#Область ПрограммныйИнтерфейс

// Открывает Страницу приветствия.
//
// Параметры:
//	ДополнительныеПараметры - Структура, Неопределено - дополнительные параметры
//		открытия;
//
Процедура ОткрытьСтраницуПриветствия(ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыФормыСтраницы = Новый Структура;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		Для Каждого КлючЗначение Из ДополнительныеПараметры Цикл
			ПараметрыФормыСтраницы.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ОткрытьФорму("Обработка.CRM_СтраницаПриветствия.Форма", ПараметрыФормыСтраницы, , Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Общего назначения

// Вызывается при начале работы системы из
// ИнтернетПоддержкаПользователейКлиент.ПриНачалеРаботыСистемы().
//
Процедура ПриНачалеРаботыСистемы() Экспорт
	
	Если НЕ CRM_ЛицензированиеЭкспортныеМетоды.ПодсистемаCRMИспользуется() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не CRM_СтраницаПриветствие.РолиДоступны("CRM_БазовыеПрава") Тогда
		Возврат;
	КонецЕсли;
	
	CRM_СтраницаПриветствие.ПроверкаВерсииКонфигурации();
	
	ТипЗапускаСтраницыПриветствия = CRM_СтраницаПриветствие.ЗначениеНастройкиПоказыватьПриНачалеРаботы();
	
	Если ТипЗапускаСтраницыПриветствия = 2 Или ТипЗапускаСтраницыПриветствия = 1 Тогда
		// Глобальный обработчик ожидания для проверки наличия новой информации.
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьНаличиеНовойИнформацииВCRM_СтраницаПриветствия", 1, Истина);
	Иначе
		ДополнительныеПараметрыОткрытияМонитора = Новый Структура;
		ДополнительныеПараметрыОткрытияМонитора.Вставить("ПриНачалеРаботы", Истина);
		ОткрытьСтраницуПриветствия(ДополнительныеПараметрыОткрытияМонитора);
		
		ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске();
		Если ПараметрыРаботыКлиента.ПоказатьОписаниеИзмененийСистемы Тогда
			Для Каждого Окно Из ПолучитьОкна() Цикл
				Для Каждого СодержимоеОкна Из Окно.Содержимое Цикл
					Если СодержимоеОкна.ИмяФормы = "ОбщаяФорма.ОписаниеИзмененийПрограммы" Тогда
						СодержимоеОкна.Активизировать();
						Возврат;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНаличиеНовойИнформацииВCRM_СтраницаПриветствия() Экспорт
	
	РезультатНачала = CRM_СтраницаПриветствие.НачатьПроверкуНаличияНовойИнформации();
	
	Если ТипЗнч(РезультатНачала) = Тип("Структура") Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатНачала.ДлительнаяОперация,
			Новый ОписаниеОповещения("ПриЗавершенииЗадания", ЭтотОбъект),
			ПараметрыОжидания);
	Иначе
		ПриЗавершенииПроверкиНаличияНовойИнформации(РезультатНачала);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗавершенииЗадания(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат.КраткоеПредставлениеОшибки) Тогда
		ПриЗавершенииПроверкиНаличияНовойИнформации("Ошибка");
	Иначе
		ПриЗавершенииПроверкиНаличияНовойИнформации(ПолучитьИзВременногоХранилища(Результат.АдресРезультата));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриНажатииОповещенияОНаличииНовойИнформации(ДополнительныеПараметры) Экспорт
	
	ОткрытьСтраницуПриветствия();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриЗавершенииПроверкиНаличияНовойИнформации(Результат)
	
	Если Результат = "Ошибка" Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Страница приветствия 1С:CRM'"),
			Новый ОписаниеОповещения("ПриНажатииОповещенияОНаличииНовойИнформации", ЭтотОбъект),
			НСтр("ru = 'Не удалось проверить наличие новой информации.
				|Подробнее см. Журнал регистрации.'"),
			БиблиотекаКартинок.Ошибка32,
			,
			"СтраницаПриветствияCRM");
		
	ИначеЕсли Результат = "ДанныеИзменены" Тогда
		
		ОткрытьСтраницуПриветствия();
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Страница приветствия 1С:CRM'"),
			Новый ОписаниеОповещения("ПриНажатииОповещенияОНаличииНовойИнформации", ЭтотОбъект),
			НСтр("ru = 'Новая информация в странице приветствия 1С:CRM.
				|Нажмите для просмотра.'"),
			БиблиотекаКартинок.ИнтернетПоддержкаПользователей,
			СтатусОповещенияПользователя.Важное,
			"СтраницаПриветствияCRM");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
