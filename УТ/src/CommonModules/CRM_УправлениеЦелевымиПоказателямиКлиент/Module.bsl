
#Область ПрограммныйИнтерфейс

// Открыть расшифровку показателя.
//
// Параметры:
//  Показатель			 - СправочникСсылка.CRM_ШаблоныКлючевыхПоказателей	 - Показатель.
//  ЗначениеАналитики	 - СправочникСсылка.Пользователи, СправочникСсылка.СтруктураПредприятия	 - Значение аналитики.
//  НачалоКонецПериода	 - Структура										 - Структура с датами.
//
Процедура ОткрытьРасшифровкуПоказателя(Показатель, ЗначениеАналитики = Неопределено,
	 НачалоКонецПериода = Неопределено, ЗаДень = Ложь) Экспорт
	
	ПараметрыОтбора = Новый Структура;
	Если ТипЗнч(ЗначениеАналитики) = Тип("СправочникСсылка.Пользователи") Тогда
		ПараметрыОтбора.Вставить("Менеджер", ЗначениеАналитики);
	ИначеЕсли ТипЗнч(ЗначениеАналитики) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда 	
		ПараметрыОтбора.Вставить("Подразделение", ЗначениеАналитики);
	КонецЕсли;
	
	Если НачалоКонецПериода = Неопределено Тогда
		ПоказательПериодАнализа = CRM_ОбщегоНазначенияСервер.ЗначениеРеквизитаОбъекта(Показатель, "ПериодАнализа");
		
		ДатаРасчетаПоказателя = ОбщегоНазначенияКлиент.ДатаСеанса();
		
		Если ПоказательПериодАнализа = "Месяц" Тогда
			
			ДатаНачала = НачалоМесяца(ДатаРасчетаПоказателя);
			ДатаОкончания = КонецМесяца(ДатаРасчетаПоказателя);
			
		ИначеЕсли ПоказательПериодАнализа = "День" Тогда
			
			ДатаНачала = НачалоДня(ДатаРасчетаПоказателя);
			ДатаОкончания = КонецДня(ДатаРасчетаПоказателя);
			
		ИначеЕсли ПоказательПериодАнализа = "Квартал" Тогда
			
			ДатаНачала = НачалоКвартала(ДатаРасчетаПоказателя);
			ДатаОкончания = КонецКвартала(ДатаРасчетаПоказателя);
			
		ИначеЕсли ПоказательПериодАнализа = "Год" Тогда
			
			ДатаНачала = НачалоГода(ДатаРасчетаПоказателя);
			ДатаОкончания = КонецГода(ДатаРасчетаПоказателя);
			
		КонецЕсли;
		
		Если ЗаДень Тогда
			ДатаОкончания = КонецДня(ДатаРасчетаПоказателя);
		КонецЕсли;
	Иначе
		ДатаНачала = НачалоКонецПериода.НачалоПериода;
		ДатаОкончания = НачалоКонецПериода.КонецПериода;
	КонецЕсли;
	
	ПериодОтчета = Новый СтандартныйПериод;
	ПериодОтчета.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
	ПериодОтчета.ДатаНачала = ДатаНачала;
	ПериодОтчета.ДатаОкончания = ДатаОкончания;

	ПараметрыОтбора.Вставить("ПериодОтчета", ПериодОтчета);
	
	ПараметрыРасшифровки = CRM_УправлениеЦелевымиПоказателямиСервер.ПолучитьПараметрыРасшифровки(Показатель,
		 ПараметрыОтбора);
	
	ИмяФормы = ПараметрыРасшифровки.Форма;
	
	Если ЗначениеЗаполнено(ИмяФормы) Тогда
		ПараметрыФормы = ПараметрыРасшифровки.ПараметрыФормы;
		Если Не ПараметрыФормы.Свойство("Расшифровка") Тогда
			ПараметрыФормы.Вставить("Расшифровка", Истина);
		КонецЕсли;
		ОткрытьФорму(ИмяФормы, ПараметрыФормы);
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИсточникСКД", Показатель);
		ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
		ПараметрыДанных = Новый Структура;
		ПараметрыДанных.Вставить("Показатель", Показатель);
		ПараметрыДанных.Вставить("ПериодОтчета", ПериодОтчета);
		ПараметрыДанных.Вставить("НачалоПериода", ДатаНачала);
		ПараметрыДанных.Вставить("КонецПериода", ДатаОкончания);
		
		ПараметрыФормы.Вставить("ПараметрыДанных", ПараметрыДанных);
		ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
		ОткрытьФорму("Отчет.CRM_РасшифровкаПроизвольныхПоказателей.Форма", ПараметрыФормы);
	КонецЕсли;
КонецПроцедуры

// Пересчитать показатели.
//
// Параметры:
//  Форма	 - ФормаКлиентскогоПриложения - Передаваемая форма. 
//  Команда	 - КомандаФормы - Команда формы.
//
Процедура ПересчитатьПоказатели(Форма, Команда) Экспорт
	
	ДлительнаяОперация = CRM_УправлениеЦелевымиПоказателямиСервер.ЗапуститьПересчетПоказателей();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru='Выполняется расчет значений показателей.';
		|en='Calculation of indicators values.'");
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	
	Оповещение = Новый ОписаниеОповещения("ПересчитатьПоказателиЗавершение",
		CRM_УправлениеЦелевымиПоказателямиКлиент, Новый Структура);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПересчитатьПоказателиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаголовокОповещения = НСтр("ru='Расчет показателей';en='Calculation of indicators'");
	
	Если Результат = Неопределено Тогда
		ТекстПояснения = НСтр("ru='Расчет показателей отменен.';en='Indicators calculation is cancelled.'");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, ,
			ТекстПояснения,
			БиблиотекаКартинок.CRM_Внимание,
			СтатусОповещенияПользователя.Информация,
			"ПересчетПоказателей");
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		ТекстПояснения = НСтр("ru='Расчет показателей завершен.';en='Calculation of indicators is completed.'");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, ,
			ТекстПояснения,
			БиблиотекаКартинок.Успешно32,
			СтатусОповещенияПользователя.Информация,
			"ПересчетПоказателей");
		
		Оповестить("ПересчетПоказателей");
		
	Иначе
		
		ТекстПояснения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось выполнить расчет показателей:"
"%1';en='Failed to calculate indicators:"
"%1'"),
			Результат.КраткоеПредставлениеОшибки);
		
		ВызватьИсключение ТекстПояснения;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПересчитатьПоказатель(Показатель) Экспорт
	
	//ДлительнаяОперация = CRM_УправлениеЦелевымиПоказателямиСервер.ЗапуститьПересчетПоказателя(Показатель);
	//
	//ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	//ПараметрыОжидания.ТекстСообщения = НСтр("ru='Выполняется расчет значений показателя '") + Строка(Показатель);
	//ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	//
	//Оповещение = Новый ОписаниеОповещения("ПересчитатьПоказательЗавершение",
	//	CRM_УправлениеЦелевымиПоказателямиКлиент, Новый Структура("Показатель", Показатель));
	//
	//ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
	CRM_УправлениеЦелевымиПоказателямиСервер.ЗапуститьПересчетПоказателя(Показатель);
	
КонецПроцедуры

Процедура ПересчитатьПоказательЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаголовокОповещения = НСтр("ru='Расчет показателя'");
	
	Если Результат = Неопределено Тогда
		ТекстПояснения = НСтр("ru='Расчет показателя отменен.'");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, ,
			ТекстПояснения,
			БиблиотекаКартинок.CRM_Внимание,
			СтатусОповещенияПользователя.Информация,
			"ПересчетПоказателей");
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		ТекстПояснения = НСтр("ru='Расчет показателя завершен.'");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, ,
			ТекстПояснения,
			БиблиотекаКартинок.Успешно32,
			СтатусОповещенияПользователя.Информация,
			"ПересчетПоказателей");
		
		Оповестить("ПересчетПоказателей");
		
	Иначе
		
		ТекстПояснения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не удалось выполнить расчет показателя:"
"%1'"),
			Результат.КраткоеПредставлениеОшибки);
		
		ВызватьИсключение ТекстПояснения;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
