
////////////////////////////////////////////////////////////////////////////////
// Методы модулей менеджеров задач (CRM)
//  
// В данный модуль вынесены методы подсистемы CRM, вызываемые из модулей типовых объектов. 
// Выносить можно только те методы, которые не вызывают стандартные методы типового модуля или обработчики форм. 
// Т.е. вызывают только те методы, что тоже вынесены из типового или не содержат таких вызовов.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Для каждого объекта необходимо задать свою #Область с именем объекта и модуля, как он называется в метаданных.
#Область ОпределениеФормОбъектов

// Процедура - Обработка получения форм объектов CRM
//
// Параметры:
//  Источник				 - ЗадачаСсылка	 - Источник. 
//  ВидФормы				 - Строка - Имя стандартной формы. 
//  Параметры				 - Струтура	 - Параметры формы. 
//  ВыбраннаяФорма			 - Строка, ФормаКлиентскогоПриложения - Содержит имя открываемой формы или объект метаданных Форма.
//  ДополнительнаяИнформация - Струтура	 - Дополнительная информация открытия формы.
//  СтандартнаяОбработка	 - Булево - Признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаПолученияФормОбъектовCRM(Источник, ВидФормы, Параметры, ВыбраннаяФорма,
	 ДополнительнаяИнформация,
	 СтандартнаяОбработка) Экспорт
	
	Если Не ТипЗнч(Источник) = Тип("ЗадачаМенеджер.ЗадачаИсполнителя") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ВидФормы = "ФормаОбъекта" И Параметры.Свойство("Ключ") Тогда
		
		Если НЕ ЗначениеЗаполнено(Параметры.Ключ.БизнесПроцесс) Тогда
			
			СтандартнаяОбработка = Ложь;
			Если ЗначениеЗаполнено(Параметры.Ключ.Предмет)
				 И ТипЗнч(Параметры.Ключ.Предмет) = Тип("ДокументСсылка.CRM_Интерес")
				 И НЕ Параметры.Свойство("ИзСпискаАктивностей") Тогда
				Параметры.Вставить("ПозиционироватьНаВзаимодействие", Параметры.Ключ);
				Параметры.Ключ = Параметры.Ключ.Предмет;
				ВыбраннаяФорма = "Документ.CRM_Интерес.ФормаОбъекта";
			Иначе
				Если Параметры.Свойство("ИзСпискаАктивностей") И Параметры.ИзСпискаАктивностей И Не Параметры.Ключ.Выполнена Тогда
					ВыбраннаяФорма = "Задача.ЗадачаИсполнителя.Форма.CRM_ФормаЛичнойЗадачиКраткая";
				Иначе
					ВыбраннаяФорма = "Задача.ЗадачаИсполнителя.Форма.CRM_ФормаЛичнойЗадачи";
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(Параметры.Ключ.БизнесПроцесс)
			И ТипЗнч(Параметры.Ключ.БизнесПроцесс) = Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс") Тогда
			
			СтандартнаяОбработка = Ложь;
			Если РегистрыСведений.CRM_ЗадачиПоИсправлениюКлиентскойБазы.ЭтоЗадачаПоИсправлениюКлиентскойБазы(Параметры.Ключ) <> Неопределено Тогда
				ВыбраннаяФорма = "БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаЗадачи";
			ИначеЕсли Параметры.Ключ.БизнесПроцесс.КартаМаршрута.ТипПроцесса = Перечисления.bpmТипыПроцессов.НезависимыйПроцесс Тогда
				ВыбраннаяФорма = "БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаЗадачиНезависимыйПроцесс";
			ИначеЕсли Параметры.Ключ.БизнесПроцесс.КартаМаршрута.ТипПроцесса = Перечисления.bpmТипыПроцессов.ПроцессОбъекта
				И ЗначениеЗаполнено(Параметры.Ключ.БизнесПроцесс.Интерес) 
				И (ТипЗнч(Параметры.Ключ.БизнесПроцесс.Интерес) = Тип("ДокументСсылка.CRM_Интерес"))
				// И ЗначениеЗаполнено(Параметры.Ключ.CRM_СостояниеИнтереса)
				И (Параметры.Ключ.БизнесПроцесс.Интерес.Ответственный = Параметры.Ключ.Исполнитель) Тогда
				//Параметры.Вставить("ПозиционироватьНаВзаимодействие", Параметры.Ключ);
				НастройкиЭтапа = CRM_БизнесПроцессыИЗадачиВызовСервера.ПолучитьНастройкиЭтапа(Параметры.Ключ);
				Если ЗначениеЗаполнено(НастройкиЭтапа.Состояние) И НЕ Параметры.Ключ.Выполнена Тогда
					Параметры.Вставить("ОткрываемаяЗадача", Параметры.Ключ);
					Параметры.Ключ = Параметры.Ключ.БизнесПроцесс.Интерес;
					ВыбраннаяФорма = "Документ.CRM_Интерес.ФормаОбъекта";
				Иначе
					ВыбраннаяФорма = "БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаЗадачиНезависимыйПроцесс";
				КонецЕсли;
			ИначеЕсли Параметры.Ключ.БизнесПроцесс.КартаМаршрута.ТипПроцесса = Перечисления.bpmТипыПроцессов.ПроцессОбъекта
				И ЗначениеЗаполнено(Параметры.Ключ.БизнесПроцесс.Интерес)
				И ЗначениеЗаполнено(Параметры.Ключ.CRM_СостояниеИнтереса) Тогда
				Параметры.Вставить("ОткрываемаяЗадача", Параметры.Ключ);
				Параметры.Ключ = Параметры.Ключ.БизнесПроцесс.Интерес;
				ИсточникНов = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Параметры.Ключ);
				Если Метаданные.Документы.Содержит(Параметры.Ключ.Метаданные()) Тогда
					CRM_МетодыМодулейМенеджеровДокументов.ОбработкаПолученияФормОбъектовCRM(ИсточникНов, ВидФормы,
						 Параметры, ВыбраннаяФорма, ДополнительнаяИнформация,
						 СтандартнаяОбработка);
				Иначе
					CRM_МетодыМодулейМенеджеровСправочников.ОбработкаПолученияФормОбъектовCRM(ИсточникНов,
						 ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация,
						 СтандартнаяОбработка);
				КонецЕсли;
			Иначе
				ВыбраннаяФорма = "БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаЗадачи";
			КонецЕсли;
			
		ИначеЕсли ВидФормы = "ФормаОбъекта" Тогда
			
			ПараметрыФормы = БизнесПроцессыИЗадачиВызовСервера.ФормаВыполненияЗадачи(Параметры.Ключ);
			ИмяФормыЗадачи = "";
			Результат = ПараметрыФормы.Свойство("ИмяФормы", ИмяФормыЗадачи);
			Если Результат Тогда
				ВыбраннаяФорма = ИмяФормыЗадачи;
				СтандартнаяОбработка = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаОбъекта" Тогда
		
		Если Параметры.Свойство("Основание") И 
			(ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее")
			ИЛИ ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее")
			ИЛИ ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.CRM_ЭтапКалендарногоПлана") 
			ИЛИ ТипЗнч(Параметры.Основание) = Тип("СправочникСсылка.МаркетинговыеМероприятия")
			ИЛИ ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.CRM_Телемаркетинг")
			ИЛИ ТипЗнч(Параметры.Основание) = Тип("СправочникСсылка.CRM_Диалоги")
			ИЛИ ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.CRM_СообщениеМессенджера")
			ИЛИ ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.ТелефонныйЗвонок")
			ИЛИ ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.CRM_Заявка")
			ИЛИ ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.CRM_Интерес")) Тогда
			
			СтандартнаяОбработка = Ложь;
			Если Параметры.Свойство("ИзСпискаАктивностей") Тогда
				ВыбраннаяФорма = "Задача.ЗадачаИсполнителя.Форма.CRM_ФормаЛичнойЗадачиКраткая";
			Иначе
				ВыбраннаяФорма = "Задача.ЗадачаИсполнителя.Форма.CRM_ФормаЛичнойЗадачи";
			КонецЕсли;
		КонецЕсли;
		
		Если Параметры.Свойство("ЗначениеКопирования") Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "Задача.ЗадачаИсполнителя.Форма.CRM_ФормаЛичнойЗадачи";
		КонецЕсли;
		
	ИначеЕсли ВидФормы = "ФормаСписка" Тогда
		
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "Задача.ЗадачаИсполнителя.Форма.CRM_ФормаСписка";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОпределениеФормОбъектов

#КонецОбласти
