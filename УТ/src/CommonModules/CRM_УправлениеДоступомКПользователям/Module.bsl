////////////////////////////////////////////////////////////////////////////////
// Управление доступом к пользователям
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Функция - Получить список доступных подразделений
//
// Параметры:
//  ВозвращатьМассив - Булево - Возвращать массив подразделений. 
//  СтрокаПоиска	 - Строка - Строка поиска. 
// 
// Возвращаемое значение:
//  Массив,ТаблицаЗначений - Список доступных подразделений. 
//
Функция ПолучитьСписокДоступныхПодразделений(ВозвращатьМассив = Ложь, СтрокаПоиска = Неопределено) Экспорт
	Запрос = Новый Запрос;
	// Если Константы.CRM_ИспользоватьДоступныхПользователей.Получить() Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	CRM_РасширенныйДоступПоПодразделениям.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ тмпРасширенныйДоступ
		|ИЗ
		|	РегистрСведений.CRM_РасширенныйДоступПоПодразделениям КАК CRM_РасширенныйДоступПоПодразделениям
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.CRM_ИспользоватьДолжностныеПозиции КАК CRM_ИспользоватьДолжностныеПозиции
		|		ПО CRM_ИспользоватьДолжностныеПозиции.Значение = ИСТИНА
		|ГДЕ
		|	CRM_РасширенныйДоступПоПодразделениям.ДолжностнаяПозиция В ИЕРАРХИИ(&ДолжностнаяПозиция)
		|	И &ДолжностнаяПозиция <> ЗНАЧЕНИЕ(Справочник.CRM_ДолжностныеПозиции.ПустаяСсылка)
		|	И &КОРПВерсия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтруктураПредприятия.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ тмпТекущееПодразделение
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	СтруктураПредприятия.ТекущийРуководитель = &ПользовательФЛ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтруктураПредприятия.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ тмпДанныеСправочника
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				тмпТекущееПодразделение.Ссылка КАК Ссылка
		|			ИЗ
		|				тмпТекущееПодразделение КАК тмпТекущееПодразделение)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	тмпРасширенныйДоступ.Подразделение КАК Подразделение
		|ИЗ
		|	тмпРасширенныйДоступ КАК тмпРасширенныйДоступ
		|ГДЕ тмпРасширенныйДоступ.Подразделение.Наименование ПОДОБНО &СтрокаПоиска
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	тмпДанныеСправочника.Ссылка
		|ИЗ
		|	тмпДанныеСправочника КАК тмпДанныеСправочника
		|ГДЕ тмпДанныеСправочника.Ссылка.Наименование ПОДОБНО &СтрокаПоиска
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Подразделение
		|ИЗ
		|	(ВЫБРАТЬ
		|		Пользователи.Подразделение КАК Подразделение
		|	ИЗ
		|		Справочник.Пользователи КАК Пользователи
		|	ГДЕ
		|		Пользователи.Ссылка = &Пользователь) КАК ВложенныйЗапрос
		|ГДЕ ВложенныйЗапрос.Подразделение.Наименование ПОДОБНО &СтрокаПоиска";
	// Иначе
	//	Запрос.Текст = "ВЫБРАТЬ
	//	|	СтруктураПредприятия.Ссылка КАК Подразделение
	//	|ИЗ
	//	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	//	|ГДЕ СтруктураПредприятия.Наименование ПОДОБНО &СтрокаПоиска";
	// КонецЕсли;
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();			   
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	Если Метаданные.Имя = "CRM" Тогда
		Запрос.УстановитьПараметр("ПользовательФЛ", ТекущийПользователь);
	Иначе	
		Запрос.УстановитьПараметр("ПользовательФЛ", ТекущийПользователь.ФизическоеЛицо);
	КонецЕсли;
	Запрос.УстановитьПараметр("ДолжностнаяПозиция", ТекущийПользователь.CRM_ДолжностнаяПозиция);
	Запрос.УстановитьПараметр("КОРПВерсия", CRM_ЛицензированиеЭкспортныеМетоды.ВариантПоставкиКОРП());
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска + "%");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ ВложенныйЗапрос.Подразделение.Наименование ПОДОБНО &СтрокаПоиска", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			 "ГДЕ тмпРасширенныйДоступ.Подразделение.Наименование ПОДОБНО &СтрокаПоиска",
			 "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ тмпДанныеСправочника.Ссылка.Наименование ПОДОБНО &СтрокаПоиска", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ СтруктураПредприятия.Наименование ПОДОБНО &СтрокаПоиска", "");
	КонецЕсли;
	ТаблицаПодразделений = Запрос.Выполнить().Выгрузить();
	ТаблицаПодразделений.Свернуть("Подразделение");
	МассивПодразделений = ТаблицаПодразделений.ВыгрузитьКолонку("Подразделение");
	СписокПодразделений = Новый СписокЗначений;
	СписокПодразделений.ЗагрузитьЗначения(МассивПодразделений);
	Если ВозвращатьМассив Тогда
		Возврат МассивПодразделений;
	Иначе	
		Возврат СписокПодразделений;	
	КонецЕсли;	
КонецФункции

// Функция - Получить список доступных пользователей
//
// Параметры:
//  ВозвращатьМассив - Булево - Возвращать массив пользователей. 
//  СтрокаПоиска	 - Строка - Строка поиска. 
//  ПолучитьБезОрганичений - Булево - Получить пользователей без ограничений по правам.
// 
// Возвращаемое значение:
//  Массив,ТаблицаЗначений - Список доступных пользователей. 
//
Функция ПолучитьСписокДоступныхПользователей(ВозвращатьМассив = Ложь, СтрокаПоиска = Неопределено,
	 ПолучитьБезОрганичений = Ложь) Экспорт
	
	ИспользоватьCRM = CRM_ОбщегоНазначенияПовтИсп.ИспользоватьCRM();
	
	Запрос = Новый Запрос;
	ЭтоРазделенныйСеанс = РаботаВМоделиСервиса.ИспользованиеРазделителяСеанса();
	Если (Константы.ИспользоватьРазделениеПоОбластямДанных.Получить() И НЕ ЭтоРазделенныйСеанс)
		 Или Не ИспользоватьCRM Тогда
		
		Если ВозвращатьМассив Тогда
			Возврат Новый Массив;
		Иначе
			Возврат Новый СписокЗначений;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ПолучитьБезОрганичений И НЕ РольДоступна("ПолныеПрава") Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	CRM_РасширенныйДоступПоПодразделениям.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ тмпРасширенныйДоступ
		|ИЗ
		|	РегистрСведений.CRM_РасширенныйДоступПоПодразделениям КАК CRM_РасширенныйДоступПоПодразделениям
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.CRM_ИспользоватьДолжностныеПозиции КАК CRM_ИспользоватьДолжностныеПозиции
		|		ПО CRM_ИспользоватьДолжностныеПозиции.Значение = ИСТИНА
		|ГДЕ
		|	CRM_РасширенныйДоступПоПодразделениям.ДолжностнаяПозиция В ИЕРАРХИИ(&ДолжностнаяПозиция)
		|	И &ДолжностнаяПозиция <> ЗНАЧЕНИЕ(Справочник.CRM_ДолжностныеПозиции.ПустаяСсылка)
		|	И &КОРПВерсия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтруктураПредприятия.Ссылка КАК Подразделение
		|ПОМЕСТИТЬ тмпТекущееПодразделение
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				СтруктураПредприятия.Ссылка КАК Подразделение
		|			ИЗ
		|				Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|			ГДЕ
		|				СтруктураПредприятия.ТекущийРуководитель = &ПользовательФЛ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтруктураПредприятия.Ссылка КАК Ссылка,
		|	СтруктураПредприятия.ТекущийРуководитель КАК ТекущийРуководитель
		|ПОМЕСТИТЬ тмпТекущееПодразделениеПользователя
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО (Пользователи.Подразделение = СтруктураПредприятия.Ссылка)
		|			И (Пользователи.Ссылка = &Пользователь)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтруктураПредприятия.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ тмпДанныеСправочника
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ
		|			(ВЫБРАТЬ
		|				тмпТекущееПодразделениеПользователя.Ссылка КАК Ссылка
		|			ИЗ
		|				тмпТекущееПодразделениеПользователя КАК тмпТекущееПодразделениеПользователя)
		|	И НЕ СтруктураПредприятия.Ссылка В
		|				(ВЫБРАТЬ
		|					тмпТекущееПодразделениеПользователя.Ссылка КАК Ссылка
		|				ИЗ
		|					тмпТекущееПодразделениеПользователя КАК тмпТекущееПодразделениеПользователя
		|				ГДЕ
		|					тмпТекущееПодразделениеПользователя.ТекущийРуководитель <> &ПользовательФЛПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ тмпДоступныеПодразделения
		|ИЗ
		|	(ВЫБРАТЬ
		|		тмпРасширенныйДоступ.Подразделение КАК Подразделение
		|	ИЗ
		|		тмпРасширенныйДоступ КАК тмпРасширенныйДоступ
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		тмпДанныеСправочника.Ссылка
		|	ИЗ
		|		тмпДанныеСправочника КАК тмпДанныеСправочника) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Пользователи.Ссылка КАК Пользователь
		|ПОМЕСТИТЬ тмпДляСвертывания
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Подразделение В
		|				(ВЫБРАТЬ
		|					тмпДоступныеПодразделения.Подразделение КАК Подразделение
		|				ИЗ
		|					тмпДоступныеПодразделения КАК тмпДоступныеПодразделения)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Пользователи.Ссылка КАК Пользователь
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Подразделение В
		|				(ВЫБРАТЬ
		|					тмпТекущееПодразделение.Подразделение КАК Подразделение
		|				ИЗ
		|					тмпТекущееПодразделение КАК тмпТекущееПодразделение)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Пользователи.Ссылка КАК Пользователь
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.Пустаяссылка)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&Пользователь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	тмпДляСвертывания.Пользователь КАК Пользователь
		|ИЗ
		|	тмпДляСвертывания КАК тмпДляСвертывания
		|	ГДЕ тмпДляСвертывания.Пользователь.Наименование ПОДОБНО &СтрокаПоиска";
	Иначе
		Запрос.Текст = "ВЫБРАТЬ
		|	Пользователи.Ссылка КАК Пользователь
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ Пользователи.Наименование ПОДОБНО &СтрокаПоиска";
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	Если Метаданные.Имя = "CRM" Тогда
		Запрос.УстановитьПараметр("ПользовательФЛ", ТекущийПользователь);
		Запрос.УстановитьПараметр("ПользовательФЛПустаяСсылка", Справочники.Пользователи.ПустаяСсылка());
	Иначе	
		Запрос.УстановитьПараметр("ПользовательФЛ", ТекущийПользователь.ФизическоеЛицо);
		Запрос.УстановитьПараметр("ПользовательФЛПустаяСсылка", Справочники.ФизическиеЛица.ПустаяСсылка());
	КонецЕсли;
	Запрос.УстановитьПараметр("ДолжностнаяПозиция", ТекущийПользователь.CRM_ДолжностнаяПозиция);
	Запрос.УстановитьПараметр("КОРПВерсия", CRM_ЛицензированиеЭкспортныеМетоды.ВариантПоставкиКОРП());
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска + "%");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ тмпДляСвертывания.Пользователь.Наименование ПОДОБНО &СтрокаПоиска", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ Пользователи.Наименование ПОДОБНО &СтрокаПоиска", "");
	КонецЕсли;
	
	ТаблицаПользователей = Запрос.Выполнить().Выгрузить();
	ТаблицаПользователей.Свернуть("Пользователь");
	МассивПользователей = ТаблицаПользователей.ВыгрузитьКолонку("Пользователь");
	МассивПользователей.Добавить(ТекущийПользователь);
	
	ЗамещаемыеПользователи = РегистрыСведений.CRM_ОтсутствиеСотрудников.ЗамещаемыеПользователи(ТекущийПользователь);
	Для Каждого Пользователь Из ЗамещаемыеПользователи Цикл
		Если МассивПользователей.Найти(Пользователь) = Неопределено Тогда
			МассивПользователей.Добавить(Пользователь);
		КонецЕсли;
	КонецЦикла;
	
	СписокПользователей = Новый СписокЗначений;
	СписокПользователей.ЗагрузитьЗначения(МассивПользователей);
	
	Если ВозвращатьМассив Тогда
		Возврат МассивПользователей;
	Иначе
		Возврат СписокПользователей;
	КонецЕсли;
КонецФункции

// Функция - Получить доступных менеджеров
//
// Параметры:
//  ПолучитьБезОрганичений - Булево - Получить пользователей без ограничений по правам.
// 
// Возвращаемое значение:
//  Массив - Список доступных пользователей. 
//
Функция ПолучитьДоступныхМенеджеров(ПолучитьБезОрганичений = Ложь) Экспорт
	 Возврат ПолучитьСписокДоступныхПользователей(Истина, , ПолучитьБезОрганичений);
КонецФункции

// Возвращает типы обращений поддержки.
//
// Возвращаемое значение:
//	Массив - типы обращений поддержки.
Функция ПолучитьТипыОбращенийПоддержки() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	CRM_ТипыОбращений.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.CRM_ТипыОбращений КАК CRM_ТипыОбращений
	|ГДЕ
	|	CRM_ТипыОбращений.Ссылка <> ЗНАЧЕНИЕ(Справочник.CRM_ТипыОбращений.Интерес)";
	ТаблицаОбращенийПоддержки = Запрос.Выполнить().Выгрузить();
	МассивОбращенийПоддержки = ТаблицаОбращенийПоддержки.ВыгрузитьКолонку("Ссылка");
	Возврат МассивОбращенийПоддержки;
КонецФункции

// Процедура устанавливат отбор в списке по доступным пользователям.
//
// Параметры:
//  Список						 - ДинамическийСписок -  Список, в котором необходимо установить отбор.
//  МассивЭлементовОтбора		 - Массив - Массив строк с названиями полей отбора.
//  МассивДоступныхПользователей - СписокЗначений - список, для которого устанавливаем отбор.
//  ВключатьВОтборПустуюСсылку	 - Булево - Включать в отбор пустую ссылку.
//  ПолучитьБезОрганичений		 - Булево - Получить без органичений.
//
Процедура УстановитьОтборВСпискеПоДоступнымпользователям(Список, МассивЭлементовОтбора,
	МассивДоступныхПользователей = Неопределено, ВключатьВОтборПустуюСсылку = Ложь, ПолучитьБезОрганичений = Ложь) Экспорт
	
	Если Не CRM_ОбщегоНазначенияПовтИсп.ИспользоватьCRM() Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Константы.CRM_ИспользоватьДоступныхПользователей.Получить() Тогда
		Возврат;
	КонецЕсли;
	
	Если МассивЭлементовОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если МассивДоступныхПользователей = Неопределено Или МассивДоступныхПользователей.Количество() = 0 Тогда
		МассивДоступныхПользователей = ПолучитьДоступныхМенеджеровИРоли(ПолучитьБезОрганичений);
	КонецЕсли;
	
	Если ВключатьВОтборПустуюСсылку Тогда
		МассивДоступныхПользователей.Добавить(Справочники.Пользователи.ПустаяСсылка());
		МассивДоступныхПользователей.Добавить(Справочники.РолиИсполнителей.ПустаяСсылка());
	КонецЕсли;
	
	Для Каждого ЭлементОтбора Из МассивЭлементовОтбора Цикл
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, ЭлементОтбора, ЭлементОтбора);
	КонецЦикла;
	
	ГруппаОтбораПоДоступнымпользователям = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(Список.Отбор.Элементы,
		"ГруппаОтбораДоступныеПользователи", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	Для Каждого ЭлементОтбора Из МассивЭлементовОтбора Цикл
		CRM_ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораСписка(Список, ЭлементОтбора,
			МассивДоступныхПользователей, ВидСравненияКомпоновкиДанных.ВСписке, ГруппаОтбораПоДоступнымпользователям);
	КонецЦикла;
	
КонецПроцедуры

// Получает доступных менеджеров и роли
//
// Параметры:
//  ПолучитьБезОрганичений - Булево - Получить пользователей без ограничений по правам.
// 
// Возвращаемое значение:
//  Массив - Список доступных пользователей. 
//
Функция ПолучитьДоступныхМенеджеровИРоли(ПолучитьБезОрганичений = Ложь) Экспорт
	
	ДоступныеПользователи = ПолучитьСписокДоступныхПользователей(Истина, , ПолучитьБезОрганичений);
	Если ДоступныеПользователи.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИсполнителиЗадач.РольИсполнителя КАК Роль
		|ИЗ
		|	РегистрСведений.ИсполнителиЗадач КАК ИсполнителиЗадач
		|ГДЕ
		|	ИсполнителиЗадач.Исполнитель В(&Исполнители)";
		
		Запрос.УстановитьПараметр("Исполнители", ДоступныеПользователи);
		ДоступныеРоли = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Роль");
		
		Если ДоступныеРоли.Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДоступныеПользователи, ДоступныеРоли, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДоступныеПользователи;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Установливает ограничение выбора пользователей.
//
// Параметры:
//	СписокЭлементов				 - Массив - Список элементов.
//	МассивДоступныхПользователей - Массив - Массив доступных пользователей.
//
Процедура УстановитьОграничениеВыбораПользователей(СписокЭлементов, МассивДоступныхПользователей = Неопределено) Экспорт
	Если СписокЭлементов = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	Если МассивДоступныхПользователей = Неопределено Тогда
		МассивДоступныхПользователей = ПолучитьДоступныхМенеджеровИРоли();
	КонецЕсли;
	
	ПараметрыВыбораПользователь = Новый Массив;
	ПараметрыВыбораПользователь.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", МассивДоступныхПользователей));
	Для Каждого ЭлементФормы Из СписокЭлементов Цикл	
		ЭлементФормы.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораПользователь);
	КонецЦикла;
КонецПроцедуры	

Функция ПолучитьСтруктуруДляУстановкиОтбора(Форма)
	СтруктураДляОтбора = Новый Структура("Список, МассивЭлементовОтбора, ЭлементСотрудник",
		Неопределено, Неопределено, Неопределено);
	Если Форма.ИмяФормы = "Документ.CRM_Взаимодействие.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборОтветственный);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.CRM_Заявка.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.CRM_Интерес.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборПользователь);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.CRM_ОтчетОРаботе.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Сотрудник");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ПолеБыстрогоОтбора_Сотрудник);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.CRM_ПланированиеВоронкиПродаж.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ПолеБыстрогоОтбора_Автор);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.CRM_РассылкаЭлектронныхПисем.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборОтветственный);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.CRM_СчетНаОплатуПокупателю.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборОтветственный);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.CRM_Телемаркетинг.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборОтветственный);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.Встреча.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.ЗапланированноеВзаимодействие.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.КоммерческоеПредложениеКлиенту.Форма.CRM_ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Менеджер");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборОтветственный);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.СообщениеSMS.Форма.SMS4B_ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Документ.ТелефонныйЗвонок.Форма.сфпФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборПоОтветственному);
		СписокЭлементов.Добавить(Форма.Элементы.ОтборПоАвтору);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Обработка.CRM_АРМ_МоиПродажи.Форма.ФормаФильтра" Тогда
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ТекущийПользователь);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
	ИначеЕсли Форма.ИмяФормы = "Отчет.CRM_ВоронкаПродаж.Форма.Форма" Тогда
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборОтветственные);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
	ИначеЕсли Форма.ИмяФормы = "Справочник.МаркетинговыеМероприятия.Форма.CRM_ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("CRM_Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборОтветственный);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Задача.ЗадачаИсполнителя.Форма.CRM_ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Исполнитель");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборИсполнитель);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Задача.ЗадачаИсполнителя.Форма.CRM_ФормаМоиЗадачи" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Исполнитель");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборОтветственный);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);	
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Обработка.CRM_КалендарьМенеджера.Форма.Форма" Тогда
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборЗначениеПользователь);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
	ИначеЕсли Форма.ИмяФормы = "Обработка.CRM_ПланированиеПроектов.Форма.ФормаПланирования" Тогда
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборЗначениеИсполнитель);
		СписокЭлементов.Добавить(Форма.Элементы.ОтборЗначениеОтветственный);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
	ИначеЕсли Форма.ИмяФормы = "Отчет.CRM_МониторЦелевыхПоказателей.Форма.ФормаМониторЦелевыхПоказателей" Тогда
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборСотрудник);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
	ИначеЕсли Форма.ИмяФормы = "Обработка.CRM_УниверсальныйЖурналДокументов.Форма.Форма" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборОтветственный);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Обработка.CRM_УниверсальныйЖурналДокументов.Форма.ФормаФиксированныйОтбор" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СписокЭлементов = Новый Массив;
		СписокЭлементов.Добавить(Форма.Элементы.ОтборОтветственный);
		СтруктураДляОтбора.Вставить("ЭлементСотрудник", СписокЭлементов);
		СтруктураДляОтбора.Вставить("Список", Форма.Список);
	ИначеЕсли Форма.ИмяФормы = "Справочник.Партнеры.Форма.CRM_ФормаСписка" Тогда
		МассивЭлементовОтбора = Новый Массив;
		МассивЭлементовОтбора.Добавить("Автор");
		МассивЭлементовОтбора.Добавить("Ответственный");
		СтруктураДляОтбора.Вставить("МассивЭлементовОтбора", МассивЭлементовОтбора);
		СтруктураДляОтбора.Вставить("Список", Форма.Интересы);
	КонецЕсли;
	CRM_УправлениеДоступомКПользователямПереопределяемый.ДополнитьСтруктуруДляУстановкиОтбора(Форма, СтруктураДляОтбора);
	Возврат СтруктураДляОтбора;
КонецФункции

// Устанавливает ограничения по пользователям.
// 
// Параметры:
//	Форма					- ФормаКлиентскогоПриложения - Форма.
//	ПолучитьБезОрганичений	- Булево - Получить без органичений.
//	ДополнительныеПараметры - Структура - Дополнительные параметры.
//
Процедура УстановитьОграниченияПоПользователям(Форма, ПолучитьБезОрганичений = Ложь,
	 ДополнительныеПараметры = Неопределено) Экспорт
	Если ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("ОтключитьОтборы") Тогда
		ПолучитьБезОрганичений = Истина;	
	КонецЕсли;
	МассивДоступныхПользователей = ПолучитьДоступныхМенеджеровИРоли(ПолучитьБезОрганичений);
	СтруктураДляОтбора = ПолучитьСтруктуруДляУстановкиОтбора(Форма);
	УстановитьОтборВСпискеПоДоступнымпользователям(СтруктураДляОтбора.Список, СтруктураДляОтбора.МассивЭлементовОтбора);
	УстановитьОграничениеВыбораПользователей(СтруктураДляОтбора.ЭлементСотрудник, МассивДоступныхПользователей);
КонецПроцедуры	

#КонецОбласти
