
#Область СлужебныеПроцедурыИФункции

Функция ИмяТаблицыЗаписейУРВ() Экспорт
	
	Возврат "CRM_РабочееВремяПользователей";
	
КонецФункции

Функция ИмяГруппыТаблицыЗаписейУРВ() Экспорт
	
	Возврат "CRM_ГруппаРабочееВремяПользователей";
	
КонецФункции

Процедура ОбновитьНумерациюСтрокТаблицыЗаписей(ТаблицаЗаписей) Экспорт
	
	Сч = 1;
	Для Каждого Запись Из ТаблицаЗаписей Цикл
		Запись.НомерСтроки = Сч;
		Сч = Сч + 1;
	КонецЦикла;
	
КонецПроцедуры

Функция ВремяУчета(ТрудозатратыВСекундах) Экспорт
	
	Возврат Окр(ТрудозатратыВСекундах / 3600, 2); // в часах
	
КонецФункции

Процедура ОбновитьЭлементТаймерУРВ(Форма, ВключенаЦветоваяИндикация) Экспорт
	
	ЭлементТаймерУРВ = Форма.Элементы.Найти("ОбщаяКомандаCRM_ТаймерУРВНажатие");
	Если ЭлементТаймерУРВ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если CRM_ТрудозатратыСервер.ПоОбъектуЗапущенТаймерУРВ(Форма.Объект.Ссылка) Тогда
		
		Форма.CRM_ТаймерУРВЗапущен = Истина;
		ЭлементТаймерУРВ.Заголовок = НСтр("ru = 'Остановить выполнение'");
		ЭлементТаймерУРВ.Картинка = БиблиотекаКартинок.CRM_НачатьУчетВремениЗапущен24;
		
	Иначе
		
		Форма.CRM_ТаймерУРВЗапущен = Ложь;
		ЭлементТаймерУРВ.Заголовок = НСтр("ru = 'Начать выполнение'");
		
		Если ВключенаЦветоваяИндикация Тогда
			
			Если CRM_ТрудозатратыСервер.ТаймерУРВСодержитДанныеПоОбъекту(Форма.Объект.Ссылка) Тогда
				ЭлементТаймерУРВ.Картинка = БиблиотекаКартинок.CRM_НачатьУчетВремениСДанными24Анимация;
			Иначе
				ЭлементТаймерУРВ.Картинка = БиблиотекаКартинок.CRM_НачатьУчетВремени24Анимация;
			КонецЕсли;
			
		Иначе
			
			Если CRM_ТрудозатратыСервер.ТаймерУРВСодержитДанныеПоОбъекту(Форма.Объект.Ссылка) Тогда
				ЭлементТаймерУРВ.Картинка = БиблиотекаКартинок.CRM_НачатьУчетВремениСДанными24;
			Иначе
				ЭлементТаймерУРВ.Картинка = БиблиотекаКартинок.CRM_НачатьУчетВремени24;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьВремяУчетаИтого(Форма) Экспорт
	
	ИмяРеквизита = "УРВ_ВремяУчетаИтого";
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	ТрудозатратыИтог = Форма[CRM_ТрудозатратыКлиентСервер.ИмяТаблицыЗаписейУРВ()].Итог("Трудозатраты");
	
	Форма[ИмяРеквизита] = CRM_ТрудозатратыКлиентСервер.ПолучитьПредставлениеВремени(ТрудозатратыИтог);
	
КонецПроцедуры

Функция ПолучитьПредставлениеВремени(Знач ВремяСек) Экспорт
	
	СтрокаПредставления = "";
	
	Если Не ЗначениеЗаполнено(ВремяСек) Тогда
		Возврат СтрокаПредставления;
	КонецЕсли;
	
	СекундВЧасе = 60 * 60;
	СекундМинуте = 60;
	
	ВремяЧасов = Цел(ВремяСек / СекундВЧасе);
	Если ВремяЧасов > 0 Тогда
		СтрокаПредставления = СтрокаПредставления + Формат(ВремяЧасов, "ЧГ=") + " ч. ";
		ВремяСек = ВремяСек - ВремяЧасов * СекундВЧасе;
	КонецЕсли;
	
	ВремяМинут = Цел(ВремяСек / СекундМинуте);
	Если ВремяМинут > 0 Тогда
		СтрокаПредставления = СтрокаПредставления + Формат(ВремяМинут, "ЧГ=") + " мин. ";
		ВремяСек = ВремяСек - ВремяМинут * СекундМинуте;
	КонецЕсли;
	
	Если ВремяСек > 0 Тогда
		СтрокаПредставления = СтрокаПредставления + Формат(ВремяСек, "ЧГ=") + " сек. ";
	КонецЕсли;
	
	Возврат СокрЛП(СтрокаПредставления);
	
КонецФункции	

#КонецОбласти
