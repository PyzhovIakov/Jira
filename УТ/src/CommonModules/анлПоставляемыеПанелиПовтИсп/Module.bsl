#Область ПрограммныйИнтерфейс

// Таблица макетов конфигурации для установки в 1С:Аналитика.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Поставляемые панели 1С:Аналитика, обнаруженные в конфигурации.
// 	 * ИмяМакета 				 - Строка - Имя общего макета, содержащего поставляемую панель.
// 	 * ДатаИзмененияКонфигурация - Дата - Дата изменений поставляемой панели в общем макете.
// 	 * ДатаИзмененияАналитика 	 - Дата - Дата изменений установленной панели на сервере 1С:Аналитика.
// 	 * ПутьКФайламМакета 		 - Строка - Путь к файлам на диске с содержимым поставляемой панели.
//
Функция ПоставляемыеПанелиКонфигурации() Экспорт

	ПараметрыДаты = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
		
	ПоставляемыеПанели = Новый ТаблицаЗначений;
	ПоставляемыеПанели.Колонки.Добавить("ИмяМакета", Новый ОписаниеТипов("Строка"));
	ПоставляемыеПанели.Колонки.Добавить("ДатаИзмененияКонфигурация", Новый ОписаниеТипов("Дата", ПараметрыДаты));
	ПоставляемыеПанели.Колонки.Добавить("ДатаИзмененияАналитика", Новый ОписаниеТипов("Дата", ПараметрыДаты));
	ПоставляемыеПанели.Колонки.Добавить("ПутьКФайламМакета", Новый ОписаниеТипов("Строка"));
	
	ПрефиксМакета = ПрефиксМакетаПоставляемойПанели();
	Для каждого ОбщийМакет Из Метаданные.ОбщиеМакеты Цикл
		
		// Имя общего макета должно содержать ПрефиксМакета
		ПозицияПрефикса = СтрНайти(ОбщийМакет.Имя, ПрефиксМакета);
		Если ПозицияПрефикса = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Общий макет должен содержать двоичные данные
		Если ОбщийМакет.ТипМакета <> Метаданные.СвойстваОбъектов.ТипМакета.ДвоичныеДанные Тогда
			Продолжить;
		КонецЕсли;
		
		Панель = ПоставляемыеПанели.Добавить();
		Панель.ИмяМакета = ОбщийМакет.Имя;
		
	КонецЦикла;
	
	Возврат ПоставляемыеПанели;	
	
КонецФункции

// Структура данных макета поставляемой панели.
// 
// Параметры:
//  ИмяМакета - Строка - Имя общего макета, содержащего поставляемую панель.
// 
// Возвращаемое значение:
//  Структура - Структура поставляемой панели
// 	 * Контент - Структура - Содержимое в формате JSON
// 	 ** Панель - Строка - Содержимое поставляемой панели в формате JSON.
// 	 ** ДополнительныеИсточники - Массив - Содержимое дополнительных источников 
// 	 							  данных поставляемой панели в формате JSON.
// 	 * Объекты - Структура - Описание объектов поставляемой панели 1С:Аналитика
// 	 ** desktop 	- Массив - Рабочие столы.
// 	 ** folder 		- Массив - Папки.
// 	 ** chart 		- Массив - Диаграммы.
// 	 ** dashboard 	- Массив - Дашборды.
// 	 ** user 		- Массив - Пользователи.
// 	 * Идентификатор - Строка - Идентификатор рабочего стола в 1С:Аналитика.
//
Функция СтруктураПоставляемойПанели(ИмяМакета)	Экспорт

	МакетПанели = Метаданные.ОбщиеМакеты[ИмяМакета];

	ПутьКФайлам = ПутьКФайламМакета(МакетПанели);
	
	Контент = КонтентПоставляемойПанели(ПутьКФайлам);
	УдалитьФайлы(ПутьКФайлам);
	
	СтруктураМакета = анлАналитика.JSONВЗначение(Контент);
	
	Возврат СтруктураМакета;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Контент макета поставляемой панели 1С:Аналитика.
// 
// Параметры:
//  МакетПанели - ОбъектМетаданныхМакет - Макет панели
// 
// Возвращаемое значение:
//  Структура - Контент макета поставляемой панели:
// * Панель - Строка - Содержимое поставляемой панели в формате JSON.
// * ДополнительныеИсточники - Массив из Структура - Содержимое дополнительных источников 
// 	 							  данных поставляемой панели в формате JSON.
//
Функция КонтентМакетаПоставляемойПанели(МакетПанели)	Экспорт
	
	ПутьКФайлам = ПутьКФайламМакета(МакетПанели);
	Контент = КонтентПоставляемойПанели(ПутьКФайлам);
	
	УдалитьФайлы(ПутьКФайлам);
	
	Возврат Контент;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Префикс общего макета, являющийся признаком, что 
// в нем содержится поставляемая панель для 1С:Аналитика.
// 
// Возвращаемое значение:
//  Строка - Префикс макета поставляемой панели.
//
Функция ПрефиксМакетаПоставляемойПанели()
	
	Возврат "ПоставляемаяПанельАналитики_";
	
КонецФункции

// Путь к временному каталогу на сервере 1С, 
// в который рапакованы файлы макета..
// 
// Параметры:
//  МакетПанели - ОбъектМетаданныхМакет - Макет поставляемой панели 1С:Аналитика.
// 
// Возвращаемое значение:
//  Строка - Путь к файлам макета.
//
Функция ПутьКФайламМакета(МакетПанели)
	
	ПутьКФайламМакета = КаталогВременныхФайлов() 
		+ "ans_presets\"
		+ МакетПанели.Имя + "\";	
	
	АрхивСФайламиПанели = ПолучитьОбщийМакет(МакетПанели.Имя);
	
	ZipФайл = Новый ЧтениеZipФайла(АрхивСФайламиПанели.ОткрытьПотокДляЧтения());
	ZipФайл.ИзвлечьВсе(ПутьКФайламМакета, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	
	Возврат ПутьКФайламМакета;
	
КонецФункции

// Содержимое макета для поставляемой панели 1С:Аналитики.
// 
// Параметры:
//  ПутьКФайлам - Строка - Путь к каталогу на сервере 1С, 
// 							в который рапакованы файлы макета.
// 
// Возвращаемое значение:
//  Строка - Содержимое макета для поставляемой панели.
//
Функция КонтентПоставляемойПанели(ПутьКФайлам)
	
	Контент = "";
	
	ПутиКФайлам = НайтиФайлы(ПутьКФайлам, "*.json", Ложь);
	Для Каждого ПутьКФайлу Из ПутиКФайлам Цикл

		СодержимоеФайла = Новый ТекстовыйДокумент;
	    СодержимоеФайла.Прочитать(ПутьКФайлу.ПолноеИмя, КодировкаТекста.UTF8);
	    Контент = СодержимоеФайла.ПолучитьТекст();
	    
	    Прервать;	
		
	КонецЦикла;
	
	Возврат Контент;
	
КонецФункции

#КонецОбласти
