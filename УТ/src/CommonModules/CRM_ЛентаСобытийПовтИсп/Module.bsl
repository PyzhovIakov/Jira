#Область СлужебныйПрограммныйИнтерфейс

Функция ОписанияОбъектовЛенты() Экспорт
	
	ОписанияОбъектов = Новый Структура;
	
	#Область Взаимодействие
	
	ОписанияОбъектов.Вставить("Взаимодействие", Новый Структура);
	
	Поля = Новый Структура;
	
	Поля.Вставить("Тема", "Описание1");
	Поля.Вставить("Результат", "Описание2");
	Поля.Вставить("Ответственный", "Поле1");
	Поля.Вставить("КонтактноеЛицо", "Поле2");
	Поля.Вставить("ВидВзаимодействия", "Поле3");
	Поля.Вставить("ТипСостояния", "Поле4");
	Поля.Вставить("Направление", "Поле5");
	Поля.Вставить("ПлановаяДатаЗавершение", "Поле6");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Истина);
	
	ОписанияОбъектов.Взаимодействие.Вставить("Поля", Поля);
	ОписанияОбъектов.Взаимодействие.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область Задача
	
	ОписанияОбъектов.Вставить("Задача", Новый Структура);
	
	Поля = Новый Структура;
	
	Поля.Вставить("Наименование", "Описание1");
	Поля.Вставить("РезультатВыполнения", "Описание2");
	Поля.Вставить("Исполнитель", "Поле1");
	Поля.Вставить("ТипСостояния", "Поле2");
	Поля.Вставить("Направление", "Поле3");
	Поля.Вставить("CRM_Личная", "Поле4");
	Поля.Вставить("СрокИсполнения", "Поле5");
	Поля.Вставить("БизнесПроцесс", "Поле6");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Истина);
	
	ОписанияОбъектов.Задача.Вставить("Поля", Поля);
	ОписанияОбъектов.Задача.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область Файл
	
	ОписанияОбъектов.Вставить("Файл", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Наименование", "Описание1");
	Поля.Вставить("Описание", "Описание2");
	Поля.Вставить("Автор", "Поле1");
	Поля.Вставить("Расширение", "Поле2");
	Поля.Вставить("Размер", "Поле3");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Истина);
	Параметры.Вставить("ЕстьФайлы", Ложь);
	
	ОписанияОбъектов.Файл.Вставить("Поля", Поля);
	ОписанияОбъектов.Файл.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область Примечание
	
	ОписанияОбъектов.Вставить("Примечание", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("ТекстСодержания", "Описание1");
	Поля.Вставить("Автор", "Поле1");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Ложь);
	
	ОписанияОбъектов.Примечание.Вставить("Поля", Поля);
	ОписанияОбъектов.Примечание.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область ТелефонныйЗвонок
	
	ОписанияОбъектов.Вставить("ТелефонныйЗвонок", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Комментарий", "Описание1");
	Поля.Вставить("АбонентПредставление", "Описание2");
	Поля.Вставить("Входящий", "Поле1");
	Поля.Вставить("сфпСостояниеЗвонка", "Поле2");
	Поля.Вставить("Автор", "Поле3");
	Поля.Вставить("Ответственный", "Поле4");
	Поля.Вставить("сфпДлительностьЗвонка", "Поле5");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Истина);
	Параметры.Вставить("ЕстьФайлы", Истина);
	
	ОписанияОбъектов.ТелефонныйЗвонок.Вставить("Поля", Поля);
	ОписанияОбъектов.ТелефонныйЗвонок.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область ЭлектронноеПисьмоВходящее
	
	ОписанияОбъектов.Вставить("ЭлектронноеПисьмоВходящее", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Тема", "Описание1");
	Поля.Вставить("ОтправительПредставление", "Описание2");
	Поля.Вставить("СписокПолучателейПисьма", "Поле1");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Истина);
	Параметры.Вставить("ЕстьФайлы", Истина);
	
	ОписанияОбъектов.ЭлектронноеПисьмоВходящее.Вставить("Поля", Поля);
	ОписанияОбъектов.ЭлектронноеПисьмоВходящее.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область ЭлектронноеПисьмоИсходящее
	
	ОписанияОбъектов.Вставить("ЭлектронноеПисьмоИсходящее", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Тема", "Описание1");
	Поля.Вставить("СписокПолучателейПисьма", "Описание2");
	Поля.Вставить("Автор", "Поле1");
	Поля.Вставить("СтатусПисьма", "Поле2");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Истина);
	Параметры.Вставить("ЕстьФайлы", Истина);
	
	ОписанияОбъектов.ЭлектронноеПисьмоИсходящее.Вставить("Поля", Поля);
	ОписанияОбъектов.ЭлектронноеПисьмоИсходящее.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область Диалог
	
	ОписанияОбъектов.Вставить("Диалог", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Наименование", "Описание1");
	Поля.Вставить("Код", "Описание2");
	Поля.Вставить("КонтактПредставление", "Поле1");
	Поля.Вставить("УчетнаяЗапись", "Поле2");
	Поля.Вставить("Ответственный", "Поле3");
	Поля.Вставить("Статус", "Поле4");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Истина);
	Параметры.Вставить("ЕстьФайлы", Истина);
	
	ОписанияОбъектов.Диалог.Вставить("Поля", Поля);
	ОписанияОбъектов.Диалог.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область СообщениеSMS
	
	ОписанияОбъектов.Вставить("СообщениеSMS", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Тема", "Описание1");
	Поля.Вставить("СписокУчастников", "Описание2");
	Поля.Вставить("Автор", "Поле1");
	Поля.Вставить("Состояние", "Поле2");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Истина);
	Параметры.Вставить("ЕстьФайлы", Истина);
	
	ОписанияОбъектов.СообщениеSMS.Вставить("Поля", Поля);
	ОписанияОбъектов.СообщениеSMS.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область Заявка
	
	ОписанияОбъектов.Вставить("Заявка", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Тема", "Описание1");
	Поля.Вставить("Организация", "Описание2");
	Поля.Вставить("Наименование", "Поле1");
	Поля.Вставить("ИсточникПолучения", "Поле2");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Ложь);
	
	ОписанияОбъектов.Заявка.Вставить("Поля", Поля);
	ОписанияОбъектов.Заявка.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область Интерес
	
	ОписанияОбъектов.Вставить("Интерес", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Тема", "Описание1");
	Поля.Вставить("ТипОбращения", "Поле1");
	Поля.Вставить("Ответственный", "Поле2");
	Поля.Вставить("Валюта", "Поле3");
	Поля.Вставить("ОжидаемаяВыручка", "Поле4");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Истина);
	
	ОписанияОбъектов.Интерес.Вставить("Поля", Поля);
	ОписанияОбъектов.Интерес.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область КоммерческоеПредложение
	
	ОписанияОбъектов.Вставить("КоммерческоеПредложение", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Синоним", "Описание1");
	Поля.Вставить("Номер", "Поле1");
	Поля.Вставить("СуммаДокумента", "Поле2");
	Поля.Вставить("Валюта", "Поле3");
	Поля.Вставить("Автор", "Поле4");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Истина);
	
	ОписанияОбъектов.КоммерческоеПредложение.Вставить("Поля", Поля);
	ОписанияОбъектов.КоммерческоеПредложение.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область Телемаркетинг
	
	ОписанияОбъектов.Вставить("Телемаркетинг", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Тема", "Описание1");
	Поля.Вставить("Автор", "Поле1");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Истина);
	
	ОписанияОбъектов.Телемаркетинг.Вставить("Поля", Поля);
	ОписанияОбъектов.Телемаркетинг.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область ПрочийДокумент
	
	ОписанияОбъектов.Вставить("ПрочийДокумент", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Синоним", "Описание1");
	Поля.Вставить("Номер", "Поле1");
	Поля.Вставить("СуммаДокумента", "Поле2");
	Поля.Вставить("Валюта", "Поле3");
	Поля.Вставить("Автор", "Поле4");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Истина);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Неопределено);
	
	ОписанияОбъектов.ПрочийДокумент.Вставить("Поля", Поля);
	ОписанияОбъектов.ПрочийДокумент.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область ЗаписьИстории
	
	ОписанияОбъектов.Вставить("ЗаписьИстории", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("СтароеЗначениеСтрока", "Описание1");
	Поля.Вставить("ЗначениеСтрока", "Описание2");
	Поля.Вставить("Автор", "Поле1");
	Поля.Вставить("ИмяПоля", "Поле2");
	Поля.Вставить("ВидЗаписи", "Поле3");
	Поля.Вставить("СтароеЗначение", "Поле4");
	Поля.Вставить("Значение", "Поле5");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Ложь);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Ложь);
	
	ОписанияОбъектов.ЗаписьИстории.Вставить("Поля", Поля);
	ОписанияОбъектов.ЗаписьИстории.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область ЗавершениеИнтереса
	
	ОписанияОбъектов.Вставить("ЗавершениеИнтереса", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Ответственный", "Поле1");
	Поля.Вставить("ЭтоПоддержка", "Поле2");
	Поля.Вставить("ЗавершенУспешно", "Поле3");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Ложь);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Ложь);
	
	ОписанияОбъектов.ЗавершениеИнтереса.Вставить("Поля", Поля);
	ОписанияОбъектов.ЗавершениеИнтереса.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область СозданиеОбъекта
	
	ОписанияОбъектов.Вставить("СозданиеОбъекта", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Описание", "Описание1");
	Поля.Вставить("Автор", "Поле1");
	Поля.Вставить("ТипОбращения", "Поле2");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Ложь);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Ложь);
	
	ОписанияОбъектов.СозданиеОбъекта.Вставить("Поля", Поля);
	ОписанияОбъектов.СозданиеОбъекта.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	#Область ДействиеСИнтересом
	
	ОписанияОбъектов.Вставить("ДействиеСИнтересом", Новый Структура);
	
	Поля = Новый Структура;
	Поля.Вставить("Действие", "Поле1");
	Поля.Вставить("Пользователь", "Поле2");
	Поля.Вставить("ОбъектДействия", "Поле3");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьКоманды", Ложь);
	Параметры.Вставить("Группировка", Ложь);
	Параметры.Вставить("ЕстьФайлы", Ложь);
	
	ОписанияОбъектов.ДействиеСИнтересом.Вставить("Поля", Поля);
	ОписанияОбъектов.ДействиеСИнтересом.Вставить("Параметры", Параметры);
	
	#КонецОбласти
	
	Возврат ОписанияОбъектов;
	
КонецФункции

Функция ТекстШаблонаЛенты() Экспорт
	
	Возврат Документы.CRM_Интерес.ПолучитьМакет("Лента_Шаблон").ПолучитьТекст();
	
КонецФункции

Функция ТекстЗаглушкиЛенты() Экспорт
	
	Возврат Документы.CRM_Интерес.ПолучитьМакет("Лента_Заглушка").ПолучитьТекст();
	
КонецФункции

Функция ТекстЗапросаДанныхЛенты(ПараметрыЗапроса) Экспорт
	
	ТекстыЗапросаМассив = Новый Массив;
	
	// Выборка объектов журнала
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЖурналДокументов.Объект КАК Ссылка,
	|	ВЫБОР
	|		КОГДА &Режим = ""ЛентаИнтереса""
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЖурналДокументов.CRM_Интерес
	|	КОНЕЦ КАК Интерес,
	|	ВЫБОР
	|		КОГДА &Режим = ""ЛентаИнтереса""
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЖурналДокументов.CRM_Интерес = ЗНАЧЕНИЕ(Документ.CRM_Интерес.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЖурналДокументов.ОсновнойИнтерес
	|			КОНЕЦ
	|	КОНЕЦ КАК ОсновнойИнтерес
	|ПОМЕСТИТЬ ОбъектыПоВладельцу
	|ИЗ
	|	РегистрСведений.CRM_ЖурналДокументов КАК ЖурналДокументов";
	ТекстыЗапросаМассив.Добавить(ТекстЗапроса);
	
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
		ТекстЗапроса = "
		|ГДЕ
		|	ЖурналДокументов.CRM_Интерес В(&Объекты)";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКлиента" Тогда
		ТекстЗапроса = "
		|ГДЕ
		|	ЖурналДокументов.Клиент В(&Объекты)
		|	И ЖурналДокументов.КонтактноеЛицо В (&ДоступныеКонтакты)";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКонтакта" Тогда
		ТекстЗапроса = "
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛица
		|		ПО ЖурналДокументов.КонтактноеЛицо = КонтактныеЛица.Ссылка
		|			И ЖурналДокументов.КонтактноеЛицо В(&Объекты)
		|			И ЖурналДокументов.Клиент = КонтактныеЛица.Владелец";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаПотенциальногоКлиента" Тогда
		ТекстЗапроса = "
		|ГДЕ
		|	ЖурналДокументов.Клиент В(&Объекты)";
	КонецЕсли;
	ТекстыЗапросаМассив.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	ТекстыЗапросаМассив.Добавить(ТекстЗапроса);
	
	// Карточки объектов
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбъектыЖурнала.Ссылка КАК Ссылка,
	|	ОбъектыЖурнала.Интерес КАК Интерес,
	|	ВЫБОР
	|		КОГДА ЗакрепленныеОбъекты.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЗаписьЗакреплена
	|ПОМЕСТИТЬ ОбъектыИИнтересыПоВладельцу
	|ИЗ
	|	ОбъектыПоВладельцу КАК ОбъектыЖурнала
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ЗакрепленныеЗаписиЛенты КАК ЗакрепленныеОбъекты
	|		ПО (ЗакрепленныеОбъекты.Ссылка = ОбъектыЖурнала.Ссылка)
	|			И (ЗакрепленныеОбъекты.Объект В (&Объекты))
	|			И (ЗакрепленныеОбъекты.Пользователь = &ТекущийПользователь)
	|ГДЕ
	|	ОбъектыЖурнала.ОсновнойИнтерес
	|	И ТИПЗНАЧЕНИЯ(ОбъектыЖурнала.Ссылка) В (&ДоступныеТипы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ДанныеКарточек.ВидОбъекта = ""ВзаимодействиеЗапланированное""
	|			ТОГДА ИСТИНА
	|		КОГДА ДанныеКарточек.ВидОбъекта = ""ЗадачаЗапланированная""
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Запланированный,
	|	ОбъектыЖурнала.Ссылка КАК Ссылка,
	|	ДанныеКарточек.ТипОбъекта КАК ТипОбъекта,
	|	ДанныеКарточек.ВидОбъекта КАК ВидОбъекта,
	|	ЕСТЬNULL(ПоследниеСообщенияДиалогов.Дата, ДанныеКарточек.Дата) КАК Дата,
	|	ДанныеКарточек.Описание1 КАК Описание1,
	|	ДанныеКарточек.Описание2 КАК Описание2,
	|	ДанныеКарточек.Поле1 КАК Поле1,
	|	ДанныеКарточек.Поле2 КАК Поле2,
	|	ДанныеКарточек.Поле3 КАК Поле3,
	|	ДанныеКарточек.Поле4 КАК Поле4,
	|	ДанныеКарточек.Поле5 КАК Поле5,
	|	ДанныеКарточек.Поле6 КАК Поле6,
	|	ДанныеКарточек.Проведен КАК Проведен,
	|	ДанныеКарточек.ПометкаУдаления КАК ПометкаУдаления,
	|	ЕСТЬNULL(ДокументИнтерес.Ссылка, НЕОПРЕДЕЛЕНО) КАК Интерес,
	|	ЕСТЬNULL(ДокументИнтерес.Тема, """") КАК ТемаИнтереса,
	|	ЕСТЬNULL(ДокументИнтерес.ТипОбращения, НЕОПРЕДЕЛЕНО) КАК ТипОбращенияИнтереса,
	|	ОбъектыЖурнала.ЗаписьЗакреплена КАК ЗаписьЗакреплена
	|	
	|ПОМЕСТИТЬ ДанныеОбъектовПредварительная
	|ИЗ
	|	ОбъектыИИнтересыПоВладельцу КАК ОбъектыЖурнала
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ДанныеКарточекЛенты КАК ДанныеКарточек
	|		ПО (ДанныеКарточек.Ссылка = ОбъектыЖурнала.Ссылка)
	|			И (ДанныеКарточек.ВидОбъекта В (&ВключенныеВидыОбъектов))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.CRM_Интерес КАК ДокументИнтерес
	|		ПО ОбъектыЖурнала.Интерес = ДокументИнтерес.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_СообщенияДиалоговПоследние КАК ПоследниеСообщенияДиалогов
	|		ПО (ПоследниеСообщенияДиалогов.Диалог = ДанныеКарточек.Ссылка)";
	ТекстыЗапросаМассив.Добавить(ТекстЗапроса);
	
	// Файлы
	Если СтрНайти(ПараметрыЗапроса.ВключенныеВидыОбъектов, "Файл") > 0 Тогда
		ДобавитьТекстЗапросаДанныхФайлов(ТекстыЗапросаМассив, ПараметрыЗапроса);
	КонецЕсли;
	
	// Заметки
	Если СтрНайти(ПараметрыЗапроса.ВключенныеВидыОбъектов, "Примечание") > 0 Тогда
		ДобавитьТекстЗапросаДанныхПримечаний(ТекстыЗапросаМассив, ПараметрыЗапроса);
	КонецЕсли;
	
	// История изменения реквизитов.
	Если СтрНайти(ПараметрыЗапроса.ВключенныеВидыОбъектов, "ЗаписьИстории") > 0 Тогда
		ДобавитьТекстЗапросаДанныхИстории(ТекстыЗапросаМассив, ПараметрыЗапроса);
	КонецЕсли;
	
	// Записи о создании объектов.
	ДобавитьТекстЗапросаДанныхЗаписейОСозданииОбъектов(ТекстыЗапросаМассив, ПараметрыЗапроса);
	
	// Действия с интересом
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
		ДобавитьТекстЗапросаДанныхДействийСИнтересом(ТекстыЗапросаМассив, ПараметрыЗапроса);
	КонецЕсли;
	
	// Изменение состояния интереса
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса"
		И СтрНайти(ПараметрыЗапроса.ВключенныеВидыОбъектов, "ИзменениеСостояния") > 0 Тогда
		ДобавитьТекстЗапросаДанныхИзмененияСостояния(ТекстыЗапросаМассив, ПараметрыЗапроса);
	КонецЕсли;
	
	// Изменение ответственного
	Если (ПараметрыЗапроса.Режим = "ЛентаИнтереса" Или ПараметрыЗапроса.Режим = "ЛентаКлиента")
		И СтрНайти(ПараметрыЗапроса.ВключенныеВидыОбъектов, "ИзменениеОтветственного") > 0 Тогда
		ДобавитьТекстЗапросаДанныхИзмененияОтветственного(ТекстыЗапросаМассив, ПараметрыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	ТекстыЗапросаМассив.Добавить(ТекстЗапроса);
	
	// Вычисление объектов истории.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АВТОНОМЕРЗАПИСИ() КАК Ключ,
	|	ДанныеОбъектовПредварительная.Запланированный КАК Запланированный,
	|	ДанныеОбъектовПредварительная.Ссылка КАК Ссылка,
	|	ДанныеОбъектовПредварительная.ТипОбъекта КАК ТипОбъекта,
	|	ДанныеОбъектовПредварительная.ВидОбъекта КАК ВидОбъекта,
	|	ДанныеОбъектовПредварительная.Дата КАК Дата,
	|	ДанныеОбъектовПредварительная.Описание1 КАК Описание1,
	|	ДанныеОбъектовПредварительная.Описание2 КАК Описание2,
	|	ДанныеОбъектовПредварительная.Поле1 КАК Поле1,
	|	ДанныеОбъектовПредварительная.Поле2 КАК Поле2,
	|	ДанныеОбъектовПредварительная.Поле3 КАК Поле3,
	|	ДанныеОбъектовПредварительная.Поле4 КАК Поле4,
	|	ДанныеОбъектовПредварительная.Поле5 КАК Поле5,
	|	ДанныеОбъектовПредварительная.Поле6 КАК Поле6,
	|	ДанныеОбъектовПредварительная.Проведен КАК Проведен,
	|	ДанныеОбъектовПредварительная.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеОбъектовПредварительная.Интерес КАК Интерес,
	|	ДанныеОбъектовПредварительная.ТемаИнтереса КАК ТемаИнтереса,
	|	ДанныеОбъектовПредварительная.ТипОбращенияИнтереса КАК ТипОбращенияИнтереса,
	|	ДанныеОбъектовПредварительная.ЗаписьЗакреплена КАК ЗаписьЗакреплена
	|ПОМЕСТИТЬ ДанныеОбъектов
	|ИЗ
	|	ДанныеОбъектовПредварительная КАК ДанныеОбъектовПредварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 50
	|	ДанныеОбъектов.Ключ КАК Ключ
	|ПОМЕСТИТЬ ДанныеОбъектовИстории
	|ИЗ
	|	ДанныеОбъектов КАК ДанныеОбъектов
	|ГДЕ
	|	НЕ ДанныеОбъектов.Запланированный
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеОбъектов.Дата УБЫВ";
	
	Если ПараметрыЗапроса.ОбъектовИстории > 0 Тогда
		ОбъектовИстории = СтрЗаменить(ПараметрыЗапроса.ОбъектовИстории, Символы.НПП, "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "50", ОбъектовИстории);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПЕРВЫЕ 50", "");
	КонецЕсли;
	ТекстыЗапросаМассив.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	ТекстыЗапросаМассив.Добавить(ТекстЗапроса);
	
	// Итоговая выборка - количество и данные объектов.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ДанныеОбъектов.Ключ) КАК Количество
	|ИЗ
	|	ДанныеОбъектов КАК ДанныеОбъектов
	|ГДЕ
	|	НЕ ДанныеОбъектов.Запланированный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НомерРазделаЗапланировано КАК Раздел,
	|	ДанныеОбъектов.Ссылка КАК Ссылка,
	|	ДанныеОбъектов.ТипОбъекта КАК ТипОбъекта,
	|	ДанныеОбъектов.ВидОбъекта КАК ВидОбъекта,
	|	ДанныеОбъектов.Дата КАК Дата,
	|	НАЧАЛОПЕРИОДА(ДанныеОбъектов.Дата, ДЕНЬ) КАК ДатаБезВремени,
	|	ГОД(ДанныеОбъектов.Дата) КАК Год,
	|	ДанныеОбъектов.Описание1 КАК Описание1,
	|	ДанныеОбъектов.Описание2 КАК Описание2,
	|	ДанныеОбъектов.Поле1 КАК Поле1,
	|	ДанныеОбъектов.Поле2 КАК Поле2,
	|	ДанныеОбъектов.Поле3 КАК Поле3,
	|	ДанныеОбъектов.Поле4 КАК Поле4,
	|	ДанныеОбъектов.Поле5 КАК Поле5,
	|	ДанныеОбъектов.Поле6 КАК Поле6,
	|	ДанныеОбъектов.Проведен КАК Проведен,
	|	ДанныеОбъектов.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеОбъектов.ТемаИнтереса КАК ТемаИнтереса,
	|	ДанныеОбъектов.ТипОбращенияИнтереса КАК ТипОбращенияИнтереса,
	|	ДанныеОбъектов.Интерес КАК Интерес,
	|	ЛОЖЬ КАК ЗаписьЗакреплена
	|ИЗ
	|	ДанныеОбъектов КАК ДанныеОбъектов
	|ГДЕ
	|	ДанныеОбъектов.Запланированный
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НомерРазделаЗакреплено,
	|	ДанныеОбъектов.Ссылка,
	|	ДанныеОбъектов.ТипОбъекта,
	|	ДанныеОбъектов.ВидОбъекта,
	|	ДанныеОбъектов.Дата,
	|	НАЧАЛОПЕРИОДА(ДанныеОбъектов.Дата, ДЕНЬ),
	|	ГОД(ДанныеОбъектов.Дата),
	|	ДанныеОбъектов.Описание1,
	|	ДанныеОбъектов.Описание2,
	|	ДанныеОбъектов.Поле1,
	|	ДанныеОбъектов.Поле2,
	|	ДанныеОбъектов.Поле3,
	|	ДанныеОбъектов.Поле4,
	|	ДанныеОбъектов.Поле5,
	|	ДанныеОбъектов.Поле6,
	|	ДанныеОбъектов.Проведен,
	|	ДанныеОбъектов.ПометкаУдаления,
	|	ДанныеОбъектов.ТемаИнтереса,
	|	ДанныеОбъектов.ТипОбращенияИнтереса,
	|	ДанныеОбъектов.Интерес,
	|	ИСТИНА
	|ИЗ
	|	ДанныеОбъектов КАК ДанныеОбъектов
	|ГДЕ
	|	ДанныеОбъектов.ЗаписьЗакреплена
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ДанныеОбъектов.Ссылка,
	|	ДанныеОбъектов.ТипОбъекта,
	|	ДанныеОбъектов.ВидОбъекта,
	|	ДанныеОбъектов.Дата,
	|	НАЧАЛОПЕРИОДА(ДанныеОбъектов.Дата, ДЕНЬ),
	|	ГОД(ДанныеОбъектов.Дата),
	|	ДанныеОбъектов.Описание1,
	|	ДанныеОбъектов.Описание2,
	|	ДанныеОбъектов.Поле1,
	|	ДанныеОбъектов.Поле2,
	|	ДанныеОбъектов.Поле3,
	|	ДанныеОбъектов.Поле4,
	|	ДанныеОбъектов.Поле5,
	|	ДанныеОбъектов.Поле6,
	|	ДанныеОбъектов.Проведен,
	|	ДанныеОбъектов.ПометкаУдаления,
	|	ДанныеОбъектов.ТемаИнтереса,
	|	ДанныеОбъектов.ТипОбращенияИнтереса,
	|	ДанныеОбъектов.Интерес,
	|	ДанныеОбъектов.ЗаписьЗакреплена
	|ИЗ
	|	ДанныеОбъектов КАК ДанныеОбъектов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОбъектовИстории КАК ДанныеОбъектовИстории
	|		ПО ДанныеОбъектов.Ключ = ДанныеОбъектовИстории.Ключ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Раздел,
	|	Дата УБЫВ,
	|	ТипОбъекта";
	ТекстыЗапросаМассив.Добавить(ТекстЗапроса);
	
	Возврат СтрСоединить(ТекстыЗапросаМассив);
	
КонецФункции

Процедура ДобавитьТекстЗапросаДанныхФайлов(ТекстыЗапроса, ПараметрыЗапроса)
	
	// Поля запроса:
	// Запланированный, Ссылка, ТипОбъекта, ВидОбъекта, Дата,
	// Описание1, Описание2, Поле1, Поле2, Поле3, Поле4, Поле5, Поле6, Проведен, ПометкаУдаления,
	// Интерес, ТемаИнтереса, ТипОбращенияИнтереса, ЗаписьЗакреплена
	
	ТекстЗапроса = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЛОЖЬ,
	|	ПрисоединенныеФайлы.Ссылка,
	|	""Файл"",
	|	""Файл"",
	|	ПрисоединенныеФайлы.ДатаСоздания,
	|	ПрисоединенныеФайлы.Наименование,
	|	ПрисоединенныеФайлы.Описание,
	|	ПрисоединенныеФайлы.Автор,
	|	ПрисоединенныеФайлы.Расширение,
	|	ПрисоединенныеФайлы.Размер,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	ПрисоединенныеФайлы.ПометкаУдаления,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ЗакрепленныеОбъекты.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	&ИмяСправочникаФайлы КАК ПрисоединенныеФайлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ЗакрепленныеЗаписиЛенты КАК ЗакрепленныеОбъекты
	|		ПО (ЗакрепленныеОбъекты.Ссылка = ПрисоединенныеФайлы.Ссылка)
	|			И (ЗакрепленныеОбъекты.Объект В (&Объекты))
	|			И (ЗакрепленныеОбъекты.Пользователь = &ТекущийПользователь)
	|ГДЕ
	|	ПрисоединенныеФайлы.ВладелецФайла В(&Объекты)
	|	И НЕ ПрисоединенныеФайлы.ЭтоГруппа
	|	И НЕ ПрисоединенныеФайлы.ПометкаУдаления";
	
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
		ИмяСправочникаФайлы = "CRM_ИнтересПрисоединенныеФайлы";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКлиента" Тогда
		ИмяСправочникаФайлы = "ПартнерыПрисоединенныеФайлы";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКонтакта" Тогда
		ИмяСправочникаФайлы = "КонтактныеЛицаПартнеровПрисоединенныеФайлы";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаПотенциальногоКлиента" Тогда
		ИмяСправочникаФайлы = "CRM_ПотенциальныеКлиентыПрисоединенныеФайлы";
	Иначе
		ВызватьИсключение НСтр("ru = 'Не удалось определить имя справочника присоединенных файлов'");
	КонецЕсли;
	ИмяСправочникаФайлы = "Справочник." + ИмяСправочникаФайлы;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяСправочникаФайлы", ИмяСправочникаФайлы);
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаДанныхПримечаний(ТекстыЗапроса, ПараметрыЗапроса)
	
	// Поля запроса:
	// Запланированный, Ссылка, ТипОбъекта, ВидОбъекта, Дата,
	// Описание1, Описание2, Поле1, Поле2, Поле3, Поле4, Поле5, Поле6, Проведен, ПометкаУдаления,
	// Интерес, ТемаИнтереса, ТипОбращенияИнтереса, ЗаписьЗакреплена
	
	ТекстЗапроса = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЛОЖЬ,
	|	Заметка.Ссылка,
	|	""Примечание"",
	|	""Примечание"",
	|	Заметка.ДатаИзменения,
	|	Заметка.ТекстСодержания,
	|	"""",
	|	Заметка.Автор,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	Заметка.ПометкаУдаления,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ЗакрепленныеОбъекты.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	Справочник.Заметки КАК Заметка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ЗакрепленныеЗаписиЛенты КАК ЗакрепленныеОбъекты
	|		ПО (ЗакрепленныеОбъекты.Ссылка = Заметка.Ссылка)
	|			И (ЗакрепленныеОбъекты.Объект В (&Объекты))
	|			И (ЗакрепленныеОбъекты.Пользователь = &ТекущийПользователь)
	|ГДЕ
	|	Заметка.Предмет В(&Объекты)
	|	И Заметка.CRM_ЗаметкаЛенты";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

КонецПроцедуры

Процедура ДобавитьТекстЗапросаДанныхИстории(ТекстыЗапроса, ПараметрыЗапроса)
	
	// Поля запроса:
	// Запланированный, Ссылка, ТипОбъекта, ВидОбъекта, Дата,
	// Описание1, Описание2, Поле1, Поле2, Поле3, Поле4, Поле5, Поле6, Проведен, ПометкаУдаления,
	// Интерес, ТемаИнтереса, ТипОбращенияИнтереса, ЗаписьЗакреплена
	
	// Запись о завершении интереса.
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
		
		ТекстЗапроса = "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|";
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЛОЖЬ,
		|	ДокументИнтерес.Ссылка,
		|	""ЗавершениеИнтереса"",
		|	""ЗавершениеИнтереса"",
		|	ДокументИнтерес.ДатаЗакрытия,
		|	"""",
		|	"""",
		|	ДокументИнтерес.Ответственный,
		|	ДокументИнтерес.ЭтоПоддержка,
		|	ВЫБОР
		|		КОГДА ДокументИнтерес.СостояниеИнтереса.ВидСостояния = ЗНАЧЕНИЕ(Перечисление.CRM_ВидыСостоянияИнтереса.УспешноеЗавершение)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО,
		|	"""",
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ
		|ИЗ
		|	Документ.CRM_Интерес КАК ДокументИнтерес
		|ГДЕ
		|	ДокументИнтерес.Ссылка В(&Объекты)
		|	И ДокументИнтерес.Завершен";
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЛОЖЬ,
	|	ЗаписиИстории.КлючУникальности,
	|	""ЗаписьИстории"",
	|	""ЗаписьИстории"",
	|	ЗаписиИстории.Период,
	|	ЗаписиИстории.СтароеЗначениеСтрока,
	|	ЗаписиИстории.ЗначениеСтрока,
	|	ЗаписиИстории.Автор,
	|	ЕСТЬNULL(ОписаниеРеквизитов.Представление, ЗаписиИстории.Реквизит),
	|	ЗаписиИстории.ВидЗаписи,
	|	ЗаписиИстории.СтароеЗначение,
	|	ЗаписиИстории.Значение,
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрСведений.CRM_ИсторияРеквизитовОбъектов КАК ЗаписиИстории
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ОписаниеРеквизитовМетаданных КАК ОписаниеРеквизитов
	|		ПО ЗаписиИстории.Реквизит = ОписаниеРеквизитов.Реквизит
	|			И ОписаниеРеквизитов.Объект = &ИмяОбъектаВладельцаИстории
	|ГДЕ
	|	ЗаписиИстории.Объект В(&Объекты)
	|		И НЕ ЗаписиИстории.ЭтоНовый
	|		И НЕ ЗаписиИстории.Реквизит В (""СостояниеИнтереса"", ""Ответственный"", ""ОсновнойМенеджер"")";
	
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
		ИмяОбъектаВладельцаИстории = """Документ.CRM_Интерес""";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКлиента" Тогда
		ИмяОбъектаВладельцаИстории = """Справочник.Партнеры""";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКонтакта" Тогда
		ИмяОбъектаВладельцаИстории = """Справочник.КонтактныеЛицаПартнеров""";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаПотенциальногоКлиента" Тогда
		ИмяОбъектаВладельцаИстории = """Справочник.CRM_ПотенциальныеКлиенты""";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяОбъектаВладельцаИстории", ИмяОбъектаВладельцаИстории);
	ТекстыЗапроса.Добавить(ТекстЗапроса);

КонецПроцедуры

Процедура ДобавитьТекстЗапросаДанныхЗаписейОСозданииОбъектов(ТекстыЗапроса, ПараметрыЗапроса)
	
	// Поля запроса:
	// Запланированный, Ссылка, ТипОбъекта, ВидОбъекта, Дата,
	// Описание1, Описание2, Поле1, Поле2, Поле3, Поле4, Поле5, Поле6, Проведен, ПометкаУдаления,
	// Интерес, ТемаИнтереса, ТипОбращенияИнтереса, ЗаписьЗакреплена
	
	ТекстЗапроса = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЛОЖЬ,
		|	ДокументИнтерес.Ссылка,
		|	""СозданиеОбъекта"",
		|	""СозданиеОбъекта"",
		|	ДокументИнтерес.Дата,
		|	ДокументИнтерес.Тема,
		|	"""",
		|	ДокументИнтерес.Автор,
		|	ДокументИнтерес.ТипОбращения,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО,
		|	"""",
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ
		|ИЗ
		|	Документ.CRM_Интерес КАК ДокументИнтерес
		|ГДЕ
		|	ДокументИнтерес.Ссылка В(&Объекты)";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКлиента" Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЛОЖЬ,
		|	Партнеры.Ссылка,
		|	""СозданиеОбъекта"",
		|	""СозданиеОбъекта"",
		|	Партнеры.ДатаРегистрации,
		|	Партнеры.Наименование,
		|	"""",
		|	Партнеры.CRM_Автор,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО,
		|	"""",
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры
		|ГДЕ
		|	Партнеры.Ссылка В(&Объекты)";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЛОЖЬ,
		|	Контакты.Ссылка,
		|	""СозданиеОбъекта"",
		|	""СозданиеОбъекта"",
		|	Контакты.ДатаРегистрацииСвязи,
		|	Контакты.Наименование,
		|	"""",
		|	Контакты.Автор,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО,
		|	"""",
		|	НЕОПРЕДЕЛЕНО,
		|	ЛОЖЬ
		|ИЗ
		|	Справочник.КонтактныеЛицаПартнеров КАК Контакты
		|ГДЕ
		|	Контакты.Ссылка В(&Объекты)";
	КонецЕсли;
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаДанныхДействийСИнтересом(ТекстыЗапроса, ПараметрыЗапроса)
	
	// Поля запроса:
	// Запланированный, Ссылка, ТипОбъекта, ВидОбъекта, Дата,
	// Описание1, Описание2, Поле1, Поле2, Поле3, Поле4, Поле5, Поле6, Проведен, ПометкаУдаления,
	// Интерес, ТемаИнтереса, ТипОбращенияИнтереса, ЗаписьЗакреплена
	
	ТекстЗапроса = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЛОЖЬ,
	|	ДействияСИнтересом.Ссылка,
	|	""ДействиеСИнтересом"",
	|	""ДействиеСИнтересом"",
	|	ДействияСИнтересом.Дата,
	|	"""",
	|	"""",
	|	ДействияСИнтересом.Действие,
	|	ДействияСИнтересом.Пользователь,
	|	ДействияСИнтересом.ОбъектДействия,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ
	|ИЗ
	|	Документ.CRM_Интерес.ДействияСИнтересом КАК ДействияСИнтересом
	|ГДЕ
	|	ДействияСИнтересом.Ссылка В(&Объекты)";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

КонецПроцедуры

Процедура ДобавитьТекстЗапросаДанныхИзмененияСостояния(ТекстыЗапроса, ПараметрыЗапроса)
	
	// Поля запроса:
	// Запланированный, Ссылка, ТипОбъекта, ВидОбъекта, Дата,
	// Описание1, Описание2, Поле1, Поле2, Поле3, Поле4, Поле5, Поле6, Проведен, ПометкаУдаления,
	// Интерес, ТемаИнтереса, ТипОбращенияИнтереса, ЗаписьЗакреплена
	
	ТекстЗапроса = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЛОЖЬ,
	|	ЗаписиИстории.КлючУникальности,
	|	""ЗаписьИстории"",
	|	""ИзменениеСостояния"",
	|	ЗаписиИстории.Период,
	|	ЗаписиИстории.СтароеЗначениеСтрока,
	|	ЗаписиИстории.ЗначениеСтрока,
	|	ЗаписиИстории.Автор,
	|	ЕСТЬNULL(ОписаниеРеквизитов.Представление, ЗаписиИстории.Реквизит),
	|	ЗаписиИстории.ВидЗаписи,
	|	ЗаписиИстории.СтароеЗначение,
	|	ЗаписиИстории.Значение,
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрСведений.CRM_ИсторияРеквизитовОбъектов КАК ЗаписиИстории
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ОписаниеРеквизитовМетаданных КАК ОписаниеРеквизитов
	|		ПО ЗаписиИстории.Реквизит = ОписаниеРеквизитов.Реквизит
	|			И ОписаниеРеквизитов.Объект = ""Документ.CRM_Интерес""
	|ГДЕ
	|	ЗаписиИстории.Объект В(&Объекты)
	|		И НЕ ЗаписиИстории.ЭтоНовый
	|		И ЗаписиИстории.Реквизит = ""СостояниеИнтереса""";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаДанныхИзмененияОтветственного(ТекстыЗапроса, ПараметрыЗапроса)
	
	// Поля запроса:
	// Запланированный, Ссылка, ТипОбъекта, ВидОбъекта, Дата,
	// Описание1, Описание2, Поле1, Поле2, Поле3, Поле4, Поле5, Поле6, Проведен, ПометкаУдаления,
	// Интерес, ТемаИнтереса, ТипОбращенияИнтереса, ЗаписьЗакреплена
	
	ТекстЗапроса = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЛОЖЬ,
	|	ЗаписиИстории.КлючУникальности,
	|	""ЗаписьИстории"",
	|	""ИзменениеОтветственного"",
	|	ЗаписиИстории.Период,
	|	ЗаписиИстории.СтароеЗначениеСтрока,
	|	ЗаписиИстории.ЗначениеСтрока,
	|	ЗаписиИстории.Автор,
	|	ЕСТЬNULL(ОписаниеРеквизитов.Представление, ЗаписиИстории.Реквизит),
	|	ЗаписиИстории.ВидЗаписи,
	|	ЗаписиИстории.СтароеЗначение,
	|	ЗаписиИстории.Значение,
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрСведений.CRM_ИсторияРеквизитовОбъектов КАК ЗаписиИстории
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ОписаниеРеквизитовМетаданных КАК ОписаниеРеквизитов
	|		ПО ЗаписиИстории.Реквизит = ОписаниеРеквизитов.Реквизит
	|			И ОписаниеРеквизитов.Объект = &ИмяОбъекта
	|ГДЕ
	|	ЗаписиИстории.Объект В(&Объекты)
	|		И НЕ ЗаписиИстории.ЭтоНовый
	|		И ЗаписиИстории.Реквизит = &ИмяРеквизита";
	
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
		ИмяОбъекта = """Документ.CRM_Интерес""";
		ИмяРеквизита = """Ответственный"""
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКлиента" Тогда
		ИмяОбъекта = """Справочник.Партнеры""";
		ИмяРеквизита = """ОсновнойМенеджер"""
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяОбъекта", ИмяОбъекта);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяРеквизита", ИмяРеквизита);
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Функция ОтборыОбъектовЛенты(Знач НастройкиЛенты) Экспорт
	
	ОтборЛенты = Новый СписокЗначений;
	
	ОтборЛенты.Добавить("ВсеСобытия",		НСтр("ru='Все события';en='All events'"),				Истина);
	
	ОтборЛенты.Добавить("Задачи",			НСтр("ru='Завершенные задачи';en='Finished Tasks'"),	Истина);
	
	ОтборЛенты.Добавить("ВзаимодействияВстречи",	НСтр("ru = 'Завершенные встречи'"),				Истина);
	
	ОтборЛенты.Добавить("Звонки",			НСтр("ru='Звонки';en='Phone Calls'"),					Истина);
	ОтборЛенты.Добавить("ВходящиеЗвонки",	НСтр("ru = 'Входящие'"),								Истина);
	ОтборЛенты.Добавить("ИсходящиеЗвонки",	НСтр("ru = 'Исходящие'"),								Истина);
	
	ОтборЛенты.Добавить("Письма",			НСтр("ru='Письма';en='Letters'"),						Истина);
	ОтборЛенты.Добавить("ВходящиеПисьма",	НСтр("ru = 'Входящие'"),								Истина);
	ОтборЛенты.Добавить("ИсходящиеПисьма",	НСтр("ru = 'Исходящие'"),								Истина);
	
	ОтборЛенты.Добавить("ВзаимодействияПрочие",	НСтр("ru = 'Прочие взаимодействия'"),				Истина);
	
	ОтборЛенты.Добавить("Чаты",				НСтр("ru='Диалоги'"),						Истина);
	
	ОтборЛенты.Добавить("SMS",				НСтр("ru='SMS';en='SMS'"),								Истина);
	
	Если НастройкиЛенты.Режим = "ЛентаИнтереса" Тогда
		ОтборЛенты.Добавить("ИзменениеСостояния",	НСтр("ru='Изменено состояние';en='Changed State'"), Истина);
	КонецЕсли;
	Если НастройкиЛенты.Режим = "ЛентаИнтереса" Или НастройкиЛенты.Режим = "ЛентаКлиента" Тогда
		ОтборЛенты.Добавить("ИзменениеОтветственного",
				НСтр("ru='Смена ответственного';en='Reponsible Person Replacement'"),
			 Истина);
	КонецЕсли;
	
	ОтборЛенты.Добавить("История",			НСтр("ru='Изменение поля';en='Changing field'"),		Истина);
	ОтборЛенты.Добавить("Примечания",		НСтр("ru='Примечания';en='Comments'"),					Истина);
	ОтборЛенты.Добавить("Документы",		НСтр("ru='Документы';en='Documents'"),					Истина);
	
	Если НастройкиЛенты.Режим = "ЛентаКлиента" Или НастройкиЛенты.Режим = "ЛентаКонтакта"
		Или НастройкиЛенты.Режим = "ЛентаПотенциальногоКлиента" Тогда
		ОтборЛенты.Добавить("Интересы",			НСтр("ru='Интересы';en='Leads'"),					Истина);
		ОтборЛенты.Добавить("ИнтересыКакПоддержка",			НСтр("ru = 'Обращения в поддержку'"),	Истина);
	КонецЕсли;
	
	ОтборЛенты.Добавить("Файлы",		НСтр("ru = 'Файлы'"),								Истина);
	
	Возврат ОтборЛенты;
	
КонецФункции

Функция ДоступныеТипыСсылокЛенты() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипыОбъектов = Новый Массив;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОбъектыЖурналаДокументов.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.CRM_ОбъектыЖурналаДокументов КАК ОбъектыЖурналаДокументов");
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТипыОбъектов;
	КонецЕсли;
	
	ТипыОбъектов = Новый Массив;
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.CRM_Взаимодействие"));
	ТипыОбъектов.Добавить(Тип("ЗадачаСсылка.ЗадачаИсполнителя"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.ТелефонныйЗвонок"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.СообщениеSMS"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.CRM_Заявка"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.CRM_Интерес"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.CRM_Телемаркетинг"));
	ТипыОбъектов.Добавить(Тип("СправочникСсылка.CRM_Диалоги"));
	
	ИсключаемыеТипы = ИсключаемыеТипыСсылокЛенты();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТипыВыборки = Выборка.ТипЗначения.Типы();
		Для Каждого ТипОбъекта Из ТипыВыборки Цикл
			Если ИсключаемыеТипы.Найти(ТипОбъекта) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если ТипыОбъектов.Найти(ТипОбъекта) = Неопределено Тогда
				ИмяОбъекта = Метаданные.НайтиПоТипу(ТипОбъекта).ПолноеИмя();
				Если ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ИмяОбъекта) Тогда
					ТипыОбъектов.Добавить(ТипОбъекта);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТипыОбъектов;
	
КонецФункции

Функция ИсключаемыеТипыСсылокЛенты() Экспорт
	
	ИсключаемыеТипы = Новый Массив;
	
	ИсключаемыеТипы.Добавить(Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс"));
	ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.CRM_СообщениеМессенджера"));
	Если Метаданные.Документы.Найти("CRM_ДокументРасчетовСКонтрагентом") <> Неопределено Тогда
		ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.CRM_ДокументРасчетовСКонтрагентом");
		ИсключаемыеТипы.Добавить(ТипЗнч(ИдентификаторОбъекта.ЗначениеПустойСсылки));
	КонецЕсли;
	
	Возврат ИсключаемыеТипы;
	
КонецФункции

#КонецОбласти
