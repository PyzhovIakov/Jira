////////////////////////////////////////////////////////////////////////////////
// Клиент-серверные процедуры и функции для работы с виджетами.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Используется для получения данных виджета из таблицы добавленных виджетов формы.
// Поиск по скрытым виджетам не осуществляется.
//
// Параметры:
//  Форма      - ФормаКлиентскогоПриложения - Форма с таблицей виджетов.
//  ИмяДанных  - Строка                     - Имя данных виджета (идентификатор) для получения его данных.
//
// Возвращаемое значение:
//   ДанныеФормыКоллекция | Неопределно  - Данные виджета, в случае, если эти данные нашлись.
//
Функция ПолучитьДанныеВиджета(Форма, ИмяДанных) Экспорт
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("Скрыт", Ложь);
	ПараметрыПоиска.Вставить("ИмяДанных", ИмяДанных);
	
	НайденныеСтроки = Форма.ВариантыВиджетовФормы.НайтиСтроки(ПараметрыПоиска);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		Возврат НайденныеСтроки[0];
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции // ПолучитьДанныеВиджета()

// Используется для получения актуальное значение аналитики, анализируя флаг динамического значения аналитики.
//
// Параметры:
//  НастройкиВиджета  - Структура - Настройки виджета.
//
// Возвращаемое значение:
//   СправничникСсылка.Пользователи | СправочникСсылка.СтруктураПредприятия | Неопределно  - Данные виджета, в случае, если эти данные нашлись.
//
Функция ПолучитьАктуальноеЗначениеАналитики(НастройкиВиджета) Экспорт
	
	Если НастройкиВиджета.ТипАналитики = ПредопределенноеЗначение("Перечисление.CRM_ВидыРазверткиПоказателей.ПоМенеджеру") Тогда
		
		Возврат ?(
			НастройкиВиджета.ДинамическоеЗначениеАналитики,
			ПользователиКлиентСервер.ТекущийПользователь(),
			НастройкиВиджета.ЗначениеАналитики);
		
	ИначеЕсли НастройкиВиджета.ТипАналитики = ПредопределенноеЗначение("Перечисление.CRM_ВидыРазверткиПоказателей.ПоПодразделению") Тогда
		
		Возврат ?(
			НастройкиВиджета.ДинамическоеЗначениеАналитики,
			CRM_ОбщегоНазначенияСервер.ЗначениеРеквизитаОбъекта(ПользователиКлиентСервер.ТекущийПользователь(), "Подразделение"),
			НастройкиВиджета.ЗначениеАналитики);
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции // ПолучитьАктуальноеЗначениеАналитики()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////