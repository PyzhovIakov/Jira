///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура выполняет открытие формы выбора параметров распознавания речи.
//
// Параметры:
//  Форма							 - ФормаКлиентскогоПриложения	 - Форма.
//  ТекущиеПараметры				 - Структура	 - Текущие параметры.
//  ОбработчикПолученияРезультата	 - ОписаниеОповещения	 - Обработчик получения результата.
//
Процедура ПоказатьВыборПараметровРаспознаванияРечи(Форма, ТекущиеПараметры, ОбработчикПолученияРезультата) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПараметрыРаспознаванияРечи", ТекущиеПараметры);
	ПараметрыОткрытия.Вставить("ИмяФормы", Форма.ИмяФормы);
	
	
	Обработчик = Новый ОписаниеОповещения("ПослеПолученияРезультатаВыбораПараметровРаспознаванияРечи",
		ЭтотОбъект, ОбработчикПолученияРезультата);
	ОткрытьФорму("Обработка.CRM_ПараметрыРаспознаванияРечи.Форма", ПараметрыОткрытия , Форма, , , , Обработчик);
	
КонецПроцедуры

// Функция возвращает параметры модели распознавания речи.
//
// Параметры:
//  ТекущиеПараметры - Структура	 - Текущие параметры.
// 
// Возвращаемое значение:
//   - ПараметрыМоделиРаспознаванияРечи
//
Функция ПараметрыМодели(ТекущиеПараметры) Экспорт
	
	ПараметрыМодели = Новый ПараметрыМоделиРаспознаванияРечи;
	ПараметрыМодели.КодЯзыка   = ТекущиеПараметры.КодЯзыка;
	ПараметрыМодели.Акустика   = ТекущиеПараметры.Акустика;
	ПараметрыМодели.Грамматика = ТекущиеПараметры.Грамматика;
	ПараметрыМодели.Версия     = ТекущиеПараметры.Версия;
	
	Возврат ПараметрыМодели;
	
КонецФункции

// Функция возвращает вариант использования расположения работы с речью.
//
// Параметры:
//  ТекущиеПараметры - Структура	 - Текущие параметры.
// 
// Возвращаемое значение:
//   - ВариантИспользованияРасположенияРаботыСРечью
//
Функция ВариантИспользования(ТекущиеПараметры) Экспорт
	
	Если ТекущиеПараметры.ВариантИспользования = "Локально" Тогда
		Возврат ВариантИспользованияРасположенияРаботыСРечью.ТолькоЛокальное;
	ИначеЕсли ТекущиеПараметры.ВариантИспользования = "ВОблаке" Тогда
		Возврат ВариантИспользованияРасположенияРаботыСРечью.ТолькоВнешнее;
	Иначе
		Возврат ВариантИспользованияРасположенияРаботыСРечью.Авто;
	КонецЕсли;
	
КонецФункции

// Функция получает параметры потокового распознавания речи.
//
// Параметры:
//  ТекущиеПараметры				 - Структура	 - Текущие параметры.
//  ОбработчикОстановкиАудиозаписи	 - ОписаниеОповещения	 - Обработчик, вызываемый при остановке аудиозаписи.
// 
// Возвращаемое значение:
//   - ПараметрыПотоковогоРаспознаванияРечи
//
Функция ПараметрыПотоковогоРаспознаванияРечи(ТекущиеПараметры, ОбработчикОстановкиАудиозаписи) Экспорт
	
	ПараметрыРаспознавания = Новый ПараметрыПотоковогоРаспознаванияРечи;
	ПараметрыРаспознавания.ОбработчикОстановкиАудиозаписи = ОбработчикОстановкиАудиозаписи;
	ПараметрыРаспознавания.ИспользоватьТолькоДополнительныеГрамматики =
		ТекущиеПараметры.ИспользоватьТолькоДополнительныеГрамматики;
	
	ПараметрыРаспознавания.ДополнительныеГрамматики.Добавить("ОбщаяГрамматика");
	ПараметрыРаспознавания.ДополнительныеГрамматики.Добавить("ПользовательскаяГрамматика");
	
	Возврат ПараметрыРаспознавания;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПослеПолученияРезультатаВыбораПараметровРаспознаванияРечи(Результат, Контекст) Экспорт
	
	Если Контекст = Неопределено Или Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст, Результат);
	
КонецПроцедуры

#КонецОбласти