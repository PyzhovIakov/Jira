// strict-types

#Область СлужебныйПрограммныйИнтерфейс

#Область УчетныеЗаписиОблачногоЭДО

Процедура ОткрытьСписокУчетныхЗаписейОблачногоЭДО() Экспорт
	
	ОткрытьФорму("Справочник.УчетныеЗаписиОблачногоЭДО.ФормаСписка");
	
КонецПроцедуры

// Параметры:
//  Оповещение - ОписаниеОповещения - Результат: Неопределено, Массив из см. СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  Владелец - Неопределено,ФормаКлиентскогоПриложения
//  Заголовок - Неопределено,Строка
Процедура ОткрытьВыборУчетныхЗаписейОблачногоЭДО(Оповещение, Владелец, Заголовок) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	
	Если ЗначениеЗаполнено(Заголовок) Тогда
		ПараметрыФормы.Вставить("Заголовок", Заголовок);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.УчетныеЗаписиОблачногоЭДО.Форма.ФормаВыбора",ПараметрыФормы,Владелец,,,,Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//  * НовоеПодключение - Булево
//  * УчетнаяЗапись - Неопределено,СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  * Логин - Строка
Функция НовыеПараметрыПодключенияОблачногоЭДО() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("НовоеПодключение", Истина);
	Параметры.Вставить("УчетнаяЗапись", Неопределено);
	Параметры.Вставить("Логин", "");
	Возврат Параметры;
КонецФункции

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
//  ПараметрыПодключения - см. НовыеПараметрыПодключенияОблачногоЭДО
Процедура ПодключитьОблачныйЭДО(ОповещениеОЗавершении = Неопределено, ПараметрыПодключения = Неопределено) Экспорт
	
	Если ПараметрыПодключения = Неопределено Тогда
		ПараметрыПодключения = НовыеПараметрыПодключенияОблачногоЭДО();
	КонецЕсли;
	
	Контекст = НовыйКонтекстПодключенияОблачногоЭДО();
	Если ОповещениеОЗавершении <> Неопределено Тогда
		Контекст.ОповещениеОЗавершении = ОповещениеОЗавершении;
	КонецЕсли;
	
	Контекст.НовоеПодключение = ПараметрыПодключения.НовоеПодключение;
	
	Оповещение = Новый ОписаниеОповещения("ПодключитьОблачныйЭДОПослеАвторизации",
		ИнтеграцияОблачногоЭДОКлиент, Контекст);
	
	АвторизоватьсяВОблачномЭДО(Оповещение, ПараметрыПодключения);
	
КонецПроцедуры

// Параметры:
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  ДополнительныеПараметры - Структура:
//  * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  * ИдентификаторОшибки - Строка
//  * ЗаголовокПроблемы - Строка
Процедура АвторизоватьсяВОблачномЭДОИзОбработчикаНеисправностей(КонтекстДиагностики, ДополнительныеПараметры) Экспорт
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"АвторизоватьсяВОблачномЭДОИзОбработчикаНеисправностейЗавершение",
		ИнтеграцияОблачногоЭДОКлиент, ДополнительныеПараметры);
	ПараметрыПодключения = НовыеПараметрыПодключенияОблачногоЭДО();
	ПараметрыПодключения.НовоеПодключение = Ложь;
	ПараметрыПодключения.УчетнаяЗапись = ДополнительныеПараметры.УчетнаяЗапись;
	АвторизоватьсяВОблачномЭДО(ОповещениеОЗавершении, ПараметрыПодключения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область АсинхронныеОперации

// Ожидает завершения асинхронной операции.
// 
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
//  АсинхронныеОперации   - Массив из см. ИнтеграцияОблачногоЭДО.НоваяАсинхроннаяОперация
Процедура ОжидатьЗавершенияАсинхронныхОпераций(ОповещениеОЗавершении, АсинхронныеОперации) Экспорт
	
	КонтекстОжидания = НовыйКонтекстОжиданияЗавершенияАсинхронныхОпераций();
	КонтекстОжидания.ОповещениеОЗавершении = ОповещениеОЗавершении;
	
	ПроверитьВыполнениеАсинхронныхОпераций(АсинхронныеОперации, КонтекстОжидания);
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - ОписаниеОповещения
//  * ЗавершенныеОперации - См. ОжидатьЗавершенияАсинхронныхОпераций.АсинхронныеОперации
Функция НовыйКонтекстОжиданияЗавершенияАсинхронныхОпераций()
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Контекст.Вставить("ЗавершенныеОперации", Новый Массив);
	Возврат Контекст;
КонецФункции

// Параметры:
//  АсинхронныеОперации - См. ОжидатьЗавершенияАсинхронныхОпераций.АсинхронныеОперации
//  КонтекстОжидания - См. НовыйКонтекстОжиданияЗавершенияАсинхронныхОпераций
Процедура ПроверитьВыполнениеАсинхронныхОпераций(АсинхронныеОперации, КонтекстОжидания)
	
	АдресаРесурсовДляПроверки = Новый Соответствие;
	ПовторитьПосле = 1;
	
	Если ЗначениеЗаполнено(АсинхронныеОперации) Тогда
		
		Для Каждого АсинхроннаяОперация Из АсинхронныеОперации Цикл
			
			Если АсинхроннаяОперация.Статус = "Выполняется" Тогда
				АдресаРесурсовДляПроверки.Вставить(АсинхроннаяОперация.ИдентификаторУчетнойЗаписи,
					АсинхроннаяОперация.АдресРесурса);
				ПовторитьПосле = Макс(ПовторитьПосле, АсинхроннаяОперация.ПовторитьПосле);
			Иначе
				КонтекстОжидания.ЗавершенныеОперации.Добавить(АсинхроннаяОперация);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АдресаРесурсовДляПроверки) Тогда
		ВыполнитьОбработкуОповещения(КонтекстОжидания.ОповещениеОЗавершении, КонтекстОжидания.ЗавершенныеОперации);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПроверитьВыполнениеАсинхронныхОперации", ЭтотОбъект, КонтекстОжидания);
	
	ОбщегоНазначенияБЭДКлиент.ВыполнитьОбработкуОповещенияЧерезИнтервал(ПовторитьПосле, Оповещение, АдресаРесурсовДляПроверки);
	
КонецПроцедуры

// Параметры:
//  АдресаРесурсовДляПроверки - Соответствие из КлючИЗначение:
//  * Ключ - ОпределяемыйТип.Организация
//  * Значение - Строка - адрес ресурса для проверки результата.
//  КонтекстОжидания - См. НовыйКонтекстОжиданияЗавершенияАсинхронныхОпераций
Процедура ПроверитьВыполнениеАсинхронныхОперации(АдресаРесурсовДляПроверки, КонтекстОжидания) Экспорт
	
	Если Не ЗначениеЗаполнено(АдресаРесурсовДляПроверки) Тогда
		ВыполнитьОбработкуОповещения(КонтекстОжидания.ОповещениеОЗавершении, КонтекстОжидания.ЗавершенныеОперации);
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ИнтеграцияОблачногоЭДОВызовСервера.ПроверитьВыполнениеАсинхронныхОперацииВФоне(
		АдресаРесурсовДляПроверки);
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатПроверкиАсинхронныхОпераций", ЭтотОбъект, КонтекстОжидания);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

// Ожидает завершения длительной операции.
// 
// Параметры:
//  ДлительнаяОперация    - См. ДлительныеОперации.ВыполнитьФункцию
//  КонтекстОжидания - См. НовыйКонтекстОжиданияЗавершенияАсинхронныхОпераций
Процедура ОбработатьРезультатПроверкиАсинхронныхОпераций(ДлительнаяОперация, КонтекстОжидания) Экспорт
	
	Если ДлительнаяОперация = Неопределено
		ИЛИ ДлительнаяОперация.Статус <> "Выполнено" Тогда
		ВыполнитьОбработкуОповещения(КонтекстОжидания.ОповещениеОЗавершении, ДлительнаяОперация);
		Возврат;
	КонецЕсли;
	
	АсинхронныеОперации = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата); // Массив из см. ИнтеграцияОблачногоЭДО.НоваяАсинхроннаяОперация
	
	ПроверитьВыполнениеАсинхронныхОпераций(АсинхронныеОперации, КонтекстОжидания);
	
КонецПроцедуры

// Параметры:
//  АсинхронныеОперации - Массив из см. ИнтеграцияОблачногоЭДО.НоваяАсинхроннаяОперация
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  * Значение - Строка - адрес значения во временном хранилище.
Функция АдресаРезультатовАсинхронныхОпераций(АсинхронныеОперации, КонтекстДиагностики) Экспорт
	
	АдресаРезультатов = Новый Соответствие; // См. АдресаРезультатовАсинхронныхОпераций
	
	Для Каждого АсинхроннаяОперация Из АсинхронныеОперации Цикл
		АдресРезультата = АдресРезультатаАсинхроннойОперации(АсинхроннаяОперация, КонтекстДиагностики);
		Если ЗначениеЗаполнено(АдресРезультата) Тогда
			АдресаРезультатов.Вставить(АсинхроннаяОперация.ИдентификаторУчетнойЗаписи, АдресРезультата);
		КонецЕсли;
	КонецЦикла;
	
	Возврат АдресаРезультатов;
	
КонецФункции

// Параметры:
//  АсинхроннаяОперация  - см. ИнтеграцияОблачногоЭДО.НоваяАсинхроннаяОперация
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//
// Возвращаемое значение:
//  Строка - адрес временного хранилища
Функция АдресРезультатаАсинхроннойОперации(АсинхроннаяОперация, КонтекстДиагностики)
		
	Если АсинхроннаяОперация = Неопределено Тогда
		Возврат "";
	ИначеЕсли АсинхроннаяОперация.Статус <> "Выполнено" Тогда
		ТекстОшибки = НСтр("ru = 'Не удалось выполнить асинхронную операцию облачного ЭДО.'");
		ПодробныйТекстОшибки = ТекстОшибки;
		Если ЗначениеЗаполнено(АсинхроннаяОперация.ПодробноеПредставлениеОшибки) Тогда
			ПодробныйТекстОшибки = ПодробныйТекстОшибки + Символы.ПС + АсинхроннаяОперация.ПодробноеПредставлениеОшибки;
		КонецЕсли;
		Если ЗначениеЗаполнено(АсинхроннаяОперация.КраткоеПредставлениеОшибки) Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + АсинхроннаяОперация.КраткоеПредставлениеОшибки;
		КонецЕсли;
		Ошибка = ОбработкаНеисправностейБЭДКлиент.НоваяОшибка(НСтр("ru = 'Выполнение операции облачного ЭДО.'"),
			ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка(), ПодробныйТекстОшибки, ТекстОшибки);
		ПодсистемыБЭД = ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД();
		ОбработкаНеисправностейБЭДКлиент.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
			ПодсистемыБЭД.ИнтеграцияОблачногоЭДО);
		Возврат "";
	КонецЕсли;
	
	Возврат АсинхроннаяОперация.АдресРезультата;
	
КонецФункции

#КонецОбласти

#Область УчетныеЗаписиОблачногоЭДО

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - ОписаниеОповещения
//  * НовоеПодключение - Булево
Функция НовыйКонтекстПодключенияОблачногоЭДО()
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Контекст.Вставить("НовоеПодключение", Ложь);
	Возврат Контекст;
	
КонецФункции

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
//  ПараметрыПодключения - см. НовыеПараметрыПодключенияОблачногоЭДО
Процедура АвторизоватьсяВОблачномЭДО(ОповещениеОЗавершении, ПараметрыПодключения)
	
	ОткрытьФорму("РегистрСведений.ПользователиОблачногоЭДО.Форма.ФормаАвторизации",
		ПараметрыПодключения,,,,,ОповещениеОЗавершении);
	
КонецПроцедуры

// Параметры:
//  ПараметрыПользователя - Неопределено
//                        - См. РегистрыСведений.ПользователиОблачногоЭДО.НовыеПараметрыПользователя
//  Контекст - см. НовыйКонтекстПодключенияОблачногоЭДО
Процедура ПодключитьОблачныйЭДОПослеАвторизации(ПараметрыПользователя, Контекст) Экспорт
	
	Если ПараметрыПользователя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПодключения = ИнтеграцияОблачногоЭДОВызовСервера.ПодключитьОблачныйЭДО(ПараметрыПользователя,
		Контекст.НовоеПодключение);
	
	Если Контекст.НовоеПодключение И ЗначениеЗаполнено(РезультатПодключения.УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(РезультатПодключения.УчетнаяЗапись);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РезультатПодключения.КлючПользователя) Тогда
		ДополнительныеПараметрыОповещения = Новый Структура;
		ДополнительныеПараметрыОповещения.Вставить("Пользователь", ПараметрыПользователя.Пользователь);
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(РезультатПодключения.КлючПользователя, 
			ДополнительныеПараметрыОповещения);
	КонецЕсли;
	
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(РезультатПодключения.КонтекстДиагностики);
	
	ВыполнитьОбработкуОповещения(Контекст.ОповещениеОЗавершении, РезультатПодключения);
	
КонецПроцедуры

// Параметры:
//  ПараметрыПользователя - Неопределено
//                        - См. РегистрыСведений.ПользователиОблачногоЭДО.НовыеПараметрыПользователя
//  ДополнительныеПараметры - см. АвторизоватьсяВОблачномЭДОИзОбработчикаНеисправностей.ДополнительныеПараметры
Процедура АвторизоватьсяВОблачномЭДОИзОбработчикаНеисправностейЗавершение(
	ПараметрыПользователя, ДополнительныеПараметры) Экспорт
	
	Если ПараметрыПользователя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КлючПользователя = ИнтеграцияОблачногоЭДОВызовСервера.ПодключитьПользователяОблачногоЭДО(
		ДополнительныеПараметры.УчетнаяЗапись, ПараметрыПользователя);
	
	Если КлючПользователя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметрыОповещения = Новый Структура;
	ДополнительныеПараметрыОповещения.Вставить("Пользователь", ПараметрыПользователя.Пользователь);
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(КлючПользователя, ДополнительныеПараметрыОповещения);
	
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = ДополнительныеПараметры.ИдентификаторОшибки;
	ВидОшибки.ЗаголовокПроблемы = ДополнительныеПараметры.ЗаголовокПроблемы;
	ВидыОшибок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидОшибки);
	ОбработкаНеисправностейБЭДКлиент.ОповеститьОбИсправленииОшибок(ВидыОшибок);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
