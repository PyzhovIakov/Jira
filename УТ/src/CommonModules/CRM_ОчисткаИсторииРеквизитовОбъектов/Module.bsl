
#Область СлужебныеПроцедурыИФункции

Процедура ОчиститьИсториюРеквизитовОбъектов() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.CRM_ОчисткаИсторииРеквизитовОбъектов);
	
	Если Не ПолучитьФункциональнуюОпцию("CRM_ОчищатьИсториюРеквизитовОбъектов") Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено() И Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПериодОчистки = Константы.CRM_ПериодХраненияИсторииРеквизитовОбъектов.Получить();
	Если ПериодОчистки = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СобытиеЖурналаРегистрации = НСтр("ru='Очистка истории реквизитов объектов'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru='Начата регламентная очистка истории реквизитов объектов'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ДатаОчистки = НачалоДня(ТекущаяДатаСеанса()) - ПериодОчистки * 86400;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	CRM_ИсторияРеквизитовОбъектов.Объект КАК Объект,
		|	CRM_ИсторияРеквизитовОбъектов.Реквизит КАК Реквизит,
		|	CRM_ИсторияРеквизитовОбъектов.КлючУникальности КАК КлючУникальности,
		|	CRM_ИсторияРеквизитовОбъектов.Период КАК Период
		|ИЗ
		|	РегистрСведений.CRM_ИсторияРеквизитовОбъектов КАК CRM_ИсторияРеквизитовОбъектов
		|ГДЕ
		|	CRM_ИсторияРеквизитовОбъектов.Период < &ДатаОчистки";
	
	Запрос.УстановитьПараметр("ДатаОчистки", ДатаОчистки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.CRM_ИсторияРеквизитовОбъектов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Прочитать();
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru='Закончена регламентная очистка истории реквизитов объектов'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

#КонецОбласти
