#Область ПрограммныйИнтерфейс

// Выполнить действие на сервере 1С:Аналитика с поставляемыми панелями.
// 
// Параметры:
//  Действие 		- Строка - Действие, которое необходимо выполнить: [Установить|Обновить].
//  МассивПанелей 	- Массив из Строка - Массив имен общих макетов, содержащих поставляемые панели.
//
Процедура ВыполнитьДействиеСПанелями(Действие = "", МассивПанелей = Неопределено)	Экспорт
	
	Если Действие = "Установить в 1С:Аналитика" Тогда
		УстановитьПанели(МассивПанелей);
	ИначеЕсли Действие = "Обновить в 1С:Аналитика" Тогда
		ОбновитьПанели(МассивПанелей);
	Иначе		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Поставляемые панели 1С:Аналитика'"),,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Действие ""%1"" не поддерживается в данной конфигурации!'"), Действие),
				БиблиотекаКартинок.ДиалогВосклицание);			
	КонецЕсли;
	
КонецПроцедуры

// Установить и обновить на сервере 1С:Аналитика поставляемые панели.
// 
// Параметры:
//  МассивПанелей 	- Массив из Строка - Массив имен общих макетов, содержащих поставляемые панели.
//
Процедура УстановитьИОбновитьПанели(МассивПанелей = Неопределено)	Экспорт

	ПанелиКУстановкеИОбновлению = 
		анлПоставляемыеПанелиВызовСервера.ПоставляемыеПанелиКонфигурацииКУстановкеИОбновлению(МассивПанелей);
	
	Если ПанелиКУстановкеИОбновлению["КУстановке"].Количество() = 0 
		И ПанелиКУстановкеИОбновлению["КОбновлению"].Количество() = 0 Тогда
		
		Если ТипЗнч(МассивПанелей) = Тип("Массив") И МассивПанелей.Количество() = 1 Тогда
			ПоказатьПредупреждение(,
				НСтр("ru = 'Выбранная панель уже установлена в 1С:Аналитика и не требует обновления.'"), 10, 
				"Поставляемые панели 1С:Аналитика");
			Иначе
			ПоказатьПредупреждение(,
				НСтр("ru = 'Отсутствуют поставляемые панели для установки или обновления в 1С:Аналитика.'"), 10, 
				"Поставляемые панели 1С:Аналитика");
		КонецЕсли;
		
	Иначе
	
		ПараметрыВопроса = Новый Структура();
		ПараметрыВопроса.Вставить("ПанелиКУстановкеИОбновлению", ПанелиКУстановкеИОбновлению);
	
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПриПолученииОтветаНаВопросОПодтвержденииДействия", ЭтотОбъект, ПараметрыВопроса);
		
		ЗаголовокВопроса = НСтр("ru = 'Поставляемые панели 1С:Аналитика'");
		Если ТипЗнч(МассивПанелей) = Тип("Массив") И МассивПанелей.Количество() = 1 Тогда
			ТекстВопроса = НСтр("ru = 'Выполнить установку и обновление в 1С:Аналитика поставляемой панели?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Выполнить установку и обновление в 1С:Аналитика всех неустановленных или требующих обновления поставляемых панелей?'");
		КонецЕсли;
	
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Загрузить и обновить в 1С:Аналитика'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки[0].Значение, ЗаголовокВопроса);
		
	КонецЕсли;
	
КонецПроцедуры

// Установить на сервере 1С:Аналитика поставляемые панели.
// 
// Параметры:
//  МассивПанелей 	- Массив из Строка - Массив имен общих макетов, содержащих поставляемые панели.
//
Процедура УстановитьПанели(МассивПанелей = Неопределено)	Экспорт

	Если НЕ анлПоставляемыеПанелиВызовСервера.СерверСистемыАналитикиДоступен() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Поставляемые панели 1С:Аналитика'"),,
			НСтр("ru = 'Сервер 1С:Аналитика не обнаружен.'"),
			БиблиотекаКартинок.ДиалогВосклицание);
		Возврат
	КонецЕсли;

	ПанелиКУстановкеИОбновлению = 
		анлПоставляемыеПанелиВызовСервера.ПоставляемыеПанелиКонфигурацииКУстановкеИОбновлению(МассивПанелей);
	
	Если ПанелиКУстановкеИОбновлению["КУстановке"].Количество() = 0 Тогда
		
		Если ТипЗнч(МассивПанелей) = Тип("Массив") И МассивПанелей.Количество() = 1 Тогда
			ПоказатьПредупреждение(,
				НСтр("ru = 'Выбранная панель уже установлена в 1С:Аналитика.'"), 10, 
				"Поставляемые панели 1С:Аналитика");
			Иначе
			ПоказатьПредупреждение(,
				НСтр("ru = 'Отсутствуют поставляемые панели для установки в 1С:Аналитика.'"), 10, 
				"Поставляемые панели 1С:Аналитика");
		КонецЕсли;
			
	Иначе
		
		ПараметрыВопроса = Новый Структура();
		ПараметрыВопроса.Вставить("ПанелиКУстановкеИОбновлению", ПанелиКУстановкеИОбновлению);
	
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПриПолученииОтветаНаВопросОПодтвержденииДействия", ЭтотОбъект, ПараметрыВопроса);
		
		ЗаголовокВопроса = НСтр("ru = 'Поставляемые панели 1С:Аналитика'");
		Если ТипЗнч(МассивПанелей) = Тип("Массив") И МассивПанелей.Количество() = 1 Тогда
			ТекстВопроса = НСтр("ru = 'Выполнить установку в 1С:Аналитика поставляемой панели?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Выполнить установку всех неустановленных в 1С:Аналитика поставляемых панелей?'");
		КонецЕсли;
	
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Установить в 1С:Аналитика'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки[0].Значение, ЗаголовокВопроса);
		
	КонецЕсли;
	
КонецПроцедуры

// Обновить на сервере 1С:Аналитика поставляемые панели.
// 
// Параметры:
//  МассивПанелей 	- Массив из Строка - Массив имен общих макетов, содержащих поставляемые панели.
//
Процедура ОбновитьПанели(МассивПанелей = Неопределено)	Экспорт

	Если НЕ анлПоставляемыеПанелиВызовСервера.СерверСистемыАналитикиДоступен() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Поставляемые панели 1С:Аналитика'"),,
			НСтр("ru = 'Сервер 1С:Аналитика не обнаружен.'"),
			БиблиотекаКартинок.ДиалогВосклицание);
		Возврат
	КонецЕсли;
	
	ПанелиКУстановкеИОбновлению = 
		анлПоставляемыеПанелиВызовСервера.ПоставляемыеПанелиКонфигурацииКУстановкеИОбновлению(МассивПанелей);
	
	Если ПанелиКУстановкеИОбновлению["КОбновлению"].Количество() = 0 Тогда
		
		Если ТипЗнч(МассивПанелей) = Тип("Массив") И МассивПанелей.Количество() = 1 Тогда
			ПоказатьПредупреждение(,
				НСтр("ru = 'Выбранная панель не требует обновления в 1С:Аналитика.'"), 10, 
				"Поставляемые панели 1С:Аналитика");
			Иначе
			ПоказатьПредупреждение(,
				НСтр("ru = 'Отсутствуют поставляемые панели для обновления в 1С:Аналитика.'"), 10, 
				"Поставляемые панели 1С:Аналитика");
		КонецЕсли;
			
	Иначе	
		
		ПараметрыВопроса = Новый Структура();
		ПараметрыВопроса.Вставить("ПанелиКУстановкеИОбновлению", ПанелиКУстановкеИОбновлению);
	
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПриПолученииОтветаНаВопросОПодтвержденииДействия", ЭтотОбъект, ПараметрыВопроса);
		
		ЗаголовокВопроса = НСтр("ru = 'Поставляемые панели 1С:Аналитика'");
		Если ТипЗнч(МассивПанелей) = Тип("Массив") И МассивПанелей.Количество() = 1 Тогда
			ТекстВопроса = НСтр("ru = 'Выполнить обновление в 1С:Аналитика поставляемой панели?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Выполнить обновление всех требующих обновления установленных в 1С:Аналитика поставляемых панелей?'");
		КонецЕсли;
	
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Обновить в 1С:Аналитика'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки[0].Значение, ЗаголовокВопроса);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Продолжение выполнения действий с поставляемыми панелями 
// на севере 1С:Аналитика после подтверрждения пользоватем.
// 
// Параметры:
//  Ответ - КодВозвратаДиалога - Ответ пользователя.
//  ДополнительныеПараметры 		- Структура - Дополнительные параметры:
//   * ПанелиКУстановкеИОбновлению - Структура - Макеты к установке и обновлению.
//
Процедура ПриПолученииОтветаНаВопросОПодтвержденииДействия(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьУстановкуИОбновлениеПанелей(ДополнительныеПараметры["ПанелиКУстановкеИОбновлению"]);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполнить установку и обновление панелей на сервере 1С:Аналитика.
// 
// Параметры:
//  ПанелиКУстановкеИОбновлению - Структура - Макеты к установке и обновлению.
//
Процедура ВыполнитьУстановкуИОбновлениеПанелей(ПанелиКУстановкеИОбновлению)

	УстановленныеПанели = Новый Массив;
	ОбновленныеПанели = Новый Массив;
	Ошибки = Неопределено;
	
	КУстановке = "КУстановке";
	КОбновлению = "КОбновлению";
		
	Если ПанелиКУстановкеИОбновлению[КУстановке].Количество() > 0 Тогда
		
		УстановленныеПанели = анлПоставляемыеПанелиВызовСервера.УстановитьПоставляемыеПанели(
			ПанелиКУстановкеИОбновлению[КУстановке], Ошибки);
			
	ИначеЕсли ПанелиКУстановкеИОбновлению[КОбновлению].Количество() > 0 Тогда
		
		ОбновленныеПанели = анлПоставляемыеПанелиВызовСервера.ОбновитьПоставляемыеПанели(
			ПанелиКУстановкеИОбновлению[КОбновлению], Ошибки);
			
	КонецЕсли;
	
	Отказ = Ложь;
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
	Если УстановленныеПанели.Количество() > 0
		ИЛИ ОбновленныеПанели.Количество() > 0 Тогда
		Оповестить("ОбновленСоставПанелейВАналитике");

		ПоказатьПредупреждение(,НСтр("ru = 'В состав панелей 1С:Аналитика включены поставляемые панели!
			|Для просмотра изменений, откройте 1С:Аналитика.'"), 10, "Поставляемые панели 1С:Аналитика");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
