
#Область ПрограммныйИнтерфейс

// Возвращает конструктор параметров добавления штампов в документ.
//
// Возвращаемое значение:
//  Структура:
//    * ПараметрыДокумента  - см. НовыеПараметрыФайла.
//    * ПараметрыШтампов    - см. НовыеПараметрыШтампов.
//    * ПараметрыРезультата - см. НовыеПараметрыПолученияРезультата.
//
Функция НовыеПараметрыШтампированияФайлаДокумента() Экспорт
	
	ПараметрыДобавления = Новый Структура;
	ПараметрыДобавления.Вставить("ПараметрыДокумента" , НовыеПараметрыФайла());
	ПараметрыДобавления.Вставить("ПараметрыШтампов"   , НовыеПараметрыШтампов());
	ПараметрыДобавления.Вставить("ПараметрыРезультата", НовыеПараметрыПолученияРезультата());
	
	Возврат ПараметрыДобавления;
	
КонецФункции

// Возвращает конструктор параметров картинки штампа.
//
// Возвращаемое значение:
//  Структура:
//    * ПараметрыШтампа     - см. НовыеПараметрыФайла.
//    * ПараметрыРазмещения - см. НовыеПараметрыРазмещения.
//    * ПравилаРасположения - см. НовыеПравилаРасположения.
//    * Настройки           - см. НовыеНастройкиШтампа.
//
Функция НовыеПараметрыВставкиШтампаПоПозиции() Экспорт
	
	ПараметрыШтампа = Новый Структура;
	
	ПараметрыШтампа.Вставить("ПараметрыШтампа"    , НовыеПараметрыФайла());
	ПараметрыШтампа.Вставить("ПараметрыРазмещения", НовыеПараметрыРазмещения());
	ПараметрыШтампа.Вставить("ПравилаРасположения", НовыеПравилаРасположения());
	ПараметрыШтампа.Вставить("Настройки"          , НовыеНастройкиШтампа());
	
	Возврат ПараметрыШтампа;
	
КонецФункции

// Возвращает конструктор параметров картинки штампа.
//
// Возвращаемое значение:
//  Структура:
//   * ЗначениеТега         - Строка - Текст, по позиции которого будет вставлена картинка штампа.
//   * ЗамещатьЗначениеТега - Булево - Признак замещения тега картинкой штампа.
//   * Штампы               - Массив из см. НовыеПараметрыВставкиШтампаПоТегам.
//
Функция НовыеПараметрыРазмещенияШтамповПоТегам() Экспорт
	
	ПараметрыШтампов = Новый Структура;
	ПараметрыШтампов.Вставить("ЗначениеТега"         , "");
	ПараметрыШтампов.Вставить("ЗамещатьЗначениеТега" , Ложь);
	ПараметрыШтампов.Вставить("Штампы"               , Новый Массив);
	
	Возврат ПараметрыШтампов;
	
КонецФункции

// Возвращает конструктор параметров картинки штампа.
//
// Возвращаемое значение:
//  Структура: 
//   * ПараметрыШтампа      - см. НовыеПараметрыФайла.
//   * ВариантВставкиШтампа - Строка    - Вариант вставки картинки штампа в текстовое содержимое документа.
//          Значение по умолчанию: "НадТекстом".
//   * Настройки            - см. НовыеНастройкиШтампа.
//
Функция НовыеПараметрыВставкиШтампаПоТегам() Экспорт
	
	ПараметрыШтампа = Новый Структура;
	
	ПараметрыШтампа.Вставить("ПараметрыШтампа"     , НовыеПараметрыФайла());
	ПараметрыШтампа.Вставить("ВариантВставкиШтампа", ВариантВставкиШтампаНадТекстом());
	ПараметрыШтампа.Вставить("Настройки"           , НовыеНастройкиШтампа());
	
	Возврат ПараметрыШтампа;
	
КонецФункции

// Возвращает конструктор параметров файла.
//
// Возвращаемое значение:
//  Структура: 
//    * ДвоичныеДанные - ДвоичныеДанные - Двоичные данные файла.
//                     - Неопределено   - Значение не инициализировано.
//    * Расширение     - Строка         - Значение расширения файла.
//
Функция НовыеПараметрыФайла() Экспорт
	
	ПараметрыФайла = Новый Структура;
	
	ПараметрыФайла.Вставить("ДвоичныеДанные" , Неопределено);
	ПараметрыФайла.Вставить("Расширение"     , "");
	
	Возврат ПараметрыФайла;
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать Интеграция1СШтампКлиентСервер.НовыеПараметрыШтампированияФайлаДокумента.
// Возвращает конструктор параметров добавления штампов в документ.
//
// Возвращаемое значение:
//  Структура:
//    * ПараметрыДокумента - Структура - Параметры документа:
//       ** ДвоичныеДанные - ДвоичныеДанные - Двоичные данные документа.
//       ** Расширение     - Строка         - Значение расширения файла.
//    * ПараметрыШтампов   - Массив из Структура:
//       ** ПараметрыШтампа - Структура - Параметры картинки штампа:
//           *** ДвоичныеДанные - ДвоичныеДанные - Двоичные данные картинки штампа.
//           *** Расширение     - Строка         - Значение расширения файла картинки штампа.
//       ** ПараметрыРазмещения - Структура - Параметры размещения картинки штампа на странице документа:
//            *** Координаты - Структура - Данные координат размещения в пикселях:
//                  **** Верх - Число - Значение отступа в пикселях от верхней границы страницы документа.
//                  **** Лево - Число - Значение отступа в пикселях от левой границы страницы документа.
//            *** Расположение - Строка - Значение расположения картинки штампа на странице документа.
//                см. #Область Словарь, Пример: ВариантРасположенияВнизуПоЦентру.
//                Значение по умолчанию: ВариантРасположенияВнизуПоЦентру.
//       ** ПравилаРасположения - Структура - Правила расположения картинки штампа на страницах документа:
//            *** ПравилоРасположения - Строка - Значение правила размещения картинки штампа на страницах документа.
//                см. #Область Словарь, Пример: ПравилоРасположенияНаКаждойСтранице
//                Значение по умолчанию: НаКаждойСтранице.
//            *** НомерСтраницы - Число - Номер страницы документа для расположения картинки штампа.
//                Значение по умолчанию: 1.
//       ** ОтступОтГраниц - Число - Отступ от границ страницы документа в пикселях при установке картинки штампа.
//       ** КоэффициентИзмененияРазмера - Число - Коэффициент изменения размера картинки штампа.
//       ** Настройки - Структура - Настройки картинки штампа на странице документа:
//            *** УровеньПрозрачности - Число - Значение уровня прозрачности от 0 до 255, где 0 - 100% значение прозрачности.
//
Функция НовыеПараметрыДобавленияШтамповВДокумент() Экспорт
	
	ПараметрыФайла = Новый Структура;
	
	ПараметрыФайла.Вставить("ДвоичныеДанные" , Неопределено);
	ПараметрыФайла.Вставить("Расширение"     , "");
	
	ПараметрыДобавления = Новый Структура;
	ПараметрыДобавления.Вставить("ПараметрыДокумента", ПараметрыФайла);
	ПараметрыДобавления.Вставить("ПараметрыШтампов"  , Новый Массив);
	
	Возврат ПараметрыДобавления;
	
КонецФункции

// Устарела. Следует использовать Интеграция1СШтампКлиентСервер.НовыеПараметрыВставкиШтампаПоПозиции.
// Возвращает конструктор параметров картинки штампа, вставляемого по позиции в документе.
//
// Возвращаемое значение:
//  Структура: 
//    * ПараметрыШтампа  - Структура - Параметры картинки штампа:
//       ** ДвоичныеДанные - ДвоичныеДанные - Двоичные данные картинки штампа.
//       ** Расширение     - Строка         - Значение расширения файла картинки штампа.
//    * ПараметрыРазмещения - Структура - Параметры размещения картинки штампа на странице документа:
//        ** Координаты   - Структура - Данные координат размещения в пикселях:
//              *** Верх - Число - Значение отступа в пикселях от верхней границы страницы документа.
//              *** Лево - Число - Значение отступа в пикселях от левой границы страницы документа.
//        ** Расположение - Строка    - Значение расположения картинки штампа на странице документа.
//               см. #Область Словарь, Пример: ВариантРасположенияВнизуПоЦентру
//               Значение по умолчанию: ВариантРасположенияВнизуПоЦентру
//    * ПравилаРасположения - Структура - Правила расположения картинки штампа на страницах документа:
//        *** ПравилоРасположения - Строка - Значение правила размещения картинки штампа на страницах документа.
//                см. #Область Словарь, Пример: ПравилоРасположенияНаКаждойСтранице
//                Значение по умолчанию: НаКаждойСтранице.
//        *** НомерСтраницы - Число - Номер страницы документа для расположения картинки штампа.
//                Значение по умолчанию: 1.
//    * ОтступОтГраниц - Число - Отступ от границ страницы документа в пикселях при установке картинки штампа.
//    * КоэффициентИзмененияРазмера - Число - Коэффициент изменения размера картинки штампа.
//    * Настройки - Структура - Настройки картинки штампа на странице документа:
//       ** УровеньПрозрачности - Число - Значение уровня прозрачности от 0 до 255, где 0 - 100% значение прозрачности.
//
Функция НовыеПараметрыШтампа() Экспорт
	
	ПараметрыШтампа = Новый Структура;
	
	ПараметрыФайла = Новый Структура;
	
	ПараметрыФайла.Вставить("ДвоичныеДанные" , Неопределено);
	ПараметрыФайла.Вставить("Расширение"     , "");
	
	ПравилаРасположения = Новый Структура;
	
	ПравилаРасположения.Вставить("ПравилоРасположения" , ПравилоРасположенияНаКаждойСтранице());
	ПравилаРасположения.Вставить("НомерСтраницы"       , 1);
	
	ПараметрыРазмещения = Новый Структура;
	
	ПараметрыРазмещения.Вставить("Координаты"   , Новый Структура("Верх, Лево",0,0));
	ПараметрыРазмещения.Вставить("Расположение" , ВариантРасположенияВнизуПоЦентру());
	
	НастройкиШтампа = Новый Структура;
	НастройкиШтампа.Вставить("УровеньПрозрачности", 255);
	
	ПараметрыШтампа.Вставить("ПараметрыШтампа"             , ПараметрыФайла);
	ПараметрыШтампа.Вставить("ПараметрыРазмещения"         , ПараметрыРазмещения);
	ПараметрыШтампа.Вставить("ПравилаРасположения"         , ПравилаРасположения);
	ПараметрыШтампа.Вставить("ОтступОтГраниц"              , 0);
	ПараметрыШтампа.Вставить("КоэффициентИзмененияРазмера" , 0);
	ПараметрыШтампа.Вставить("Настройки"                   , НастройкиШтампа);
	
	Возврат ПараметрыШтампа;
	
КонецФункции

#КонецОбласти

#Область ЗначенияКонстантПоУмолчанию

// Возвращает значение отступа в пикселях по умолчанию.
//
// Возвращаемое значение:
//   Число - значение отступа в пикселях. Значение по умолчанию - 10 пикселей
//
Функция ЗначениеОтступаОтКраевСтраницыПоУмолчанию() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ЗначениеОтступаОтКраевСтраницыПоУмолчанию();
	
КонецФункции

// Возвращает коэффициент прозрачности картинки штампа по умолчанию.
// Градация следующая: при значении 0 - 100% прозрачность, при значении 255 - 0% прозрачность.
//
// Возвращаемое значение:
//   Число - значение коэффициента прозрачности от 0 до 255. Значение по умолчанию - 128.
//
Функция ЗначениеКоэффициентаПрозрачностиШтампаПоУмолчанию() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ЗначениеКоэффициентаПрозрачностиШтампаПоУмолчанию();
	
КонецФункции

// Возвращает коэффициент прозрачности картинки штампа, если прозрачность не используется.
// Градация следующая: при значении 0 - 100% прозрачность, при значении 255 - 0% прозрачность.
//
// Возвращаемое значение:
//   Число - значение коэффициента 255.
//
Функция ЗначениеКоэффициентаПрозрачностиШтампаНепрозрачный() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ЗначениеКоэффициентаПрозрачностиШтампаНепрозрачный();
	
КонецФункции

// Возвращает значение прозрачности картинки штампа, когда прозрачность используется.
//
// Возвращаемое значение:
//   Строка - значение прозрачности "Прозрачный".
//
Функция ЗначениеПрозрачностиПрозрачный() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ЗначениеПрозрачностиПрозрачный();
	
КонецФункции

#КонецОбласти

#Область Словарь

// Возвращает вариант расположения штампа в верхнем левом углу.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ВариантРасположенияВверхуСлева() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ВариантРасположенияВверхуСлева();
	
КонецФункции

// Возвращает вариант расположения штампа в верхнем правом углу.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ВариантРасположенияВверхуСправа() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ВариантРасположенияВверхуСправа();
	
КонецФункции

// Возвращает вариант расположения штампа вверху по центру страницы.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ВариантРасположенияВверхуПоЦентру() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ВариантРасположенияВверхуПоЦентру();
	
КонецФункции

// Возвращает вариант расположения штампа в нижнем левом углу.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ВариантРасположенияВнизуСлева() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ВариантРасположенияВнизуСлева();
	
КонецФункции

// Возвращает вариант расположения штампа в нижнем правом углу.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ВариантРасположенияВнизуСправа() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ВариантРасположенияВнизуСправа();
	
КонецФункции

// Возвращает вариант расположения штампа внизу по центру страницы.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ВариантРасположенияВнизуПоЦентру() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ВариантРасположенияВнизуПоЦентру();
	
КонецФункции

// Возвращает вариант расположения штампа произвольно по координатам.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ВариантРасположенияПоКоординатам() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ВариантРасположенияПоКоординатам();
	
КонецФункции

// Возвращает вариант расположения штампа по тегам.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ВариантРасположенияПоТегам() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ВариантРасположенияПоТегам();
	
КонецФункции

// Возвращает правило расположения штампа на каждой странице.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ПравилоРасположенияНаКаждойСтранице() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ПравилоРасположенияНаКаждойСтранице();
	
КонецФункции

// Возвращает правило расположения штампа на первой странице.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ПравилоРасположенияНаПервойСтранице() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ПравилоРасположенияНаПервойСтранице();
	
КонецФункции

// Возвращает правило расположения штампа на последней странице.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ПравилоРасположенияНаПоследнейСтранице() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ПравилоРасположенияНаПоследнейСтранице();
	
КонецФункции

// Возвращает правило расположения штампа на каждой странице кроме последней.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ПравилоРасположенияНаКаждойСтраницеКромеПоследней() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ПравилоРасположенияНаКаждойСтраницеКромеПоследней();
	
КонецФункции

// Возвращает правило расположения штампа на конкретной странице.
//
// Возвращаемое значение:
//   Строка - значение расположения.
//
Функция ПравилоРасположенияНаКонкретнойСтранице() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ПравилоРасположенияНаКонкретнойСтранице();
	
КонецФункции

// Возвращает вариант вставки штампа - раздвигая текст со всех сторон.
//
// Возвращаемое значение:
//   Строка - значение вставки.
//
Функция ВариантВставкиШтампаРаздвигаяТекст() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ВариантВставкиШтампаРаздвигаяТекст();
	
КонецФункции

// Возвращает вариант вставки штампа - над текстом.
//
// Возвращаемое значение:
//   Строка - значение вставки.
//
Функция ВариантВставкиШтампаНадТекстом() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ВариантВставкиШтампаНадТекстом();
	
КонецФункции

#КонецОбласти

// Возвращает массив допустимых расширений файлов
//
// Возвращаемое значение:
//  Массив Из Строка - Массив строк расширений.
//
Функция ДопустимыеРасширенияФайловШтампов() Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.ДопустимыеРасширенияФайловШтампов();
	
КонецФункции

// См. Интеграция1СШтампСлужебныйКлиентСервер.РасширениеОсновногоФайлаПоддерживается
//
Функция РасширениеОсновногоФайлаПоддерживается(Расширение) Экспорт
	
	Возврат Интеграция1СШтампСлужебныйКлиентСервер.РасширениеОсновногоФайлаПоддерживается(Расширение);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает конструктор параметров добавляемых картинок штампов.
//
// Возвращаемое значение:
//  Структура:
//    * ПоПозиционированию - Массив из см. НовыеПараметрыВставкиШтампаПоПозиции.
//    * ПоТегам            - Массив из см. НовыеПараметрыРазмещенияШтамповПоТегам.
//
Функция НовыеПараметрыШтампов()
	
	ПараметрыШтампов = Новый Структура;
	ПараметрыШтампов.Вставить("ПоПозиционированию" , Новый Массив);
	ПараметрыШтампов.Вставить("ПоТегам"            , Новый Массив);
	
	Возврат ПараметрыШтампов;
	
КонецФункции

// Возвращает конструктор параметров получения результата.
//
// Возвращаемое значение:
//  Структура:
//    * ФорматРезультата - Строка - Расширение файла, в котором необходимо получить результат.
//          Значение по умолчанию: "pdf".
//       Список форматов см. Интеграция1СШтампСлужебныйКлиентСервер.ПоддерживаемыеРасширенияФайловДляШтампирования
//
Функция НовыеПараметрыПолученияРезультата()
	
	ПараметрыРезультата = Новый Структура;
	ПараметрыРезультата.Вставить("ФорматРезультата" , "pdf");
	
	Возврат ПараметрыРезультата;
	
КонецФункции

// Возвращает конструктор параметров размещения картинки штампа на странице документа.
//
// Возвращаемое значение:
//  Структура:
//    * Координаты     - Структура - Данные координат размещения в пикселях:
//      ** Верх - Число - Значение отступа в пикселях от верхней границы страницы документа.
//      ** Лево - Число - Значение отступа в пикселях от левой границы страницы документа.
//    * Расположение   - Строка    - Значение расположения картинки штампа на странице документа.
//          Значение по умолчанию: ВариантРасположенияВнизуПоЦентру.
//    * ОтступОтГраниц - Число    - Отступ от границ страницы документа в пикселях при установке картинки штампа.
//          Значение по умолчанию: 10.
//
Функция НовыеПараметрыРазмещения()
	
	ПараметрыРазмещения = Новый Структура;
	ПараметрыРазмещения.Вставить("Координаты"    , Новый Структура("Верх, Лево",0,0));
	ПараметрыРазмещения.Вставить("Расположение"  , ВариантРасположенияВнизуПоЦентру());
	ПараметрыРазмещения.Вставить("ОтступОтГраниц", 10);
	
	Возврат ПараметрыРазмещения;
	
КонецФункции

// Возвращает конструктор правил расположения картинки штампа на страницах документа.
//
// Возвращаемое значение:
//  Структура:
//    * ПравилоРасположения - Строка - Значение правила размещения картинки штампа на страницах документа.
//          Значение по умолчанию: НаКаждойСтранице.
//    * НомерСтраницы       - Число - Номер страницы документа для расположения картинки штампа.
//          Используется для правила размещения: НаКонкретнойСтранице.
//          Значение по умолчанию: 1.
//
Функция НовыеПравилаРасположения()
	
	ПравилаРасположения = Новый Структура;
	
	ПравилаРасположения.Вставить("ПравилоРасположения" , ПравилоРасположенияНаКаждойСтранице());
	ПравилаРасположения.Вставить("НомерСтраницы"       , 1);
	
	Возврат ПравилаРасположения;
	
КонецФункции

// Возвращает конструктор настройки картинки штампа на странице документа.
//
// Возвращаемое значение:
//  Структура:
//   * УровеньПрозрачности         - Число - Значение уровня прозрачности от 0 до 255, где 0 - 100% значение прозрачности.
//   * КоэффициентИзмененияРазмера - Число - Коэффициент изменения размера картинки штампа.
//
Функция НовыеНастройкиШтампа()
	
	НастройкиШтампа = Новый Структура;
	НастройкиШтампа.Вставить("УровеньПрозрачности"        , 255);
	НастройкиШтампа.Вставить("КоэффициентИзмененияРазмера", 1);
	
	Возврат НастройкиШтампа;
	
КонецФункции

#КонецОбласти
