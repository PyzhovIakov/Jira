
////////////////////////////////////////////////////////////////////////////////////////////////
// КЛИЕНТСКИЕ ПРОЦЕДУРЫ И ФУНКЦИИ ПОДСИСТЕМЫ "УПРАВЛЕНИЕ ПРОЕКТАМИ"

#Область ПрограммныйИнтерфейс

// Создает шаблон проекта на основании проекта.
//
// Параметры:
//	Проект	- СправочникСсылка	- Проект, на основании которого создаем шаблон проекта.
//
Процедура СоздатьШаблонНаОснованииПроекта(Проект) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьШаблонНаОснованииПроектаПродолжение", ЭтотОбъект, Проект);
	ОткрытьФорму("Справочник.CRM_ШаблоныПроектов.Форма.ФормаВыбораПараметровСоздания", , , , , ,
		 ОписаниеОповещения,
		 РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

// Продолжает создавать шаблон проекта на основании проекта.
//
// Параметры:
//	Параметры - Структура - структура параметров.
//	Проект	- СправочникСсылка	- проект, на основании которого создаем шаблон проекта.
//
Процедура СоздатьШаблонНаОснованииПроектаПродолжение(Параметры, Проект) Экспорт

	Если Параметры <> Неопределено Тогда
		НовыйШаблон = CRM_УправлениеПроектамиВызовСервера.СоздатьШаблонНаОснованииПроекта(Проект, Параметры);
		Если ЗначениеЗаполнено(НовыйШаблон) Тогда
			ПоказатьЗначение(, НовыйШаблон);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
