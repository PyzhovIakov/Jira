////////////////////////////////////////////////////////////////////////////////
// Клиентские процедуры и функции для работы с виджетами.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Универсальная процедура для открытия выбора варианта виджета из форм.
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения - Форма, из которой происходит открытие и созданы виджеты.
//
Процедура ОткрытьВыборВариантаВиджета(Форма) Экспорт
	
	ИсключаемыеВариантыВиджетов = Новый Массив;
	
	Для Каждого ТекущиеДанныеВариантаВиджета Из Форма.ВариантыВиджетовФормы Цикл
		Если Не ТекущиеДанныеВариантаВиджета.Скрыт Тогда
			ИсключаемыеВариантыВиджетов.Добавить(ТекущиеДанныеВариантаВиджета.ВариантВиджета);
		КонецЕсли;
	КонецЦикла;
	
	// --------------
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьВиджетЗавершение", Форма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВариантРаботыВиджетов", Форма.ВариантРаботыВиджетов);
	ПараметрыФормы.Вставить("ИсключаемыеВариантыВиджетов", ИсключаемыеВариантыВиджетов);
	
	ОткрытьФорму(
		"Справочник.CRM_ВариантыВиджетов.ФормаВыбора",
		ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // ОткрытьВыборВиджета()

// Универсальная процедура для открытия расшифровок по виджетам.
//
// Параметры:
//  Форма          - ФормаКлиентскогоПриложения - Форма, из которой происходит открытие и созданы виджеты.
//  ДанныеВиджета  - ДанныеФормыКоллекция       - Данные виджета по которому будут открываться расшифровка.
//
Процедура ОткрытьРасшифровкуВиджета(Форма, ДанныеВиджета) Экспорт
	
	CRM_ЦентрМониторингаКлиент.ЗаписатьОперациюБизнесСтатистики(
		"CRM_Статистика.Показатели.Виджеты.Удобства.КоличествоНажатийГиперссылкаПодробнее");
	
	Если ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.КлючевойПоказатель") Тогда
		
		ЗначениеАналитики = CRM_РаботаСВиджетамиКлиентСервер.ПолучитьАктуальноеЗначениеАналитики(ДанныеВиджета.Настройки);
		
		Если ДанныеВиджета.Переопределяемый Тогда
			
			Если ТипЗнч(ЗначениеАналитики) = Тип("СправочникСсылка.Пользователи") Тогда
				МассивПользователей = Новый Массив;
				МассивПользователей.Добавить(ЗначениеАналитики);
			Иначе
				МассивПользователей = CRM_УправлениеЦелевымиПоказателямиСервер.ДоступныеПользователи(Истина);
			КонецЕсли;
			
			Если ДанныеВиджета.Виджет = ПредопределенноеЗначение("Справочник.CRM_Виджеты.ВыполнениеПланаПродаж")
					Или ДанныеВиджета.Виджет = ПредопределенноеЗначение("Справочник.CRM_Виджеты.ВыполнениеПланаПродажКол") Тогда
				
				УсловияОтбора = Новый Структура;
				УсловияОтбора.Вставить("Согласовано",			Истина);
				УсловияОтбора.Вставить("ПериодОтчета",			Неопределено); // TODO: посмотреть еще раз.
				УсловияОтбора.Вставить("ГруппировкаПериода",	ПредопределенноеЗначение("Перечисление.Периодичность.ПустаяСсылка"));
				
				Если ТипЗнч(ЗначениеАналитики) = Тип("СправочникСсылка.Пользователи") Тогда
					УсловияОтбора.Вставить("Менеджер", ЗначениеАналитики);
				ИначеЕсли ТипЗнч(ЗначениеАналитики) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
					УсловияОтбора.Вставить("Подразделение", ЗначениеАналитики);
				КонецЕсли;
				
				Если ДанныеВиджета.Настройки.Свойство("ПланПродаж") Тогда
					УсловияОтбора.Вставить("ПланПродаж", ДанныеВиджета.Настройки.ПланПродаж);
				КонецЕсли;
				
				// --------------
				
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("Отбор",					УсловияОтбора);
				ПараметрыФормы.Вставить("СформироватьПриОткрытии",	Истина);
				
				ОткрытьФорму("Отчет.CRM_АнализПлановПродаж.Форма", ПараметрыФормы);
				
			КонецЕсли;
			
		Иначе
			
			Показатель = CRM_ОбщегоНазначенияСервер.ЗначениеРеквизитаОбъекта(
				ДанныеВиджета.Виджет, "КлючевойПоказатель");
			
			CRM_УправлениеЦелевымиПоказателямиКлиент.ОткрытьРасшифровкуПоказателя(
				Показатель, ЗначениеАналитики, , Истина);
			
		КонецЕсли;
		
	ИначеЕсли ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.ВоронкаПродаж") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВариантВиджета", ДанныеВиджета.ВариантВиджета);
		ПараметрыФормы.Вставить("НастройкиВариантаВиджета", ДанныеВиджета.Настройки);
		ПараметрыФормы.Вставить("ДействиеСВариантомВиджета", "Просмотр");
		
		ОткрытьФорму(
			"Отчет.CRM_ВоронкаПродаж.Форма.Форма",
			ПараметрыФормы,
			Форма.ЭтотОбъект,
			Форма.КлючУникальности);
		
	ИначеЕсли ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.ДинамикаПродаж") Тогда
		
		// +CRM_Модуль
		//Отборы = Новый Структура;
		//Отборы.Вставить("ПериодОтчета",			ДанныеВиджета.Настройки.СтандартныйПериод);
		//Отборы.Вставить("ГруппировкаПериода",	ДанныеВиджета.Настройки.Периодичность);
		//
		//ПоказатьРасшифровкуОтчета(Форма, ДанныеВиджета, "Отчет.CRM_АнализПродажПланФакт.Форма", Отборы);
		ДанныеВиджетаСтруктура = Новый Структура;
		ДанныеВиджетаСтруктура.Вставить("ТипВиджета", ДанныеВиджета.ТипВиджета);
		ДанныеВиджетаСтруктура.Вставить("Настройки", ДанныеВиджета.Настройки);
		
		НастройкиКомпоновщика = CRM_РаботаСВиджетамиВызовСервера.ПолучитьНастройкиКомпоновщикаДанныхДляВариантаВиджета(ДанныеВиджетаСтруктура, Истина);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ДанныеВиджета", ДанныеВиджетаСтруктура);
		
		ОткрытьФорму("Отчет.ВыручкаИСебестоимостьПродаж.Форма", ПараметрыФормы, Форма, Форма.КлючУникальности);
		// -CRM_Модуль
		
	ИначеЕсли ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.ДебиторскаяЗадолженность") Тогда
		
		// +CRM_Модуль
		//Отборы = Новый Структура;
		//Отборы.Вставить("Период",				ДанныеВиджета.Настройки.Период.Дата);
		//Отборы.Вставить("ГруппировкаПериода",	ДанныеВиджета.Настройки.Периодичность);
		//
		//ПоказатьРасшифровкуОтчета(Форма, ДанныеВиджета, "Отчет.CRM_ДебиторскаяЗадолженностьПоСрокамДолга.Форма", Отборы);
		ДанныеВиджетаСтруктура = Новый Структура;
		ДанныеВиджетаСтруктура.Вставить("ТипВиджета", ДанныеВиджета.ТипВиджета);
		ДанныеВиджетаСтруктура.Вставить("Настройки", ДанныеВиджета.Настройки);
		
		НастройкиКомпоновщика = CRM_РаботаСВиджетамиВызовСервера.ПолучитьНастройкиКомпоновщикаДанныхДляВариантаВиджета(ДанныеВиджетаСтруктура, Истина);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ДанныеВиджета", ДанныеВиджетаСтруктура);
		
		ОткрытьФорму("Отчет.ЗадолженностьКлиентовПоСрокам.Форма", ПараметрыФормы, Форма, Форма.КлючУникальности);
		// -CRM_Модуль
		
	ИначеЕсли ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.ДинамикаСтатусовКлиентов") Тогда
		
		Отборы = Новый Структура;
		Отборы.Вставить("ПериодОтчета",		ДанныеВиджета.Настройки.СтандартныйПериод);
		Отборы.Вставить("Периодичность",	ДанныеВиджета.Настройки.Периодичность);
		
		ПоказатьРасшифровкуОтчета(Форма, ДанныеВиджета, "Отчет.CRM_ДинамикаСтатусовКлиентов.Форма", Отборы);
		
	ИначеЕсли ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.ТрудозатратыВремениПоВидамРабот") Тогда
		
		Отборы = Новый Структура;
		Отборы.Вставить("ПериодОтчета", ДанныеВиджета.Настройки.СтандартныйПериод);
		
		ПоказатьРасшифровкуОтчета(Форма, ДанныеВиджета, "Отчет.CRM_АнализТрудозатрат.Форма", Отборы);
		
		
	КонецЕсли;
	
КонецПроцедуры // ОткрытьРасшифровкуВиджета()

// Универсальная процедура для открытия настроек виджета.
//
// Параметры:
//  Форма                                  - ФормаКлиентскогоПриложения - Форма, из которой происходит
//                                                                        открытие и созданы виджеты.
//  ДанныеВиджета                          - ДанныеФормыКоллекция       - Данные виджета по которому будут
//                                                                        открываться настройки.
//  ОписаниеОповещенияОбИзмененииНастроек  - ОписаниеОповещения         - Описание оповещения для вызова процедуры
//                                                                        при изменении настроек варианта виджета.
//
Процедура ОткрытьНастройкиВиджета(Форма, ДанныеВиджета, ОписаниеОповещенияОбИзмененииНастроек = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("ДанныеВиджета", ДанныеВиджета);
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещенияОбИзмененииНастроек);
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
		"ОткрытьНастройкиВиджетаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВариантВиджета", ДанныеВиджета.ВариантВиджета);
	
	Если ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.КлючевойПоказатель")
			Или ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.ДинамикаПродаж")
			Или ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.ДебиторскаяЗадолженность")
			Или ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.ДинамикаСтатусовКлиентов")
			Или ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.СтатистикаТелефонныхЗвонков")
			Или ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.ТрудозатратыВремениПоВидамРабот") Тогда
		
		ОткрытьФорму(
			"Справочник.CRM_ВариантыВиджетов.Форма.ФормаНастройки",
			ПараметрыФормы,
			Форма.ЭтотОбъект,
			Форма.КлючУникальности, , ,
			ОписаниеОповещенияОЗакрытии,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ДанныеВиджета.ТипВиджета = ПредопределенноеЗначение("Перечисление.CRM_ТипыИсточниковДанныхВиджетов.ВоронкаПродаж") Тогда
		
		ПараметрыФормы.Вставить("ВариантВиджета", ДанныеВиджета.ВариантВиджета);
		ПараметрыФормы.Вставить("НастройкиВариантаВиджета", ДанныеВиджета.Настройки);
		ПараметрыФормы.Вставить("ДействиеСВариантомВиджета", "Редактировать");
		
		ОткрытьФорму(
			"Отчет.CRM_ВоронкаПродаж.Форма.Форма",
			ПараметрыФормы,
			Форма.ЭтотОбъект,
			Форма.КлючУникальности, , ,
			ОписаниеОповещенияОЗакрытии,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры // ОткрытьНастройкиВиджета()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Процедура-обработчик оповещения при закрытии формы настроек варианта виджета.
// Вызывает пользовательское описание оповещения, переданное в <ОткрытьНастройкиВиджета>
//
// Параметры:
//  РезультатЗакрытия        - Структура | Неопределено - Новый настройки варианта виджета.
//  ДополнительныеПараметры  - Структура                - Содержит данные виджета и пользователькое
//                                                        описание оповещения.
//
Процедура ОткрытьНастройкиВиджетаЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		
		ДополнительныеПараметры.ДанныеВиджета.Настройки = РезультатЗакрытия;
		
		Если ДополнительныеПараметры.ОписаниеОповещения <> Неопределено Тогда
			#Если ВебКлиент Тогда
				ДополнительныеПараметры.Форма.ПриИзмененииНастроекВариантаВиджета(
					ДополнительныеПараметры.ДанныеВиджета,
					Неопределено);
			#Иначе
				ВыполнитьОбработкуОповещения(
					ДополнительныеПараметры.ОписаниеОповещения,
					ДополнительныеПараметры.ДанныеВиджета);
			#КонецЕсли
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОткрытьНастройкиВиджетаЗавершение()

// Общая процедура для открытия расшифровки виджета.
//
// Параметры:
//  Форма          - ФормаКлиентскогоПриложения - Форма-владелец.
//  ДанныеВиджета  - ДанныеФормыКоллекция       - Данные виджета по которому открывается расшифровка.
//  Отчет          - Строка                     - Отчет для открытия.
//  Отборы         - Структура                  - Отборы для отчета.
//
Процедура ПоказатьРасшифровкуОтчета(Форма, ДанныеВиджета, Отчет, Отборы)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "ДанныеВиджетаРасшифровка");
	ПараметрыФормы.Вставить("Отбор", Отборы);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ЗначениеАналитики = CRM_РаботаСВиджетамиКлиентСервер.ПолучитьАктуальноеЗначениеАналитики(ДанныеВиджета.Настройки);
	
	МассивЗначенийОтбора = Новый СписокЗначений;
	МассивЗначенийОтбора.Добавить(ЗначениеАналитики);
	
	Если ТипЗнч(ЗначениеАналитики) = Тип("СправочникСсылка.Пользователи") Тогда
		ПараметрыФормы.Отбор.Вставить("Менеджеры", ЗначениеАналитики);
	ИначеЕсли ТипЗнч(ЗначениеАналитики) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		ПараметрыФормы.Отбор.Вставить("Подразделения", ЗначениеАналитики);
	КонецЕсли;
	
	ОткрытьФорму(Отчет, ПараметрыФормы, Форма, Форма.КлючУникальности);
	
КонецПроцедуры // ПоказатьРасшифровкуОтчета()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
