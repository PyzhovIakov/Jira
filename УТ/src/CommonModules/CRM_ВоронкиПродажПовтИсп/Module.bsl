
////////////////////////////////////////////////////////////////////////////////
// Воронки продаж повторного использования
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Функция возвращает Истина, если объект включен в этап хотя бы одной воронки.
//
// Параметры:
//  ОбъектВоронки	 - ЗадачаОбъект.ЗадачаИсполнителя, ДокументОбъект	 - Объект воронки.
// 
// Возвращаемое значение:
//   - Булево
//
Функция ОбъектВключенВЭтапВоронки(ОбъектВоронки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	CRM_ВоронкиПродажСостав.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.CRM_ВоронкиПродаж.Состав КАК CRM_ВоронкиПродажСостав
		|ГДЕ
		|	CRM_ВоронкиПродажСостав.ТочкаМаршрута = &ТочкаМаршрута";
	
	Если ТипЗнч(ОбъектВоронки) = Тип("ЗадачаСсылка.ЗадачаИсполнителя")
		И ЗначениеЗаполнено(ОбъектВоронки.БизнесПроцесс)
		И ТипЗнч(ОбъектВоронки.БизнесПроцесс) = Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс") Тогда
		
		ТочкаМаршрута = ОбъектВоронки.CRM_ТочкаМаршрута;
	Иначе
		ТочкаМаршрута = ПолучитьСсылкуПВХВидыОбъектовБизнесПроцессовПоТипу(ТипЗнч(ОбъектВоронки), Истина);
	КонецЕсли;
	
	Если ТочкаМаршрута = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
		РезультатЗапроса = Запрос.Выполнить();
		Возврат Не РезультатЗапроса.Пустой();
	КонецЕсли;
	
КонецФункции

// Функция возвращает ссылку ПВХ по типу.
//
// Параметры:
//  Тип							 - ОписаниеТипов - Тип элемента ПВХ.
//  ПолучатьПомеченныеНаУдаление - Булево		 - Признак получения помеченных на удаление.
//  МассивСсылок				 - Массив		 - Массив элементов ПВХ.
// 
// Возвращаемое значение:
//   - Неопределено, Массив		 - Массив ссылок на элемент ПВХ.
//
Функция ПолучитьСсылкуПВХВидыОбъектовБизнесПроцессовПоТипу(Тип, ПолучатьПомеченныеНаУдаление = Ложь,
	МассивСсылок = Неопределено) Экспорт
	
	МассивТипов = Новый Массив();
	МассивТипов.Добавить(Тип);
	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	МассивСсылок = ПолучитьМассивЭлементовПВХВидыОбъектовБизнесПроцессовПоОписаниюТипов(ОписаниеТипов,
		 ПолучатьПомеченныеНаУдаление);
	Если МассивСсылок.Количество() = 0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат МассивСсылок[0];
	КонецЕсли;
	
КонецФункции // ПолучитьСсылкуПВХВидыОбъектовБизнесПроцессовПоТипу()

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

// Функция возвращает массив элементов ПВХ по описанию типов.
//
// Параметры:
//  ОписаниеТипов				 - ОписаниеТипов - Описание типов.
//  ПолучатьПомеченныеНаУдаление - Булево		 - Признак получения помеченных на удаление.
// 
// Возвращаемое значение:
//  - Неопределено, Массив - Массив элементов ПВХ.
//
Функция ПолучитьМассивЭлементовПВХВидыОбъектовБизнесПроцессовПоОписаниюТипов(ОписаниеТипов,
	ПолучатьПомеченныеНаУдаление = Ложь)
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Новый Массив();
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОбъектыЖурнала.Ссылка					КАК Ссылка,
	|	ОбъектыЖурнала.Наименование				КАК Наименование,
	|	ОбъектыЖурнала.ТипЗначения				КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.CRM_ВидыОбъектовБизнесПроцессов КАК ОбъектыЖурнала
	|" + ?(ПолучатьПомеченныеНаУдаление, "", "
	|ГДЕ
	|	НЕ ОбъектыЖурнала.ПометкаУдаления") + "
	|УПОРЯДОЧИТЬ ПО
	|	ОбъектыЖурнала.ПометкаУдаления");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Для Каждого Тип Из ОписаниеТипов.Типы() Цикл
			Если Выборка.ТипЗначения.СодержитТип(Тип) Тогда
				Результат.Добавить(Выборка.Ссылка);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПолучитьМассивЭлементовПВХВидыОбъектовБизнесПроцессовПоОписаниюТипов()

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
