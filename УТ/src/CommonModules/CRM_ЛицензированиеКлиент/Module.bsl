////////////////////////////////////////////////////////////////////////////////
// Лицензирование клиент (CRM)
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Функция формирует строку, сообщающую о том, что сервис недоступен.
//
// Параметры:
//  ОписаниеОшибки			 - Строка	 - Описание ошибки
//  УточнениеТекстаСообщения - Строка	 - Описание ошибки
// 
// Возвращаемое значение:
//   - ФорматированнаяСтрока
//
Функция СформироватьСтрокуСервисНедоступен(ОписаниеОшибки = "", УточнениеТекстаСообщения = "") Экспорт

	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Внимание! Подписка 1С:КП Отраслевой отсутствует!'"),
		 Новый Шрифт(, , Истина),
		 WebЦвета.Красный));

	Уточнение = ?(УточнениеТекстаСообщения = "", НСтр("ru='Данный сервис'"), УточнениеТекстаСообщения);
	ТекстСообщения = Символы.ПС + Уточнение + " " + НСтр("ru='недоступен для использования.'");
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ТекстСообщения = ТекстСообщения + Символы.ПС + НСтр("ru='(Описание ошибки смотрите в Журнале регистрации).'");
	КонецЕсли;
	
	ТекстСообщения = ТекстСообщения + Символы.ПС + Символы.ПС + 
		НСтр("ru='Описание подключения 1С:КП Отраслевой смотрите на '");
	
	МассивСтрок.Добавить(ТекстСообщения);
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='сайте компании 1С.'"), , , ,
		 "https://portal.1c.ru/applications/1C-Industry-subscription/"));
	
	Возврат Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецФункции // СформироватьСТрокуСервисНедоступен()

// Функция выполняет проверку подключения отраслевых сервисов.
//
// Параметры:
//  ОписаниеОповещения		 - ОписаниеОповещения, Неопределено	 - Описание оповещения.
//  УточнениеТекстаСообщения - Строка	 - Уточнение текста сообщения.
// 
// Возвращаемое значение:
//   - Булево
//
Функция ПроверитьПодключениеОтраслевыхСервисовИОткрыть(ОписаниеОповещения = Неопределено,
	 УточнениеТекстаСообщения = "") Экспорт
	
	ОписаниеОшибкиСервисы = "";
	СервисыДоступны = CRM_ЛицензированиеЭкспортныеМетоды.ПроверитьПодключениеКОтраслевымСервисам(ОписаниеОшибкиСервисы);
	
	Если НЕ СервисыДоступны Тогда
		
		ТекстПредупреждения = СформироватьСТрокуСервисНедоступен(ОписаниеОшибкиСервисы, УточнениеТекстаСообщения);
		
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат Ложь;
	Иначе
		Если ОписаниеОповещения <> Неопределено Тогда 
			ВыполнитьОбработкуОповещения(ОписаниеОповещения);
		КонецЕсли;
		Возврат Истина;
	КонецЕсли;
	
КонецФункции // ПроверитьПодключениеОтраслевыхСервисовИОткрыть()

#КонецОбласти
