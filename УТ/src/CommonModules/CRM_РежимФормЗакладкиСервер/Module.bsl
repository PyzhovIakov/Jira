
////////////////////////////////////////////////////////////////////////////////
// CRM режим форм закладки сервер
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// При закрытии на сервере.
//
// Параметры:
//  ЭтаФорма - ФормаКлиентскогоПриложения - Передаваемая форма.
//
Процедура ПриЗакрытииНаСервере(ЭтаФорма) Экспорт
	Если Не CRM_РежимФормЗакладкиСерверПовтИсп.ИспользуетсяРежимЗакладок() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтаФорма, "ИмяФормы") Тогда
		ИмяФормы = ЭтаФорма.ИмяФормы;
	Иначе
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Структура();
	Если ИмяФормы = "ОбщаяФорма.ПанельОтчетов" Тогда
		Если ЭтаФорма.Параметры.Свойство("Подсистемы") Тогда
			Параметры.Вставить("Подсистемы", ЭтаФорма.Параметры.Подсистемы);
		КонецЕсли;
		Если ЭтаФорма.Параметры.Свойство("ИмяФормы") Тогда
			Параметры.Вставить("ИмяФормы", ЭтаФорма.Параметры.ИмяФормы);
		КонецЕсли;
	КонецЕсли;
	ОбновитьСоставОткрытыхФорм(ИмяФормы, Параметры, Истина);
	
КонецПроцедуры

// Восстановление формы при запуске сеанса.
//
// Параметры:
//  ЭтаФорма			 - ФормаКлиентскогоПриложения	 - Передаваемая форма.
//  Отказ				 - Булево			 - Признак отказа.
//  СтандартнаяОбработка - Булево			 - Передается признак выполнения стандартной (системной) обработки события.
// 
// Возвращаемое значение:
//  Булово - Признак необходимости восстановления формы при запуске.
//
Функция ВосстановлениеФормыПриЗапускеСеанса(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт 
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Если ЭтаФорма.Параметры.Свойство("_НачалоРаботыСистемы") Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ВосстанавливатьФормуПриЗапуске = CRM_ХранилищеНастроек.Загрузить(ЭтаФорма.ИмяФормы, "ВосстанавливатьФормуПриОткрытии");
	Если НЕ ЗначениеЗаполнено(ВосстанавливатьФормуПриЗапуске) Тогда
		ВосстанавливатьФормуПриЗапуске = Истина;
	КонецЕсли;	
	
	ЭтаФорма.Элементы.КнопкаВосстанавливатьФормуПриОткрытии.Пометка = ВосстанавливатьФормуПриЗапуске;
	
	// Добавление формы к списку открываемых при запуске приложения в зависимости от настройки.
	Если ВосстанавливатьФормуПриЗапуске Тогда
		// алгоритм по умолчанию
		ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	Иначе
		// Форму удаляем из открываемых при запуске.
		ПриСозданииНаСервере(ЭтаФорма, Истина, СтандартнаяОбработка);
		// Если это начало системы и галочка восстановления снята, то форма не открывается.
		Если ЭтаФорма.Параметры.Свойство("_НачалоРаботыСистемы") Тогда
			Возврат Ложь;
		КонецЕсли;			
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПризнакСтартСистемы() Экспорт
	Данные = CRM_ХранилищеНастроек.Загрузить("СтартСистемыВРежимеЗакладокПризнакСтартСистемы");
	Возврат (Данные = Истина);
КонецФункции

Процедура СохранитьПризнакСтартСистемы(Признак) Экспорт
	CRM_ХранилищеНастроек.Сохранить("СтартСистемыВРежимеЗакладокПризнакСтартСистемы", , Признак);
КонецПроцедуры

Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	Если Не CRM_РежимФормЗакладкиСерверПовтИсп.ИспользуетсяРежимЗакладок() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьПризнакСтартСистемы() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтаФорма, "ИмяФормы") Тогда
		ИмяФормы = ЭтаФорма.ИмяФормы;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ЭтаФорма.Параметры.Свойство("_НачалоРаботыСистемы") И НЕ Отказ Тогда
		// Создание происходит при начале работы системы, обновлять состав открываемых форм не нужно
		// но если передали Отказ, то нужно удалить эту форму из открываемых при запуске.
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Структура();
	Если ИмяФормы = "ОбщаяФорма.ПанельОтчетов" Тогда
		Если ЭтаФорма.Параметры.Свойство("Подсистемы") Тогда
			Параметры.Вставить("Подсистемы", ЭтаФорма.Параметры.Подсистемы);
		КонецЕсли;
		Если ЭтаФорма.Параметры.Свойство("ИмяФормы") Тогда
			Параметры.Вставить("ИмяФормы", ЭтаФорма.Параметры.ИмяФормы);
		КонецЕсли;
	КонецЕсли;
	ОбновитьСоставОткрытыхФорм(ИмяФормы, Параметры);
	
КонецПроцедуры

Функция ФормаВынесенаНаРабочийСтол(ИмяФормы) Экспорт
	
	НастройкиНачальнойСтраницы = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиНачальнойСтраницы");
	
	Если НастройкиНачальнойСтраницы = Неопределено Тогда
		НастройкиНачальнойСтраницы = Новый НастройкиНачальнойСтраницы;
	КонецЕсли;
	
	Если НастройкиНачальнойСтраницы = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СоставФорм = НастройкиНачальнойСтраницы.ПолучитьСоставФорм();
	
	Для Ин = 0 По СоставФорм.ЛеваяКолонка.ВГраница() Цикл
		Если СоставФорм.ЛеваяКолонка[Ин] = ИмяФормы Тогда
			Возврат Истина;
		КонецЕсли; 
	КонецЦикла;
	
	Для Ин = 0 По СоставФорм.ПраваяКолонка.ВГраница() Цикл
		Если СоставФорм.ПраваяКолонка[Ин] = ИмяФормы Тогда
			Возврат Истина;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

Процедура ОбновитьСоставОткрытыхФорм(Знач ИмяФормы, Параметры, ТолькоУдалить = Ложь, Пользователь = Неопределено)
	ТекущийСоставОткрытыхФорм = ПолучитьТекущийСоставОткрытыхФорм(Пользователь);
	
	нИндекс = CRM_РежимФормЗакладкиКлиентСервер.НайтиФормуВСоставеФорм(ИмяФормы, Параметры, ТекущийСоставОткрытыхФорм);
	Если нИндекс <> Неопределено Тогда
		ТекущийСоставОткрытыхФорм.Удалить(нИндекс);
	КонецЕсли;
	
	Если ТолькоУдалить Тогда
		СохранитьТекущийСоставОткрытыхФорм(ТекущийСоставОткрытыхФорм, Пользователь);
		Возврат;
	КонецЕсли;
	
	Если ФормаВынесенаНаРабочийСтол(ИмяФормы) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОписаниеФормы = CRM_РежимФормЗакладкиКлиентСервер.ПолучитьСтруктуруБланкОписанияФормы();
	СтруктураОписаниеФормы.ИмяФормы = ИмяФормы;
	СтруктураОписаниеФормы.Параметры = Параметры;
	
	ТекущийСоставОткрытыхФорм.Добавить(СтруктураОписаниеФормы);
	СохранитьТекущийСоставОткрытыхФорм(ТекущийСоставОткрытыхФорм, Пользователь);
КонецПроцедуры

Функция ПолучитьТекущийСоставОткрытыхФорм(Пользователь = Неопределено) Экспорт
	// +Рабочий стол
	Если CRM_РабочийСтолСервер.ПолучитьНастройкиОткрытия().ОткрыватьРабочийСтол
		 ИЛИ CRM_РабочийСтолСервер.ПолучитьНастройкиОткрытия().БлокироватьИнтерфейс Тогда
		Возврат Новый Массив();
	КонецЕсли;
	// -Рабочий стол
	Данные = CRM_ХранилищеНастроек.Загрузить("ФормыДляОткрытияПриСтартеСистемыВРежимеЗакладок", , , Пользователь);
	Если ТипЗнч(Данные) <> Тип("Массив") Тогда
		Данные = Новый Массив();
	КонецЕсли;
	Возврат Данные;
КонецФункции

Процедура СохранитьТекущийСоставОткрытыхФорм(ТекущийСоставОткрытыхФорм, Пользователь = Неопределено) Экспорт
	CRM_ХранилищеНастроек.Сохранить("ФормыДляОткрытияПриСтартеСистемыВРежимеЗакладок", ,
		 ТекущийСоставОткрытыхФорм, ,
		 Пользователь);
КонецПроцедуры

#КонецОбласти
