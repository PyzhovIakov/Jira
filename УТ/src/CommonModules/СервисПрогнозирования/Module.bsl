
#Область ПрограммныйИнтерфейс

#Область РаботаСВнешнимиРесурсами
///////////////////////////////////////////////////////////////////////////////
// Инициализации параметров сеанса.

///////////////////////////////////////////////////////////////////////////////
// Работа с запросами API.

// Выполняет итоговый запрос согласно структуре входящих параметров.
// 
// Параметры:
//  Метод - Строка - Метод HTTP-запроса.
//  URLЗапроса - Строка - Строка соединения с сервером.
//  Заголовки - Неопределено, Соответствие из Строка - Заголовки.
//  ТелоЗапроса - Строка - Тело запроса в формате JSON.
// 
// Возвращаемое значение:
//  см. СтруктураОтвета.
Функция ВыполнитьHTTPЗапрос(Метод,
	URLЗапроса,
	Заголовки = Неопределено,
	ТелоЗапроса = "") Экспорт
	
	СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
	
	СтруктураURI = СтруктураURI(URLЗапроса);
	
	ИнформацияОРесурсе = Новый Структура("Метод, РесурсСервиса, Заголовки",
		Метод,
		URLЗапроса,
		"");
	
	Если ЗначениеЗаполнено(Заголовки) Тогда
		Для Каждого КлючЗначение Из Заголовки Цикл
			ИнформацияОРесурсе.Заголовки = ИнформацияОРесурсе.Заголовки + КлючЗначение.Ключ + ":" 
				+ КлючЗначение.Значение + ";";
		КонецЦикла;
	КонецЕсли;
	
	Таймаут = Неопределено;
	НастройкиСервиса = ПолучитьАвторизационныеНастройкиСервиса(); // Запрос авторизационных данных.
	НастройкиСервиса.Свойство("ТаймаутСетевогоСоединения", Таймаут);
	Таймаут = ?(Таймаут = Неопределено, 120, Таймаут);
	Таймаут = ?(Таймаут < 10, 10, Таймаут);
	Таймаут = ?(Таймаут > 600, 600, Таймаут);
	
	Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("http");
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт, , , Прокси, Таймаут);
	
	HTTPЗапрос = Новый HTTPЗапрос(СтруктураURI.ПутьНаСервере);
	
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	HTTPЗапрос.Заголовки.Вставить("Charset", "utf-8");
	
	Если ЗначениеЗаполнено(Заголовки) Тогда
		Для Каждого КлючЗначение Из Заголовки Цикл
			HTTPЗапрос.Заголовки.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Если Метод <> "GET" Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать);
	КонецЕсли;
	
	Результат = Неопределено;
	СтруктураРезультата = СтруктураОтвета();
	
	Попытка
		Результат = HTTPСоединение.ВызватьHTTPМетод(Метод, HTTPЗапрос);
		РегистрыСведений.ЖурналСервисаПрогнозирования.ЗаписатьИнформациюОбОбмене(ИнформацияОРесурсе,
			Результат.КодСостояния,
			ТелоЗапроса,
			Ложь,
			Истина);
	Исключение
		// Запрос не дошел до HTTP-Сервера.
		// Failure when receiving data from the peer.
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = СтрШаблон(НСтр("ru='По запросу URL: %1 произошла сетевая ошибка. %2'"),
			URLЗапроса,
			ПодробноеПредставлениеОшибки);
		
		РегистрыСведений.ЖурналСервисаПрогнозирования.ЗаписатьИнформациюОбОбмене(ИнформацияОРесурсе,
			-1,
			ТелоЗапроса,
			Истина,
			Истина);
		РегистрыСведений.ЖурналСервисаПрогнозирования.ЗаписатьИнформациюОбОбмене(ИнформацияОРесурсе,
			-1,
			ТекстСообщения,
			Истина,
			Ложь);
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru='При попытке обращения к сервису произошла сетевая ошибка: %1.
				|URL запроса: %2.
				|Попробуйте еще раз. В случае повторения ошибки обратитесь в техподдержку.'"),
			ПодробноеПредставлениеОшибки, URLЗапроса);
		ВызватьИсключение ТекстСообщения;
		
	КонецПопытки;
	
	Если Результат <> Неопределено Тогда
		ЕстьОшибка = Ложь;
		Если Результат.КодСостояния >= 400 И Результат.КодСостояния < 500  Тогда // Код статуса 4XX, ошибка запроса
			ЕстьОшибка = Истина;
			Если Метод = "GET" Тогда
				ШаблонТекста = НСтр("ru='По запросу URL: %1 получен код состояния %2 - ошибка запроса.
					|Метод: %3'");
				ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, Результат.КодСостояния, Метод);
			Иначе
				ШаблонТекста = НСтр("ru='По запросу URL: %1 получен код состояния %2 - ошибка запроса.
					|Метод: %3
					|Тело запроса: %4'");
				ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, Результат.КодСостояния, Метод, Лев(ТелоЗапроса, 200));
			КонецЕсли;
				
			РегистрыСведений.ЖурналСервисаПрогнозирования.ЗаписатьИнформациюОбОбмене(ИнформацияОРесурсе,
				Результат.КодСостояния,
				ТекстСообщения,
				Истина,
				Ложь);

			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
		КонецЕсли;
		
		Если Результат.КодСостояния >= 500 И Результат.КодСостояния < 600 Тогда // Код статуса 5XX, ошибка сервера
			ЕстьОшибка = Истина;
			
			Если Метод = "GET" Тогда
				ШаблонТекста = НСтр("ru='По запросу URL: %1 получен код состояния %2 - ошибка сервера.
					|Метод: %3'");
				ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, Результат.КодСостояния, Метод);
			Иначе
				ШаблонТекста = НСтр("ru='По запросу URL: %1 получен код состояния %2 - ошибка сервера.
					|Метод: %3
					|Тело запроса: %4'");
				ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, Результат.КодСостояния, Метод, Лев(ТелоЗапроса, 200));
			КонецЕсли;
			
			РегистрыСведений.ЖурналСервисаПрогнозирования.ЗаписатьИнформациюОбОбмене(ИнформацияОРесурсе,
				Результат.КодСостояния,
				ТекстСообщения,
				Истина,
				Ложь);
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
		КонецЕсли;
		
		// Обрабатываем перенаправление.
		Если Результат.КодСостояния >= 300 И Результат.КодСостояния < 400 Тогда // Код статуса 3XX, Перенаправление.
			
			Если Метод = "GET" Тогда
				ШаблонТекста = НСтр("ru='По запросу URL: %1 получен код состояния %2 - перенаправление.
					|Метод: %3'");
				ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, Результат.КодСостояния, Метод);
			Иначе
				ШаблонТекста = НСтр("ru='По запросу URL: %1 получен код состояния %2 - перенаправление.
					|Метод: %3
					|Тело запроса: %4'");
				ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, Результат.КодСостояния, Метод, Лев(ТелоЗапроса, 200));
			КонецЕсли;
			
			Если Результат.КодСостояния = 302 Тогда // Код статуса 302, Постоянное перенаправление.
				АдресРесурса = Результат.Заголовки.Получить("Location");
				
				Если АдресРесурса <> Неопределено Тогда
					
					Если Метод = "GET" Тогда
						ШаблонТекста = НСтр("ru='По запросу URL: %1 производится попытка выполнения по новому адресу %2.
							|Метод: %3'");
						ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, АдресРесурса, Метод);
					Иначе
						ШаблонТекста = НСтр("ru='По запросу URL: %1 получен код состояния %2 - перенаправление.
							|Метод: %3
							|Тело запроса: %4'");
						ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, АдресРесурса, Метод, Лев(ТелоЗапроса, 200));
					КонецЕсли;
					
					ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Предупреждение, , , ТекстСообщения);
					
					URLЗапроса = СтрЗаменить(URLЗапроса, СтруктураURI.Хост, АдресРесурса);
					Попытка
						ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапроса);
					Исключение
						
						Если Метод = "GET" Тогда
							ШаблонТекста = НСтр("ru='По запросу URL: %1 попытка выполнения(перенаправления) его по новому адресу %2 произошла с ошибкой.
								|Метод: %3'");
							ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, АдресРесурса, Метод);
						Иначе
							ШаблонТекста = НСтр("ru='По запросу URL: %1 попытка выполнения(перенаправления) его по новому адресу %2 произошла с ошибкой.
								|Метод: %3
								|Тело запроса: %4'");
							ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, АдресРесурса, Метод, Лев(ТелоЗапроса, 200));
						КонецЕсли;
						
						ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
					КонецПопытки;
				Иначе
					
					Если Метод = "GET" Тогда
					ШаблонТекста = НСтр("ru='По запросу URL: %1 получен код состояния %2 - постоянное перенаправление, но сервер не сообщил адрес ресурса.
							|Метод: %3'");
						ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, Результат.КодСостояния, Метод);
					Иначе
						ШаблонТекста = НСтр("ru='По запросу URL: %1 получен код состояния %2 - постоянное перенаправление, но сервер не сообщил адрес ресурса.
							|Метод: %3
							|Тело запроса: %4'");
						ТекстСообщения = СтрШаблон(ШаблонТекста, URLЗапроса, Результат.КодСостояния, Метод, Лев(ТелоЗапроса, 200));
					КонецЕсли;
					
					ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		СтруктураРезультата = СтруктураОтветаСервиса(Результат);
		
		РегистрыСведений.ЖурналСервисаПрогнозирования.ЗаписатьИнформациюОбОбмене(ИнформацияОРесурсе,
			Результат.КодСостояния,
			Результат.ПолучитьТелоКакСтроку(),
			ЕстьОшибка,
			Ложь);
		
	КонецЕсли;
	
	Возврат СтруктураРезультата;
	
КонецФункции

#КонецОбласти

#Область Авторизация

// Получить тикет ИТС.
// 
// Возвращаемое значение:
//  Структура - Ответ функции получения тикета ИТС:
// * Тикет - Строка -
// * ТекстОшибки - Строка -
Функция ПолучитьТикет() Экспорт
	
	ВозвращаемоеЗначение = Новый Структура("Тикет, ТекстОшибки", "", "");
	
	Тикет = "";
	ВладелецТикета = "https://forecast.1c.ai";
	УстановитьПривилегированныйРежим(Истина);
	МодульИнтернетПоддержкаПользователей = СервисПрогнозирования.МодульИнтернетПоддержкаПользователей();
	Если МодульИнтернетПоддержкаПользователей = Неопределено Тогда
		ТекстСообщенияОбОшибке = ТекстОшибкиНеУдалосьПолучитьМодульИнтернетПоддержкаПользователей();
		ВозвращаемоеЗначение.ТекстОшибки = ТекстСообщенияОбОшибке;
		ВозвращаемоеЗначение.Тикет = Тикет;
		Возврат ВозвращаемоеЗначение;
	Иначе
		РезультатПолученияТикета =
			МодульИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки(ВладелецТикета);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	Если Не ПустаяСтрока(РезультатПолученияТикета.Тикет) Тогда
		// Вызвать целевой сервис, используя полученный тикет.
		Тикет = РезультатПолученияТикета.Тикет;
	ИначеЕсли РезультатПолученияТикета.КодОшибки = "НеверныйЛогинИлиПароль" Тогда
		// Отсутствуют сохраненные данные аутентификации или данные аутентификации некорректны.
		ТекстСообщенияОбОшибке = ТекстОшибкиНеверныйЛогинПарольИТС();
		ВозвращаемоеЗначение.ТекстОшибки = ТекстСообщенияОбОшибке;
	Иначе
		// При получении тикета возникла сетевая или иная ошибка.
		ТекстСообщенияОбОшибке = ТекстОшибкиНеУдалосьПолучитьТикет();
		ВозвращаемоеЗначение.ТекстОшибки = ТекстСообщенияОбОшибке;
	КонецЕсли;
	
	ВозвращаемоеЗначение.Тикет = Тикет;
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Отправить заявку на подключение к сервису.
// 
// Возвращаемое значение:
//  Булево -
Функция ОтправитьЗаявкуВСервис() Экспорт
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	Если Не ЗначениеЗаполнено(НастройкиСервиса.ТокенПриложения) Тогда
		Ответ = ПолучитьТикет();
		Если Не ПустаяСтрока(Ответ.ТекстОшибки) Тогда
			СтруктураОтвета = СтруктураОтвета();
			СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
			Возврат СтруктураОтвета;
		КонецЕсли;
		
		Ответ = ЗарегистрироватьПриложениеВСервисеПоИТС(Ответ.Тикет, Ложь);
		Если Не ПустаяСтрока(Ответ.ТекстОшибки) Тогда
			Возврат Ответ;
		КонецЕсли;
		
		НастройкиСервиса = ПолучитьНастройкиСервиса();
	КонецЕсли;
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("feedback");
	ПараметрыАдреса.Добавить(НастройкиСервиса.ИдентификаторПриложения);
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	ТелоЗапроса = Новый Структура();
	
	Заголовки = Новый Соответствие();
	
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ЗаявкаНаПодключение",
			СтруктураОтвета.КодСостояния);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Возвращает ответ заявки на перерегистрацию с целью восстановления доступа на новой версии сервиса
// или ошибку получения тикета ИТС.
// 
// Параметры:
//  ТокенПриложения - Строка - переданный токен приложения.
// 
// Возвращаемое значение:
//  см. ПолучитьТикет
Функция ПеререгистрироватьПриложениеВСервисеПоИТС(ТокенПриложения = "") Экспорт
	
	Ответ = ПолучитьТикет();
	Если ПустаяСтрока(Ответ.ТекстОшибки) Тогда
		Возврат ЗарегистрироватьПриложениеВСервисеПоИТС(Ответ.Тикет, Истина, ТокенПриложения);
	Иначе
		Возврат Ответ;
	КонецЕсли;
	
КонецФункции

// Зарегистрировать приложение в сервисе по ИТС.
// 
// Параметры:
//  Тикет - Строка -
//  ПеререгистрацияПриложения - Булево - признак заявки перерегистрации на новой версии сервиса.
//  ТокенПриложения           - Строка - переданный токен приложения.
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ЗарегистрироватьПриложениеВСервисеПоИТС(Тикет, ПеререгистрацияПриложения, ТокенПриложения = "") Экспорт
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("auth");
	ПараметрыАдреса.Добавить("its");
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	НастройкиСервиса = ПолучитьАвторизационныеНастройкиСервиса(); // Запрос авторизационных данных.
	
	ВложенноеТелоЗапроса = Новый Структура();
	ВложенноеТелоЗапроса.Вставить(ПолучитьПредставлениеСвойстваСервиса("ИмяБазы"), НастройкиСервиса.ИмяБазы);
	Если ПеререгистрацияПриложения Тогда
		ВложенноеТелоЗапроса.Вставить(ПолучитьПредставлениеСвойстваСервиса("ТокенПриложенияПредыдущейВерсии"),
			?(ПустаяСтрока(ТокенПриложения), НастройкиСервиса.ТокенПриложения, ТокенПриложения));
	КонецЕсли;
	
	ТелоЗапроса = Новый Структура();
	ТелоЗапроса.Вставить("ticket",
		Тикет);
	ТелоЗапроса.Вставить(ПолучитьПредставлениеСвойстваСервиса("Тариф"),
		НастройкиСервиса.Тариф);
	ТелоЗапроса.Вставить("scope",
		ВложенноеТелоЗапроса);
	
	Заголовки = Новый Соответствие();
	
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
		Если СтруктураОтвета.КодСостояния >= 400 И СтруктураОтвета.КодСостояния < 600 Тогда
			СвойстваЗапросаСОшибкой = СвойстваЗапросаВСервис();
			СвойстваЗапросаСОшибкой.Метод = Метод;
			СвойстваЗапросаСОшибкой.Адрес = URLЗапроса;
			СвойстваЗапросаСОшибкой.Тело  = ТелоЗапросаJSON;
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "РегистрацияПриложения",
				Неопределено, СвойстваЗапросаСОшибкой);
		Иначе
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "РегистрацияПриложения");
		КонецЕсли;
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
		Если ПеререгистрацияПриложения И ПустаяСтрока(СтруктураОтвета.ТекстОшибки) Тогда
			ОчиститьИнформациюСервиса();
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Повторная регистрация приложения в сервисе по ИТС.
// 
// Параметры:
//  Тикет - Строка - 
//  ИдентификаторПриложения - Строка - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПовторнаяРегистрацияПриложенияВСервисеПоИТС(Тикет, ИдентификаторПриложения) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("auth");
	ПараметрыАдреса.Добавить("its");
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ВложенноеТелоЗапроса = Новый Структура();
	ВложенноеТелоЗапроса.Вставить(ПолучитьПредставлениеСвойстваСервиса("ИмяБазы"),
		НастройкиСервиса.ИмяБазы);
	
	ТелоЗапроса = Новый Структура();
	ТелоЗапроса.Вставить("ticket",
		Тикет);
	ТелоЗапроса.Вставить("application_uuid",
		ИдентификаторПриложения);
	ТелоЗапроса.Вставить(ПолучитьПредставлениеСвойстваСервиса("Тариф"),
		НастройкиСервиса.Тариф);
	ТелоЗапроса.Вставить("scope",
		ВложенноеТелоЗапроса);
	
	Заголовки = Новый Соответствие();
	
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "РегистрацияПриложения",
			Неопределено);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Обновить токен доступа приложения в сервисе.
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ОбновитьТокенДоступаПриложенияВСервисе() Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("auth_application");
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	НастройкиСервиса = ПолучитьАвторизационныеНастройкиСервиса(); // Запрос авторизационных данных.
	
	ТелоЗапроса = Новый Структура();
	ТелоЗапроса.Вставить(ПолучитьПредставлениеСвойстваСервиса("ТокенОбновления"),
		НастройкиСервиса.ТокенОбновления);
	
	Заголовки = Новый Соответствие();
	
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "РегистрацияПриложения");
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Выдать права приложению в сервисе.
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ВыдатьПраваПриложениюВСервисе() Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("copy_grants");
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	НастройкиСервиса = ПолучитьАвторизационныеНастройкиСервиса(); // Запрос авторизационных данных.
	
	ТелоЗапроса = Новый Структура();
	ТелоЗапроса.Вставить(ПолучитьПредставлениеСвойстваСервиса("ИдентификаторПриложения"),
		НастройкиСервиса.ТокенПриложения);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПользователя);
	
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Выдать права приложению в сервисе.
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьСтатусПодключенияКСервису() Экспорт
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("auth");
	ПараметрыАдреса.Добавить("info");
	
	Метод = "GET";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	НастройкиСервиса = ПолучитьАвторизационныеНастройкиСервиса(); // Запрос авторизационных данных.
	
	ТелоЗапроса = "";
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучениеСтатуса",
			Неопределено);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// См. ИнтернетПоддержкаПользователейПереопределяемый.ПриИзмененииДанныхАутентификацииИнтернетПоддержки()
//
Процедура ПриИзмененииДанныхАутентификации(ДанныеПользователя) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	НастройкиАвторизации = ПолучитьАвторизационныеНастройкиСервиса();
	УстановитьПривилегированныйРежим(Ложь);
	
	Настройки = Новый Структура(НастройкиАвторизации);
	
	// Регистрация по пользователю ИТС
	// Не производилась регистрация в сервисе
	Если ПустаяСтрока(Настройки.ТокенПриложения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеПользователя = Неопределено Тогда
		
		// При удалении данных аутентификации пользователя интернет-поддержки.
		Настройки.ИзмененыНастройкиИнтернетПоддержки = Истина;
		ОбновитьНастройкиСервиса(Настройки);
		УстановитьПараметрыСеансаНастроекСервисаПрогнозирования();
		
	Иначе
		
		// При возврате актуальных данных аутентификации пользователя интернет-поддержки.
		Если Настройки.ИзмененыНастройкиИнтернетПоддержки Тогда
			Если ДанныеПользователя.Логин = Настройки.ЛогинИТС Тогда
				Настройки.ИзмененыНастройкиИнтернетПоддержки = Ложь;
				ОбновитьНастройкиСервиса(Настройки);
				УстановитьПараметрыСеансаНастроекСервисаПрогнозирования();
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		// При изменении данных аутентификации пользователя интернет-поддержки.
		Если ДанныеПользователя.Логин <> Настройки.ЛогинИТС Тогда
			Настройки.ИзмененыНастройкиИнтернетПоддержки = Истина;
			ОбновитьНастройкиСервиса(Настройки);
			УстановитьПараметрыСеансаНастроекСервисаПрогнозирования();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область БлокировкаСервиса

// Выполняет анализ выполненного перехода.
//
// Возвращаемое значение:
//  Строка - "ПереходаНеБыло"
//           "КоробкаВОблако"
//           "ОблакоВКоробку"
//           "ОблакоВОблако".
//
Функция ВариантВыполненногоПерехода() Экспорт
	
	ДоПерехода = РегистрыСведений.ПараметрыАвторизацииИскусственногоИнтеллекта.СохраненныеНастройки(
		Перечисления.СервисыИскусственногоИнтеллекта.СервисПрогнозирования);
	
	РазделениеВключено        = РаботаВМоделиСервиса.РазделениеВключено();
	ЗначениеРазделителяСеанса = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
	
	СменаРежимаРаботы = (ДоПерехода.РазделениеВключено <> РазделениеВключено);
	Если СменаРежимаРаботы Тогда
		
		Если ДоПерехода.РазделениеВключено Тогда
			Результат = "ОблакоВКоробку";
		Иначе
			Результат = "КоробкаВОблако";
		КонецЕсли;
		
	Иначе // Было облако осталось облаком, была коробка осталась коробкой
		
		СменаОбласти = РазделениеВключено И (ДоПерехода.ЗначениеРазделителяСеанса <> ЗначениеРазделителяСеанса);
		Если СменаОбласти Тогда
			Результат = "ОблакоВОблако";
		Иначе
			Результат = "ПереходаНеБыло";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Сброс настроек в сервисе в рамках блокирования сервиса из-за изменения режима работы.
// 
// Параметры:
//  ТребуетсяПолныйСбросНастроек - Булево - Признак необходимости в полном сбросе настроек.
//
Процедура ЗаблокироватьСервисПрогнозирования(ТребуетсяПолныйСбросНастроек) Экспорт
	
	Если ТребуетсяПолныйСбросНастроек Тогда
		СброситьНастройкиСервиса();
		ОчиститьИнформациюСервиса();
	Иначе
		СброситьНастройкиАвторизацииВСервисе();
	КонецЕсли;
	
КонецПроцедуры

// Выполняет анализ выполненного перехода.
//
// Возвращаемое значение:
//  Структура - содержит параметры авторизации искусственного интеллекта:
//  * РазделениеВключено - Булево - признак включения разделения.
//  * ЗначениеРазделителяСеанса - Число - значение разделителя сеанса.
//  * АдресПриложения - Строка - адрес приложения.
//
Функция ТекущиеПараметрыАвторизацииИИ() Экспорт
	
	Результат = РегистрыСведений.ПараметрыАвторизацииИскусственногоИнтеллекта.НачальныеНастройки();
	Результат.РазделениеВключено        = РаботаВМоделиСервиса.РазделениеВключено();
	Результат.ЗначениеРазделителяСеанса = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
	
	Если Результат.РазделениеВключено Тогда
		Попытка
			Результат.АдресПриложения = ПрограммныйИнтерфейсСервиса.СвойстваПриложения(Результат.ЗначениеРазделителяСеанса).АдресПриложения;
		Исключение
			Результат.АдресПриложения = "";
		КонецПопытки;
	Иначе
		Результат.АдресПриложения = "";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Коллекции

// Проверка коллекций на изменение состава данных.
//
// Параметры:
//  НастройкиСервиса - Неопределено, Структура из Произвольный - см. СервисПрогнозирования.ПолучитьНастройкиСервиса
//
// Возвращаемое значение:
//  Массив Из Строка - перечень имен коллекций с неактуальными конфигурациями.
Функция НеактуальныеКонфигурацииКоллекций(НастройкиСервиса = Неопределено) Экспорт
	
	Если НастройкиСервиса = Неопределено Тогда
		НастройкиСервиса = ПолучитьНастройкиСервиса();
	КонецЕсли;
	
	КоллекцииСНеактуальнымиКонфигурациями = Новый Массив();
	
	Для Каждого КлючЗначение Из НастройкиСервиса.Коллекции Цикл
		
		ОписаниеКоллекции = КлючЗначение.Значение;
		Если Не ОписаниеКоллекции.Выгружать Тогда
			Продолжить;
		КонецЕсли;
		
		ИнфоКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(КлючЗначение.Ключ);
		Если Не ЗначениеЗаполнено(ИнфоКоллекции.ИдКоллекции)
			Или Число(ИнфоКоллекции.ИдКоллекции) < 1 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИнфоКоллекции.ИзмененФормат Тогда
			КоллекцииСНеактуальнымиКонфигурациями.Добавить(ОписаниеКоллекции.ИмяВИсточнике);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат КоллекцииСНеактуальнымиКонфигурациями;
	
КонецФункции

// Создать коллекции.
// 
// Параметры:
//  КоллекцииДляСоздания - Неопределено, Массив из Строка - Коллекции для создания
Процедура СоздатьКоллекции(КоллекцииДляСоздания = Неопределено) Экспорт
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	Для Каждого КлючЗначениеКоллекции Из НастройкиСервиса.Коллекции Цикл
		Если КоллекцииДляСоздания <> Неопределено
			И КоллекцииДляСоздания.Найти(КлючЗначениеКоллекции.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеКоллекции = КлючЗначениеКоллекции.Значение;
		
		КоллекцияВыгружалась = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.КоллекцияВыгружалась(КлючЗначениеКоллекции.Ключ);
		ИнфоКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(КлючЗначениеКоллекции.Ключ);
		
		Если ОписаниеКоллекции.Выгружать Тогда
			Если Не КоллекцияВыгружалась Тогда
				СоздатьКоллекцию(КлючЗначениеКоллекции.Ключ, ОписаниеКоллекции.ИмяВСервисе);
			ИначеЕсли КоллекцияВыгружалась
				И ИнфоКоллекции.ИзмененФормат Тогда
				УдалитьКоллекцию(КлючЗначениеКоллекции.Ключ);
				СоздатьКоллекцию(КлючЗначениеКоллекции.Ключ, ОписаниеКоллекции.ИмяВСервисе);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Очистить или удалить коллекцию на сервере.
// 
// Параметры:
//  ИдКоллекции - Число - Ид коллекции
//  УдалитьКоллекцию - Булево - Удалить коллекцию
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ОчиститьУдалитьКоллекцию(ИдКоллекции, УдалитьКоллекцию = Ложь) Экспорт
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("data");
	ПараметрыАдреса.Добавить(ИдКоллекции);
	
	Если УдалитьКоллекцию Тогда
		Метод = "DELETE";
	Иначе
		Метод = "PUT";
	КонецЕсли;
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	ТелоЗапроса = Новый Структура();
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции


// Выгрузить коллекции.
// 
// Параметры:
//  КоллекцииДляВыгрузки - Массив из Строка - Коллекции для выгрузки
//  АдресХранилища - Строка - Адрес хранилища, передаваемый при вызове в фоне
Процедура ВыгрузитьКоллекции(КоллекцииДляВыгрузки = Неопределено, АдресХранилища = Неопределено) Экспорт
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ЗапланированоВыгрузитьКоллекций = 0;
	Если КоллекцииДляВыгрузки <> Неопределено Тогда
		ЗапланированоВыгрузитьКоллекций = КоллекцииДляВыгрузки.Количество();
	Иначе
		Для Каждого КлючЗначениеКоллекции Из НастройкиСервиса.Коллекции Цикл
			ОписаниеКоллекции = КлючЗначениеКоллекции.Значение;
			Если ОписаниеКоллекции.Выгружать Тогда
				ЗапланированоВыгрузитьКоллекций = ЗапланированоВыгрузитьКоллекций + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
	СводкаОбменаДанными.ОбменДаннымиАктивен = Истина;
	СводкаОбменаДанными.ЗапланированоВыгрузитьКоллекций = ЗапланированоВыгрузитьКоллекций;
	СводкаОбменаДанными.ВыгруженоОбъектовКоллекции = 0;
	СводкаОбменаДанными.ЗапланированоВыгрузитьОбъектовКоллекции = 0;
	СводкаОбменаДанными.ВыгружаемаяСейчасКоллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПустаяСсылка();
	РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
	
	ВыгруженоКоллекций = 0;
	Для Каждого КлючЗначениеКоллекции Из НастройкиСервиса.Коллекции Цикл
		Если КоллекцииДляВыгрузки <> Неопределено
			И КоллекцииДляВыгрузки.Найти(КлючЗначениеКоллекции.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеКоллекции = КлючЗначениеКоллекции.Значение;
		Выгружать = ОписаниеКоллекции.Выгружать;
		Если Не Выгружать Тогда
			Продолжить;
		КонецЕсли;
		Категориальный = ОписаниеКоллекции.Категориальный;
		
		Коллекция = КлючЗначениеКоллекции.Ключ;
		СводкаПоКоллекции = СервисПрогнозированияПереопределяемый.ПолучитьСводкуПоКоллекции(Коллекция, НастройкиСервиса);
		
		Если СводкаПоКоллекции.КоличествоДанных = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
		СводкаОбменаДанными.ВыгруженоКоллекций = ВыгруженоКоллекций;
		СводкаОбменаДанными.ВыгружаемаяСейчасКоллекция = Коллекция;
		СводкаОбменаДанными.ВыгруженоОбъектовКоллекции = 0;
		СводкаОбменаДанными.ЗапланированоВыгрузитьОбъектовКоллекции = СводкаПоКоллекции.КоличествоДанных;
		
		РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
		
		ОбработаноПолностью = Истина;
		Если Категориальный Тогда
			ОбработаноПолностью = ВыгрузитьКатегориальнуюКоллекцию(Коллекция);
		Иначе
			ОбработаноПолностью = ВыгрузитьПериодическуюКоллекцию(Коллекция,
				СводкаПоКоллекции.ДатаПервойЗаписи,
				СводкаПоКоллекции.ДатаПоследнейЗаписи);
		КонецЕсли;
		
		Если Не ОбработаноПолностью Тогда
			Прервать;
		КонецЕсли;
		ВыгруженоКоллекций = ВыгруженоКоллекций + 1;
		
		СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
		СводкаОбменаДанными.ВыгруженоКоллекций = ВыгруженоКоллекций;
		РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
		
	КонецЦикла;
	
	СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
	СводкаОбменаДанными.ОбменДаннымиАктивен = Ложь;
	СводкаОбменаДанными.ДатаЗавершения = ТекущаяДатаСеанса();
	РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
	
КонецПроцедуры

#КонецОбласти

#Область Модели

// Проверить создать модель.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПроверитьСоздатьМодель(ВидПлана) Экспорт
	
	ПроверитьВидПлана(ВидПлана);
	
	СтруктураОтвета = Новый Структура("ТекстОшибки");
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования") <= 0 Тогда
		Ответ = СоздатьМодель(ВидПлана);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Создать модель.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция СоздатьМодель(ВидПлана) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели > 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстМоделиУжеНазначенИдентификатор();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ИмяМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "Наименование");
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("models");
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	ТелоЗапроса = Новый Структура();
	ТелоЗапроса.Вставить("modelName",
		ИмяМодели);
	ТелоЗапроса.Вставить("service",
		НастройкиСервиса.ИмяСервиса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
		Если СтруктураОтвета.КодСостояния >= 400 И СтруктураОтвета.КодСостояния < 600 Тогда
			СвойстваЗапросаСОшибкой = СвойстваЗапросаВСервис();
			СвойстваЗапросаСОшибкой.Метод = Метод;
			СвойстваЗапросаСОшибкой.Адрес = URLЗапроса;
			СвойстваЗапросаСОшибкой.Тело  = ТелоЗапросаJSON;
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "СозданиеМодели",
				ВидПлана, СвойстваЗапросаСОшибкой);
		Иначе
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "СозданиеМодели",
				ВидПлана);
		КонецЕсли;
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

#КонецОбласти

#Область КонфигурацииМоделей

// Проверить создать конфигурацию модели.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПроверитьСоздатьКонфигурациюМодели(ВидПлана) Экспорт
	
	ПроверитьВидПлана(ВидПлана);
	Ответ = СоздатьКонфигурациюМодели(ВидПлана);
	Возврат Ответ;
	
КонецФункции

// Создать конфигурацию модели.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция СоздатьКонфигурациюМодели(ВидПлана) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели <= 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНеПолученИдентификаторМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ТребуемыеРеквизитыДополнительно = "ЗаполнятьПоХарактеристикамНоменклатуры, ЗаполнятьПартнераВТЧ, ЗаполнятьСкладВТЧ, ТипПлана";
	ТребуемыеРеквизиты = СервисПрогнозированияПереопределяемыйКлиентСервер.РеквизитыВидаПланаДляСервисаПрогнозирования();
	ТребуемыеРеквизиты = ТребуемыеРеквизиты + ", " + ТребуемыеРеквизитыДополнительно;
	НастройкиВидаПлана = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПлана, ТребуемыеРеквизиты);
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("models");
	ПараметрыАдреса.Добавить(ИдМодели);
	ПараметрыАдреса.Добавить("config");
	
	Метод = "PUT";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Если РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.КоличествоВыгруженныхКоллекций() = 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНетКоллекцийДляВыгрузки();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	НеЗаполняетсяСклад          = Не НастройкиВидаПлана.ЗаполнятьСкладВТЧ;
	НеЗаполняетсяКлиент         = Не НастройкиВидаПлана.ЗаполнятьПартнераВТЧ;
	НеЗаполняетсяХарактеристика = Не НастройкиВидаПлана.ЗаполнятьПоХарактеристикамНоменклатуры;
	
	ОсновнаяКоллекция      = Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи;
	КлючиОсновнойКоллекции = КлючиОсновнойКоллекции();
	
	ДополнительныеКоллекции            = Новый Массив(); // extraCollections - list of dict.
	ОбъектыПрогнозированияПоКоллекциям = Новый Массив(); // userDataSources - list of dict.
	
	ИнфоОсновнойКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(
		ОсновнаяКоллекция);
	Если Не ЗначениеЗаполнено(ИнфоОсновнойКоллекции.ИдКоллекции)
		Или Число(ИнфоОсновнойКоллекции.ИдКоллекции) < 1 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНеПолученИдентификаторОсновнойКоллекции();
	КонецЕсли;
	ИДОсновнойКоллекции = СтрЗаменить(ИнфоОсновнойКоллекции.ИдКоллекции, Символы.НПП, ""); // mainCollection - string.
	
	ОписаниеОсновнойКоллекции = НастройкиСервиса.Коллекции[ОсновнаяКоллекция];
	Для Каждого ВложенныйКлючЗначение Из ОписаниеОсновнойКоллекции.ВложенноеОписание Цикл
		ОписаниеРеквизита = ВложенныйКлючЗначение.Значение;
		Если Не ОписаниеРеквизита.Выгружать Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяРеквизитаВСервисе = ОписаниеРеквизита.ИмяВСервисе;
		Если ИмяРеквизитаВСервисе = "shop_id" И НеЗаполняетсяСклад Тогда
			Продолжить;
		ИначеЕсли ИмяРеквизитаВСервисе = "customer_id" И НеЗаполняетсяКлиент Тогда
			Продолжить;
		ИначеЕсли ИмяРеквизитаВСервисе = "item_variant_id" И НеЗаполняетсяХарактеристика Тогда
			Продолжить;
		КонецЕсли;
		Если ИмяРеквизитаВСервисе = "quantity" Тогда
			Продолжить; // target.
		КонецЕсли;
		
		ОбъектПрогнозирования = Новый Структура();
		ОбъектПрогнозирования.Вставить("collectionId",      ИДОсновнойКоллекции);
		ОбъектПрогнозирования.Вставить("fieldName",         ИмяРеквизитаВСервисе);
		Если ОписаниеРеквизита.ТипДанных = "string" Тогда
			Если КлючиОсновнойКоллекции.Найти(ИмяРеквизитаВСервисе) <> Неопределено Тогда
				ОбъектПрогнозирования.Вставить("forecastObjectKey", 1);
				Если ИмяРеквизитаВСервисе = "item_id" Тогда
					Если НастройкиВидаПлана.ТипПлана = Перечисления.ТипыПланов.ПланПродажПоКатегориям
						И НастройкиВидаПлана.ВариантПрогнозированияПоКатегориям = 0 Тогда
						ОбъектПрогнозирования.Вставить("forecastObjectKey", 0); // "category_processing" = "group".
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ОписаниеРеквизита.ТипДанных = "number" Тогда
			Если СтрНайти(ИмяРеквизитаВСервисе, "price") Тогда
				ОбъектПрогнозирования.Вставить("knownFuture", 1);
			КонецЕсли;
			ОбъектПрогнозирования.Вставить("aggregationMode", "mean");
			Если СтрНайти(ИмяРеквизитаВСервисе, "price") Тогда
				СпособЗаполненияПропущенных = Новый Структура();
				СпособЗаполненияПропущенных.Вставить("method", "ffill");
				ОбъектПрогнозирования.Вставить("conditionalMissedFillerMode", СпособЗаполненияПропущенных);
				Если НастройкиВидаПлана.ВзвешиваниеОбъектовПриПодсчетеМетрики > 0 Тогда
					ОбъектПрогнозирования.Вставить("usedForMetricWeighing", 1);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		ОбъектыПрогнозированияПоКоллекциям.Добавить(ОбъектПрогнозирования);
	КонецЦикла;
	
	// Обработка данных дополнительных коллекций.
	Для Каждого КлючЗначение Из НастройкиСервиса.Коллекции Цикл
		
		Если КлючЗначение.Ключ = ОсновнаяКоллекция Тогда
			Продолжить; // Обработка данных основной коллекции происходит отдельно выше.
		КонецЕсли;
		
		ОписаниеКоллекции = КлючЗначение.Значение;
		Если Не ОписаниеКоллекции.Выгружать Тогда
			Продолжить;
		КонецЕсли;
		
		ИнфоКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(КлючЗначение.Ключ);
		Если Не ЗначениеЗаполнено(ИнфоКоллекции.ИдКоллекции)
			Или Число(ИнфоКоллекции.ИдКоллекции) < 1 Тогда
			Продолжить;
		КонецЕсли;
		
		ИДКоллекции             = СтрЗаменить(ИнфоКоллекции.ИдКоллекции, Символы.НПП, "");
		ДополнительнаяКоллекция = Новый Структура("collectionId", ИДКоллекции);
		КлючиСоединения         = Новый Массив(); // mainCollectionJoinKeys, extraCollectionJoinKeys - list of string.
		Для Каждого ВложенныйКлючЗначение Из ОписаниеКоллекции.ВложенноеОписание Цикл
			ОписаниеРеквизита = ВложенныйКлючЗначение.Значение;
			Если Не ОписаниеРеквизита.Выгружать Тогда
				Продолжить;
			КонецЕсли;
			
			ИмяРеквизитаВСервисе = ОписаниеРеквизита.ИмяВСервисе;
			Если КлючиОсновнойКоллекции.Найти(ИмяРеквизитаВСервисе) <> Неопределено Тогда // item_id, shop_id, customer_id, item_variant_id.
				
				Если ИмяРеквизитаВСервисе = "shop_id" И НеЗаполняетсяСклад Тогда
					Продолжить;
				ИначеЕсли ИмяРеквизитаВСервисе = "customer_id" И НеЗаполняетсяКлиент Тогда
					Продолжить;
				ИначеЕсли ИмяРеквизитаВСервисе = "item_variant_id" И НеЗаполняетсяХарактеристика Тогда
					Продолжить;
				КонецЕсли;
				КлючиСоединения.Добавить(ИмяРеквизитаВСервисе);
				Продолжить; // Ключи соединения с коллекцией продаж.
				
			КонецЕсли;
			
			ОбъектПрогнозирования = Новый Структура();
			ОбъектПрогнозирования.Вставить("collectionId", ИДКоллекции);
			ОбъектПрогнозирования.Вставить("fieldName",    ИмяРеквизитаВСервисе);
			Если ОписаниеРеквизита.ТипДанных = "string" Тогда
				
				Если ИмяРеквизитаВСервисе = "category_id"
					И НастройкиВидаПлана.ТипПлана = Перечисления.ТипыПланов.ПланПродажПоКатегориям Тогда
					ОбъектПрогнозирования.Вставить("forecastObjectKey", 1); // "category_processing" = "group or reduce".
				КонецЕсли;
				
			ИначеЕсли ОписаниеРеквизита.ТипДанных = "number" Тогда
				ОбъектПрогнозирования.Вставить("aggregationMode", "mean");
				Если ИмяРеквизитаВСервисе = "stock_quantity" Тогда
					СпособЗаполненияПропущенных = Новый Структура();
					СпособЗаполненияПропущенных.Вставить("method", "ffill");
					ОбъектПрогнозирования.Вставить("conditionalMissedFillerMode", СпособЗаполненияПропущенных);
				КонецЕсли;
			КонецЕсли;
			ОбъектыПрогнозированияПоКоллекциям.Добавить(ОбъектПрогнозирования);
			
		КонецЦикла;
		ДополнительнаяКоллекция.Вставить("mainCollectionJoinKeys",  КлючиСоединения);
		ДополнительнаяКоллекция.Вставить("extraCollectionJoinKeys", КлючиСоединения);
		ДополнительныеКоллекции.Добавить(ДополнительнаяКоллекция);
	КонецЦикла;
	
	СпособРасчетаСреднегоИСтандартногоОтклонения = "auto";
	Если НастройкиВидаПлана.РассчитыватьОтклонениеПоСезоннымЗначениям = 1 Тогда
		СпособРасчетаСреднегоИСтандартногоОтклонения = "season";
	ИначеЕсли НастройкиВидаПлана.РассчитыватьОтклонениеПоСезоннымЗначениям = 2 Тогда
		СпособРасчетаСреднегоИСтандартногоОтклонения = "trend";
	КонецЕсли;
	
	ВерхняяГраницаВыброса         = НастройкиВидаПлана.ВерхняяГраницаВыброса;
	НижняяГраницаВыброса          = НастройкиВидаПлана.НижняяГраницаВыброса;
	Если НастройкиВидаПлана.СглаживаниеВыбросовИсторическихДанных = 0 Тогда
		СпособСглаживанияВыбросов = "no";
		ВерхняяГраницаВыброса     = 3;
		НижняяГраницаВыброса      = 3;
	ИначеЕсли НастройкиВидаПлана.СглаживаниеВыбросовИсторическихДанных = 1 Тогда
		СпособСглаживанияВыбросов = "bound";
	Иначе
		СпособСглаживанияВыбросов = "mean";
	КонецЕсли;
	
	СвойстваОбработкиВыбросов = Новый Структура();
	СвойстваОбработкиВыбросов.Вставить("smoothingType",      СпособСглаживанияВыбросов);
	СвойстваОбработкиВыбросов.Вставить("stdUpperBound",      ВерхняяГраницаВыброса);
	СвойстваОбработкиВыбросов.Вставить("stdLowerBound",      НижняяГраницаВыброса);
	СвойстваОбработкиВыбросов.Вставить("stdCalculatingType", СпособРасчетаСреднегоИСтандартногоОтклонения);
	
	СвойстваПоказателяОсновнойКоллекции = Новый Структура(); // target - Структура из fieldName, aggregationMode, missedFillerMode, isNonnegative.
	СвойстваПоказателяОсновнойКоллекции.Вставить("fieldName",       "quantity");
	СвойстваПоказателяОсновнойКоллекции.Вставить("aggregationMode", "sum");
	СвойстваПоказателяОсновнойКоллекции.Вставить("isNonnegative",   1);
	
	НомераКвантилейПрогноза = Новый Массив();
	НомераКвантилейПрогноза.Добавить(1);
	НомераКвантилейПрогноза.Добавить(10);
	НомераКвантилейПрогноза.Добавить(25);
	НомераКвантилейПрогноза.Добавить(50);
	НомераКвантилейПрогноза.Добавить(90);
	НомераКвантилейПрогноза.Добавить(99);
	
	ТелоЗапроса = Новый Структура();
	
	// Базовые параметры.
	ТелоЗапроса.Вставить("mainCollection", ИДОсновнойКоллекции);
	ТелоЗапроса.Вставить("dateGranularity", ПреобразоватьВТипСервиса(НастройкиВидаПлана.Периодичность)); // timeframe в ПП.
	Если НастройкиВидаПлана.Периодичность = Перечисления.Периодичность.Неделя Тогда
		ДеньНеделиНачалаПрогноза = ПреобразоватьВТипСервиса(НастройкиВидаПлана.ДеньНеделиНачалаПрогноза); // Строка -
		ТелоЗапроса.Вставить("startWeekDay", ДеньНеделиНачалаПрогноза);
	КонецЕсли;
	ТелоЗапроса.Вставить("horizon",         НастройкиВидаПлана.КоличествоПериодов);
	ТелоЗапроса.Вставить("target",          СвойстваПоказателяОсновнойКоллекции);
	ТелоЗапроса.Вставить("dtStartForecast", ПреобразоватьВТипСервиса(НастройкиВидаПлана.НачалоПрогнозирования));
	
	// Параметры связей с дополнительными коллекциями.
	ТелоЗапроса.Вставить("extraCollections", ДополнительныеКоллекции);
	// Параметры пользовательских реквизитов, используемых для подсчета и разреза данных.
	ТелоЗапроса.Вставить("userDataSources",  ОбъектыПрогнозированияПоКоллекциям);
	
	// Продолжение базовых параметров.
	ТелоЗапроса.Вставить("userMetric",          НастройкиВидаПлана.МетрикаОценкиКачестваПрогноза);
	ТелоЗапроса.Вставить("predictionQuantiles", НомераКвантилейПрогноза);
	
	// Параметры способа обработки выбросов.
	ТелоЗапроса.Вставить("outliersProcessing", СвойстваОбработкиВыбросов);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
		Если СтруктураОтвета.КодСостояния >= 400 И СтруктураОтвета.КодСостояния < 600 Тогда
			СвойстваЗапросаСОшибкой = СвойстваЗапросаВСервис();
			СвойстваЗапросаСОшибкой.Метод = Метод;
			СвойстваЗапросаСОшибкой.Адрес = URLЗапроса;
			СвойстваЗапросаСОшибкой.Тело  = ТелоЗапросаJSON;
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "СозданиеКонфигурацииМодели",
				ВидПлана, СвойстваЗапросаСОшибкой);
		Иначе
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "СозданиеКонфигурацииМодели",
				ВидПлана);
		КонецЕсли;
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

#КонецОбласти

#Область ОбучениеМоделей

// Получить типы сервисов.
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьТипыСервисов() Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьАвторизационныеНастройкиСервиса(); // Запрос авторизационных данных.
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("forecast");
	
	Метод = "GET";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
		
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
		
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ОбновлениеИнформацииОМоделях");
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Запустить обучение.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ЗапуститьОбучение(ВидПлана) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели <= 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНеПолученИдентификаторМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	Идентификаторы = РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ИдентификаторыОбученияВидаПлана(ВидПлана);
	Если Идентификаторы <> Неопределено
		И ЗначениеЗаполнено(Идентификаторы.ИдОбучения)
		И (Идентификаторы.СтатусОбучения = СтатусОбучается()
			Или Идентификаторы.СтатусОбучения = СтатусОжидаетОбучения()
			Или Идентификаторы.СтатусОбучения = СтатусГотовКПолучению()) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОжиданиеЗавершенияОбученияМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	Если ИдетОбменДанными() Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОбучениеНевозможноИдетВыгрузкаДанных();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить(НастройкиСервиса.ИмяСервиса);
	ПараметрыАдреса.Добавить("models");
	ПараметрыАдреса.Добавить(ИдМодели);
	ПараметрыАдреса.Добавить("train");
	
	ПараметрыЗапроса = Новый Соответствие();
	ПараметрыЗапроса.Вставить("ServiceType",  НастройкиСервиса.ИмяСервиса);
	ПараметрыЗапроса.Вставить("ModelID",      Число(ИдМодели));
	ПараметрыЗапроса.Вставить("make_default", 1);
	
	Метод = "GET";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса, ПараметрыЗапроса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	ТелоЗапроса = Новый Структура();
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
		Если СтруктураОтвета.КодСостояния >= 400 И СтруктураОтвета.КодСостояния < 600 Тогда
			СвойстваЗапросаСОшибкой = СвойстваЗапросаВСервис();
			СвойстваЗапросаСОшибкой.Метод = Метод;
			СвойстваЗапросаСОшибкой.Адрес = URLЗапроса;
			СвойстваЗапросаСОшибкой.Тело  = ТелоЗапросаJSON;
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ЗапускОбученияМодели",
				ВидПлана, СвойстваЗапросаСОшибкой);
		Иначе
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ЗапускОбученияМодели",
				ВидПлана);
		КонецЕсли;
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Получить статус обучения.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов -
// 
// Возвращаемое значение:
//  Структура - из:
//  * ТекстОшибки - Строка -
//  * ВидПлана - СправочникСсылка.ВидыПланов -
//  * ИдОбучения - Строка -
//  * Статус - Строка, Неопределено -
Функция ПолучитьСтатусОбучения(ВидПлана) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели <= 0 Тогда
		ТекстОшибки = ТекстНеПолученИдентификаторМодели();
		РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ДобавитьТекстОшибкиКОбучениюПоВидуПлана(ВидПлана,
			ТекстОшибки);
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ОтветФункции = Новый Структура();
	ОтветФункции.Вставить("ТекстОшибки", "");
	
	ИдентификаторыОбучения = РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ИдентификаторыОбученияВидаПлана(ВидПлана);
	Если ИдентификаторыОбучения = Неопределено Тогда
		ОтветФункции.ТекстОшибки = ТекстНеНайденаИнформацияОбученияМодели();
		Возврат ОтветФункции;
	КонецЕсли;
	ИдОбучения = ИдентификаторыОбучения.ИдОбучения;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить(НастройкиСервиса.ИмяСервиса);
	ПараметрыАдреса.Добавить("models");
	ПараметрыАдреса.Добавить(ИдМодели);
	ПараметрыАдреса.Добавить("result");
	ПараметрыАдреса.Добавить(ИдОбучения);
	
	Метод = "GET";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",  НастройкиСервиса.ТокенПриложения);
	Заголовки.Вставить("ServiceType", НастройкиСервиса.ИмяСервиса);
	Заголовки.Вставить("ModelID",     ИдМодели);
	Заголовки.Вставить("resultUUID",  ИдОбучения);
	
	СтруктураОтвета = Неопределено;
	СтатусОбучения = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		
		ДополнительныйПараметр = Новый Структура("ВидПлана, ИдОбучения", ВидПлана, ИдОбучения);
		Если СтруктураОтвета.КодСостояния >= 400 И СтруктураОтвета.КодСостояния < 600 Тогда
			СвойстваЗапросаСОшибкой = СвойстваЗапросаВСервис();
			СвойстваЗапросаСОшибкой.Метод = Метод;
			СвойстваЗапросаСОшибкой.Адрес = URLЗапроса;
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ОбновлениеИнформацииОСтатусеОбучения",
				ДополнительныйПараметр, СвойстваЗапросаСОшибкой);
		Иначе
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ОбновлениеИнформацииОСтатусеОбучения",
				ДополнительныйПараметр);
		КонецЕсли;
		СтатусОбучения = Ответ.ВозвращаемоеЗначение;
		ОтветФункции.Вставить("ТекстОшибки",Ответ.ТекстОшибки);
	КонецЕсли;
	
	ОтветФункции.Вставить("ВидПлана", ВидПлана);
	ОтветФункции.Вставить("ИдОбучения", ИдОбучения);
	ОтветФункции.Вставить("Статус", СтатусОбучения);
	
	Возврат ОтветФункции;
	
КонецФункции

#Область ПолучениеПрогнозов

// Получить прогноз сервиса.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - Вид плана
//  ИдОбучения - Строка - Ид обучения
//  ИмяМодели - Строка - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьПрогнозСервиса(ВидПлана, ИдОбучения, ИмяМодели = Неопределено) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели <= 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНеПолученИдентификаторМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить(НастройкиСервиса.ИмяСервиса);
	ПараметрыАдреса.Добавить("models");
	ПараметрыАдреса.Добавить(ИдМодели);
	
	Если ЗначениеЗаполнено(ИмяМодели)
		И ИмяМодели <> "auto" Тогда
		ТекстАдресаНаборДанных = "process?target_data=predictions_full";
	Иначе
		ТекстАдресаНаборДанных = "process?target_data=predictions";
		ИмяМодели = "auto";
	КонецЕсли;
	ПараметрыАдреса.Добавить(ТекстАдресаНаборДанных);
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP", НастройкиСервиса.ТокенПриложения);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		СтруктураОтветаКачествоМодели = Новый Массив();
		
		ДополнительныйПараметр = Новый Структура("ВидПлана, ИдОбучения, КачествоМодели",
			ВидПлана, ИдОбучения, СтруктураОтветаКачествоМодели);
		ДополнительныйПараметр.Вставить("ИмяМодели", ИмяМодели);
		Если СтруктураОтвета.КодСостояния >= 400 И СтруктураОтвета.КодСостояния < 600 Тогда
			СвойстваЗапросаСОшибкой = СвойстваЗапросаВСервис();
			СвойстваЗапросаСОшибкой.Метод = Метод;
			СвойстваЗапросаСОшибкой.Адрес = URLЗапроса;
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучениеПрогнозаСервиса",
				ДополнительныйПараметр, СвойстваЗапросаСОшибкой);
		Иначе
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучениеПрогнозаСервиса",
				ДополнительныйПараметр);
		КонецЕсли;
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Получить качество моделей.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьКачествоМоделей(ВидПлана) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели <= 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНеПолученИдентификаторМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить(НастройкиСервиса.ИмяСервиса);
	ПараметрыАдреса.Добавить("models");
	ПараметрыАдреса.Добавить(ИдМодели);
	ПараметрыАдреса.Добавить("process?target_data=models_quality");
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
		
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучениеКачестваМоделей",
			Неопределено);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Получить качество моделей по объектам.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьКачествоМоделейПоОбъектам(ВидПлана) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели <= 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНеПолученИдентификаторМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить(НастройкиСервиса.ИмяСервиса);
	ПараметрыАдреса.Добавить("models");
	ПараметрыАдреса.Добавить(ИдМодели);
	ПараметрыАдреса.Добавить("process?target_data=models_item_quality");
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучениеКачестваМоделей",
			Неопределено);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Получить фактические значения и предсказания на тестовых периодах.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьФактическиеЗначенияИПредсказанияНаТестовыхПериодах(ВидПлана) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели <= 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНеПолученИдентификаторМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить(НастройкиСервиса.ИмяСервиса);
	ПараметрыАдреса.Добавить("models");
	ПараметрыАдреса.Добавить(ИдМодели);
	ПараметрыАдреса.Добавить("process?target_data=test_models");
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучитьФактическиеЗначенияИПредсказанияНаТестовыхПериодах",
			Неопределено);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Получить историю обучения.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов -
//  ОтборПоОбъекту - Структура - содержит идентификатор объекта и/или детализации:
// * shop_id - Строка - Идентификатор склада, по которому будут считываться данные
// * item_id - Строка - Идентификатор товара, по которому будут считываться данные
// * customer_id - Строка - Идентификатор клиента, по которому будут считываться данные
// * item_variant_id - Строка - Идентификатор характеристики товара, по которому будут считываться данные
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьИсториюОбучения(ВидПлана, ОтборПоОбъекту) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели <= 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНеПолученИдентификаторМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить(НастройкиСервиса.ИмяСервиса);
	ПараметрыАдреса.Добавить("models");
	ПараметрыАдреса.Добавить(ИдМодели);
	
	КоличествоВПорции = 1000;
	ШаблонЗапроса = "process?target_data=history&limit=%1";
	ПараметрыАдреса.Добавить(СтрШаблон(ШаблонЗапроса, Формат(КоличествоВПорции, "ЧГ=")));
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP", НастройкиСервиса.ТокенПриложения);
	
	ТелоЗапроса = Новый Структура();
	Если ОтборПоОбъекту.Количество() Тогда
		ТелоЗапроса                    = ОтборПоОбъекту;
		УстановленаФильтрацияПоОбъекту = Истина;
	Иначе
		УстановленаФильтрацияПоОбъекту = Ложь;
	КонецЕсли;
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучитьИсториюОбучения",
			Неопределено);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
		СтруктураОтвета.Вставить("ФильтрацияПоОбъекту", УстановленаФильтрацияПоОбъекту);
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Получить прогноз сервиса расширенный.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьПрогнозСервисаРасширенный(ВидПлана) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели <= 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНеПолученИдентификаторМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить(НастройкиСервиса.ИмяСервиса);
	ПараметрыАдреса.Добавить("models");
	ПараметрыАдреса.Добавить(ИдМодели);
	ПараметрыАдреса.Добавить("process?target_data=final_prediction");
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучитьПрогнозСервисаРасширенный",
			Неопределено);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Получить список коллекций на сервере.
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьПереченьКоллекцийНаСервере() Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьАвторизационныеНастройкиСервиса(); // Запрос авторизационных данных.
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("data");
	
	Метод = "GET";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучитьПереченьКоллекцийНаСервере",
			Неопределено);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
КонецФункции

// Получить инфо коллекции на сервере.
// 
// Параметры:
//  ИдКоллекции - Число - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьИнфоКоллекцииНаСервере(ИдКоллекции) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьАвторизационныеНастройкиСервиса(); // Запрос авторизационных данных.
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("data");
	ПараметрыАдреса.Добавить(Число(ИдКоллекции));
	ПараметрыАдреса.Добавить("info");
	
	Метод = "GET";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучитьИнфоКоллекцииНаСервере",
			Неопределено);
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПолучениеДополнительнойИнформации

// Статус обучения модели.
// 
// Параметры:
//  СценарийПрогнозирования - СправочникСсылка.СценарииТоварногоПланирования - 
//  ВидПлана - СправочникСсылка.ВидыПланов - 
//  ПрерватьОбучение - Булево - 
//  ОжидаетсяИнформацияОПрогнозе - Булево - 
//  ОжидаетсяЗагрузкаПрогноза - Булево - 
// 
// Возвращаемое значение:
//  см. СтатусОбученияМоделиОтвет.
Функция СтатусОбученияМодели(СценарийПрогнозирования, ВидПлана, ПрерватьОбучение,
	ОжидаетсяИнформацияОПрогнозе = Ложь,
	ОжидаетсяЗагрузкаПрогноза = Ложь) Экспорт
	
	Отказ = Ложь;
	ЕстьИнформацияОСтатусеОбучения = Истина;
	Если Не ЗначениеЗаполнено(ВидПлана) Тогда
		СтатусОбучения = Новый ФорматированнаяСтрока(НСтр("ru='Нет прогнозов. Не выбран вид плана.'"));
		ЕстьИнформацияОСтатусеОбучения = Ложь;
		Отказ = Истина;
	ИначеЕсли Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ЗаполнятьПоДаннымСервиса") Тогда
		СтатусОбучения = Новый ФорматированнаяСтрока(НСтр("ru='Нет прогнозов. Вид плана не настроен для работы с сервисом прогнозирования.'"));
		ЕстьИнформацияОСтатусеОбучения = Ложь;
		Отказ = Истина;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	Если СценарийПрогнозирования = НастройкиСервиса.СценарийПрогнозированияФактИПлан
		И ЗначениеЗаполнено(СценарийПрогнозирования) Тогда
		ШаблонТекстаОшибки = НСтр("ru='Сценарий прогнозирования %1 запрещен для выбора, поскольку используется для подмены данных о продажах.'");
		ТекстОшибки = СтрШаблон(ШаблонТекстаОшибки, СценарийПрогнозирования);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "СценарийПрогнозирования");
		СценарийПрогнозирования = Справочники.СценарииТоварногоПланирования.ПустаяСсылка();
		Отказ = Истина;
	ИначеЕсли СценарийПрогнозирования = НастройкиСервиса.СценарийПрогнозированияКонтрольныхПланов
		И ЗначениеЗаполнено(СценарийПрогнозирования) Тогда
		ШаблонТекстаОшибки = НСтр("ru='Сценарий прогнозирования %1 запрещен для выбора, поскольку используется для выгрузки экспертных планов продаж.'");
		ТекстОшибки = СтрШаблон(ШаблонТекстаОшибки, СценарийПрогнозирования);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "СценарийПрогнозирования");
		СценарийПрогнозирования = Справочники.СценарииТоварногоПланирования.ПустаяСсылка();
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Ответ = СтатусОбученияМоделиОтвет(ЕстьИнформацияОСтатусеОбучения, СтатусОбучения);
		Возврат Ответ;
	КонецЕсли;
	
	ИнфоМодели = РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ИнформацияОбОбученииМодели(ВидПлана);
	
	ДатаПостроенияПрогноза        = Дата(1, 1, 1);
	ДатаОбновленияСтатусаОбучения = Дата(1, 1, 1);
	ИдОбучения                    = "";
	СтатусОбучения                = "";
	ТекстОшибки                   = "";
	Готовность                    = 0;
	
	Если ИнфоМодели.ЗагруженнаяМодель <> Неопределено Тогда
		ДатаПостроенияПрогноза = ИнфоМодели.ЗагруженнаяМодель.ВремяИзмененияСостояния;
	КонецЕсли;
	
	Если ИнфоМодели.ОбучающаясяМодель <> Неопределено Тогда
		ДатаОбновленияСтатусаОбучения = ИнфоМодели.ОбучающаясяМодель.ВремяИзмененияСостояния;
		ИдОбучения                    = ИнфоМодели.ОбучающаясяМодель.ИдОбучения;
		СтатусОбучения                = НРег(ИнфоМодели.ОбучающаясяМодель.СтатусОбучения);
		Готовность                    = ИнфоМодели.ОбучающаясяМодель.Готовность;
		ТекстОшибки                   = ИнфоМодели.ОбучающаясяМодель.ТекстОшибки;
	КонецЕсли;
	
	Если ПрерватьОбучение Тогда
		СтатусОбучения = СтатусПрерваноОбучение();
		РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ЗаполнитьИдентификаторОбученияПоВидуПлана(
			ВидПлана,
			ИдОбучения,
			СтатусОбучения,
			ТекстОшибки,
			Готовность);
		СтатусОбучения = НРег(СтатусОбучения);
		ПрерваноОбучение = Истина;
	Иначе
		ПрерваноОбучение = СтрНайти(СтатусОбучения, "прервано") > 0;
	КонецЕсли;
	
	ЕстьОшибка = Не ПустаяСтрока(ТекстОшибки) Или СтрНайти(СтатусОбучения, "ошибка") > 0;
	ГотовКПолучению = СтрНайти(СтатусОбучения, "готов") > 0;
	
	ПроцентВыполнения = НСтр("ru='готовность: %1%%'");
	ПроцентВыполнения = СтрШаблон(ПроцентВыполнения, Готовность);
	
	СтатусПрогнозаИОбучения = Новый ФорматированнаяСтрока("");
	
	Если ДатаПостроенияПрогноза > ДатаОбновленияСтатусаОбучения Тогда
		// Есть прогноз, обучение еще не запущено.
		
		ШаблонСтроки = "%1: %2, <a href=""ЗапуститьОбучение"">%3</a>, <a href=""ЗагрузитьПрогноз"">%4</a>.";
		ТекстЗаголовок = НСтр("ru='Последний прогноз построен'");
		ТекстГиперссылки = НСтр("ru='запросить новый прогноз'");
		ТекстГиперссылкиПовторнаяЗагрузка = НСтр("ru='получить прогноз по другой модели'");
		СтатусПрогнозаИОбучения = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки,
			ТекстЗаголовок,
			Строка(ДатаПостроенияПрогноза),
			ТекстГиперссылки,
			ТекстГиперссылкиПовторнаяЗагрузка);
		
	ИначеЕсли ДатаПостроенияПрогноза > Дата(1, 1, 1)
		И ДатаПостроенияПрогноза <= ДатаОбновленияСтатусаОбучения Тогда
		// Есть прогноз, запущено повторное обучение.
		
		Если ПрерваноОбучение Тогда
			ШаблонСтроки = "%1: %2. %3: %4 <a href=""ЗапуститьОбучение"">%5</a>.";
			ТекстЗаголовок = НСтр("ru='Последний прогноз построен'");
			ТекстЗаголовок2 = НСтр("ru='Прервано обучение'");
			ТекстГиперссылки = НСтр("ru='запросить новый прогноз'");
			СтатусПрогнозаИОбучения = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки,
				ТекстЗаголовок,
				Строка(ДатаПостроенияПрогноза),
				ТекстЗаголовок2,
				Строка(ТекущаяДатаСеанса()),
				ТекстГиперссылки);
		ИначеЕсли Не ЕстьОшибка
			И ГотовКПолучению Тогда
			ШаблонСтроки = "%1: %2. %3, %4 <a href=""ЗагрузитьПрогноз"">%5</a>.";
			ТекстЗаголовок = НСтр("ru='Последний прогноз построен'");
			ТекстЗаголовок2 = НСтр("ru='Готов к получению'");
			ТекстГиперссылки = НСтр("ru='получить прогноз'");
			СтатусПрогнозаИОбучения = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки,
				ТекстЗаголовок,
				Строка(ДатаПостроенияПрогноза),
				ТекстЗаголовок2,
				ПроцентВыполнения,
				ТекстГиперссылки);
		ИначеЕсли Не ЕстьОшибка Тогда
			ШаблонСтроки                       = "%1: %2. %3, %4 %5, %6.";
			ТекстЗаголовок                     = НСтр("ru='Последний прогноз построен'");
			ТекстЗаголовок2                    = НСтр("ru='Идет обучение'");
			ТекстГиперссылки                   = НСтр("ru='обновить статус'");
			ТекстГиперссылкиПрерыванияОбучения = НСтр("ru='прервать обучение'");
			ШаблонСтроки = СтрШаблон(ШаблонСтроки,
				ТекстЗаголовок,
				Строка(ДатаПостроенияПрогноза),
				ТекстЗаголовок2,
				ПроцентВыполнения,
				"<a href=""ЗагрузитьПрогноз"">%1</a>",
				"<a href=""ПрерватьОбучение"">%2</a>");
			СтатусПрогнозаИОбучения = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки,
				ТекстГиперссылки,
				ТекстГиперссылкиПрерыванияОбучения);
		Иначе
			ШаблонСтроки = "%1: %2, <a href=""ТекстОшибки"">%3</a>, <a href=""ЗапуститьОбучение"">%4</a>, <a href=""ЗагрузитьПрогноз"">%5</a>.";
			ТекстЗаголовок = НСтр("ru='Последний прогноз построен'");
			ТекстГиперссылки = НСтр("ru='запросить новый прогноз'");
			ТекстГиперссылки2 = НСтр("ru='обновить статус'");
			СтатусПрогнозаИОбучения = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки,
				ТекстЗаголовок,
				Строка(ДатаПостроенияПрогноза),
				СтатусОбучения,
				ТекстГиперссылки,
				ТекстГиперссылки2);
		КонецЕсли;
		
	ИначеЕсли ДатаПостроенияПрогноза = Дата(1, 1, 1)
		И ДатаОбновленияСтатусаОбучения > Дата(1, 1, 1) Тогда
		// Нет прогноза, запущено обучение.
		
		Если ПрерваноОбучение Тогда
			
			ШаблонСтроки = "%1. %2: %3. <a href=""ЗапуститьОбучение"">%4</a>.";
			ТекстЗаголовок = НСтр("ru='Нет построенных прогнозов'");
			ТекстЗаголовок2 = НСтр("ru='Прервано обучение'");
			ТекстГиперссылки = НСтр("ru='запросить новый прогноз'");
			СтатусПрогнозаИОбучения = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки,
				ТекстЗаголовок,
				ТекстЗаголовок2,
				Строка(ТекущаяДатаСеанса()),
				ТекстГиперссылки);
		ИначеЕсли Не ЕстьОшибка Тогда
			ШаблонСтроки = "%1. %2, %3 <a href=""ЗагрузитьПрогноз"">%4</a>, <a href=""ПрерватьОбучение"">%5</a>.";
			ТекстЗаголовок = НСтр("ru='Нет построенных прогнозов'");
			ТекстЗаголовок2 = НСтр("ru='Идет обучение'");
			ТекстГиперссылки = НСтр("ru='обновить статус'");
			ТекстГиперссылкиПрерыванияОбучения = НСтр("ru='прервать обучение'");
			СтатусПрогнозаИОбучения = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки,
				ТекстЗаголовок,
				ТекстЗаголовок2,
				ПроцентВыполнения,
				ТекстГиперссылки,
				ТекстГиперссылкиПрерыванияОбучения);
		Иначе
			ШаблонСтроки = "%1, <a href=""ТекстОшибки"">%2</a>, <a href=""ЗапуститьОбучение"">%3</a>, <a href=""ЗагрузитьПрогноз"">%4</a>.";
			ТекстЗаголовок = НСтр("ru='Нет построенных прогнозов'");
			ТекстГиперссылки = НСтр("ru='запросить новый прогноз'");
			ТекстГиперссылки2 = НСтр("ru='обновить статус'");
			СтатусПрогнозаИОбучения = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки,
				ТекстЗаголовок,
				СтатусОбучения,
				ТекстГиперссылки,
				ТекстГиперссылки2);
		КонецЕсли;
		
	Иначе
		// Нет прогноза, нет обучения.
		
		ШаблонСтроки = "%1, <a href=""ЗапуститьОбучение"">%2</a>.";
		ТекстЗаголовок = НСтр("ru='Нет построенных прогнозов'");
		ТекстГиперссылки = НСтр("ru='запросить прогноз'");
		СтатусПрогнозаИОбучения = СтроковыеФункции.ФорматированнаяСтрока(ШаблонСтроки,
			ТекстЗаголовок,
			ТекстГиперссылки);
		
		ЕстьИнформацияОСтатусеОбучения = Ложь;
		
	КонецЕсли;
	
	Если ОжидаетсяИнформацияОПрогнозе Тогда
		СтатусПрогнозаИОбучения = Новый ФорматированнаяСтрока(НСтр("ru='ожидание ответа'"));
	ИначеЕсли ОжидаетсяЗагрузкаПрогноза Тогда
		СтатусПрогнозаИОбучения = Новый ФорматированнаяСтрока(НСтр("ru='ожидание ответа'"));
	КонецЕсли;
	
	МодельОбучается = ИнфоМодели.ОбучающаясяМодель <> Неопределено
		И (ИнфоМодели.ОбучающаясяМодель.СтатусОбучения = СтатусОбучается()
			Или ИнфоМодели.ОбучающаясяМодель.СтатусОбучения = СтатусОжидаетОбучения()
			Или ИнфоМодели.ОбучающаясяМодель.СтатусОбучения = СтатусНеизвестен());
	ЕстьЗагруженнаяМодель = ИнфоМодели.ЗагруженнаяМодель <> Неопределено 
		И ИнфоМодели.ЗагруженнаяМодель.СтатусОбучения = СтатусЗагружен();
	
	Ответ = СтатусОбученияМоделиОтвет(ЕстьИнформацияОСтатусеОбучения,
		СтатусПрогнозаИОбучения,
		Не МодельОбучается
			И ГотовКПолучению,
		Не МодельОбучается
			И ЕстьЗагруженнаяМодель);
	Возврат Ответ;
	
КонецФункции

// Получить стоимость обучения.
// 
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов - 
// 
// Возвращаемое значение:
//  - Неопределено.
//  - см. СтруктураОтвета.
Функция ПолучитьСтоимостьОбученияМодели(ВидПлана) Экспорт
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ИдентификаторМоделиПрогнозирования");
	Если ИдМодели <= 0 Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстНеПолученИдентификаторМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	Идентификаторы = РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ИдентификаторыОбученияВидаПлана(ВидПлана);
	Если Идентификаторы <> Неопределено
		И ЗначениеЗаполнено(Идентификаторы.ИдОбучения)
		И (Идентификаторы.СтатусОбучения = СтатусОбучается()
			Или Идентификаторы.СтатусОбучения = СтатусОжидаетОбучения()
			Или Идентификаторы.СтатусОбучения = СтатусГотовКПолучению()) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОжиданиеЗавершенияОбученияМодели();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	Если ИдетОбменДанными() Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОбучениеНевозможноИдетВыгрузкаДанных();
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("forecast");
	ПараметрыАдреса.Добавить("models");
	ПараметрыАдреса.Добавить(Формат(ИдМодели, "ЧГ=;"));
	ПараметрыАдреса.Добавить("estimate_forecast_cost");
	
	Метод = "GET";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	НастройкиСервиса = ПолучитьАвторизационныеНастройкиСервиса(); // Запрос авторизационных данных.
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		Если СтруктураОтвета.КодСостояния >= 400 И СтруктураОтвета.КодСостояния < 600 Тогда
			СвойстваЗапросаСОшибкой = СвойстваЗапросаВСервис();
			СвойстваЗапросаСОшибкой.Метод = Метод;
			СвойстваЗапросаСОшибкой.Адрес = URLЗапроса;
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучениеСтоимости",
				ВидПлана, СвойстваЗапросаСОшибкой);
		Иначе
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ПолучениеСтоимости",
				ВидПлана);
		КонецЕсли;
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

#КонецОбласти

#Область НастройкиСервиса
// Получить настройки сервиса.
// 
// Возвращаемое значение:
//  см. ШаблонНастроекСервиса.
Функция ПолучитьНастройкиСервиса() Экспорт
	
	ВсеНастройкиСервиса = ШаблонНастроекСервиса();
	
	СохраненныеНастройкиСервиса = Константы.НастройкиСервисаПрогнозирования.Получить().Получить();
	
	Для Каждого КлючЗначение Из ВсеНастройкиСервиса Цикл
		Если СохраненныеНастройкиСервиса <> Неопределено
			И СохраненныеНастройкиСервиса.Свойство(КлючЗначение.Ключ)
			И ЗначениеЗаполнено(СохраненныеНастройкиСервиса[КлючЗначение.Ключ]) Тогда
			
			Если ((ТипЗнч(КлючЗначение.Значение) = Тип("Структура")
				Или ТипЗнч(КлючЗначение.Значение) = Тип("Соответствие"))
					И КлючЗначение.Значение.Количество() > 0) Тогда
				
				Для Каждого КлючЗначениеВнутренний Из КлючЗначение.Значение Цикл
					ЭтоСтруктура = ТипЗнч(СохраненныеНастройкиСервиса[КлючЗначение.Ключ]) = Тип("Структура");
					КлючСтроковый = ТипЗнч(КлючЗначениеВнутренний.Ключ) = Тип("Строка");
					
					КлючСодержитсяВСтруктуре = СохраненныеНастройкиСервиса[КлючЗначение.Ключ] <> Неопределено
						И ЭтоСтруктура
						И КлючСтроковый
						И СохраненныеНастройкиСервиса[КлючЗначение.Ключ].Свойство(КлючЗначениеВнутренний.Ключ)
						И ЗначениеЗаполнено(СохраненныеНастройкиСервиса[КлючЗначение.Ключ][КлючЗначениеВнутренний.Ключ]);
						
					КлючСодержитсяВСоответствии = СохраненныеНастройкиСервиса[КлючЗначение.Ключ] <> Неопределено
						И Не ЭтоСтруктура
						И СохраненныеНастройкиСервиса[КлючЗначение.Ключ].Получить(КлючЗначениеВнутренний.Ключ) <> Неопределено;
					
					Если КлючСодержитсяВСтруктуре Или КлючСодержитсяВСоответствии Тогда
						ВсеНастройкиСервиса[КлючЗначение.Ключ][КлючЗначениеВнутренний.Ключ]
							= СохраненныеНастройкиСервиса[КлючЗначение.Ключ][КлючЗначениеВнутренний.Ключ];
					КонецЕсли;
				КонецЦикла;
				
			Иначе
				ВсеНастройкиСервиса[КлючЗначение.Ключ] = СохраненныеНастройкиСервиса[КлючЗначение.Ключ];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВсеНастройкиСервиса;
	
КонецФункции

// Получить настройки сервиса из шаблона.
// 
// Возвращаемое значение:
//  см. ШаблонНастроекСервиса.
Функция ПолучитьШаблонныеНастройкиСервиса() Экспорт
	
	Возврат ШаблонНастроекСервиса();
	
КонецФункции

// Проверка корректности настроек сервиса и возврат результата.
//
// Параметры:
//  ВидПлана                - СправочникСсылка.ВидыПланов -
//  ТипПлана                - ПеречислениеСсылка.ТипыПланов -
//  СценарийПрогнозирования - СправочникСсылка.СценарииТоварногоПланирования -
//  ПрерватьНаПервойОшибке  - Булево - Признак прерывания проверок при нахождении первой ошибки
//
// Возвращаемое значение:
//  см. ПроверяемыеОшибки.
Функция РезультатПроверкиКорректностиНастроекСервиса(ВидПлана, ТипПлана, СценарийПрогнозирования, ПрерватьНаПервойОшибке) Экспорт
	
	ЕстьОшибки = Ложь;
	Ошибки     = ПроверяемыеОшибки();
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	// Проверка регистрации и активации.
	Если Не ЗначениеЗаполнено(НастройкиСервиса.ТокенПриложения) Тогда
		Ошибки.СтатусПодключения = "НеАвторизован";
		ЕстьОшибки = Истина;
		Если ПрерватьНаПервойОшибке Тогда
			Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
			Возврат Ошибки;
		КонецЕсли;
	ИначеЕсли НастройкиСервиса.СтатусПодключения <> СтатусПодключенияАктивен() Тогда
		Ошибки.СтатусПодключения = "НеАктивирован";
		ЕстьОшибки = Истина;
		Если ПрерватьНаПервойОшибке Тогда
			Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
			Возврат Ошибки;
		КонецЕсли;
	КонецЕсли;
	
	// Проверка актуальности состава данных.
	НеактуальныеКоллекции = НеактуальныеКонфигурацииКоллекций(НастройкиСервиса);
	Если НеактуальныеКоллекции.Количество() > 0
		И Не ИдетОбменДанными() Тогда
		Ошибки.НеактуальныеКоллекции = НеактуальныеКоллекции;
		ЕстьОшибки = Истина;
		Если ПрерватьНаПервойОшибке Тогда
			Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
			Возврат Ошибки;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидПлана) Тогда
		Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
		Возврат Ошибки;
	КонецЕсли;
	
	НастройкиВидаПлана = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПлана,
		"Периодичность, НачалоПрогнозирования, ДеньНеделиНачалаПрогноза, КоличествоПериодов,
		|ЗаполнятьПартнераВТЧ, ЗаполнятьПоХарактеристикамНоменклатуры");
	ЗаполнитьЗначенияСвойств(Ошибки.ДанныеНастроек, НастройкиВидаПлана);
	
	НачалоПрогнозирования = НастройкиВидаПлана.НачалоПрогнозирования;
	Если НастройкиВидаПлана.Периодичность = Перечисления.Периодичность.День Тогда
		МаксимальноДопустимыйРазрывМеждуДатами = 0;
	ИначеЕсли НастройкиВидаПлана.Периодичность = Перечисления.Периодичность.Неделя Тогда
		МаксимальноДопустимыйРазрывМеждуДатами = 7;
		РазницаДеньНедели = НомерДняНеделиНачалаПрогноза(НастройкиВидаПлана.ДеньНеделиНачалаПрогноза)
			- ДеньНедели(НачалоПрогнозирования);
		// При различии дня недели, сервис сдвинет дату начала прогнозирования к следующей соответствующей дню недели.
		// К примеру, указан вторник, но дата на среду. Тогда сервис сдвинет дату на следующий вторник.
		// Или если указана среда и дата на понедельник, то сдвинет дату к среде.
		Если РазницаДеньНедели > 0 Тогда
			НачалоПрогнозирования = НачалоПрогнозирования + 86400 * РазницаДеньНедели;
			Ошибки.ДанныеНастроек.НачалоПрогнозирования = НачалоПрогнозирования;
		ИначеЕсли РазницаДеньНедели < 0 Тогда
			НачалоПрогнозирования = НачалоПрогнозирования + 86400 * (7 + РазницаДеньНедели);
			Ошибки.ДанныеНастроек.НачалоПрогнозирования = НачалоПрогнозирования;
		КонецЕсли;
	ИначеЕсли НастройкиВидаПлана.Периодичность = Перечисления.Периодичность.Месяц Тогда
		МаксимальноДопустимыйРазрывМеждуДатами = 31;
	КонецЕсли;
	МаксимальноДопустимаяДата = Дата(1,1,1);
	
	// Проверка наличия нужной аналитики на сервисе.
	Если ТипПлана =  Перечисления.ТипыПланов.ПланПродажПоКатегориям Тогда
		ДетализацииВыгрузки = НастройкиСервиса.ДетализацииВыгрузки;
		Если Не ДетализацииВыгрузки.КодКатегории Тогда
			Ошибки.ОтсутствиеОжидаемыхДетализаций.КодКатегории = Истина;
			ЕстьОшибки = Истина;
			Если ПрерватьНаПервойОшибке Тогда
				Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
				Возврат Ошибки;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ДетализацииВыгрузки = НастройкиСервиса.ДетализацииВыгрузки;
		Если НастройкиВидаПлана.ЗаполнятьПартнераВТЧ
			И Не ДетализацииВыгрузки.КодКлиента Тогда
				Ошибки.ОтсутствиеОжидаемыхДетализаций.КодКлиента = Истина;
				ЕстьОшибки = Истина;
				Если ПрерватьНаПервойОшибке Тогда
					Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
					Возврат Ошибки;
				КонецЕсли;
		КонецЕсли;
		Если НастройкиВидаПлана.ЗаполнятьПоХарактеристикамНоменклатуры
			И Не ДетализацииВыгрузки.КодХарактеристики Тогда
				Ошибки.ОтсутствиеОжидаемыхДетализаций.КодХарактеристики = Истина;
				ЕстьОшибки = Истина;
				Если ПрерватьНаПервойОшибке Тогда
					Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
					Возврат Ошибки;
				КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НастройкиСервиса.ВыгружатьОстаткиЗаВсеВремя
		Или НастройкиСервиса.ДатаОкончанияВыгрузкиОстатковИПродаж < Дата(2000, 1, 1) Тогда
		ДатыПродаж          = СервисПрогнозированияПереопределяемый.ПолучитьДатыНачалаОкончанияПродаж(НастройкиСервиса, Истина);
		ДатаНачалаПродаж    = ДатыПродаж.ДатаНачалаПродаж;
		ДатаОкончанияПродаж = ДатыПродаж.ДатаОкончанияПродаж;
	Иначе
		ДатаНачалаПродаж    = НастройкиСервиса.ДатаНачалаВыгрузкиОстатковИПродаж;
		ДатаОкончанияПродаж = НастройкиСервиса.ДатаОкончанияВыгрузкиОстатковИПродаж;
	КонецЕсли;
	
	ДанныеДляПроверки = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	ОписаниеДанных    = Новый Структура("НоваяВерсия, Данные", Ложь, "");
	ОписаниеОшибки    = Новый Структура;
	
	НоваяСтрока        = ДанныеДляПроверки.Добавить();
	НоваяСтрока.Дата   = НачалоДня(НачалоПрогнозирования);
	НоваяСтрока.Раздел = "Планирование";
	НоваяСтрока.Объект = СценарийПрогнозирования; // СправочникСсылка.СценарииТоварногоПланирования -
	
	// В зависимости от периодичности определяется минимальный разрыв, от которого прогнозирование невозможно.
	Если НачалоПрогнозирования > ДатаОкончанияПродаж Тогда
		
		РазницаМеждуДатамиВДнях = (НачалоДня(НачалоПрогнозирования) - НачалоДня(ДатаОкончанияПродаж)) / 86400;
		
		Если РазницаМеждуДатамиВДнях > МаксимальноДопустимыйРазрывМеждуДатами Тогда
			Ошибки.ДанныеПоДатам.ДатаОкончанияПродаж       = ДатаОкончанияПродаж;
			Ошибки.ДанныеПоДатам.РазницаМеждуДатами        = РазницаМеждуДатамиВДнях;
			МаксимальноДопустимаяДата = НачалоПрогнозирования
				- (РазницаМеждуДатамиВДнях - МаксимальноДопустимыйРазрывМеждуДатами) * 86400;
			Ошибки.ДанныеПоДатам.МаксимальноДопустимаяДата = МаксимальноДопустимаяДата;
			ЕстьОшибки = Истина;
			Если ПрерватьНаПервойОшибке Тогда
				Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
				Возврат Ошибки;
			КонецЕсли;
			
			// Проверка попадания на дату запрета при уменьшении разрыва данных.
			НоваяСтрока        = ДанныеДляПроверки.Добавить();
			НоваяСтрока.Дата   = НачалоДня(МаксимальноДопустимаяДата);
			НоваяСтрока.Раздел = "Планирование";
			НоваяСтрока.Объект = СценарийПрогнозирования; // СправочникСсылка.СценарииТоварногоПланирования -
		КонецЕсли;
		
	ИначеЕсли НачалоПрогнозирования < ДатаНачалаПродаж Тогда
		Ошибки.ДанныеПоДатам.ДатаНачалаПродаж = ДатаНачалаПродаж;
		ЕстьОшибки = Истина;
		Если ПрерватьНаПервойОшибке Тогда
			Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
			Возврат Ошибки;
		КонецЕсли;
	КонецЕсли;
	
	Если ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ДанныеДляПроверки, ОписаниеДанных, ОписаниеОшибки) Тогда
		
		// При попадании в запрет нескольких дат, вернет данные по минимальной, а не несколько запретов.
		Если ОписаниеОшибки.Запреты.Количество() > 0 Тогда
			СтрокаЗапрета = ОписаниеОшибки.Запреты[0];
			Если СтрокаЗапрета.Дата = МаксимальноДопустимаяДата Тогда
				Ошибки.ДанныеПоДатам.ДатаЗапретаПриИсправленииРазрыва = СтрокаЗапрета.ДатаЗапрета;
			Иначе
				ЗаполнитьЗначенияСвойств(Ошибки.ДанныеЗапрета, СтрокаЗапрета);
				// Проверка недопустимого разрыва данных при сдвигании даты за дату запрета.
				ДатаЗаЗапретом          = КонецДня(СтрокаЗапрета.ДатаЗапрета) + 1;
				РазницаМеждуДатамиВДнях = (ДатаЗаЗапретом - НачалоДня(ДатаОкончанияПродаж)) / 86400;
				Если РазницаМеждуДатамиВДнях > МаксимальноДопустимыйРазрывМеждуДатами Тогда
					Ошибки.ДанныеЗапрета.РазницаМеждуДатамиЗаЗапретом = РазницаМеждуДатамиВДнях;
				КонецЕсли;
			КонецЕсли;
			ЕстьОшибки = Истина;
			Если ПрерватьНаПервойОшибке Тогда
				Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
				Возврат Ошибки;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверка допустимости горизонта по товару, у которого больше всего дней продаж.
	ДанныеТопТовара = СервисПрогнозированияПереопределяемый.ДанныеТопТовараПоЧастотеПродаж(
		НастройкиСервиса, НастройкиВидаПлана.Периодичность, ДатаНачалаПродаж, ДатаОкончанияПродаж);
	
	РазницаПоМинТребуемойИсториейПродаж = (НастройкиВидаПлана.КоличествоПериодов + 1) - ДанныеТопТовара.ПериодовПродаж;
	Если РазницаПоМинТребуемойИсториейПродаж > 0 Тогда
		Ошибки.ДанныеПоГоризонту.КоличествоНеХватающихПериодов = РазницаПоМинТребуемойИсториейПродаж;
		Ошибки.ДанныеПоГоризонту.ТоповыйТовар                  = ДанныеТопТовара.Товар;
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Ошибки.ЕстьПользовательскиеОшибки = ЕстьОшибки;
	
	Возврат Ошибки;
	
КонецФункции

// Попробовать исправить некорректные настройки пользователя.
//
// Параметры:
//  ВидПлана - СправочникСсылка.ВидыПланов -
//  Ошибки   - Массив Из Структура - перечень исправляемых ошибок:
//    * Причина    - Строка - Текстовый идентификатор ошибки.
//    * Исправлено - Булево - Признак исправления ошибки.
//  Данные   - Структура - Данные, на основе которых выполняется исправление ошибок.
//
// Возвращаемое значение:
//  Структура - результат исправления ошибок:
//    * Ошибки - Массив Из Структура:
//        ** Причина    - Строка - Текстовый идентификатор ошибки.
//        ** Исправлено - Булево - Признак исправления ошибки.
//    * ТекстИсправлений - Строка - текст информации по результатам исправления
Функция ИсправитьПользовательскиеОшибки(ВидПлана, Ошибки, Данные) Экспорт
	
	Периодичность                              = Данные.Периодичность;
	ТекущаяДатаНачалаПрогнозирования           = Данные.НачалоПрогнозирования;
	ТекущийГоризонт                            = Данные.КоличествоПериодов;
	ТекущийДеньНедели                          = Неопределено;
	ТребуетсяПерезаписатьГоризонт              = Ложь;
	ТребуетсяПерезаписатьНачалоПрогнозирования = Ложь;
	ТекстИсправленийНастроекСервиса            = "";
	ТекстИсправленийНастроекВидаПлана          = "";
	ТекстПоясненийИсправленияДаты              = "";
	
	Для Каждого Ошибка Из Ошибки Цикл
		
		Если Ошибка.Причина = "ОтсутствиеОжидаемыхДетализаций" Тогда
			
			Настройки = ПолучитьНастройкиСервиса();
			СтрокиИсправленийНастроек = Новый Массив;
			
			ОтсутствиеОжидаемыхДетализаций = Данные.ОтсутствиеОжидаемыхДетализаций;
			Если ОтсутствиеОжидаемыхДетализаций.КодКлиента
				Или ОтсутствиеОжидаемыхДетализаций.КодХарактеристики Тогда
				ОписаниеКоллекции = Настройки.Коллекции[Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи];
				Если ОтсутствиеОжидаемыхДетализаций.КодКлиента Тогда
					ОписаниеКоллекции.ВложенноеОписание.КодКлиента.Выгружать = Истина;
					СтрокиИсправленийНастроек.Добавить(
						НСтр("ru = 'Отмечено к выгрузке вложенное поле ""Код клиента"" у коллекции ""Продажи"".'"));
				КонецЕсли;
				Если ОтсутствиеОжидаемыхДетализаций.КодХарактеристики Тогда
					ОписаниеКоллекции.ВложенноеОписание.КодХарактеристики.Выгружать = Истина;
					СтрокиИсправленийНастроек.Добавить(
						НСтр("ru = 'Отмечено к выгрузке вложенное поле ""Код характеристики"" у коллекции ""Продажи"".'"));
				КонецЕсли;
			КонецЕсли;
			Если ОтсутствиеОжидаемыхДетализаций.КодКатегории Тогда
				ОписаниеКоллекции  = Настройки.Коллекции[Перечисления.КоллекцииСервисаПрогнозированияПродаж.Товары];
				ОписаниеКоллекции.Выгружать                                = Истина;
				ОписаниеКоллекции.ВложенноеОписание.КодКатегории.Выгружать = Истина;
				СтрокиИсправленийНастроек.Добавить(
					НСтр("ru = 'Отмечено к выгрузке вложенное поле ""Код категории"" у коллекции ""Товары"".'"));
			КонецЕсли;
			СтрокиИсправленийНастроек.Добавить(
				НСтр("ru = 'Заново запустите полную выгрузку данных для передачи значений детализаций в сервис.'"));
			
			Попытка
				ОбновитьНастройкиСервиса(Настройки);
				ТекстИсправленийНастроекСервиса   = СтрСоединить(СтрокиИсправленийНастроек, Символы.ПС);
				Ошибка.Исправлено                 = Истина;
			Исключение
				КраткоеПредставлениеОшибки        = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				ТекстИсправленийНастроекВидаПлана = НСтр("ru = 'Не удалось отметить к выгрузке необходимые поля по причине:
					|%1.'");
				ТекстИсправленийНастроекВидаПлана = СтрШаблон(ТекстИсправленийНастроекВидаПлана, КраткоеПредставлениеОшибки);
			КонецПопытки;
			
			Продолжить;
			
		ИначеЕсли Ошибка.Причина = "НеХватаетДанныхПоГоризонту" Тогда
			ТекущийГоризонт               = ТекущийГоризонт - Данные.КоличествоНеХватающихПериодов;
			ТребуетсяПерезаписатьГоризонт = Истина;
			Ошибка.Исправлено             = Истина;
			Продолжить;
		КонецЕсли;
		
		// Ошибки даты начала прогнозирования.
		Если Ошибка.Причина = "ДатаЗапрета" Тогда
			
			Если ТекущаяДатаНачалаПрогнозирования <= Данные.ДатаЗапрета Тогда
				ТекущаяДатаНачалаПрогнозирования           = КонецДня(Данные.ДатаЗапрета) + 1;
				ТребуетсяПерезаписатьНачалоПрогнозирования = Истина;
				Ошибка.Исправлено                          = Истина;
				ТекстПоясненийИсправленияДаты              = ТекстПоясненийИсправленияДаты
					+ Символы.ПС + НСтр("ru = 'попадания в дату запрета'");
			КонецЕсли;
			
		ИначеЕсли Ошибка.Причина = "НачалоПрогнозированияРазрывДатаОкончания" Тогда
			
			Если ТекущаяДатаНачалаПрогнозирования > Данные.МаксимальноДопустимаяДата Тогда
				ТекущаяДатаНачалаПрогнозирования           = Данные.МаксимальноДопустимаяДата;
				ТребуетсяПерезаписатьНачалоПрогнозирования = Истина;
				Ошибка.Исправлено                          = Истина;
				ТекстПоясненийИсправленияДаты              = ТекстПоясненийИсправленияДаты
					+ Символы.ПС + НСтр("ru = 'недопустимого разрыва (отсутствия) данных продаж между датами начала прогнозирования и последней продажи.'");
			КонецЕсли;
			
		ИначеЕсли Ошибка.Причина = "НачалоПрогнозированияМеньшеДатыНачала" Тогда
			
			Если ТекущаяДатаНачалаПрогнозирования < Данные.ДатаНачалаПродаж Тогда
				ТекущаяДатаНачалаПрогнозирования           = Данные.ДатаНачалаПродаж;
				ТребуетсяПерезаписатьНачалоПрогнозирования = Истина;
				Ошибка.Исправлено                          = Истина;
				ТекстПоясненийИсправленияДаты              = ТекстПоясненийИсправленияДаты
					+ Символы.ПС + НСтр("ru = 'начало прогнозирования до даты первой продажи.'");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТребуетсяПерезаписатьГоризонт
		Или ТребуетсяПерезаписатьНачалоПрогнозирования Тогда
		
		Если ТребуетсяПерезаписатьГоризонт Тогда
			ТекстИсправленийНастроекВидаПлана = НСтр("ru = 'Уменьшен горизонт прогнозирования на %1. Текущий горизонт - %2.'");
			ТекстИсправленийНастроекВидаПлана = СтрШаблон(ТекстИсправленийНастроекВидаПлана,
				Данные.КоличествоНеХватающихПериодов, ТекущийГоризонт);
		КонецЕсли;
		Если ТребуетсяПерезаписатьНачалоПрогнозирования Тогда
			Если ПустаяСтрока(ТекстИсправленийНастроекВидаПлана) Тогда
				ТекстИсправленийНастроекВидаПлана = НСтр("ru = 'Сдвинута дата начала прогнозирования к %1 для исправления:'");
			Иначе
				ТекстИсправленийНастроекВидаПлана = ТекстИсправленийНастроекВидаПлана
					+ Символы.ПС + НСтр("ru = 'Сдвинута дата начала прогнозирования к %1 для исправления:'");
			КонецЕсли;
			ТекстИсправленийНастроекВидаПлана = СтрШаблон(ТекстИсправленийНастроекВидаПлана, ТекущаяДатаНачалаПрогнозирования);
			ТекстИсправленийНастроекВидаПлана = ТекстИсправленийНастроекВидаПлана + ТекстПоясненийИсправленияДаты;
			
			Если Периодичность = Перечисления.Периодичность.Неделя Тогда
				ТекущийДеньНедели = ДеньНеделиПоНомеру(ДеньНедели(ТекущаяДатаНачалаПрогнозирования));
			КонецЕсли;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.ВидыПланов");
			ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", ВидПлана);
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			ОбъектВидПлана = ВидПлана.ПолучитьОбъект(); // СправочникОбъект.ВидыПланов
			ОбъектВидПлана.КоличествоПериодов           = ТекущийГоризонт;
			ОбъектВидПлана.НачалоПрогнозирования        = ТекущаяДатаНачалаПрогнозирования;
			Если ТекущийДеньНедели <> Неопределено Тогда
				ОбъектВидПлана.ДеньНеделиНачалаПрогноза = ТекущийДеньНедели;
			КонецЕсли;
			ОбъектВидПлана.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			
			ОтменитьТранзакцию();
			СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.ВидыПланов,
				,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстИсправленийНастроекВидаПлана = ТекстПриИсправленииНастроекВидаПланаПроизошлаОшибка(
				КраткоеПредставлениеОшибки, Строка(ВидПлана));
			Для Каждого Ошибка Из Ошибки Цикл
				Если Ошибка.Причина <> "ОтсутствиеОжидаемыхДетализаций" Тогда
					Ошибка.Исправлено = Ложь;
				КонецЕсли;
			КонецЦикла;
			
		КонецПопытки;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстИсправленийНастроекСервиса) Тогда
		ТекстИсправлений = ТекстИсправленийНастроекВидаПлана;
	Иначе
		ТекстИсправлений = ТекстИсправленийНастроекСервиса + Символы.ПС + Символы.ПС + ТекстИсправленийНастроекВидаПлана;
	КонецЕсли;
	
	РезультатИсправления = Новый Структура;
	РезультатИсправления.Вставить("Ошибки",           Ошибки);
	РезультатИсправления.Вставить("ТекстИсправлений", ТекстИсправлений);
	
	Возврат РезультатИсправления;
	
КонецФункции

// Сбросить настройки сервиса в значения по умолчанию.
//
// Параметры:
//  Настройки - Неопределено, Структура из Произвольный - см. СервисПрогнозирования.ПолучитьНастройкиСервиса
Процедура СброситьНастройкиСервиса(Настройки = Неопределено) Экспорт
	
	СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
									УровеньЖурналаРегистрации.Информация,
									,
									,
									ТекущаяДатаСеанса());
	
	НастройкиПоУмолчанию = ШаблонНастроекСервиса();
	ХранилищеПараметров = Новый ХранилищеЗначения(НастройкиПоУмолчанию);
	ЗаписатьНастройкиСервисаПрогнозирования(ХранилищеПараметров);
	
	УстановитьПараметрыСеансаНастроекСервисаПрогнозирования();
	
	Если Настройки <> Неопределено Тогда
		Настройки = НастройкиПоУмолчанию;
	КонецЕсли;
	
КонецПроцедуры

// Сбросить настройки авторизации в сервисе в значения по умолчанию.
// 
// Параметры:
//  Настройки - Неопределено, Структура из Произвольный - см. СервисПрогнозирования.ПолучитьНастройкиСервиса
Процедура СброситьНастройкиАвторизацииВСервисе(Настройки = Неопределено) Экспорт
	
	СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
	                         УровеньЖурналаРегистрации.Информация,
	                         ,
	                         ,
	                         ТекущаяДатаСеанса());
	
	Если Настройки = Неопределено Тогда
		Настройки = ПолучитьНастройкиСервиса();
	КонецЕсли;
	НастройкиПоУмолчанию = ШаблонНастроекСервиса();
	
	Настройки.АдресПодключения                   = НастройкиПоУмолчанию.АдресПодключения;
	Настройки.ВерсияАПИ                          = НастройкиПоУмолчанию.ВерсияАПИ;
	Настройки.ЛогинИТС                           = НастройкиПоУмолчанию.ЛогинИТС;
	Настройки.ТокенПриложения                    = НастройкиПоУмолчанию.ТокенПриложения;
	Настройки.ТокенОбновления                    = НастройкиПоУмолчанию.ТокенОбновления;
	Настройки.ИдентификаторПриложения            = НастройкиПоУмолчанию.ИдентификаторПриложения;
	Настройки.ДатаОбновленияТокенов              = НастройкиПоУмолчанию.ДатаОбновленияТокенов;
	Настройки.ИмяБазы                            = НастройкиПоУмолчанию.ИмяБазы;
	Настройки.Тариф                              = НастройкиПоУмолчанию.Тариф;
	Настройки.СтатусПодключения                  = НастройкиПоУмолчанию.СтатусПодключения;
	Настройки.Баланс                             = НастройкиПоУмолчанию.Баланс;
	Настройки.РазрешенОбменССервисом             = НастройкиПоУмолчанию.РазрешенОбменССервисом;
	Настройки.ИзмененыНастройкиИнтернетПоддержки = НастройкиПоУмолчанию.ИзмененыНастройкиИнтернетПоддержки;
	Настройки.ЗаявкаНаПодключениеОтправлена      = НастройкиПоУмолчанию.ЗаявкаНаПодключениеОтправлена;
	
	// Устаревшие настройки
	Настройки.ТипАвторизации                     = НастройкиПоУмолчанию.ТипАвторизации;
	Настройки.НазваниеКомпании                   = НастройкиПоУмолчанию.НазваниеКомпании;
	Настройки.ФИОКонтактногоЛица                 = НастройкиПоУмолчанию.ФИОКонтактногоЛица;
	Настройки.ЭлектроннаяПочта                   = НастройкиПоУмолчанию.ЭлектроннаяПочта;
	Настройки.НомерТелефона                      = НастройкиПоУмолчанию.НомерТелефона;
	Настройки.НомерТелефонаЗначенияПолей         = НастройкиПоУмолчанию.НомерТелефонаЗначенияПолей;
	Настройки.НомерТелефонаКомментарий           = НастройкиПоУмолчанию.НомерТелефонаКомментарий;
	Настройки.Логин                              = НастройкиПоУмолчанию.Логин;
	Настройки.Пароль                             = НастройкиПоУмолчанию.Пароль;
	
	ХранилищеПараметров = Новый ХранилищеЗначения(Настройки);
	ЗаписатьНастройкиСервисаПрогнозирования(ХранилищеПараметров);
	
	УстановитьПараметрыСеансаНастроекСервисаПрогнозирования();
	
КонецПроцедуры

// Восстановить удаленные настройки и пересоздать настройки с некорректным типом.
Процедура ВосстановитьНастройкиСервиса() Экспорт
	
	СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
									УровеньЖурналаРегистрации.Информация,
									,
									,
									ТекущаяДатаСеанса());
	
	НастройкиПоУмолчанию = ШаблонНастроекСервиса();
	ТекущиеНастройки = ПолучитьНастройкиСервиса();
	ВосстановитьНастройкиПоШаблону(ТекущиеНастройки, НастройкиПоУмолчанию);
	
	ТекущиеНастройки.Версия = НастройкиПоУмолчанию.Версия;
	
	УстановитьПривилегированныйРежим(Истина);
	Пароль = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища("ПарольПользователяСервисаПрогнозирования",
		"Пароль");
	Если Не ЗначениеЗаполнено(Пароль)
		И ЗначениеЗаполнено(ТекущиеНастройки.Пароль) Тогда
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище("ПарольПользователяСервисаПрогнозирования",
			ТекущиеНастройки.Пароль, "Пароль");
		ТекущиеНастройки.Пароль = "";
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	ОбновитьНастройкиСервиса(ТекущиеНастройки);
	УстановитьПараметрыСеансаНастроекСервисаПрогнозирования();
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Регистрирует список сервисов, которые доступны для использования в конфигурации.
//
// Параметры:
//   СервисыСопровождения - Массив Из Структура - массив структур, описывающий сервисы доступные для использовании в конфигурации;
//     Значение - Структура - см. ПодключениеСервисовСопровождения.НовыйОписательСервиса().
//
Процедура ПриДобавленииОписанийСервисовСопровождения(СервисыСопровождения) Экспорт
	
	ОписательСервиса = ПодключениеСервисовСопровождения.НовыйОписательСервиса();
	ОписательСервиса.Идентификатор   = СервисПрогнозированияПереопределяемыйКлиентСервер.ИдентификаторСервиса();
	ОписательСервиса.Наименование    = НСтр("ru = '1С:Прогнозирование продаж'");
	ОписательСервиса.Описание        = НСтр("ru = 'Автоматизированное прогнозирование продаж товаров и услуг
		| с возможностью выбора оптимального метода и вычислительными мощностями в облаке.'");
	СервисыСопровождения.Добавить(ОписательСервиса);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Разное

// Получает значения часто используемых и редко изменяемых настроек сервиса прогнозирования.
// Если требуется получить настройку не заданную в этой функции, следует вызвать метод
// который получит полный список настроек - ПолучитьНастройкиСервиса().
// 
// Возвращаемое значение:
//  см. ШаблонАвторизационныхНастроекСервиса.
Функция ПолучитьАвторизационныеНастройкиСервиса() Экспорт
	
	Возврат ПараметрыСеанса.НастройкиСервисаПрогнозирования;
	
КонецФункции

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииОбработчиковУстановкиПараметровСеанса.
Процедура ПриДобавленииОбработчиковУстановкиПараметровСеанса(Обработчики) Экспорт
	
	Обработчики.Вставить("НастройкиСервисаПрогнозирования", "СервисПрогнозирования.УстановкаПараметровСеанса");
	
КонецПроцедуры

// Устанавливает параметры сеанса работы с сервисом прогнозирования продаж.
//
// Параметры:
//   ИмяПараметра - Строка - 
//   УстановленныеПараметры - Массив из Строка - 
//
Процедура УстановкаПараметровСеанса(ИмяПараметра, УстановленныеПараметры) Экспорт
	
	Если ИмяПараметра = "НастройкиСервисаПрогнозирования" Тогда
		УстановитьПараметрыСеансаНастроекСервисаПрогнозирования();
		УстановленныеПараметры.Добавить("НастройкиСервисаПрогнозирования");
	КонецЕсли;
	
КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииОбработчиковУстановкиПараметровСеанса
Процедура УстановитьПараметрыСеансаНастроекСервисаПрогнозирования() Экспорт
	
	ТекущиеНастройкиСервисаСтруктура = ШаблонАвторизационныхНастроекСервиса();
	ВсеНастройкиСервиса = ПолучитьНастройкиСервиса();
	ЗаполнитьЗначенияСвойств(ТекущиеНастройкиСервисаСтруктура, ВсеНастройкиСервиса);
	
	УстановитьПривилегированныйРежим(Истина);
	Пароль = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища("ПарольПользователяСервисаПрогнозирования",
		"Пароль");
	ТекущиеНастройкиСервисаСтруктура.Пароль = Пароль;
	
	ПараметрыСеанса.НастройкиСервисаПрогнозирования = Новый ФиксированнаяСтруктура(ТекущиеНастройкиСервисаСтруктура);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Идет обмен данными.
// 
// Возвращаемое значение:
//  Булево -
Функция ИдетОбменДанными() Экспорт
	
	СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
	Если СводкаОбменаДанными.ОбменДаннымиАктивен
		И СводкаОбменаДанными.ТипОбмена = 0 Тогда
		НастройкиСервиса = ПолучитьНастройкиСервиса();
		Если ТекущаяДатаСеанса() > СводкаОбменаДанными.ДатаОбновленияЗаписи + НастройкиСервиса.ТаймаутВыгрузки Тогда
			СводкаОбменаДанными.ОбменДаннымиАктивен = Ложь;
			СводкаОбменаДанными.ЕстьОшибка = Истина;
			СводкаОбменаДанными.ТекстОшибки = ТекстТаймаутВыгрузкиДанных();
			РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
		Иначе
			Возврат Истина;
		КонецЕсли;
	ИначеЕсли СводкаОбменаДанными.ОбменДаннымиАктивен
		И СводкаОбменаДанными.ТипОбмена = 1 Тогда
		НастройкиСервиса = ПолучитьНастройкиСервиса();
		Если ТекущаяДатаСеанса() > СводкаОбменаДанными.ДатаОбновленияЗаписи + НастройкиСервиса.ТаймаутЗагрузки Тогда
			СводкаОбменаДанными.ОбменДаннымиАктивен = Ложь;
			СводкаОбменаДанными.ЕстьОшибка = Истина;
			СводкаОбменаДанными.ТекстОшибки = ТекстТаймаутЗагрузкиДанных();
			РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
		Иначе
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Это уникальный идентификатор.
// 
// Параметры:
//  ТекстСсылки - Строка - 
// 
// Возвращаемое значение:
//  Булево - 
Функция ЭтоУникальныйИдентификатор(ТекстСсылки) Экспорт
	
	Возврат СтрДлина(ТекстСсылки) = 36 И СтрЧислоВхождений(ТекстСсылки, "-") = 4;
	
КонецФункции

// Это пустой уникальный идентификатор.
// 
// Параметры:
//  ТекстСсылки - Строка - 
// 
// Возвращаемое значение:
//  Булево - 
Функция ЭтоПустойИдентификатор(ТекстСсылки) Экспорт
	
	ПустойИдентификатор = Ложь;
	Если ТекстСсылки = "00000000-0000-0000-0000-000000000000"
		Или ПустаяСтрока(ТекстСсылки) Тогда
		ПустойИдентификатор = Истина;
	КонецЕсли;
	
	Возврат ПустойИдентификатор;
	
КонецФункции

// Пустое значение сервиса.
// 
// Параметры:
//  Значение - Произвольный - 
// 
// Возвращаемое значение:
//  Булево - 
Функция ПустоеЗначениеСервиса(Значение) Экспорт
	
	Если Значение = Неопределено
		Или Значение = "x0"
		Или Значение = "NaN"
		Или Значение = "" // category_id может быть пустым.
		Или Значение = "0"
		Или Значение = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Получить уникальный идентификатор.
// 
// Параметры:
//  ТекстИдентификатора - Строка - 
// 
// Возвращаемое значение:
//  УникальныйИдентификатор
Функция ПолучитьУникальныйИдентификатор(ТекстИдентификатора) Экспорт
	
	Если ТекстИдентификатора = Неопределено
		Или ТекстИдентификатора = "x0"
		Или ТекстИдентификатора = "NaN" Тогда
		ПустойИдентификатор = "00000000-0000-0000-0000-000000000000";
		Возврат Новый УникальныйИдентификатор(ПустойИдентификатор);
	КонецЕсли;
	
	Возврат Новый УникальныйИдентификатор(ТекстИдентификатора);
	
КонецФункции

// Дата сервиса в дату программы.
// 
// Параметры:
//  СтрокаДаты - Строка - 
// 
// Возвращаемое значение:
//  Дата - Дата сервиса в дату программы
Функция ДатаСервисаВДатуПрограммы(СтрокаДаты) Экспорт
	
	ЭлементыДаты = СтрРазделить(СтрокаДаты, "-");
	ДатаПродажи = Дата(ЭлементыДаты[0], ЭлементыДаты[1], ЭлементыДаты[2]);
	Возврат ДатаПродажи;
	
КонецФункции

// Обновить настройки сервиса.
// 
// Параметры:
//  НовыеНастройки - см. ШаблонНастроекСервиса.
//  
Процедура ОбновитьНастройкиСервиса(НовыеНастройки) Экспорт
	
	ТекущиеНастройки = ПолучитьНастройкиСервиса();
	
	Для Каждого КлючЗначение Из ТекущиеНастройки Цикл
		
		Если НовыеНастройки.Свойство(КлючЗначение.Ключ)
			И НовыеНастройки[КлючЗначение.Ключ] <> КлючЗначение.Значение Тогда
			ТекущиеНастройки[КлючЗначение.Ключ] = НовыеНастройки[КлючЗначение.Ключ];
		КонецЕсли;
		
	КонецЦикла;
	
	ХранилищеПараметров = Новый ХранилищеЗначения(ТекущиеНастройки);
	
	ЗаписатьНастройкиСервисаПрогнозирования(ХранилищеПараметров);
	
КонецПроцедуры

// Диапазон дат для выгрузки.
// 
// Параметры:
//  Коллекция - ПеречислениеСсылка.КоллекцииСервисаПрогнозированияПродаж - 
//  НастройкиСервиса - Неопределено, Структура из Произвольный - см. СервисПрогнозирования.ПолучитьНастройкиСервиса
//  ДатаНачалаВводаДанных - Дата - Нижняя граница периода, нужна чтобы не перебирать все возможные даты в прошлом.
//  ДатаАктуальности - Дата, Неопределено - Переопределяет текущую позицию (курсор) выгрузки периодической коллекции.
//  ИгнорироватьНастройкиПорционности - Булево - при взводе флага будет возвращен диапазон ДатаАктуальности - ТекущаяДата.
// 
// Возвращаемое значение:
//  Структура - Диапазон дат для выгрузки:
// * ДатаНачала - Дата - 
// * ДатаОкончания - Дата - 
Функция ДиапазонДатДляВыгрузки(Коллекция,
	НастройкиСервиса,
	ДатаНачалаВводаДанных,
	ДатаАктуальности = Неопределено,
	ИгнорироватьНастройкиПорционности = Ложь) Экспорт
	
	День = 86400;
	
	ДатаНачалаВводаДанных = ?(ТипЗнч(ДатаНачалаВводаДанных) = Тип("Null"), Дата(1, 1, 1), ДатаНачалаВводаДанных);
	ДатаНачалаВводаДанных = ?(ТипЗнч(ДатаНачалаВводаДанных) = Тип("Неопределено"), Дата(1, 1, 1), ДатаНачалаВводаДанных);
	
	Если НастройкиСервиса = Неопределено Тогда
		НастройкиСервиса = ПолучитьНастройкиСервиса();
	КонецЕсли;
	ПорцияВыгрузкиПериодическихДанныхВДнях = НастройкиСервиса.ПорцияВыгрузкиПериодическихДанныхВДнях;
	
	Если ДатаАктуальности = Неопределено Тогда
		СводкаПоКоллекции= РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(Коллекция);
		ДатаАктуальности = СводкаПоКоллекции.ДатаАктуальности;
	КонецЕсли;
	ДатаАктуальности = Макс(ДатаАктуальности, ДатаНачалаВводаДанных);
	
	Если Не НастройкиСервиса.ВыгружатьОстаткиЗаВсеВремя
		И ДатаАктуальности < НастройкиСервиса.ДатаНачалаВыгрузкиОстатковИПродаж
		И Коллекция <> Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПланыПродаж Тогда
		ДатаАктуальности = НастройкиСервиса.ДатаНачалаВыгрузкиОстатковИПродаж;
	ИначеЕсли Не НастройкиСервиса.ВыгружатьКонтрольныеПланыЗаВсеВремя
		И ДатаАктуальности < НастройкиСервиса.ДатаНачалаВыгрузкиКонтрольныхПланов
		И Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПланыПродаж Тогда
		ДатаАктуальности = НастройкиСервиса.ДатаНачалаВыгрузкиКонтрольныхПланов;
	КонецЕсли;
	
	Если ИгнорироватьНастройкиПорционности Тогда
		ДатаОкончания = НачалоДня(ТекущаяДатаСеанса()) + День;
	Иначе
		ДатаОкончания = ДатаАктуальности + (ПорцияВыгрузкиПериодическихДанныхВДнях * День);
	КонецЕсли;
	
	Если Не НастройкиСервиса.ВыгружатьОстаткиЗаВсеВремя
		И ДатаОкончания > НастройкиСервиса.ДатаОкончанияВыгрузкиОстатковИПродаж
		И Коллекция <> Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПланыПродаж Тогда
		Если НастройкиСервиса.ДатаОкончанияВыгрузкиОстатковИПродаж > Дата(2000, 1, 1) Тогда
			ДатаОкончания = НастройкиСервиса.ДатаОкончанияВыгрузкиОстатковИПродаж;
		КонецЕсли;
	ИначеЕсли Не НастройкиСервиса.ВыгружатьКонтрольныеПланыЗаВсеВремя
		И ДатаОкончания > НастройкиСервиса.ДатаОкончанияВыгрузкиКонтрольныхПланов
		И Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПланыПродаж Тогда
		Если НастройкиСервиса.ДатаОкончанияВыгрузкиКонтрольныхПланов > Дата(2000, 1, 1) Тогда
			ДатаОкончания = НастройкиСервиса.ДатаОкончанияВыгрузкиКонтрольныхПланов;
		КонецЕсли;
	КонецЕсли;
	
	ДатаАктуальности = НачалоДня(ДатаАктуальности);
	ДатаОкончания = НачалоДня(ДатаОкончания);
	
	Возврат Новый Структура("ДатаНачала, ДатаОкончания", ДатаАктуальности, ДатаОкончания);
	
КонецФункции

// Модуль интернет поддержка пользователей.
// 
// Возвращаемое значение:
//  ОбщийМодуль, Неопределено - Модуль интернет поддержка пользователей.
Функция МодульИнтернетПоддержкаПользователей() Экспорт
	
	Попытка
		Возврат ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
	Исключение
		Возврат Неопределено;
	КонецПопытки; 
	
КонецФункции

// Доступны права администратора сервиса прогнозирования продаж.
// 
// Возвращаемое значение:
//  Булево
Функция ДоступныПраваАдминистратораСервисаПрогнозированияПродаж() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.Константы.НастройкиОтборовНаВыгрузкуСервисаПрогнозирования);
	
КонецФункции

// Доступны права на изменение настроек сервиса прогнозирования продаж.
// 
// Возвращаемое значение:
//  Булево
Функция ДоступныПраваИзмененияНастроекСервисаПрогнозированияПродаж() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.Константы.НастройкиСервисаПрогнозирования);
	
КонецФункции

// Представить таблицу строкой.
// 
// Параметры:
//  Таблица - ТаблицаЗначений -
//  ДобавлятьРазделитель - Булево -
// 
// Возвращаемое значение:
//  Строка - Представить таблицу строкой
Функция ПредставитьТаблицуСтрокой(Таблица, ДобавлятьРазделитель) Экспорт
	
	ШаблонСтроки = НСтр("ru = '%1: %2'");
	ШаблонНоваяСтрока = "---------------";
	
	ИменаКолонок = Новый Массив();
	Для Каждого Колонка Из Таблица.Колонки Цикл // КолонкаТаблицыЗначений.
		ИменаКолонок.Добавить(Колонка.Имя);
	КонецЦикла;
	ДанныеПоСтрокам = Новый Массив();
	Для Каждого Строка Из Таблица Цикл
		ДанныеПоКолонкам = Новый Массив();
		Для Каждого Колонка Из ИменаКолонок Цикл
			КолонкаТекущейСтроки = СтрШаблон(ШаблонСтроки, Колонка, Строка[Колонка]);
			ДанныеПоКолонкам.Добавить(КолонкаТекущейСтроки);
		КонецЦикла;
		Если ДобавлятьРазделитель Тогда
			ДанныеПоКолонкам.Добавить(ШаблонНоваяСтрока);
		КонецЕсли;
		ДанныеМоделиСтрокой = СтрСоединить(ДанныеПоКолонкам, Символы.ПС);
		ДанныеПоСтрокам.Добавить(ДанныеМоделиСтрокой);
	КонецЦикла;
	СобраннаяСтрока = СтрСоединить(ДанныеПоСтрокам, Символы.ПС);
	
	Возврат СобраннаяСтрока;
	
КонецФункции

// Получить общий отбор выгрузки.
// 
// Возвращаемое значение:
//  Неопределено, НастройкиКомпоновкиДанных - 
Функция ПолучитьОбщийОтборВыгрузки() Экспорт
	
	НастройкиОтборов = Неопределено;
	ЗначениеКонстанты = Константы.НастройкиОтборовНаВыгрузкуСервисаПрогнозирования.Получить();
	Если ЗначениеЗаполнено(ЗначениеКонстанты) Тогда
		ХранилищеПараметров = ЗначениеКонстанты.Получить();
		Если ЗначениеЗаполнено(ХранилищеПараметров)
			И ХранилищеПараметров.Свойство("ОбщийОтборВыгрузки") Тогда
			НастройкиОтборов = ХранилищеПараметров.ОбщийОтборВыгрузки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкиОтборов;
	
КонецФункции

// Получить строковое представление отборов на выгрузку данных.
// 
// Возвращаемое значение:
//  Строка - 
Функция ПолучитьОтборыВыгрузкиСтрокой() Экспорт
	
	Отборы = ПолучитьОбщийОтборВыгрузки();
	Если Отборы = Неопределено Тогда
		ОтборыСтрокой = "";
	Иначе
		ОтборыСтрокой = Строка(Отборы.Отбор);
	КонецЕсли;
	
	Возврат ОтборыСтрокой;
	
КонецФункции

// Возвращает список периодичностей, которые можно использовать для работы с сервисом прогнозирования.
//
// Возвращаемое значение:
//  СписокЗначений Из ПеречислениеСсылка.Периодичность - список значений периодичностей, поддерживаемых сервисом прогнозирования.
//
Функция ПоддерживаемыеПериодичностиСервисом() Экспорт
	
	ПоддерживаемыеПериодичности = Новый СписокЗначений();
	ПоддерживаемыеПериодичности.Добавить(Перечисления.Периодичность.День);
	ПоддерживаемыеПериодичности.Добавить(Перечисления.Периодичность.Неделя);
	ПоддерживаемыеПериодичности.Добавить(Перечисления.Периодичность.Месяц);
	Возврат ПоддерживаемыеПериодичности;
	
КонецФункции

// Возвращает номер дня недели по значению дня недели начала прогноза.
//
// Параметры:
//  ДеньНеделиНачалаПрогноза - ПеречислениеСсылка.ДниНедели -
//
// Возвращаемое значение:
//  Число -
//
Функция НомерДняНеделиНачалаПрогноза(ДеньНеделиНачалаПрогноза) Экспорт
	
	Если ДеньНеделиНачалаПрогноза = Перечисления.ДниНедели.ПустаяСсылка() Тогда
		// Значение по умолчанию.
		НомерДняНедели = 1;
	ИначеЕсли ДеньНеделиНачалаПрогноза = Перечисления.ДниНедели.Понедельник Тогда
		НомерДняНедели = 1;
	ИначеЕсли ДеньНеделиНачалаПрогноза = Перечисления.ДниНедели.Вторник Тогда
		НомерДняНедели = 2;
	ИначеЕсли ДеньНеделиНачалаПрогноза = Перечисления.ДниНедели.Среда Тогда
		НомерДняНедели = 3;
	ИначеЕсли ДеньНеделиНачалаПрогноза = Перечисления.ДниНедели.Четверг Тогда
		НомерДняНедели = 4;
	ИначеЕсли ДеньНеделиНачалаПрогноза = Перечисления.ДниНедели.Пятница Тогда
		НомерДняНедели = 5;
	ИначеЕсли ДеньНеделиНачалаПрогноза = Перечисления.ДниНедели.Суббота Тогда
		НомерДняНедели = 6;
	ИначеЕсли ДеньНеделиНачалаПрогноза = Перечисления.ДниНедели.Воскресенье Тогда
		НомерДняНедели = 7;
	КонецЕсли;
	
	Возврат НомерДняНедели;
	
КонецФункции

// Возвращает день недели по номеру дня недели.
//
// Параметры:
//  НомерДняНедели - Число -
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ДниНедели -
//
Функция ДеньНеделиПоНомеру(НомерДняНедели) Экспорт
	
	ДеньНедели = Перечисления.ДниНедели.ПустаяСсылка();
	Если НомерДняНедели = 1 Тогда
		ДеньНедели = Перечисления.ДниНедели.Понедельник;
	ИначеЕсли НомерДняНедели = 2 Тогда
		ДеньНедели = Перечисления.ДниНедели.Вторник;
	ИначеЕсли НомерДняНедели = 3 Тогда
		ДеньНедели = Перечисления.ДниНедели.Среда;
	ИначеЕсли НомерДняНедели = 4 Тогда
		ДеньНедели = Перечисления.ДниНедели.Четверг;
	ИначеЕсли НомерДняНедели = 5 Тогда
		ДеньНедели = Перечисления.ДниНедели.Пятница;
	ИначеЕсли НомерДняНедели = 6 Тогда
		ДеньНедели = Перечисления.ДниНедели.Суббота;
	ИначеЕсли НомерДняНедели = 7 Тогда
		ДеньНедели = Перечисления.ДниНедели.Воскресенье;
	КонецЕсли;
	
	Возврат ДеньНедели;
	
КонецФункции

// Получить представление ошибки внутренний.
// 
// Параметры:
//  ТекстОшибки - Строка - Текст ошибки полученный от сервиса прогнозирования продаж.
//  ЕстьЯвноОпределеннаяОшибка - Булево - Был ли текст ошибки определен в специальной секции "error" ответа сервера.
// 
// Возвращаемое значение:
//  Строка - Пользовательское представление ошибки.
Функция ПолучитьПредставлениеОшибкиВнутренний(ТекстОшибки, ЕстьЯвноОпределеннаяОшибка) Экспорт
	
	ПредставлениеОшибки = "";
	ПредставлениеОтсутствующегоЧисла = НСтр("ru='{нет числа}'");
	ПредставлениеОтсутствующейДаты = НСтр("ru='{нет даты}'");
	
	// Если пустой ответ, ошибки нет.
	// Если ответ начинается с t_, это идентификатор обучения, ошибки нет.
	Если ТекстОшибки = ""
		Или СтрДлина(ТекстОшибки) = 38 И Лев(ТекстОшибки, 2) = "t_"
		Или ТекстОшибки = "true" Тогда
		Возврат "";
	КонецЕсли;
	
	ЧислаВСтроке = Новый Массив;
	ДатыВСтроке = Новый Массив;
	
	ИзвестныеОшибки = ОшибкиСервисаПрогнозирования();
	Для Каждого ОписаниеОшибки Из ИзвестныеОшибки Цикл
		Если СтрНайти(НРег(ТекстОшибки), ОписаниеОшибки.ТекстОшибки) > 0 Тогда
			ПредставлениеОшибки = ОписаниеОшибки.ПредставлениеОшибки;
			Если ОписаниеОшибки.ПараметрыДляИзвлечения.Количество() > 0 Тогда
				ЧислаВСтроке = ИзвлечьЧислаИзСтроки(ТекстОшибки);
				ДатыВСтроке = ИзвлечьДатыИзСтроки(ТекстОшибки);
			КонецЕсли;
			
			ПараметрыДляПодстановки = Новый Массив;
			Для Каждого ПараметрИзвлечения Из ОписаниеОшибки.ПараметрыДляИзвлечения Цикл
				Если ПараметрИзвлечения = "Число" Тогда
					Если ЧислаВСтроке.Количество() > 0 Тогда
						ПараметрыДляПодстановки.Добавить(ЧислаВСтроке[0]);
						ЧислаВСтроке.Удалить(0);
					Иначе
						ПараметрыДляПодстановки.Добавить(ПредставлениеОтсутствующегоЧисла);
					КонецЕсли;
				ИначеЕсли ПараметрИзвлечения = "Дата" Тогда
					Если ДатыВСтроке.Количество() > 0 Тогда
						ПараметрыДляПодстановки.Добавить(Формат(ДатыВСтроке[0], "ДЛФ=D"));
						ДатыВСтроке.Удалить(0);
					Иначе
						ПараметрыДляПодстановки.Добавить(ПредставлениеОтсутствующейДаты);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтрокуИзМассива(ПредставлениеОшибки,
				ПараметрыДляПодстановки);
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьЯвноОпределеннаяОшибка И ПустаяСтрока(ПредставлениеОшибки) Тогда
		// Пример непредусмотренной ошибки:
		// "unfortunatly, unexpected error occured, id for support: 7b050700-XXXXXXX"
		ПредставлениеОшибки = ТекстВоВремяОбращенияКСервисуПроизошлаОшибка(ТекстОшибки);
	КонецЕсли;
	
	Возврат ПредставлениеОшибки;
	
КонецФункции

#КонецОбласти

#Область РегламентныеЗадания

// Процедура обработки регламентного задания по выгрузке данных
//
Процедура ВыгрузкаДанныхВСервисПрогнозированияПродаж() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ВыгрузкаДанныхВСервисПрогнозированияПродаж);
	
	РегистрыСведений.ЖурналСервисаПрогнозирования.ПроверитьОчиститьЖурналСервисаПрогнозирования();
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	Если НастройкиСервиса.ВыгрузкаДанныхПоРасписаниюАктивна Тогда
		СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
									УровеньЖурналаРегистрации.Информация,
									,
									,
									ТекущаяДатаСеанса());
		
		// 1. Полная выгрузка
		СоздатьКоллекции();
		ВыгрузитьКоллекции();
		
		// 2. Инициировать обучение по моделям, чей срок подошел.
		Если НастройкиСервиса.ВыгрузкаДанныхПоРасписаниюЗапускатьОбучение Тогда
			ЗапуститьАвтообновлениеПрогнозов();
			
			СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
										УровеньЖурналаРегистрации.Информация,
										,
										,
										ТекущаяДатаСеанса());
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура обработки регламентного задания по загрузке данных
//
Процедура ЗагрузкаДанныхИзСервисаПрогнозированияПродаж() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ЗагрузкаДанныхИзСервисаПрогнозированияПродаж);
	
	РегистрыСведений.ЖурналСервисаПрогнозирования.ПроверитьОчиститьЖурналСервисаПрогнозирования();
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	Если НастройкиСервиса.ЗагрузкаДанныхПоРасписаниюАктивна Тогда
		СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
									УровеньЖурналаРегистрации.Информация,
									,
									,
									ТекущаяДатаСеанса());
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыПланов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВидыПланов КАК ВидыПланов
		|ГДЕ
		|	НЕ ВидыПланов.ПометкаУдаления
		|	И ВидыПланов.ЗаполнятьПоДаннымСервиса
		|	И ВидыПланов.ИдентификаторМоделиПрогнозирования > 0";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		МоделиПрогнозирования = Новый Массив();
		Пока Выборка.Следующий() Цикл
			МоделиПрогнозирования.Добавить(Выборка.Ссылка);
		КонецЦикла;
		
		// 1. Обновить инфо по всем ИД обучения, которые в статусе Обучение
		Для Каждого МодельПрогнозирования Из МоделиПрогнозирования Цикл
			ИнфоСтатусаОбучения = ПолучитьСтатусОбучения(МодельПрогнозирования);
			Если ИнфоСтатусаОбучения.Свойство("Статус")
				И ИнфоСтатусаОбучения.Статус = СтатусГотовКПолучению() Тогда
				ПолучитьПрогнозСервиса(МодельПрогнозирования, ИнфоСтатусаОбучения.ИдОбучения);
			КонецЕсли;
		КонецЦикла;
		
		// 2. Загрузить данные по тем ИД, по котором статус К получению
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
									УровеньЖурналаРегистрации.Информация,
									,
									,
									ТекущаяДатаСеанса());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СтатусыПодключения

// Служебный статус подключения Активен.
// 
// Возвращаемое значение:
//  Число - 
Функция СтатусПодключенияАктивен() Экспорт
	Возврат 1;
КонецФункции

// Служебный статус подключения В очереди.
// 
// Возвращаемое значение:
//  Число - 
Функция СтатусПодключенияВОчереди() Экспорт
	Возврат -1;
КонецФункции

// Служебный статус подключения Неизвестен.
// 
// Возвращаемое значение:
//  Число - 
Функция СтатусПодключенияНеПодключено() Экспорт
	Возврат 0;
КонецФункции

// Служебный статус обучения СтатусНеизвестен().
// 
// Возвращаемое значение:
//  Строка - 
Функция СтатусНеизвестен() Экспорт
	Возврат НСтр("ru = 'Статус неизвестен'");
КонецФункции

// Служебный статус обучения СтатусОжидаетОбучения().
// 
// Возвращаемое значение:
//  Строка - 
Функция СтатусОжидаетОбучения() Экспорт
	Возврат НСтр("ru = 'Ожидает обучения'");
КонецФункции

// Служебный статус обучения СтатусОбучается().
// 
// Возвращаемое значение:
//  Строка - 
Функция СтатусОбучается() Экспорт
	Возврат НСтр("ru = 'Обучается'");
КонецФункции

// Служебный статус обучения СтатусПрерваноОбучение().
// 
// Возвращаемое значение:
//  Строка - 
Функция СтатусПрерваноОбучение() Экспорт
	
	Возврат НСтр("ru = 'Прервано обучение'");
	
КонецФункции

// Служебный статус обучения СтатусОшибкаОбучения().
// 
// Возвращаемое значение:
//  Строка - 
Функция СтатусОшибкаОбучения() Экспорт
	Возврат НСтр("ru = 'Ошибка обучения'");
КонецФункции

// Служебный статус обучения СтатусГотовКПолучению().
// 
// Возвращаемое значение:
//  Строка - 
Функция СтатусГотовКПолучению() Экспорт
	Возврат НСтр("ru = 'Готов к получению'");
КонецФункции

// Служебный статус обучения СтатусЗагружен().
// 
// Возвращаемое значение:
//  Строка - 
Функция СтатусЗагружен() Экспорт
	Возврат НСтр("ru = 'Загружен'");
КонецФункции

#КонецОбласти

#Область ШаблоныИСтруктуры

// Шаблон настроек сервиса.
// 
// Возвращаемое значение:
//  Структура - Шаблон настроек сервиса:
// * Версия - Число -
// * НазваниеКомпании - Строка - 
// * ФИОКонтактногоЛица - Строка - 
// * ЭлектроннаяПочта - Строка - 
// * НомерТелефона - Строка -  
// * НомерТелефонаЗначенияПолей - Строка - 
// * НомерТелефонаКомментарий - Строка - 
// * ЗаявкаНаПодключениеОтправлена - Булево - 
// * ТипАвторизации - Число -
// * АдресПодключения - Строка -
// * ВерсияАПИ - Строка -
// * Логин - Строка -
// * Пароль - Строка -
// * ЛогинИТС - Строка -
// * ТокенПриложения - Строка -
// * ТокенОбновления - Строка -
// * ИдентификаторПриложения - Строка -
// * ДатаОбновленияТокенов - Дата -
// * ИмяБазы - Строка -
// * Тариф - Строка -
// * СтатусПодключения - Число -
// * Баланс - Число -
// * ДатаОкончанияМаксимальногоТарифа - Дата - Дата окончания тарифа с самым длинным периодом действия.
// * РазрешенОбменССервисом - Булево -
// * ИзмененыНастройкиИнтернетПоддержки - Булево -
// * ВыгрузкаДанныхПоРасписаниюАктивна - Булево -
// * ВыгрузкаДанныхПоРасписаниюЗапускатьОбучение - Булево -
// * ЗагрузкаДанныхПоРасписаниюАктивна - Булево -
// * ИспользоватьРеальныеОстаткиИПродажи - Булево -
// * ИспользоватьПлановыеОстаткиИПродажи - Булево -
// * СценарийПрогнозированияФактИПлан - СправочникСсылка.СценарииТоварногоПланирования -
// * ВыгружатьОстаткиЗаВсеВремя - Булево -
// * ДатаНачалаВыгрузкиОстатковИПродаж - Дата -
// * ДатаОкончанияВыгрузкиОстатковИПродаж - Дата -
// * УчетПотерянныхПродаж - Число -
// * КоэффициентВосстановленияУчетаПотерянныхПродаж - Число -
// * ПорцияВыгрузкиКатегориальныхДанных - Число -
// * ПорцияВыгрузкиПериодическихДанныхВДнях - Число -
// * СценарийПрогнозированияКонтрольныхПланов - СправочникСсылка.СценарииТоварногоПланирования -
// * ВыгружатьКонтрольныеПланыЗаВсеВремя - Булево -
// * ДатаНачалаВыгрузкиКонтрольныхПланов - Дата -
// * ДатаОкончанияВыгрузкиКонтрольныхПланов - Дата -
// * РеквизитАналогиТовара - Строка -
// * РеквизитАналогиТовараВИсточнике - Строка -
// * РеквизитАналогиТовараСвойство - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения -
// * РеквизитАналогиТовараПредставление - Строка -
// * Коллекции - см. СервисПрогнозированияПереопределяемый.ПолучитьОписаниеРеквизитовВсехКоллекций.
// * ИдКоллекций - Структура - описывает идентификаторы коллекций:
// ** Ключ - Строка - Имя коллекции
// ** Значение - Число - Номер коллекции
// * ИзмененияКоллекций - см. СервисПрогнозированияПереопределяемый.ПолучитьОписаниеРеквизитовВсехКоллекций.
// * ИмяСервиса - Строка -
// * МоделиПрогнозирования - Соответствие из Строка -
// * ОкруглятьШтучныеПозиции - Булево -
// * ИзмененАПИСервиса - Булево -
// * ВыгрузкаАктивна - Булево -
// * ДатаПоследнейВыгрузки - Дата -
// * ТаймаутВыгрузки - Число -
// * ТаймаутЗагрузки - Число -
// * ТаймаутСетевогоСоединения - Число -
// * ДетализацииВыгрузки - Структура:
//   ** Ключ - Строка - Имя дополнительной детализации
//   ** Значение - Булево - Признак выгрузки детализации в сервисе
// * ИспользоватьПростойРежимМастераПодключения - Булево - 
// * ДостаточноДанныхДляПостроенияПрогноза - Булево - 
// * ПользователиЗакрывшиеРекламу - Массив Из СправочникСсылка.Пользователи - 
// * ПользовательПринявшийПользовательскоеСоглашение - Строка - 
// * ПринятоПользовательскоеСоглашение - Булево - 
// * ИмяВыгружаемойСейчасКоллекции - Строка -
// * ЗапланированоВыгрузитьКоллекций - Число -
// * ВыгруженоКоллекций - Число -
// * АвтоматическиОчищатьЖурналСервисаПрогнозирования - Булево
// * ПорогОчисткиЖурналаСервисаПрогнозирования - Число - 
// * ТекущаяОшибка - см. ДанныеТекущейОшибки.
Функция ШаблонНастроекСервиса() Экспорт
	
	Коллекции = СервисПрогнозированияПереопределяемый.ПолучитьОписаниеРеквизитовВсехКоллекций();
	
	ВсеНастройкиСервиса = Новый Структура();
	ВсеНастройкиСервиса.Вставить("Версия", 2.0);
	
	// Заявка на подключение
	ВсеНастройкиСервиса.Вставить("ЗаявкаНаПодключениеОтправлена", Ложь);
	
	// Авторизация.
	ВсеНастройкиСервиса.Вставить("АдресПодключения", "http://forecast.1c.ai");
	ВсеНастройкиСервиса.Вставить("ВерсияАПИ", "v3");
	ВсеНастройкиСервиса.Вставить("ЛогинИТС", ""); // Сохраняется в рамках библиотеки ИПП.
	ВсеНастройкиСервиса.Вставить("ТокенПриложения", "");
	ВсеНастройкиСервиса.Вставить("ТокенОбновления", "");
	ВсеНастройкиСервиса.Вставить("ИдентификаторПриложения", "");
	ВсеНастройкиСервиса.Вставить("ДатаОбновленияТокенов", '00010101');
	ВсеНастройкиСервиса.Вставить("ИмяБазы", Метаданные.Имя);
	ВсеНастройкиСервиса.Вставить("Тариф", "FORECAST_ERP_PILOT");
	ВсеНастройкиСервиса.Вставить("СтатусПодключения", СтатусПодключенияНеПодключено());
	ВсеНастройкиСервиса.Вставить("Баланс", 0);
	ВсеНастройкиСервиса.Вставить("ДатаОкончанияМаксимальногоТарифа", Дата(1,1,1));
	ВсеНастройкиСервиса.Вставить("ПользовательПринявшийПользовательскоеСоглашение", "");
	ВсеНастройкиСервиса.Вставить("ПринятоПользовательскоеСоглашение", Ложь);
	
	ВсеНастройкиСервиса.Вставить("РазрешенОбменССервисом", Истина);
	ВсеНастройкиСервиса.Вставить("ИзмененыНастройкиИнтернетПоддержки", Ложь);
	ВсеНастройкиСервиса.Вставить("АвтоматическиОчищатьЖурналСервисаПрогнозирования", Истина);
	ВсеНастройкиСервиса.Вставить("ПорогОчисткиЖурналаСервисаПрогнозирования", 50000);
	ВсеНастройкиСервиса.Вставить("ВыгрузкаДанныхПоРасписаниюАктивна", Ложь);
	ВсеНастройкиСервиса.Вставить("ВыгрузкаДанныхПоРасписаниюЗапускатьОбучение", Ложь);
	ВсеНастройкиСервиса.Вставить("ЗагрузкаДанныхПоРасписаниюАктивна", Ложь);
	ВсеНастройкиСервиса.Вставить("ИспользоватьРеальныеОстаткиИПродажи", Истина);
	ВсеНастройкиСервиса.Вставить("ИспользоватьПлановыеОстаткиИПродажи", Ложь);
	ВсеНастройкиСервиса.Вставить("СценарийПрогнозированияФактИПлан", Справочники.СценарииТоварногоПланирования.ПустаяСсылка());
	ВсеНастройкиСервиса.Вставить("ВыгружатьОстаткиЗаВсеВремя", Истина);
	ВсеНастройкиСервиса.Вставить("ДатаНачалаВыгрузкиОстатковИПродаж", '00010101');
	ВсеНастройкиСервиса.Вставить("ДатаОкончанияВыгрузкиОстатковИПродаж", '00010101');
	ВсеНастройкиСервиса.Вставить("УчетПотерянныхПродаж", 0);
	ВсеНастройкиСервиса.Вставить("КоэффициентВосстановленияУчетаПотерянныхПродаж", 0.5);
	ВсеНастройкиСервиса.Вставить("ПорцияВыгрузкиКатегориальныхДанных", 1000);
	ВсеНастройкиСервиса.Вставить("ПорцияВыгрузкиПериодическихДанныхВДнях", 90);
	ВсеНастройкиСервиса.Вставить("СценарийПрогнозированияКонтрольныхПланов", Справочники.СценарииТоварногоПланирования.ПустаяСсылка());
	ВсеНастройкиСервиса.Вставить("ВыгружатьКонтрольныеПланыЗаВсеВремя", Истина);
	ВсеНастройкиСервиса.Вставить("ДатаНачалаВыгрузкиКонтрольныхПланов", '00010101');
	ВсеНастройкиСервиса.Вставить("ДатаОкончанияВыгрузкиКонтрольныхПланов", '00010101');
	ВсеНастройкиСервиса.Вставить("Коллекции", Коллекции);
	ВсеНастройкиСервиса.Вставить("ИдКоллекций", Новый Структура());
	ВсеНастройкиСервиса.Вставить("ИмяСервиса", "forecast");
	ВсеНастройкиСервиса.Вставить("МоделиПрогнозирования", Новый Соответствие());
	ВсеНастройкиСервиса.Вставить("ОкруглятьШтучныеПозиции", Ложь);
	ВсеНастройкиСервиса.Вставить("ИзмененАПИСервиса", Ложь);
	ВсеНастройкиСервиса.Вставить("ТекущаяОшибка", ДанныеТекущейОшибки());
	
	// Информация о выгрузке.
	ВсеНастройкиСервиса.Вставить("ДатаПоследнейВыгрузки", '00010101');
	ВсеНастройкиСервиса.Вставить("ТаймаутВыгрузки", 60*60*5); // 5 часов.
	ВсеНастройкиСервиса.Вставить("ТаймаутЗагрузки", 60*5); // 5 минут.
	ВсеНастройкиСервиса.Вставить("ТаймаутСетевогоСоединения", 60*2); // 2 минуты.
	ВсеНастройкиСервиса.Вставить("ДетализацииВыгрузки", КонтролируемыеДетализации());
	
	// Настройки форм.
	ВсеНастройкиСервиса.Вставить("ИспользоватьПростойРежимМастераПодключения", Истина);
	
	// Реклама.
	ВсеНастройкиСервиса.Вставить("ДостаточноДанныхДляПостроенияПрогноза", Ложь);
	ВсеНастройкиСервиса.Вставить("ДатаОбновленияДостаточностиДанных",     Дата(1,1,1));
	ВсеНастройкиСервиса.Вставить("ПользователиЗакрывшиеРекламу",          Новый Массив());
	
	// Устаревшие настройки.
	ВсеНастройкиСервиса.Вставить("ИмяВыгружаемойСейчасКоллекции", "");
	ВсеНастройкиСервиса.Вставить("ЗапланированоВыгрузитьКоллекций", 0);
	ВсеНастройкиСервиса.Вставить("ВыгруженоКоллекций", 0);
	ВсеНастройкиСервиса.Вставить("ВыгрузкаАктивна", Ложь);
	ВсеНастройкиСервиса.Вставить("РеквизитАналогиТовара", "");
	ВсеНастройкиСервиса.Вставить("РеквизитАналогиТовараВИсточнике", "");
	ВсеНастройкиСервиса.Вставить("РеквизитАналогиТовараСвойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка());
	ВсеНастройкиСервиса.Вставить("РеквизитАналогиТовараПредставление", "");
	ВсеНастройкиСервиса.Вставить("ИзмененияКоллекций", Новый Соответствие);
	ВсеНастройкиСервиса.Вставить("НазваниеКомпании", "");
	ВсеНастройкиСервиса.Вставить("ФИОКонтактногоЛица", "");
	ВсеНастройкиСервиса.Вставить("ЭлектроннаяПочта", "");
	ВсеНастройкиСервиса.Вставить("НомерТелефона", "");
	ВсеНастройкиСервиса.Вставить("НомерТелефонаЗначенияПолей", "");
	ВсеНастройкиСервиса.Вставить("НомерТелефонаКомментарий", "");
	ВсеНастройкиСервиса.Вставить("ТипАвторизации", 1);
	ВсеНастройкиСервиса.Вставить("Логин", "");
	ВсеНастройкиСервиса.Вставить("Пароль", ""); // Сохраняется в безопасном хранилище.
	
	Возврат ВсеНастройкиСервиса;
	
КонецФункции

// Шаблон авторизационных настроек сервиса.
// 
// Возвращаемое значение:
//  Структура - Шаблон авторизационных настроек сервиса:
// * Версия - Число -
// * ТипАвторизации - Строка -
// * АдресПодключения - Строка -
// * ВерсияАПИ - Строка -
// * Логин - Строка -
// * Пароль - Строка -
// * ЛогинИТС - Строка -
// * ТокенПриложения - Строка -
// * ТокенОбновления - Строка -
// * ИдентификаторПриложения - Строка -
// * ДатаОбновленияТокенов - Дата -
// * ИмяБазы - Строка -
// * Тариф - Строка -
// * РазрешенОбменССервисом - Булево -
// * ИзмененыНастройкиИнтернетПоддержки - Булево -
// * ТаймаутСетевогоСоединения - Число - 
Функция ШаблонАвторизационныхНастроекСервиса() Экспорт
	
	Структура = Новый Структура();
	Структура.Вставить("Версия", 0);
	Структура.Вставить("АдресПодключения", "");
	Структура.Вставить("ВерсияАПИ", "");
	Структура.Вставить("ЛогинИТС", "");
	Структура.Вставить("ТокенПриложения", "");
	Структура.Вставить("ТокенОбновления", "");
	Структура.Вставить("ИдентификаторПриложения", "");
	Структура.Вставить("ДатаОбновленияТокенов", '00010101');
	Структура.Вставить("ИмяБазы", "");
	Структура.Вставить("Тариф", "");
	Структура.Вставить("РазрешенОбменССервисом", Ложь);
	Структура.Вставить("ИзмененыНастройкиИнтернетПоддержки", Ложь);
	Структура.Вставить("ТаймаутСетевогоСоединения", 120);
	
	// Устаревшие настройки
	Структура.Вставить("ТипАвторизации", "");
	Структура.Вставить("Логин", "");
	Структура.Вставить("Пароль", "");
	
	Возврат Структура;
	
КонецФункции

// Функция-конструктор ошибок, проверяемых перед запуском прогнозирования и выводимых в отчете по проблемам.
//
// Возвращаемое значение:
//  Структура:
//     * ЕстьПользовательскиеОшибки     - Булево - Флаг наличия хотя бы 1 ошибки.
//     * ДанныеНастроек                 - Структура:
//        ** Периодичность         - ПеречислениеСсылка.Периодичность - Периодичность прогноза.
//        ** НачалоПрогнозирования - Дата - Дата начала прогноза.
//        ** КоличествоПериодов    - Число - Горизонт прогноза.
//     * СтатусПодключения              - Строка - Проверка на авторизацию и активацию пользователя.
//     * НеактуальныеКоллекции          - Массив Из Строка -
//     * ОтсутствиеОжидаемыхДетализаций - см. КонтролируемыеДетализации.
//     * ДанныеЗапрета                  - Структура:
//        ** ДатаЗапрета                  - Дата - Дата запрета по разделу планирования.
//        ** Описание                     - Строка - Описание найденного запрета.
//        ** РазницаМеждуДатамиЗаЗапретом - Число - Количество дней без продаж между датами за запретом и окончанием продаж.
//     * ДанныеПоГоризонту              - Структура:
//        ** КоличествоНеХватающихПериодов - Число - количество дней продаж, не хватающих для возможности построения прогноза.
//        ** ТоповыйТовар                  - Строка - Имя самого часто продаваемого товара по количеству.
//     * ДанныеПоДатам                  - Структура:
//        ** ДатаНачалаПродаж                 - Дата -
//        ** ДатаОкончанияПродаж              - Дата -
//        ** МаксимальноДопустимаяДата        - Дата -
//        ** РазницаМеждуДатами               - Число -
//        ** ДатаЗапретаПриИсправленииРазрыва - Дата -
//
Функция ПроверяемыеОшибки() Экспорт
	
	ДанныеНастроек        = Новый Структура("Периодичность, НачалоПрогнозирования, КоличествоПериодов",
		Перечисления.Периодичность.ПустаяСсылка(), Дата(1,1,1), 0);
	
	НеактуальныеКоллекции = Новый Массив;
	
	ДанныеЗапрета         = Новый Структура;
	ДанныеЗапрета.Вставить("ДатаЗапрета",                      Дата(1,1,1));
	ДанныеЗапрета.Вставить("Описание",                         "");
	ДанныеЗапрета.Вставить("РазницаМеждуДатамиЗаЗапретом",     0);
	
	ДанныеПоГоризонту     = Новый Структура("КоличествоНеХватающихПериодов, ТоповыйТовар", 0, "");
	
	ДанныеПоДатам         = Новый Структура;
	ДанныеПоДатам.Вставить("ДатаНачалаПродаж",                 Дата(1,1,1));
	ДанныеПоДатам.Вставить("ДатаОкончанияПродаж",              Дата(1,1,1));
	ДанныеПоДатам.Вставить("МаксимальноДопустимаяДата",        Дата(1,1,1));
	ДанныеПоДатам.Вставить("РазницаМеждуДатами",               0);
	ДанныеПоДатам.Вставить("ДатаЗапретаПриИсправленииРазрыва", Дата(1,1,1));
	
	Ошибки = Новый Структура;
	Ошибки.Вставить("ЕстьПользовательскиеОшибки",     Ложь);
	Ошибки.Вставить("ДанныеНастроек",                 ДанныеНастроек);
	Ошибки.Вставить("СтатусПодключения",              "");
	Ошибки.Вставить("НеактуальныеКоллекции",          НеактуальныеКоллекции);
	Ошибки.Вставить("ОтсутствиеОжидаемыхДетализаций", КонтролируемыеДетализации());
	Ошибки.Вставить("ДанныеЗапрета",                  ДанныеЗапрета);
	Ошибки.Вставить("ДанныеПоГоризонту",              ДанныеПоГоризонту);
	Ошибки.Вставить("ДанныеПоДатам",                  ДанныеПоДатам);
	Возврат Ошибки;
	
КонецФункции

// Функция-конструктор данных последней возникшей ошибки запроса к сервису.
//
// Возвращаемое значение:
//  Структура:
// * Запрос      - Строка - Внутреннее название запроса (действия)
// * Метод       - Строка -
// * Адрес       - Строка - Url запроса
// * Тело        - Строка - Тело запроса в json
// * ТекстОшибки - Строка - 
// * Коллекция   - ПеречислениеСсылка.КоллекцииСервисаПрогнозированияПродаж -
// * ВидПлана    - СправочникСсылка.ВидыПланов -
Функция ДанныеТекущейОшибки() Экспорт
	
	ДанныеОшибки = Новый Структура;
	ДанныеОшибки.Вставить("Запрос",      "");
	ДанныеОшибки.Вставить("Метод",       "");
	ДанныеОшибки.Вставить("Адрес",       "");
	ДанныеОшибки.Вставить("Тело",        "");
	ДанныеОшибки.Вставить("ТекстОшибки", "");
	ДанныеОшибки.Вставить("Коллекция",   Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПустаяСсылка());
	ДанныеОшибки.Вставить("ВидПлана",    Справочники.ВидыПланов.ПустаяСсылка());
	Возврат ДанныеОшибки;
	
КонецФункции

// Функция-конструктор аналитик, по которым осуществляется контроль их учета на сервисе.
//
// Возвращаемое значение:
//  Структура - контролируемые дополнительные детализации:
// * КодКлиента - Булево - 
// * КодХарактеристики - Булево - 
// * КодКатегории - Булево - 
Функция КонтролируемыеДетализации() Экспорт
	
	ДетализацииВыгрузки = Новый Структура;
	ДетализацииВыгрузки.Вставить("КодКлиента",        Ложь);
	ДетализацииВыгрузки.Вставить("КодХарактеристики", Ложь);
	ДетализацииВыгрузки.Вставить("КодКатегории",      Ложь);
	Возврат ДетализацииВыгрузки;
	
КонецФункции

// Структура ответа.
// 
// Возвращаемое значение:
//  Структура - Структура ответа:
// * Ответ - Произвольный, Неопределено - 
// * КодСостояния - Число, Неопределено - 
// * ДесериализованноеЗначение - Произвольный, Неопределено - 
// * КоличествоДанныхНаСервере - Число, Неопределено - 
// * ТекстОшибки - Строка, Неопределено - 
Функция СтруктураОтвета() Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Ответ", Неопределено);
	СтруктураРезультата.Вставить("КодСостояния", Неопределено);
	СтруктураРезультата.Вставить("ДесериализованноеЗначение", Неопределено);
	
	СтруктураРезультата.Вставить("КоличествоДанныхНаСервере", Неопределено);
	СтруктураРезультата.Вставить("ТекстОшибки", Неопределено);
	
	Возврат СтруктураРезультата;
	
КонецФункции

// Структура ответа сервиса.
// 
// Параметры:
//  Результат - Неопределено, HTTPОтвет - Результат
// 
// Возвращаемое значение:
//  см. СтруктураОтвета.
Функция СтруктураОтветаСервиса(Результат) Экспорт
	
	ТелоОтветаНаЗапрос = Результат.ПолучитьТелоКакСтроку();
	ДесериализованноеЗначение = ПрочитатьТелоЗапросаJSON(ТелоОтветаНаЗапрос);
	
	СтруктураРезультата = СтруктураОтвета();
	СтруктураРезультата.Ответ = ТелоОтветаНаЗапрос;
	СтруктураРезультата.КодСостояния = Результат.КодСостояния;
	СтруктураРезультата.ДесериализованноеЗначение = ДесериализованноеЗначение;
	
	Возврат СтруктураРезультата;
	
КонецФункции

// Ответ функции обработки ответа сервиса.
// 
// Возвращаемое значение:
//  Структура - Ответ функции обработки ответа сервиса:
// * ВозвращаемоеЗначение - Неопределено -
// * ТекстОшибки - Строка -
Функция ОтветФункцииОбработкиОтветаСервиса() Экспорт
	
	Ответ = Новый Структура();
	Ответ.Вставить("ВозвращаемоеЗначение", Неопределено);
	Ответ.Вставить("ТекстОшибки", "");
	
	Возврат Ответ;
	
КонецФункции

Функция СоответствиеИменСвойств(ВключаяОбратные = Истина) Экспорт
	
	СоответствиеИмен = Новый Соответствие();
	// Основные
	СоответствиеИмен.Вставить("ТокенПриложения",                 "access_token");
	СоответствиеИмен.Вставить("ТокенПриложенияПредыдущейВерсии", "old_access_token");
	СоответствиеИмен.Вставить("ТокенОбновления",                 "refresh_token");
	СоответствиеИмен.Вставить("ИдентификаторПриложения",         "application_uuid");
	СоответствиеИмен.Вставить("ИмяСервиса",                      "service_name");
	СоответствиеИмен.Вставить("ИмяБазы",                         "application_name");
	СоответствиеИмен.Вставить("Тариф",                           "tariff");
	// Коллекции
	СоответствиеИмен.Вставить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи,    "sales");
	СоответствиеИмен.Вставить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Товары,     "products");
	СоответствиеИмен.Вставить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.ХарактеристикиНоменклатуры, "product_variants");
	СоответствиеИмен.Вставить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Покупатели, "customers");
	СоответствиеИмен.Вставить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Склады,     "shops");
	СоответствиеИмен.Вставить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Остатки,    "stock");
	СоответствиеИмен.Вставить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Промо,      "promo");
	
	// Устаревшие
	СоответствиеИмен.Вставить("Логин",                    "login");
	СоответствиеИмен.Вставить("Пароль",                   "password");
	СоответствиеИмен.Вставить("НовыйПользователь",        "new_user");
	СоответствиеИмен.Вставить("НазваниеКомпании",         "company_name");
	СоответствиеИмен.Вставить("ФИОКонтактногоЛица",       "client_name");
	СоответствиеИмен.Вставить("ЭлектроннаяПочта",         "email");
	СоответствиеИмен.Вставить("НомерТелефона",            "tel");
	СоответствиеИмен.Вставить("НомерТелефонаКомментарий", "tel_comment");
	
	Если ВключаяОбратные Тогда
		СоответствиеИменОбратное = Новый Соответствие();
		
		Для Каждого КлючЗначение Из СоответствиеИмен Цикл
			СоответствиеИменОбратное.Вставить(КлючЗначение.Значение, КлючЗначение.Ключ);
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(СоответствиеИмен, СоответствиеИменОбратное, Ложь);
	КонецЕсли;
	
	Возврат СоответствиеИмен;
	
КонецФункции

// Статус обучения модели ответ.
// 
// Параметры:
//  ЕстьИнформацияОСтатусеОбучения - Булево - 
//  СтатусПрогнозаИОбучения - Строка, ФорматированнаяСтрока - 
//  ГотовКПолучению - Булево - 
//  ЕстьЗагруженнаяМодель - Булево - 
// 
// Возвращаемое значение:
//  Структура - Статус обучения модели ответ:
// * ЕстьИнформацияОСтатусеОбучения - Булево - 
// * СтатусПрогнозаИОбучения - Строка, ФорматированнаяСтрока -
// * ГотовКПолучению - Булево -
// * Загружен - Булево -
Функция СтатусОбученияМоделиОтвет(ЕстьИнформацияОСтатусеОбучения,
	СтатусПрогнозаИОбучения,
	ГотовКПолучению = Ложь,
	ЕстьЗагруженнаяМодель = Ложь) Экспорт
	
	Структура = Новый Структура();
	Структура.Вставить("ЕстьИнформацияОСтатусеОбучения", ЕстьИнформацияОСтатусеОбучения);
	Структура.Вставить("СтатусПрогнозаИОбучения", СтатусПрогнозаИОбучения);
	Структура.Вставить("ГотовКПолучению", ГотовКПолучению);
	Структура.Вставить("Загружен", ЕстьЗагруженнаяМодель);
	
	Возврат Структура;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСВнешнимиРесурсами

Функция ВыполнитьHTTPЗапросСКонтролемПередачиДанных(Метод,
	URLЗапроса,
	ИдКоллекции,
	ТелоЗапроса,
	Заголовки = Неопределено,
	ДобавляетсяОбъектов = 0)
	
	СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
	
	Для Счетчик = 0 По 3 Цикл
		ВозвращаемоеЗначение = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапроса);
		
		ПринятоОбъектовСервером = 0;
		Если ВозвращаемоеЗначение.ДесериализованноеЗначение <> Неопределено
			И ТипЗнч(ВозвращаемоеЗначение.ДесериализованноеЗначение["newObjects"]) = Тип("Число") Тогда
			ПринятоОбъектовСервером = ВозвращаемоеЗначение.ДесериализованноеЗначение["newObjects"];
		КонецЕсли;
		
		ИнфоКоллекции = ПолучитьИнфоКоллекцииНаСервере(ИдКоллекции);
		ТекущееКоличествоДанных = 0;
		Если ТипЗнч(ИнфоКоллекции) <> Неопределено
			И ИнфоКоллекции.Свойство("ДесериализованноеЗначение")
			И ТипЗнч(ИнфоКоллекции.ДесериализованноеЗначение) = Тип("Соответствие") Тогда
			ИнфоКоллекцииОбработанное = ИнфоКоллекции.ДесериализованноеЗначение;
			Если ИнфоКоллекцииОбработанное.Получить("info") <> Неопределено Тогда
				ТекущееКоличествоДанных = Число(ИнфоКоллекцииОбработанное["info"]["rowCount"]);
			КонецЕсли;
		КонецЕсли;
		
		ДанныеНаСервере = (ДобавляетсяОбъектов = ПринятоОбъектовСервером);
		ДанныеНаСервереЧастично = (ДобавляетсяОбъектов <> ПринятоОбъектовСервером);
		ДанныхНетНаСервере = (ПринятоОбъектовСервером = 0);
		
		Если ДанныеНаСервере Тогда
			ВозвращаемоеЗначение.КоличествоДанныхНаСервере = ТекущееКоличествоДанных;
			Возврат ВозвращаемоеЗначение;
		ИначеЕсли ДанныхНетНаСервере Тогда
			// Произведем еще одну попытку.
		ИначеЕсли ДанныеНаСервереЧастично Тогда
			// Произошла ошибка, из-за которой данные стали некорректны.
			ТекстСообщения = ТекстСервисПринялНеВсеВыгружаемыеДанные(ДобавляетсяОбъектов, ПринятоОбъектовСервером);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
			
			ВозвращаемоеЗначение.КоличествоДанныхНаСервере = ТекущееКоличествоДанных;
			Возврат ВозвращаемоеЗначение;
		КонецЕсли;
		
	КонецЦикла;
	
	// Произошла ошибка, данные не были отправлены.
	ТекстСообщения = ТекстСервисНеПринялДанные();
	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
	
	Возврат СтруктураОтвета();
	
КонецФункции

// Функция формирует запрос исходя из входных параметров
//
// Параметры:
//  ПараметрыАдреса - Массив из Строка, Число - содержит массив параметров для формирования итогового запроса URL.
//  ПараметрыЗапроса - Соответствие из Строка - содержит соответствие параметров для формирования итогового запроса URL.
// Возвращаемое значение:
//  Строка - Итоговый запрос к API.
//
Функция СформироватьПутьЗапроса(ПараметрыАдреса, ПараметрыЗапроса = Неопределено) 
	
	НастройкиСервиса = ПолучитьАвторизационныеНастройкиСервиса(); // Запрос авторизационных данных.
	ИтоговыйЗапрос = НастройкиСервиса.АдресПодключения 
		+ ?(Прав(НастройкиСервиса.АдресПодключения, 1) = "/", "", "/") + "api/" + НастройкиСервиса.ВерсияАПИ + "/";
	
	Для каждого Параметр Из ПараметрыАдреса Цикл
		ОбработанныйПараметр = ?(ТипЗнч(Параметр) = Тип("Строка"), Параметр,
			Формат(Параметр, "ЧГ="));
		ИтоговыйЗапрос = ИтоговыйЗапрос + ?(Прав(ИтоговыйЗапрос, 1) = "/", "", "/") + ОбработанныйПараметр;
	КонецЦикла;
	
	Если ТипЗнч(ПараметрыЗапроса) = Тип("Соответствие") Тогда 
		Если ПараметрыЗапроса.Количество() > 0 Тогда
			ИтоговыйЗапрос = ИтоговыйЗапрос + "?";
		КонецЕсли;
		Для каждого Параметр Из ПараметрыЗапроса Цикл
			ИтоговыйЗапрос = ИтоговыйЗапрос + Параметр.Ключ + "=" + Параметр.Значение + "&";
		КонецЦикла;
		ИтоговыйЗапрос = Лев(ИтоговыйЗапрос, СтрДлина(ИтоговыйЗапрос) - ?(Прав(ИтоговыйЗапрос, 1) = "&", 1, 0));
	КонецЕсли;
	
	Возврат ИтоговыйЗапрос
	
КонецФункции

#КонецОбласти

#Область Разное

Функция ВыгрузитьКатегориальнуюКоллекцию(ИмяКоллекции)
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
		СводкаОбменаДанными.ОбменДаннымиАктивен = Ложь;
		СводкаОбменаДанными.ТекстОшибки         = ТекстОшибки;
		СводкаОбменаДанными.ЕстьОшибка          = Истина;
		СводкаОбменаДанными.ДатаЗавершения      = ТекущаяДатаСеанса();
		РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(
			СводкаОбменаДанными);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	ИнфоОКоллекции = НастройкиСервиса.Коллекции[ИмяКоллекции];
	Выгружать = ИнфоОКоллекции.Выгружать;
	
	Если Не Выгружать Тогда
		Возврат Истина;
	КонецЕсли;
	
	СводкаПоКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(ИмяКоллекции);
	ИдКоллекции = СтрЗаменить(СводкаПоКоллекции.ИдКоллекции, " ", "");
	Если ПустаяСтрока(ИдКоллекции)
		Или Число(ИдКоллекции) < 1 Тогда
		СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
		СводкаОбменаДанными.ОбменДаннымиАктивен = Ложь;
		СводкаОбменаДанными.ТекстОшибки         = ТекстНеПолученИдентификаторКоллекции(ИмяКоллекции);
		СводкаОбменаДанными.ЕстьОшибка          = Истина;
		СводкаОбменаДанными.ДатаЗавершения      = ТекущаяДатаСеанса();
		РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	ДанныеВыгружались = СводкаПоКоллекции.ДатаНачалаПоследнейВыгрузки > Дата(1, 1, 1);
	
	ДоступныеРеквизиты = ИнфоОКоллекции.ВложенноеОписание;
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("data");
	ПараметрыАдреса.Добавить(Число(ИдКоллекции));
	
	Если ДанныеВыгружались Тогда
		// Удаление данных на сервере и полная выгрузка.
		Метод = "PUT";
	Иначе
		Метод = "POST";
	КонецЕсли;
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	ВыгружаемыеДанныеИсходныйНабор = СервисПрогнозированияПереопределяемый.ПолучитьИсходнуюКоллекцию(ИмяКоллекции);
	
	СводкаПоКоллекции.ДатаНачалаПоследнейВыгрузки = ТекущаяДатаСеанса();
	
	ОбщееКоличествоОбъектов = 0;
	ТекущееКоличествоДанныхНаСервере = 0;
	ЕстьОшибки = Ложь;
	ТекстОшибки = "";
	
	КлючеваяОперация = "ОбщийМодуль.СервисПрогнозированияПереопределяемый.ВыгрузитьКатегориальнуюКоллекцию";
	Итерация = 0;
	КоличествоПериодовВыгрузки = ВыгружаемыеДанныеИсходныйНабор.Количество();
	ЗамерятьРазВ = Цел(КоличествоПериодовВыгрузки / 3);
	
	ТекущееКоличествоОбъектов = 0;
	ДиапазоныВыгрузки = ОпределитьДиапазоны(ВыгружаемыеДанныеИсходныйНабор, НастройкиСервиса.ПорцияВыгрузкиКатегориальныхДанных);
	Для Каждого Диапазон Из ДиапазоныВыгрузки Цикл
		
		ТребуетсяВыполнитьЗамер = КоличествоПериодовВыгрузки < 6
			Или Итерация % ЗамерятьРазВ = 0;
		Если ТребуетсяВыполнитьЗамер Тогда
			ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(КлючеваяОперация);
		КонецЕсли;
		
		ИсходныйНабор = ОтфильтроватьИсходныйНаборПоДиапазону(ВыгружаемыеДанныеИсходныйНабор, Диапазон);
		ВыгружаемыеДанные = СервисПрогнозированияПереопределяемый.ПолучитьКоллекцию(ИмяКоллекции, ИсходныйНабор, НастройкиСервиса);
		
		ПреобразованныеДанные = ТаблицуЗначенийКатегориальнойКоллекцииВМассивСтруктур(
			ВыгружаемыеДанные, ДоступныеРеквизиты); // Массив -
		ТекущееКоличествоОбъектов = ПреобразованныеДанные.Количество();
		ОбщееКоличествоОбъектов   = ОбщееКоличествоОбъектов + ТекущееКоличествоОбъектов;
		
		Если ТекущееКоличествоОбъектов > 0 Тогда
			ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ПреобразованныеДанные, ЕстьОшибки);
			
			Если ЕстьОшибки Тогда
				ТекстОшибки = ТекстОшибкиФормированияОбъектаДляВыгрузки();
				Прервать;
			КонецЕсли;
			
			СтруктураОтвета = Неопределено;
			Если НастройкиСервиса.РазрешенОбменССервисом Тогда
				СтруктураОтвета = ВыполнитьHTTPЗапросСКонтролемПередачиДанных(Метод,
					URLЗапроса,
					ИдКоллекции,
					ТелоЗапросаJSON,
					Заголовки,
					ТекущееКоличествоОбъектов);
				ТекущееКоличествоДанныхНаСервере = СтруктураОтвета.КоличествоДанныхНаСервере;
				ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, ИмяКоллекции);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Метод = "PUT" Тогда
			СводкаПоКоллекции.ДатаНачалаПоследнейВыгрузки = ТекущаяДатаСеанса();
			Метод = "POST"; // Удаление имеющейся коллекции на сервере требуется только в первый раз.
		КонецЕсли;
		
		СводкаПоКоллекции.ДатаПоследнейВыгрузки = ТекущаяДатаСеанса();
		СводкаПоКоллекции.ИдКоллекции = Число(ИдКоллекции);
		СводкаПоКоллекции.КоличествоДанныхВыгруженное = ОбщееКоличествоОбъектов;
		СводкаПоКоллекции.КоличествоДанныхНаСервере = ТекущееКоличествоДанныхНаСервере;
		СводкаПоКоллекции.ИзмененФормат = Ложь;
		СводкаПоКоллекции.ИзмененыОтборы = Ложь;
		
		РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ЗаписатьИнформациюКоллекции(ИмяКоллекции, СводкаПоКоллекции);
		
		СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
		СводкаОбменаДанными.ДатаЗавершения = ТекущаяДатаСеанса();
		СводкаОбменаДанными.ВыгруженоОбъектовКоллекции = СводкаОбменаДанными.ВыгруженоОбъектовКоллекции + ТекущееКоличествоОбъектов;
		РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
		
		Если ТребуетсяВыполнитьЗамер Тогда
			ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, ВыгружаемыеДанные.Количество() / 100);
		КонецЕсли;
		
		Если Не СводкаОбменаДанными.ОбменДаннымиАктивен Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Итерация = Итерация + 1;
		
	КонецЦикла;
	
	Если ЕстьОшибки Тогда
		СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
		СводкаОбменаДанными.ОбменДаннымиАктивен = Ложь;
		СводкаОбменаДанными.ТекстОшибки = ТекстОшибки;
		СводкаОбменаДанными.ЕстьОшибка = Истина;
		СводкаОбменаДанными.ДатаЗавершения = ТекущаяДатаСеанса();
		РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
		
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ВыгрузитьПериодическуюКоллекцию(ИмяКоллекции, ДатаПервойЗаписи, ДатаПоследнейЗаписи)
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
		СводкаОбменаДанными.ОбменДаннымиАктивен = Ложь;
		СводкаОбменаДанными.ТекстОшибки         = ТекстОшибки;
		СводкаОбменаДанными.ЕстьОшибка          = Истина;
		СводкаОбменаДанными.ДатаЗавершения      = ТекущаяДатаСеанса();
		РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(
			СводкаОбменаДанными);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	ИнфоОКоллекции = НастройкиСервиса.Коллекции[ИмяКоллекции];
	Выгружать = ИнфоОКоллекции.Выгружать;
	
	Если Не Выгружать Тогда
		Возврат Истина;
	КонецЕсли;
	
	ДоступныеРеквизиты = ИнфоОКоллекции.ВложенноеОписание;
	
	СводкаПоКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(ИмяКоллекции);
	ИдКоллекции = СтрЗаменить(СводкаПоКоллекции.ИдКоллекции, " ", "");
	Если ПустаяСтрока(ИдКоллекции)
		Или Число(ИдКоллекции) < 1 Тогда
		СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
		СводкаОбменаДанными.ОбменДаннымиАктивен = Ложь;
		СводкаОбменаДанными.ТекстОшибки         = ТекстНеПолученИдентификаторКоллекции(ИмяКоллекции);
		СводкаОбменаДанными.ЕстьОшибка          = Истина;
		СводкаОбменаДанными.ДатаЗавершения      = ТекущаяДатаСеанса();
		РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(
			СводкаОбменаДанными);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	ИзмененыОтборы = СводкаПоКоллекции.ИзмененыОтборы;
	ДатаАктуальности = СводкаПоКоллекции.ДатаАктуальности;
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("data");
	ПараметрыАдреса.Добавить(Число(ИдКоллекции));
	
	Если ИзмененыОтборы Тогда
		// Удаление данных на сервере и полная выгрузка.
		Метод = "PUT";
		СводкаПоКоллекции.ДатаАктуальности = Дата(1, 1, 1);
		РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ЗаписатьИнформациюКоллекции(ИмяКоллекции, СводкаПоКоллекции);
	Иначе
		Метод = "POST";
	КонецЕсли;
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	ДатаАктуальности = СводкаПоКоллекции.ДатаАктуальности;
	НоваяДатаАктуальности = ДатаАктуальности;
	КоличествоНеудач = 0;
	
	СводкаПоКоллекции.ДатаНачалаПоследнейВыгрузки = ТекущаяДатаСеанса();
	
	ОбщееКоличествоОбъектов = 0;
	ТекущееКоличествоДанныхНаСервере = 0;
	ЕстьОшибки = Ложь;
	ТекстОшибки = "";
	
	Если ДатаАктуальности < ДатаПервойЗаписи Тогда
		ДатаАктуальности = ДатаПервойЗаписи;
	КонецЕсли;
	СекундВДне = 24 * 60 * 60;
	ПорцияВыгрузкиПериодическихДанныхВДнях = НастройкиСервиса.ПорцияВыгрузкиПериодическихДанныхВДнях;
	КоличествоПериодовВыгрузки = Цел((ДатаПоследнейЗаписи - ДатаАктуальности)
		/ (СекундВДне * ПорцияВыгрузкиПериодическихДанныхВДнях));
	
	КлючеваяОперация = "ОбщийМодуль.СервисПрогнозированияПереопределяемый.ВыгрузитьПериодическуюКоллекцию";
	Итерация = 0;
	ЗамерятьРазВ = Цел(КоличествоПериодовВыгрузки / 3);
	ЗамерБылПропущен = Ложь;
	
	Пока ЕстьНевыгруженныйПериод(ИмяКоллекции) Цикл
		
		ТребуетсяВыполнитьЗамер = КоличествоПериодовВыгрузки < 6
			Или (Итерация <> 0 И Итерация % ЗамерятьРазВ = 0)
			Или ЗамерБылПропущен;
		Если ТребуетсяВыполнитьЗамер Тогда
			ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(КлючеваяОперация);
			ЗамерБылПропущен = Ложь;
		КонецЕсли;
		
		ПорцияВыгрузки = СервисПрогнозированияПереопределяемый.ПолучитьКоллекцию(ИмяКоллекции, Неопределено, НастройкиСервиса);
		Если ПорцияВыгрузки.Свойство("ПричинаОшибки") Тогда
			ЕстьОшибки = Истина;
			Если ПорцияВыгрузки.ПричинаОшибки = "НеУказанСценарийПрогнозированияКонтрольныхПланов" Тогда
				ТекстОшибки = ТекстОшибкиНеУказанСценарийПрогнозированияКонтрольныхПланов();
			КонецЕсли;
			Прервать;
		КонецЕсли;
		
		СброситьДатуНачалаВыгрузки = Ложь;
		НоваяДатаНачалаВыгрузки = Неопределено;
		
		ТекущееКоличествоОбъектов = 0;
		Если ПорцияВыгрузки.КоличествоДанных > 0 Тогда
			
			ДанныеКоллекции = ТаблицуЗначенийПериодическойКоллекцииВМассивСтруктур(
				ПорцияВыгрузки.Коллекция, ДоступныеРеквизиты); // Структура -
			ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ДанныеКоллекции.ПреобразованныеДанные, ЕстьОшибки);
			
			Если ЕстьОшибки Тогда
				ТекстОшибки = ТекстОшибкиФормированияОбъектаДляВыгрузки();
				Прервать;
			КонецЕсли;
			
			ТекущееКоличествоОбъектов = ДанныеКоллекции.КоличествоДанных;
			ОбщееКоличествоОбъектов = ОбщееКоличествоОбъектов + ТекущееКоличествоОбъектов;
			
			СтруктураОтвета = Неопределено;
			Если НастройкиСервиса.РазрешенОбменССервисом Тогда
				СтруктураОтвета = ВыполнитьHTTPЗапросСКонтролемПередачиДанных(Метод,
					URLЗапроса,
					ИдКоллекции,
					ТелоЗапросаJSON,
					Заголовки,
					ТекущееКоличествоОбъектов);
				ТекущееКоличествоДанныхНаСервере = СтруктураОтвета.КоличествоДанныхНаСервере;
				ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, ИмяКоллекции);
			КонецЕсли;
			
			Если Метод = "PUT" Тогда
				СброситьДатуНачалаВыгрузки = Истина;
				НоваяДатаНачалаВыгрузки = ТекущаяДатаСеанса();
				Метод = "POST"; // Удаление имеющейся коллекции на сервере требуется только в первый раз.
			КонецЕсли;
		Иначе
			ЗамерБылПропущен = Истина;
		КонецЕсли;
		
		// Следующую итерацию начинаем со следующего дня.
		НоваяДатаАктуальности = НачалоДня(ПорцияВыгрузки.ДатаОкончанияВыборкиПорции) + 86400;
		
		Если ДатаАктуальности = НоваяДатаАктуальности Тогда
			КоличествоНеудач = КоличествоНеудач + 1;
			Если КоличествоНеудач > 5 Тогда
				ЕстьОшибки = Истина;
				ТекстОшибки = ТекстОшибкиПроизошлоЗацикливание();
				Прервать;
			КонецЕсли;
		Иначе
			ДатаАктуальности = НоваяДатаАктуальности;
			КоличествоНеудач = 0;
		КонецЕсли;
		
		СводкаПоКоллекции.ДатаАктуальности = НоваяДатаАктуальности;
		СводкаПоКоллекции.ДатаПоследнейВыгрузки = ТекущаяДатаСеанса();
		СводкаПоКоллекции.ИзмененыОтборы = Ложь;
		СводкаПоКоллекции.ИзмененФормат = Ложь;
		СводкаПоКоллекции.ИдКоллекции = Число(ИдКоллекции);
		СводкаПоКоллекции.КоличествоДанныхВыгруженное = ОбщееКоличествоОбъектов;
		СводкаПоКоллекции.КоличествоДанныхНаСервере = ТекущееКоличествоДанныхНаСервере;
		
		Если СброситьДатуНачалаВыгрузки Тогда
			СводкаПоКоллекции.ДатаНачалаПоследнейВыгрузки = НоваяДатаНачалаВыгрузки;
		КонецЕсли;
		
		РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ЗаписатьИнформациюКоллекции(ИмяКоллекции, СводкаПоКоллекции);
		
		СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
		СводкаОбменаДанными.ДатаЗавершения = ТекущаяДатаСеанса();
		СводкаОбменаДанными.ВыгруженоОбъектовКоллекции = СводкаОбменаДанными.ВыгруженоОбъектовКоллекции + ТекущееКоличествоОбъектов;
		РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
		
		Если ТребуетсяВыполнитьЗамер Тогда
			ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, ПорцияВыгрузки.КоличествоДанных);
		КонецЕсли;
		
		Если Не СводкаОбменаДанными.ОбменДаннымиАктивен Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Итерация = Итерация + 1;
		
	КонецЦикла;
	
	Если ЕстьОшибки Тогда
		СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
		СводкаОбменаДанными.ОбменДаннымиАктивен = Ложь;
		СводкаОбменаДанными.ТекстОшибки = ТекстОшибки;
		СводкаОбменаДанными.ЕстьОшибка = Истина;
		СводкаОбменаДанными.ДатаЗавершения = ТекущаяДатаСеанса();
		РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
		
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ОчиститьИнформациюСервиса() Экспорт
	
	РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ОчиститьКоллекции();
	РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ОчиститьСтатусыОбменаДанными();
	ОчиститьИдентификаторыМоделей();
	
КонецПроцедуры

Процедура ОчиститьИдентификаторыМоделей()
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыПланов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыПланов КАК ВидыПланов
	|ГДЕ
	|	НЕ ВидыПланов.ПометкаУдаления
	|	И ВидыПланов.ЗаполнятьПоДаннымСервиса
	|	И ВидыПланов.ИдентификаторМоделиПрогнозирования > 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ВидПлана = Выборка.Ссылка;
		
		НачатьТранзакцию();
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.ВидыПланов");
			ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", ВидПлана);
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			ОбъектВидПлана = ВидПлана.ПолучитьОбъект(); // СправочникОбъект.ВидыПланов
			ОбъектВидПлана.ИдентификаторМоделиПрогнозирования = -1;
			ОбъектВидПлана.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.ВидыПланов,
				,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстОшибки = ТекстПриОчисткеИдентификатораВидаПланаПроизошлаОшибка(
				КраткоеПредставлениеОшибки, Строка(ВидПлана));
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ВидПлана);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьТехническуюОшибку() Экспорт
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	НастройкиСервиса.ТекущаяОшибка.Запрос      = "";
	НастройкиСервиса.ТекущаяОшибка.ТекстОшибки = "";
	НастройкиСервиса.ТекущаяОшибка.Метод       = "";
	НастройкиСервиса.ТекущаяОшибка.Адрес       = "";
	НастройкиСервиса.ТекущаяОшибка.Тело        = "";
	НастройкиСервиса.ТекущаяОшибка.Коллекция   = Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПустаяСсылка();
	НастройкиСервиса.ТекущаяОшибка.ВидПлана    = Справочники.ВидыПланов.ПустаяСсылка();
	ОбновитьНастройкиСервиса(НастройкиСервиса);
	
КонецПроцедуры

Процедура ЗапуститьАвтообновлениеПрогнозов()
	
	НеактуальныеКоллекции = НеактуальныеКонфигурацииКоллекций();
	Если НеактуальныеКоллекции.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыПланов.Ссылка КАК Ссылка,
	|	ВидыПланов.ПериодичностьОбновленияПрогноза КАК ПериодичностьОбновленияПрогноза,
	|	ВидыПланов.КоличествоОбновленийЗаПериод КАК КоличествоОбновленийЗаПериод
	|ИЗ
	|	Справочник.ВидыПланов КАК ВидыПланов
	|ГДЕ
	|	НЕ ВидыПланов.ПометкаУдаления
	|	И ВидыПланов.ЗаполнятьПоДаннымСервиса
	|	И ВидыПланов.АвтоматическиОбновлятьПрогноз";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		День = 86400;
		ДнейДоСледующегоОбновления = День * Выборка.КоличествоОбновленийЗаПериод;
		Если Выборка.ПериодичностьОбновленияПрогноза = Перечисления.Периодичность.Неделя Тогда
			ДнейДоСледующегоОбновления = ДнейДоСледующегоОбновления * 7;
		ИначеЕсли Выборка.ПериодичностьОбновленияПрогноза = Перечисления.Периодичность.Декада Тогда
			ДнейДоСледующегоОбновления = ДнейДоСледующегоОбновления * 10;
		ИначеЕсли Выборка.ПериодичностьОбновленияПрогноза = Перечисления.Периодичность.Месяц Тогда
			ДнейДоСледующегоОбновления = ДнейДоСледующегоОбновления * 31;
		ИначеЕсли Выборка.ПериодичностьОбновленияПрогноза = Перечисления.Периодичность.Квартал Тогда
			ДнейДоСледующегоОбновления = ДнейДоСледующегоОбновления * 92;
		ИначеЕсли Выборка.ПериодичностьОбновленияПрогноза = Перечисления.Периодичность.Полугодие Тогда
			ДнейДоСледующегоОбновления = ДнейДоСледующегоОбновления * 182;
		ИначеЕсли Выборка.ПериодичностьОбновленияПрогноза = Перечисления.Периодичность.Год Тогда
			ДнейДоСледующегоОбновления = ДнейДоСледующегоОбновления * 365;
		КонецЕсли;
		
		ИнфоОбучения = РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ИдентификаторыОбученияВидаПлана(
			Выборка.Ссылка, Ложь);
		Если ИнфоОбучения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИнфоОбучения.ВремяИзмененияСостояния + ДнейДоСледующегоОбновления <= ТекущаяДатаСеанса() Тогда
			ПроверитьСоздатьМодель(Выборка.Ссылка);
			ПроверитьСоздатьКонфигурациюМодели(Выборка.Ссылка);
			ЗапуститьОбучение(Выборка.Ссылка);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Функция ТаблицуЗначенийКатегориальнойКоллекцииВМассивСтруктур(ТаблицаЗначений,
	ДоступныеРеквизиты = Неопределено, Диапазон = Неопределено)
	
	МассивСтруктур = Новый Массив();
	Если ТаблицаЗначений.Количество() = 0 Тогда
		Возврат МассивСтруктур;
	КонецЕсли;
	
	ЛеваяГраница  = 0;
	ПраваяГраница = ТаблицаЗначений.Количество() - 1;
	Если Диапазон <> Неопределено Тогда
		ЛеваяГраница  = Диапазон["ЛеваяГраница"];
		ПраваяГраница = Диапазон["ПраваяГраница"];
	КонецЕсли;
	
	Для ТекущийИндекс = ЛеваяГраница По ПраваяГраница Цикл
		
		СтрокаТаблицы = ТаблицаЗначений[ТекущийИндекс];
		ДанныеОбъекта = Новый Структура();
		
		Для Каждого КлючЗначениеРеквизита Из ДоступныеРеквизиты Цикл
			ОписаниеРеквизита = КлючЗначениеРеквизита.Значение;
			
			Если ОписаниеРеквизита.Выгружать Тогда
				ИмяВСервисе = ОписаниеРеквизита.ИмяВСервисе;
				
				Если ОписаниеРеквизита.ЭтоПользовательскоеПоле Тогда
					ЧастиКлюча         = СтрРазделить(ОписаниеРеквизита.ИмяВИсточнике, "_");
					ТипРеквизита       = ЧастиКлюча[0];
					ИмяРеквизита       = ЧастиКлюча[1];
					ИмяРеквизитаСТипом = ТипРеквизита + "_" + ИмяРеквизита;
					ЗначениеРеквизита  = СтрокаТаблицы[ИмяРеквизитаСТипом];
					Если ЗначениеРеквизита = Неопределено Тогда
						ДанныеОбъекта.Вставить(ИмяВСервисе, "");
						Продолжить;
					КонецЕсли;
					Если ТипЗнч(ЗначениеРеквизита) = Тип("Число") Тогда
						ЗначениеРеквизита = Строка(ЗначениеРеквизита);
						ДанныеОбъекта.Вставить(ИмяВСервисе, ЗначениеРеквизита);
						Продолжить;
					КонецЕсли;
					ДанныеОбъекта.Вставить(ИмяВСервисе, ПреобразоватьВТипСервиса(ЗначениеРеквизита));
				Иначе
					ЗначениеРеквизита = СтрокаТаблицы[ОписаниеРеквизита.ИмяВИсточнике];
					Если ТипЗнч(ЗначениеРеквизита) = Тип("Число") Тогда
						ЗначениеРеквизита = Строка(ЗначениеРеквизита);
						ДанныеОбъекта.Вставить(ИмяВСервисе, ЗначениеРеквизита);
						Продолжить;
					КонецЕсли;
					ДанныеОбъекта.Вставить(ИмяВСервисе, ПреобразоватьВТипСервиса(ЗначениеРеквизита));
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ДанныеОбъекта.Вставить("timeseries", Новый Массив()); // У категориальных коллекций не заполняется временной ряд.
		МассивСтруктур.Добавить(ДанныеОбъекта);
		
	КонецЦикла;
	
	Возврат МассивСтруктур;
	
КонецФункции

Функция ТаблицуЗначенийПериодическойКоллекцииВМассивСтруктур(ТаблицаЗначений,
	ДоступныеРеквизиты = Неопределено, Диапазон = Неопределено)
	
	ДанныеКоллекции  = Новый Структура();
	МассивСтруктур   = Новый Массив();
	КоличествоДанных = 0;
	
	Если ТаблицаЗначений.Количество() = 0 Тогда
		ДанныеКоллекции.Вставить("ПреобразованныеДанные", МассивСтруктур);
		ДанныеКоллекции.Вставить("КоличествоДанных",      КоличествоДанных);
		Возврат ДанныеКоллекции;
	КонецЕсли;
	
	ЛеваяГраница  = 0;
	ПраваяГраница = ТаблицаЗначений.Количество() - 1;
	Если Диапазон <> Неопределено Тогда
		ЛеваяГраница  = Диапазон["ЛеваяГраница"];
		ПраваяГраница = Диапазон["ПраваяГраница"];
	КонецЕсли;
	
	ПроверочныеРеквизиты = Новый Структура();
	
	Для ТекущийИндекс = ЛеваяГраница По ПраваяГраница Цикл
		
		ДобавленыКлючевыеИСтатическиеРеквизиты = Истина;
		СтрокаТаблицы = ТаблицаЗначений[ТекущийИндекс];
		
		Если ТекущийИндекс = 0 Тогда
			ДанныеОбъекта                          = Новый Структура();
			ДинамическиеДанныеОбъекта              = Новый Массив();
			ДобавленыКлючевыеИСтатическиеРеквизиты = Ложь;
		Иначе
			СледующийОбъект = Ложь;
			Для Каждого КлючЗначениеПроверочногоРеквизита Из ПроверочныеРеквизиты Цикл
				ПроверяемыйРеквизит = СтрокаТаблицы[КлючЗначениеПроверочногоРеквизита.Ключ];
				Если ПроверяемыйРеквизит <> КлючЗначениеПроверочногоРеквизита.Значение Тогда
					СледующийОбъект = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если СледующийОбъект Тогда
				// Заполнение динамических данных по датам, перед началом заполнения следующего объекта.
				КоличествоДанных = КоличествоДанных + ДинамическиеДанныеОбъекта.Количество();
				ДанныеОбъекта.Вставить("timeseries", ДинамическиеДанныеОбъекта);
				МассивСтруктур.Добавить(ДанныеОбъекта);
				
				ДанныеОбъекта                          = Новый Структура();
				ДинамическиеДанныеОбъекта              = Новый Массив();
				ДобавленыКлючевыеИСтатическиеРеквизиты = Ложь;
			КонецЕсли;
		КонецЕсли;
		ДатаЗаписиОбъекта = СтрокаТаблицы.Дата;
		ДинамическиеРеквизиты = Новый Структура();
		ДинамическиеРеквизиты.Вставить("date", ДатаЗаписиОбъекта);
		
		Для Каждого КлючЗначениеРеквизита Из ДоступныеРеквизиты Цикл
			ОписаниеРеквизита = КлючЗначениеРеквизита.Значение;
			
			Если ОписаниеРеквизита.Выгружать Тогда
				
				Если ОписаниеРеквизита.ТипДанных = "string"
					И ДобавленыКлючевыеИСтатическиеРеквизиты Тогда
					Продолжить;
				КонецЕсли;
				ИмяВСервисе   = ОписаниеРеквизита.ИмяВСервисе;
				ИмяВИсточнике = ОписаниеРеквизита.ИмяВИсточнике;
				
				Если ОписаниеРеквизита.ЭтоПользовательскоеПоле Тогда
					ЧастиКлюча         = СтрРазделить(ИмяВИсточнике, "_");
					ТипРеквизита       = ЧастиКлюча[0];
					ИмяРеквизита       = ЧастиКлюча[1];
					ИмяРеквизитаСТипом = ТипРеквизита + "_" + ИмяРеквизита;
					ЗначениеРеквизита  = СтрокаТаблицы[ИмяРеквизитаСТипом];
					Если ОписаниеРеквизита.ТипДанных = "string" Тогда
						ПроверочныеРеквизиты.Вставить(ИмяВИсточнике, ЗначениеРеквизита);
						Если ЗначениеРеквизита = Неопределено Тогда
							ДанныеОбъекта.Вставить(ИмяВСервисе, "");
							Продолжить;
						КонецЕсли;
						ДанныеОбъекта.Вставить(ИмяВСервисе, ПреобразоватьВТипСервиса(ЗначениеРеквизита));
					Иначе // number.
						Если ЗначениеРеквизита = Неопределено Тогда
							ДинамическиеРеквизиты.Вставить(ИмяВСервисе, 0);
							Продолжить;
						КонецЕсли;
						ДинамическиеРеквизиты.Вставить(ИмяВСервисе, ПреобразоватьВТипСервиса(ЗначениеРеквизита));
					КонецЕсли;
				Иначе
					Если ОписаниеРеквизита.ТипДанных = "string" Тогда
						ЗначениеРеквизита = СтрокаТаблицы[ИмяВИсточнике];
						ПроверочныеРеквизиты.Вставить(ИмяВИсточнике, ЗначениеРеквизита);
						ДанныеОбъекта.Вставить(ИмяВСервисе, ПреобразоватьВТипСервиса(ЗначениеРеквизита));
					Иначе // number.
						ДинамическиеРеквизиты.Вставить(ИмяВСервисе,
							ПреобразоватьВТипСервиса(СтрокаТаблицы[ИмяВИсточнике]));
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		ДинамическиеДанныеОбъекта.Добавить(ДинамическиеРеквизиты);
		
	КонецЦикла;
	
	// Добавление последней пары обекта.
	Если ДинамическиеДанныеОбъекта.Количество() > 0 Тогда
		КоличествоДанных = КоличествоДанных + ДинамическиеДанныеОбъекта.Количество();
		ДанныеОбъекта.Вставить("timeseries", ДинамическиеДанныеОбъекта);
		МассивСтруктур.Добавить(ДанныеОбъекта);
	КонецЕсли;
	
	ДанныеКоллекции.Вставить("ПреобразованныеДанные", МассивСтруктур);
	ДанныеКоллекции.Вставить("КоличествоДанных",      КоличествоДанных);
	Возврат ДанныеКоллекции;
	
КонецФункции

Функция ПреобразоватьВТипСервиса(ВходящееЗначение)
	
	ТипЗначения = ТипЗнч(ВходящееЗначение);
	МетаданныеЗначения = Неопределено; // ОбъектМетаданных. 
	
	ЭтоПримитивныйТип =  ТипЗначения = Тип("Дата")
		Или ТипЗначения = Тип("Число")
		Или ТипЗначения = Тип("Строка")
		Или ТипЗначения = Тип("Булево")
		Или ТипЗначения = Тип("Неопределено")
		Или ТипЗначения = Тип("Null");
	
	Если Не ЭтоПримитивныйТип Тогда
		Если ТипЗначения <> Тип("УникальныйИдентификатор") Тогда
			МетаданныеЗначения = ВходящееЗначение.Метаданные(); // ОбъектМетаданных.
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗначения = Тип("Null") Тогда
		Возврат "";
	ИначеЕсли ТипЗначения = Тип("Неопределено") Тогда
		Возврат "";
	ИначеЕсли ТипЗначения = Тип("Число") Тогда
		Возврат ВходящееЗначение;
	ИначеЕсли ТипЗначения = Тип("Дата") Тогда
		Возврат Строка(Формат(ВходящееЗначение, "ДФ=yyyy-MM-ddTHH:mm:ss"));
	ИначеЕсли ТипЗначения = Тип("ПеречислениеСсылка.ДниНедели") Тогда
		
		Если ВходящееЗначение = Перечисления.ДниНедели.ПустаяСсылка() Тогда
			Возврат "";
		ИначеЕсли ВходящееЗначение = Перечисления.ДниНедели.Понедельник Тогда
			Возврат "Mon";
		ИначеЕсли ВходящееЗначение = Перечисления.ДниНедели.Вторник Тогда
			Возврат "Tue";
		ИначеЕсли ВходящееЗначение = Перечисления.ДниНедели.Среда Тогда
			Возврат "Wed";
		ИначеЕсли ВходящееЗначение = Перечисления.ДниНедели.Четверг Тогда
			Возврат "Thu";
		ИначеЕсли ВходящееЗначение = Перечисления.ДниНедели.Пятница Тогда
			Возврат "Fri";
		ИначеЕсли ВходящееЗначение = Перечисления.ДниНедели.Суббота Тогда
			Возврат "Sat";
		ИначеЕсли ВходящееЗначение = Перечисления.ДниНедели.Воскресенье Тогда
			Возврат "Sun";
		КонецЕсли;
		
	ИначеЕсли ТипЗначения = Тип("УникальныйИдентификатор") Тогда
		Возврат Строка(ВходящееЗначение);
	ИначеЕсли МетаданныеЗначения <> Неопределено
		И (ОбщегоНазначения.ЭтоСправочник(МетаданныеЗначения)
			Или ОбщегоНазначения.ЭтоДокумент(МетаданныеЗначения)) Тогда
		Возврат Строка(ВходящееЗначение.УникальныйИдентификатор());
	ИначеЕсли МетаданныеЗначения <> Неопределено
		И ОбщегоНазначения.ЭтоПеречисление(МетаданныеЗначения)
		И МетаданныеЗначения.Имя = Метаданные.Перечисления.Периодичность.Имя Тогда
		
		Если ВходящееЗначение = Перечисления.Периодичность.День Тогда
			Возврат "day";
		ИначеЕсли ВходящееЗначение = Перечисления.Периодичность.Неделя Тогда
			Возврат "week";
		ИначеЕсли ВходящееЗначение = Перечисления.Периодичность.Месяц Тогда
			Возврат "month";
		Иначе
			ВызватьИсключение ТекстВыбраннаяПериодичностьНеПредусмотрена(Строка(ВходящееЗначение));
		КонецЕсли;
	ИначеЕсли МетаданныеЗначения <> Неопределено
		И ОбщегоНазначения.ЭтоПеречисление(МетаданныеЗначения) Тогда
		
		Возврат Строка(ВходящееЗначение);
		
	ИначеЕсли ТипЗначения = Тип("Строка") Тогда
		Возврат ВходящееЗначение;
	Иначе
		Возврат Строка(ВходящееЗначение);
	КонецЕсли;
	
КонецФункции

// Функция разбирает входящую строку и определяет структуру URI
//
Функция СтруктураURI(Знач СтрокаURI)
	
	СтрокаURI = СокрЛП(СтрокаURI);
	
	// схема
	Схема = "";
	Позиция = СтрНайти(СтрокаURI, "://");
	Если Позиция > 0 Тогда
		Схема = НРег(Лев(СтрокаURI, Позиция - 1));
		СтрокаURI = Сред(СтрокаURI, Позиция + 3);
	КонецЕсли;
	
	// строка соединения и путь на сервере
	СтрокаСоединения = СтрокаURI;
	ПутьНаСервере = "";
	Позиция = СтрНайти(СтрокаСоединения, "/");
	Если Позиция > 0 Тогда
		ПутьНаСервере = Сред(СтрокаСоединения, Позиция + 1);
		СтрокаСоединения = Лев(СтрокаСоединения, Позиция - 1);
	КонецЕсли;
	
	// информация пользователя и имя сервера
	СтрокаАвторизации = "";
	ИмяСервера = СтрокаСоединения;
	Позиция = СтрНайти(СтрокаСоединения, "@");
	Если Позиция > 0 Тогда
		СтрокаАвторизации = Лев(СтрокаСоединения, Позиция - 1);
		ИмяСервера = Сред(СтрокаСоединения, Позиция + 1);
	КонецЕсли;
	
	// логин и пароль
	Логин = СтрокаАвторизации;
	Пароль = "";
	Позиция = СтрНайти(СтрокаАвторизации, ":");
	Если Позиция > 0 Тогда
		Логин = Лев(СтрокаАвторизации, Позиция - 1);
		Пароль = Сред(СтрокаАвторизации, Позиция + 1);
	КонецЕсли;
	
	// хост и порт
	Хост = ИмяСервера;
	Порт = "";
	Позиция = СтрНайти(ИмяСервера, ":");
	Если Позиция > 0 Тогда
		Хост = Лев(ИмяСервера, Позиция - 1);
		Порт = Сред(ИмяСервера, Позиция + 1);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Схема", Схема);
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	Результат.Вставить("ИмяСервера", ИмяСервера);
	Результат.Вставить("Хост", Хост);
	Результат.Вставить("Порт", ?(Порт <> "", Число(Порт), Неопределено));
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	
	Возврат Результат;
	
КонецФункции

// Функция десериализует строку формата JSON, полученную от сервера, и возвращает результат этого действия.
//
// Параметры:
//  СтрокаВФорматеJSON - Строка - Строка в формате JSON.
//
// Возвращаемое значение:
//  Произвольный - десериализованное значение чтения JSON.
//
Функция ПрочитатьТелоЗапросаJSON(СтрокаВФорматеJSON)
	
	ДесериализованноеЗначение = Неопределено;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаВФорматеJSON);
	// Строка может быть не в формате JSON (например при ошибке)
	Попытка
		ДесериализованноеЗначение = ПрочитатьJSON(ЧтениеJSON, Истина);
	Исключение
		СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , , 
			ТекстНевозможноПрочитатьJSON(СтрокаВФорматеJSON));
	КонецПопытки;
	ЧтениеJSON.Закрыть();
	
	Возврат ДесериализованноеЗначение;
	
КонецФункции

// Функция формирует строку JSON из передаваемого для записи объекта.
//
// Параметры:
//  ЗаписываемыйОбъект - Произвольный - записываемый объект в тело запроса JSON.
//  ЕстьОшибки - Булево - 
//
// Возвращаемое значение:
//  Строка - Строка в формате JSON
//
Функция СформироватьТелоЗапросаJSON(ЗаписываемыйОбъект, ЕстьОшибки = Ложь)
	
	СтрокаJSON = Новый ЗаписьJSON;
	СтрокаJSON.УстановитьСтроку();
	Попытка
		ЗаписатьJSON(СтрокаJSON, ЗаписываемыйОбъект);
	Исключение
		ЕстьОшибки = Истина;
		СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстНевозможноСформироватьJSON(ЗаписываемыйОбъект, ОписаниеОшибки));
	КонецПопытки;
	ТелоЗапросаJSON = СтрокаJSON.Закрыть();
	
	Возврат ТелоЗапросаJSON;
	
КонецФункции

Функция ПолучитьПредставлениеСвойстваСервиса(ИмяСвойства, ВыдаватьОшибку = Истина)
	
	СоответствиеИменСвойств = СоответствиеИменСвойств();
	Если СоответствиеИменСвойств.Получить(ИмяСвойства) = Неопределено Тогда
		Если ВыдаватьОшибку Тогда
			ТекстСообщения = ТекстНеНайденаНастройка(ИмяСвойства);
			ВызватьИсключение ТекстСообщения;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СоответствиеИменСвойств[ИмяСвойства];
	
КонецФункции

// Функция-конструктор свойств http-запроса в сервис.
//
// Возвращаемое значение:
//  Структура - где:
//     * Метод 	- Строка -
//     * Адрес 	- Строка -
//     * Тело 	- Строка -
//
Функция СвойстваЗапросаВСервис()
	
	Свойства = Новый Структура;
	Свойства.Вставить("Метод", "");
	Свойства.Вставить("Адрес", "");
	Свойства.Вставить("Тело",  "");
	Возврат Свойства;
	
КонецФункции

Функция ЕстьСвойствоСтруктурыСоответствия(Коллекция, ИмяСвойства)
	
	Если ТипЗнч(Коллекция) = Тип("Структура") Тогда
		Возврат Коллекция.Свойство(ИмяСвойства);
	ИначеЕсли ТипЗнч(Коллекция) = Тип("Соответствие") Тогда
		Возврат Коллекция[ИмяСвойства] <> Неопределено;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ИзвлечьДатыИзСтроки(Строка)
	
	РегулярноеВыражение = "(\d{4}\-\d{2}\-\d{2} \d{2}:\d{2}:\d{2})";
	
	Подстроки = ИзвлечьПодстрокиПоРегулярномуВыражению(Строка, РегулярноеВыражение);
	
	МассивДат = Новый Массив;
	
	Если Подстроки <> Неопределено Тогда
		Для Каждого Подстрока Из Подстроки Цикл
			РазделеннаяДата = СтрРазделить(Подстрока, "- :", Ложь);
			Год = РазделеннаяДата[0];
			Месяц = РазделеннаяДата[1];
			День = РазделеннаяДата[2];
			МассивДат.Добавить(Дата(Год, Месяц, День));
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивДат;
	
КонецФункции

Функция ИзвлечьЧислаИзСтроки(Строка)
	
	РегулярноеВыражение = "\b([\d.\d]+[^:-])\b";
	
	Подстроки = ИзвлечьПодстрокиПоРегулярномуВыражению(Строка, РегулярноеВыражение);
	
	МассивЧисел = Новый Массив();
	
	Если Подстроки <> Неопределено Тогда
		Для Каждого Подстрока Из Подстроки Цикл
			МассивЧисел.Добавить(Число(Подстрока));
		КонецЦикла;
	КонецЕсли;

	Возврат МассивЧисел;
	
КонецФункции

Функция ИзвлечьПодстрокиПоРегулярномуВыражению(Строка, РегулярноеВыражение)
	
	НайденныеПодстроки = Новый Массив;
	
	ВсеРезультатыПоиска = СтрНайтиВсеПоРегулярномуВыражению(Строка, РегулярноеВыражение);
	Для Каждого РезультатПоиска Из ВсеРезультатыПоиска Цикл
		Группы = РезультатПоиска.ПолучитьГруппы();
	
		Для Каждого Группа Из Группы Цикл
			Если Группа.НачальнаяПозиция <> 0 Тогда
				Подстрока = Сред(Строка, Группа.НачальнаяПозиция, Группа.Длина); 
				НайденныеПодстроки.Добавить(Подстрока);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат НайденныеПодстроки;
	
КонецФункции

Функция ОпределитьДиапазоны(КоллекцияДляОпределения, КоличествоВПорции = 1000)
	
	Диапазоны = Новый Массив();
	
	КоличествоЭлементов = КоллекцияДляОпределения.Количество();
	ЦелыхПорций = Цел(КоличествоЭлементов / КоличествоВПорции);
	ПоследняяПорция = КоличествоЭлементов % КоличествоВПорции;
	
	Если ЦелыхПорций > 0 Тогда
		Для НомерПорции = 0 По ЦелыхПорций - 1 Цикл
			ЭлементДиапазона = Новый Структура("ЛеваяГраница, ПраваяГраница");
			ЭлементДиапазона["ЛеваяГраница"] = НомерПорции * КоличествоВПорции;
			ЭлементДиапазона["ПраваяГраница"] = НомерПорции * КоличествоВПорции + КоличествоВПорции - 1;
			Диапазоны.Добавить(ЭлементДиапазона);
		КонецЦикла;
	КонецЕсли;
	
	Если ПоследняяПорция > 0 Тогда
		ЭлементДиапазона = Новый Структура("ЛеваяГраница, ПраваяГраница");
		ЭлементДиапазона["ЛеваяГраница"] = ЦелыхПорций * КоличествоВПорции;
		ЭлементДиапазона["ПраваяГраница"] = КоличествоЭлементов - 1;
		Диапазоны.Добавить(ЭлементДиапазона);
	КонецЕсли;
	
	Возврат Диапазоны;
	
КонецФункции

Функция ОтфильтроватьИсходныйНаборПоДиапазону(ИсходныйНабор, Диапазон)
	
	ОтфильтрованныйНабор = Новый Массив;
	
	Для Индекс = Диапазон.ЛеваяГраница По Диапазон.ПраваяГраница Цикл
		ОтфильтрованныйНабор.Добавить(ИсходныйНабор[Индекс]);
	КонецЦикла;
	
	Возврат ОтфильтрованныйНабор;
	
КонецФункции

Процедура ПроверитьВидПлана(ВидПлана)
	
	Если Не ЗначениеЗаполнено(ВидПлана) Тогда
		ВызватьИсключение ТекстПустойВидПлана();
	ИначеЕсли Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ЗаполнятьПоДаннымСервиса") Тогда
		ВызватьИсключение ТекстНекорректныйВидПлана();
	КонецЕсли;
	
КонецПроцедуры

Функция ПередВызовомМетодаСервиса()
	
	ПолучитьСтатусПодключенияКСервису();
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	Если НастройкиСервиса.СтатусПодключения = СтатусПодключенияАктивен() Тогда
		ТекстОшибки = "";
	ИначеЕсли НастройкиСервиса.СтатусПодключения = СтатусПодключенияВОчереди() Тогда
		ТекстОшибки = ТекстОжидаетсяПодключение();
	Иначе
		ТекстОшибки = ТекстНеПодключено();
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

// Создать коллекцию.
// 
// Параметры:
//  Коллекция - ПеречислениеСсылка.КоллекцииСервисаПрогнозированияПродаж - 
//  ТипКоллекции - Строка - Коллекция (по данным сервиса)
// 
// Возвращаемое значение:
//  см. СтруктураОтвета.
Функция СоздатьКоллекцию(Коллекция, ТипКоллекции = "sales")
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("data");
	
	Метод = "POST";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	ОписаниеКоллекции = НастройкиСервиса.Коллекции[Коллекция]; // см. СервисПрогнозированияПереопределяемыйКлиентСервер.ОписаниеВыгружаемогоЭлементаДанных.
	Категориальный = ОписаниеКоллекции.Категориальный;
	
	КонфигурацияКоллекции = Новый Структура();
	Если Не Категориальный Тогда
		СвойстваВременногоРядаКоллекции = Новый Структура();
		СвойстваВременногоРядаКоллекции.Вставить("dateGranularity", "day");
		КонфигурацияКоллекции.Вставить("tsProperties", СвойстваВременногоРядаКоллекции);
	КонецЕсли;
	
	КлючиОсновнойКоллекции = КлючиОсновнойКоллекции();
	
	СпецификацияПолейКоллекции = Новый Массив();
	Для Каждого КлючЗначениеРеквизита Из ОписаниеКоллекции.ВложенноеОписание Цикл
		
		ОписаниеРеквизита = КлючЗначениеРеквизита.Значение;
		Если Не ОписаниеРеквизита.Выгружать Тогда
			Продолжить;
		КонецЕсли;
		ИмяРеквизитаВСервисе = ОписаниеРеквизита.ИмяВСервисе;
		
		Если ОписаниеРеквизита.ТипДанных = "string" Тогда
			Если ОписаниеРеквизита.Обязательный
				Или КлючиОсновнойКоллекции.Найти(ИмяРеквизитаВСервисе) <> Неопределено Тогда
				ТипРеквизита = "key";
			Иначе
				ТипРеквизита = "static";
			КонецЕсли;
		Иначе
			ТипРеквизита = "dynamic"; // По типам number ожидается dynamic.
		КонецЕсли;
		
		СвойстваПоляКоллекции = СвойстваСпецификацииПоляКоллекции();
		СвойстваПоляКоллекции.Вставить("name",     ИмяРеквизитаВСервисе);
		СвойстваПоляКоллекции.Вставить("type",     ТипРеквизита);
		СвойстваПоляКоллекции.Вставить("dataType", ОписаниеРеквизита.ТипДанных);
		СпецификацияПолейКоллекции.Добавить(СвойстваПоляКоллекции);
		
	КонецЦикла;
	КонфигурацияКоллекции.Вставить("columnProperties", СпецификацияПолейКоллекции);
	
	ТелоЗапроса = Новый Структура();
	ТелоЗапроса.Вставить("dataName", ТипКоллекции);
	ТелоЗапроса.Вставить("dataType", "timeseries"); // Одинаковый тип как у периодических, так и категориальных коллекций.
	ТелоЗапроса.Вставить("config",   КонфигурацияКоллекции);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP", НастройкиСервиса.ТокенПриложения);
	
	ТелоЗапросаJSON = СформироватьТелоЗапросаJSON(ТелоЗапроса);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки, ТелоЗапросаJSON);
		Если СтруктураОтвета.КодСостояния >= 400 И СтруктураОтвета.КодСостояния < 600 Тогда
			СвойстваЗапросаСОшибкой = СвойстваЗапросаВСервис();
			СвойстваЗапросаСОшибкой.Метод = Метод;
			СвойстваЗапросаСОшибкой.Адрес = URLЗапроса;
			СвойстваЗапросаСОшибкой.Тело  = ТелоЗапросаJSON;
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ОбновлениеИнформацииОКоллекциях",
				Коллекция, СвойстваЗапросаСОшибкой);
		Иначе
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "ОбновлениеИнформацииОКоллекциях",
				Коллекция);
		КонецЕсли;
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
		
		// Контроль наличия нужных аналитик на сервисе.
		Если ПустаяСтрока(Ответ.ТекстОшибки) Тогда
			Если Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи Тогда
				ДетализацииВыгрузки = НастройкиСервиса.ДетализацииВыгрузки;
				ДетализацииВыгрузки.КодКлиента        = ОписаниеКоллекции.ВложенноеОписание.КодКлиента.Выгружать;
				ДетализацииВыгрузки.КодХарактеристики = ОписаниеКоллекции.ВложенноеОписание.КодХарактеристики.Выгружать;
				ОбновитьНастройкиСервиса(НастройкиСервиса);
			ИначеЕсли Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.Товары Тогда
				ДетализацииВыгрузки = НастройкиСервиса.ДетализацииВыгрузки;
				ДетализацииВыгрузки.КодКатегории      = ОписаниеКоллекции.ВложенноеОписание.КодКатегории.Выгружать;
				ОбновитьНастройкиСервиса(НастройкиСервиса);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Удалить коллекцию.
// 
// Параметры:
//  Коллекция - ПеречислениеСсылка.КоллекцииСервисаПрогнозированияПродаж - 
// 
// Возвращаемое значение:
//  см. СтруктураОтвета.
Функция УдалитьКоллекцию(Коллекция)
	
	ТекстОшибки = ПередВызовомМетодаСервиса();
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СтруктураОтвета = СтруктураОтвета();
		СтруктураОтвета.ТекстОшибки = ТекстОшибки;
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	СводкаПоКоллекции= РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(Коллекция);
	ИдКоллекции = СводкаПоКоллекции.ИдКоллекции;
	
	ПараметрыАдреса = Новый Массив;
	ПараметрыАдреса.Добавить("data");
	ПараметрыАдреса.Добавить(Число(ИдКоллекции));
	
	Метод = "DELETE";
	URLЗапроса = СформироватьПутьЗапроса(ПараметрыАдреса);
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("X-AUTH-APP",
		НастройкиСервиса.ТокенПриложения);
	
	СтруктураОтвета = Неопределено;
	Если НастройкиСервиса.РазрешенОбменССервисом Тогда
		СтруктураОтвета = ВыполнитьHTTPЗапрос(Метод, URLЗапроса, Заголовки);
		Если СтруктураОтвета.КодСостояния >= 400 И СтруктураОтвета.КодСостояния < 600 Тогда
			СвойстваЗапросаСОшибкой = СвойстваЗапросаВСервис();
			СвойстваЗапросаСОшибкой.Метод = Метод;
			СвойстваЗапросаСОшибкой.Адрес = URLЗапроса;
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "УдалениеКоллекции",
				Коллекция, СвойстваЗапросаСОшибкой);
		Иначе
			Ответ = ОбработатьОтветСервиса(СтруктураОтвета.ДесериализованноеЗначение, "УдалениеКоллекции",
				Коллекция);
		КонецЕсли;
		СтруктураОтвета.ТекстОшибки = Ответ.ТекстОшибки;
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

// Функция-конструктор ключей основной коллекции "Продажи".
//
// Возвращаемое значение:
//  Массив Из Строка -
Функция КлючиОсновнойКоллекции()
	
	КлючиКоллекции = Новый Массив();
	КлючиКоллекции.Добавить("item_id");
	КлючиКоллекции.Добавить("shop_id");
	КлючиКоллекции.Добавить("customer_id");
	КлючиКоллекции.Добавить("item_variant_id");
	
	Возврат КлючиКоллекции;
	
КонецФункции

// Функция-конструктор свойств спецификации поля коллекции "columnProperties".
//
// Возвращаемое значение:
//  Структура - где:
//     * name 		- Строка - Название поля
//     * type 		- Строка - Тип поля
//     * dataType 	- Строка - Тип данных
//
Функция СвойстваСпецификацииПоляКоллекции()
	
	СвойстваПоляКоллекции = Новый Структура();
	СвойстваПоляКоллекции.Вставить("name",     "");
	СвойстваПоляКоллекции.Вставить("type",     "");
	СвойстваПоляКоллекции.Вставить("dataType", "");
	
	Возврат СвойстваПоляКоллекции;
	
КонецФункции

Процедура ЗаписатьНастройкиСервисаПрогнозирования(ЗначениеДляЗаписи)
	НачатьТранзакцию();
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Константа.НастройкиСервисаПрогнозирования");
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();

		Константы.НастройкиСервисаПрогнозирования.Установить(ЗначениеДляЗаписи);

		ЗафиксироватьТранзакцию();

	Исключение
		ОтменитьТранзакцию();
		СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Константы.НастройкиСервисаПрогнозирования,
			,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#Область ОбработкаОтветаСервиса

// Описание ошибки сервиса прогнозирования.
// 
// Параметры:
//  ТекстОшибки - Строка - Текст ошибки, который поступает из сервиса прогнозирования продаж в 1с.
//  ПредставлениеОшибки - Строка - Представление ошибки для пользователя.
//  ПараметрыДляИзвлечения - Массив из Строка, Строка - Порядок и типы извлекаемых сущностей через запятую.
//  																  Возможные значения: Число, Дата.
// 
// Возвращаемое значение:
//  Структура - Описание ошибки сервиса прогнозирования:
// * ТекстОшибки - Строка - 
// * ПредставлениеОшибки - Строка - 
// * ПараметрыДляИзвлечения - Массив из Строка - 
Функция ОписаниеОшибкиСервисаПрогнозирования(ТекстОшибки,
	ПредставлениеОшибки,
	ПараметрыДляИзвлечения = Неопределено)
	
	ПараметрыДляИзвлеченияТипизированный = Новый Массив;
	Если ТипЗнч(ПараметрыДляИзвлечения) = Тип("Массив") Тогда
		ПараметрыДляИзвлеченияТипизированный = ПараметрыДляИзвлечения;
	КонецЕсли;
	Если ТипЗнч(ПараметрыДляИзвлечения) = Тип("Строка") Тогда
		ПараметрыДляИзвлечения = СтрЗаменить(ПараметрыДляИзвлечения, " ", "");
		ПараметрыДляИзвлеченияТипизированный = СтрРазделить(ПараметрыДляИзвлечения, ",", Ложь);
	КонецЕсли;
	
	ОписаниеОшибки = Новый Структура();
	ОписаниеОшибки.Вставить("ТекстОшибки", ТекстОшибки);
	ОписаниеОшибки.Вставить("ПредставлениеОшибки", ПредставлениеОшибки);
	ОписаниеОшибки.Вставить("ПараметрыДляИзвлечения", ПараметрыДляИзвлеченияТипизированный);
	
	Возврат ОписаниеОшибки;
	
КонецФункции

Функция ОшибкиСервисаПрогнозирования()
	
	ИзвестныеОшибки = Новый Массив;
	
	// Ошибки, имеющие сущности в тексте, которые необходимо извлечь, например даты или числа.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("forbidden for user",
			ТекстНедостаточноПравДляПользователя(),
			"Число"));
	
	// Пример полного текста ошибки:
	// "there is not enough data to build predictions for the specified start date.last known events date is 2024-08-06 00:00:00.start predictions date is 2024-11-07 00:00:00.try to decrease prediction date."
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("try to decrease prediction date",
			ТекстТребуетсяСдвинутьДатуНачалаПрогнозаКДатеПродажи(),
			"Дата, Дата"));
	
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("try to increase prediction date",
			ТекстТребуетсяДобавитьПродажиДляПостроенияПрогноза(),
			"Дата"));
	
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("no one item has enought sales history",
			ТекстТребуетсяДнейДляПостроенияПрогноза(),
			"Число"));
	
	// Пример полного текста ошибки:
	// "there are no history before the specified start prediction date.first known history date is 2021-09-01 00:00:00.start predictions date is 2004-11-07 00:00:00."
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("there are no history before the specified start prediction",
			ТекстНетПродажДоДатыЗапросаПрогноза(),
			"Дата, Дата"));
	
	// Пример полного текста ошибки:
	// "there is not enough data to build predictions for the specified start date.1 history periods is available.prediction horizon is 5, minimum required history is 6 periods.try to reduce value of horizon or evaluation periods. or try to load more history data."
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("history periods is available",
			ТекстТребуетсяДобавитьПродажиДляПостроенияПрогнозаБезДаты()));
	
	// Известные ошибки.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("timeout while authorization", ТекстТаймаутАутентификации()));
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("application not authorized", ТекстТаймаутАутентификации()));
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("данные для аутентификации не найдены",
			ТекстДанныеАутентификацииНеНайдены()));
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("sales collection is empty", ТекстКоллекцияПродажПустая()));
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("no processor for that model, check if model is trained",
			ТекстВидПланаНеОбучен()));
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("if specified category collection, column ""category_id"" should present in products collection",
			ТекстНеОтмеченаКВыгрузкеКолонкаКатегория()));
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("server is overloaded",
			ТекстСерверПерегружен()));
	
	// Иные возможные ошибки.
	
	// Can't read collection config (collectionID {collection}).
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("can't read collection config",
			ТекстНевозможноПрочитатьКонфигурациюКоллекции(),
			"Число"));
	
	// Can't read collection data (collectionID {collection}).
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("can't read collection data",
			ТекстНевозможноПрочитатьКоллекцию(),
			"Число"));
	
	// Configuration is empty, can't proceed.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("configuration is empty, can't proceed",
			ТекстКонфигурацияМоделиНеЗадана()));
	
	// Can't read model result {key} (modelID {model_id}).
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("can't read model result",
			ТекстНевозможноПрочитатьСохраненныйПрогноз(),
			"Число"));
	
	// Invalid target value {target_data}. Valid targets : {valid_target}.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("invalid target value",
			ТекстНевозможноПрочитатьВыбранныйПрогноз()));
			
	// Model granularity ({model_gran}) must be equal or greater than data granularity ({data_gran}).
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("model granularity", ТекстНекорректнаяГранулярность()));
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("no such uuid", ТекстИдентификаторНеНайден()));
	// not enough credits on accaunt.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("not enough credits", ТекстНедостаточноБаланса()));
	// the timeout of ...
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("the timeout", ТекстТаймаутСервиса()));
	
	// Технические ошибки по которым невозможно дать однозначную рекомендацию.
	// Требуется индивидуальная оценка специалистом.
	
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("the json object must be str, bytes or bytearray",
			ТекстПриПолученииДанныхПроизошлаОшибка()));
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("list index out of range", ТекстПриПолученииДанныхПроизошлаОшибка()));
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("division by zero", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// timeout was set for the execution and it has ellapsed terminate received by timeout
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("timeout was set for", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// ['periodicity', 'period_number', 'seasonal_coefficient'] not in index
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("not in index", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// unfortunatly, unexpected error occured, id for support
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("unexpected error occured", ТекстПриПолученииДанныхПроизошлаОшибка()));
	
	// Target field ({model_config['target']['fieldName']}) must be numeric and dynamic.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("must be numeric and dynamic", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// Incorrect format of dt_start_forecast, got {model_config['dtStartForecast']}, must be YYYY.MM.DD or YYYY-MM-DD.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("incorrect format of dt_start_forecast", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// Some field presents multiple times at mainCollectionJoinKeys.
	// Some field presents multiple times at extraCollectionJoinKeys.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("some field presents multiple times at", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// mainCollectionJoinKeys must be key fields of main collection.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("must be key fields of main collection", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// extraCollection {'collectionId'} doesn't contain merge keys {invalid_merge_keys}.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("doesn't contain merge keys", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// Collection {fconf['collectionId']} doen't contains field {fconf['fieldName']}.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("doen't contains field", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// forecastObjectKey must be key field of main collection or static field of any collection.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("must be key field of main collection or static field", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// String field {fconf['fieldName']} doesn't support aggregation mode {fconf['aggregationMode']}.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("doesn't support aggregation mode", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// String field {fconf['fieldName']} doesn't support filling mode {fconf['conditionalMissedFillerMode']['method']}.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("doesn't support filling mode", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// Some sources present multiple times at userDataSources.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("some sources present multiple times", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// Multiple sources present for metric weighting at userDataSources.
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("multiple sources present for", ТекстПриПолученииДанныхПроизошлаОшибка()));
	// Operation cannot be accomplished in current state
	ИзвестныеОшибки.Добавить(
		ОписаниеОшибкиСервисаПрогнозирования("operation cannot be accomplishe", ТекстПриПолученииДанныхПроизошлаОшибка()));
	
	Возврат ИзвестныеОшибки;
	
КонецФункции

Функция ПолучитьПредставлениеОшибки(СтруктураОтвета)
	
	ОбработанныйОтвет = "";
	ЕстьЯвноОпределеннаяОшибка = Ложь;
	Если ТипЗнч(СтруктураОтвета) = Тип("Строка") Тогда
		ОбработанныйОтвет = НРег(СтруктураОтвета);
	ИначеЕсли ТипЗнч(СтруктураОтвета) = Тип("Соответствие") Тогда
		Если ЗначениеЗаполнено(СтруктураОтвета["error"]) Тогда
			ОбработанныйОтвет = НРег(СтруктураОтвета["error"]);
		ИначеЕсли ЗначениеЗаполнено(СтруктураОтвета["message"]) Тогда
			// Ключ, в котором может возвращаться ошибка.
			ОбработанныйОтвет = НРег(СтруктураОтвета["message"]);
		КонецЕсли;
		ЕстьЯвноОпределеннаяОшибка = Истина;
	КонецЕсли;
	
	Возврат СервисПрогнозированияПовтИсп.ПолучитьПредставлениеОшибки(ОбработанныйОтвет, ЕстьЯвноОпределеннаяОшибка);
	
КонецФункции

Функция ОбработатьОтветСервиса(СтруктураОтвета, ИмяДействия,
	ДополнительныйПараметр = Неопределено,
	СвойстваЗапроса = Неопределено)
	
	Ответ = ОбработатьОтветСервисаВнутренний(СтруктураОтвета, ИмяДействия, СвойстваЗапроса, ДополнительныйПараметр);
	Если Не ПустаяСтрока(Ответ.ТекстОшибки) Тогда
		СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
		ШаблонЗаписиЖурналаРегистрации = НСтр("ru = 'Произошла ошибка во время выполнения действия %1, текст ошибки: %2'");
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Ошибка, , ,
			СтрШаблон(ШаблонЗаписиЖурналаРегистрации, ИмяДействия, Ответ.ТекстОшибки));
	КонецЕсли;
	
	Возврат Ответ;

КонецФункции

Функция ОбработатьОтветСервисаВнутренний(СтруктураОтвета, ИмяДействия, СвойстваЗапроса, ДополнительныйПараметр = Неопределено)
	
	Ответ = ОтветФункцииОбработкиОтветаСервиса();
	
	ДействияССобственнойОбработкойСпецифическихОшибок = Новый Массив();
	
	// Ошибки при этих действиях будут показаны не сразу, а через нажатие на специальную гиперссылку.
	ДействияССобственнойОбработкойСпецифическихОшибок.Добавить("ОбновлениеИнформацииОСтатусеОбучения");
	ДействияССобственнойОбработкойСпецифическихОшибок.Добавить("ПолучениеПрогнозаСервиса");
	
	ЭтоДействиеССобственнойОбработкойСпецифическихОшибок 
		= ДействияССобственнойОбработкойСпецифическихОшибок.Найти(ИмяДействия) <> Неопределено;
	
	ТекстОшибки = ПолучитьПредставлениеОшибки(СтруктураОтвета);
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		
		Если СвойстваЗапроса <> Неопределено Тогда
			Если ТипЗнч(СтруктураОтвета) = Тип("Строка") Тогда
				ТехническийТекстОшибки = НРег(СтруктураОтвета);
			ИначеЕсли ТипЗнч(СтруктураОтвета) = Тип("Соответствие") Тогда
				Если ЗначениеЗаполнено(СтруктураОтвета["error"]) Тогда
					ТехническийТекстОшибки = НРег(СтруктураОтвета["error"]);
				ИначеЕсли ЗначениеЗаполнено(СтруктураОтвета["message"]) Тогда
					// Ключ, в котором может возвращаться ошибка.
					ТехническийТекстОшибки = НРег(СтруктураОтвета["message"]);
				КонецЕсли;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НастройкиСервиса.ТекущаяОшибка, СвойстваЗапроса);
			НастройкиСервиса.ТекущаяОшибка.Запрос      = ИмяДействия;
			НастройкиСервиса.ТекущаяОшибка.ТекстОшибки = ТехническийТекстОшибки;
			Если ДополнительныйПараметр = Неопределено Тогда
				НастройкиСервиса.ТекущаяОшибка.Коллекция     = Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПустаяСсылка();
				НастройкиСервиса.ТекущаяОшибка.ВидПлана      = Справочники.ВидыПланов.ПустаяСсылка();
			Иначе
				ТипДополнительногоПараметра = ТипЗнч(ДополнительныйПараметр);
				Если ТипДополнительногоПараметра = Тип("ПеречислениеСсылка.КоллекцииСервисаПрогнозированияПродаж") Тогда
					НастройкиСервиса.ТекущаяОшибка.Коллекция = ДополнительныйПараметр;
					НастройкиСервиса.ТекущаяОшибка.ВидПлана  = Справочники.ВидыПланов.ПустаяСсылка();
				ИначеЕсли ТипДополнительногоПараметра = Тип("СправочникСсылка.ВидыПланов") Тогда
					НастройкиСервиса.ТекущаяОшибка.Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПустаяСсылка();
					НастройкиСервиса.ТекущаяОшибка.ВидПлана  = ДополнительныйПараметр;
				ИначеЕсли ТипДополнительногоПараметра = Тип("Структура") Тогда
					НастройкиСервиса.ТекущаяОшибка.Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПустаяСсылка();
					НастройкиСервиса.ТекущаяОшибка.ВидПлана  = ДополнительныйПараметр.ВидПлана;
				КонецЕсли;
			КонецЕсли;
			ОбновитьНастройкиСервиса(НастройкиСервиса);
		КонецЕсли;
		
		Если Не ЭтоДействиеССобственнойОбработкойСпецифическихОшибок Тогда
			Ответ.ТекстОшибки = ТекстОшибки;
			Возврат Ответ;
		КонецЕсли;
		
	ИначеЕсли ИмяДействия = НастройкиСервиса.ТекущаяОшибка.Запрос Тогда
		Если ДополнительныйПараметр = Неопределено
			Или ДополнительныйПараметр = НастройкиСервиса.ТекущаяОшибка.Коллекция
			Или ДополнительныйПараметр = НастройкиСервиса.ТекущаяОшибка.ВидПлана
			Или (ТипЗнч(ДополнительныйПараметр) = Тип("Структура")
				И ДополнительныйПараметр.ВидПлана = НастройкиСервиса.ТекущаяОшибка.ВидПлана) Тогда
			НастройкиСервиса.ТекущаяОшибка.Запрос      = "";
			НастройкиСервиса.ТекущаяОшибка.ТекстОшибки = "";
			НастройкиСервиса.ТекущаяОшибка.Коллекция   = Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПустаяСсылка();
			НастройкиСервиса.ТекущаяОшибка.ВидПлана    = Справочники.ВидыПланов.ПустаяСсылка();
			ОбновитьНастройкиСервиса(НастройкиСервиса);
		КонецЕсли;
	КонецЕсли;
	
	ЭтоИтерируемыйТип = ТипЗнч(СтруктураОтвета) = Тип("Структура")
			Или ТипЗнч(СтруктураОтвета) = Тип("Соответствие")
			Или ТипЗнч(СтруктураОтвета) = Тип("Массив");
	
	Если ИмяДействия = "РегистрацияПользователя"
		И СтруктураОтвета <> Неопределено Тогда
		
		НастройкиСервиса.ТокенПользователя = СтруктураОтвета["token"];
		ОбновитьНастройкиСервиса(НастройкиСервиса);
		УстановитьПараметрыСеансаНастроекСервисаПрогнозирования();
		
	ИначеЕсли ИмяДействия = "РегистрацияПриложения"
		И СтруктураОтвета <> Неопределено Тогда
		
		Если ЭтоИтерируемыйТип Тогда
			НастройкиСервиса.ТокенПриложения = СтруктураОтвета["access_token"];
			НастройкиСервиса.ТокенОбновления = СтруктураОтвета["refresh_token"];
			НастройкиСервиса.ИдентификаторПриложения = СтруктураОтвета["application_uuid"];
			НастройкиСервиса.ДатаОбновленияТокенов = ТекущаяДатаСеанса();
			Если СтруктураОтвета["status"] = "active" Тогда
				НастройкиСервиса.СтатусПодключения = СтатусПодключенияАктивен();
				НастройкиСервиса.ИзмененАПИСервиса = Ложь;
			ИначеЕсли СтруктураОтвета["status"] = "pending" Тогда
				НастройкиСервиса.СтатусПодключения = СтатусПодключенияВОчереди();
				НастройкиСервиса.ИзмененАПИСервиса = Ложь;
			Иначе
				НастройкиСервиса.СтатусПодключения = СтатусПодключенияНеПодключено();
			КонецЕсли;
			УстановитьПривилегированныйРежим(Истина);
			ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
			УстановитьПривилегированныйРежим(Ложь);
			Если ДанныеАутентификации <> Неопределено Тогда
				НастройкиСервиса.ЛогинИТС = ДанныеАутентификации.Логин;
			КонецЕсли;
			ОбновитьНастройкиСервиса(НастройкиСервиса);
			УстановитьПараметрыСеансаНастроекСервисаПрогнозирования();
		Иначе
			Ответ.ТекстОшибки = ТекстВоВремяРегистрацииПриложенияПроизошлаОшибка(СтруктураОтвета);
			Возврат Ответ;
		КонецЕсли;
		
	ИначеЕсли ИмяДействия = "ЗаявкаНаПодключение"
		И СтруктураОтвета <> Неопределено Тогда
		
		Если ДополнительныйПараметр = 200 Тогда
			НастройкиСервиса.ЗаявкаНаПодключениеОтправлена = Истина;
			ОбновитьНастройкиСервиса(НастройкиСервиса);
		КонецЕсли;
		
	ИначеЕсли ИмяДействия = "ПолучениеСтатуса" Тогда
		
		Если Не ЭтоИтерируемыйТип Тогда
			Ответ.ТекстОшибки = ТекстПриПолученииДанныхПроизошлаОшибка();
			Возврат Ответ;
		КонецЕсли;
		
		Если СтруктураОтвета["status"] = "active" Тогда
			НастройкиСервиса.СтатусПодключения = СтатусПодключенияАктивен();
		ИначеЕсли СтруктураОтвета["status"] = "pending" Тогда
			НастройкиСервиса.СтатусПодключения = СтатусПодключенияВОчереди();
		Иначе
			НастройкиСервиса.СтатусПодключения = СтатусПодключенияНеПодключено();
		КонецЕсли;
		НастройкиСервиса.Баланс = СтруктураОтвета["balance"]["1с-" + НастройкиСервиса.ИмяСервиса];
		
		ДатаОкончанияТарифаСтрокой = СтруктураОтвета["expiration_date"];
		Если ДатаОкончанияТарифаСтрокой = Неопределено
			Или ДатаОкончанияТарифаСтрокой = "None" Тогда
			НастройкиСервиса.ДатаОкончанияМаксимальногоТарифа = Дата(1,1,1);
		Иначе
			// Пример строкового формата даты ответа "2025-04-05T23:59:59".
			ШаблонДаты = Лев(СтрЗаменить(ДатаОкончанияТарифаСтрокой, "-", ""), 8);
			НастройкиСервиса.ДатаОкончанияМаксимальногоТарифа = Дата(ШаблонДаты);
		КонецЕсли;
		
		ОбновитьНастройкиСервиса(НастройкиСервиса);
		
	ИначеЕсли ИмяДействия = "ОбновлениеИнформацииОКоллекциях"
		И СтруктураОтвета <> Неопределено Тогда
		
		Если Не ЭтоИтерируемыйТип Тогда
			Ответ.ТекстОшибки = ТекстПриПолученииИдентификатораКоллекцииПроизошлаОшибка(СтруктураОтвета, ДополнительныйПараметр);
			Возврат Ответ;
		КонецЕсли;
		
		ИнфоКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(ДополнительныйПараметр);
		ИнфоКоллекции.ИдКоллекции = СтруктураОтвета["dataID"];
		РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ЗаписатьИнформациюКоллекции(ДополнительныйПараметр, ИнфоКоллекции);
		
	ИначеЕсли ИмяДействия = "УдалениеКоллекции"
		И СтруктураОтвета <> Неопределено Тогда
		
		Если Не ЭтоИтерируемыйТип
			И Не ПустаяСтрока(СтруктураОтвета) Тогда
			Ответ.ТекстОшибки = ТекстПриУдаленииКоллекцииПроизошлаОшибка(СтруктураОтвета, ДополнительныйПараметр);
			Возврат Ответ;
		КонецЕсли;
		
		ИнфоКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(ДополнительныйПараметр);
		ИнфоКоллекции.ИдКоллекции                 = -1;
		ИнфоКоллекции.ИзмененФормат               = Ложь;
		ИнфоКоллекции.ДатаАктуальности            = Дата(1, 1, 1);
		ИнфоКоллекции.ДатаНачалаПоследнейВыгрузки = Дата(1, 1, 1);
		ИнфоКоллекции.ДатаПоследнейВыгрузки       = Дата(1, 1, 1);
		РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ЗаписатьИнформациюКоллекции(ДополнительныйПараметр, ИнфоКоллекции);
		
	ИначеЕсли ИмяДействия = "СозданиеМодели"
		И СтруктураОтвета <> Неопределено Тогда
		
		Если Не ЭтоИтерируемыйТип Тогда
			Ответ.ТекстОшибки = ТекстПриПолученииДанныхПроизошлаОшибка();
			Возврат Ответ;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.ВидыПланов");
			ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", ДополнительныйПараметр);
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			ВидПлана = ДополнительныйПараметр.ПолучитьОбъект(); // СправочникОбъект.ВидыПланов
			ВидПлана.ИдентификаторМоделиПрогнозирования = СтруктураОтвета["modelId"];
			ВидПлана.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.ВидыПланов,
				,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Ответ.ТекстОшибки = ТекстПриПолученииИдентификатораВидаПланаПроизошлаОшибка(КраткоеПредставлениеОшибки);
		КонецПопытки;
		
	ИначеЕсли ИмяДействия = "ОбновлениеИнформацииОМоделях"
		И СтруктураОтвета <> Неопределено Тогда
		
	ИначеЕсли ИмяДействия = "СозданиеКонфигурацииМодели"
		И СтруктураОтвета <> Неопределено Тогда
		
		ЗапросВыполнен = (ТипЗнч(СтруктураОтвета) = Тип("Булево")
				И СтруктураОтвета)
			Или (ТипЗнч(СтруктураОтвета) = Тип("Строка")
				И СтруктураОтвета = "true");
		Если Не ЗапросВыполнен Тогда
			Ответ.ТекстОшибки = ТекстПриПолученииДанныхПроизошлаОшибка();
			Возврат Ответ;
		КонецЕсли;
		
	ИначеЕсли ИмяДействия = "ПолучениеСтоимости" Тогда
		
		Если СтруктураОтвета = Неопределено Тогда
			Возврат Ответ;
		КонецЕсли;
		
	ИначеЕсли ИмяДействия = "ЗапускОбученияМодели"
		И СтруктураОтвета = Неопределено Тогда
		Ответ.ТекстОшибки = ТекстПриПолученииДанныхПроизошлаОшибка();
	ИначеЕсли ИмяДействия = "ЗапускОбученияМодели"
		И СтруктураОтвета <> Неопределено Тогда
		
		Если Лев(СтруктураОтвета, 2) = "t_" Тогда
			СтатусОбучения = СтатусОжидаетОбучения();
			РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ЗаполнитьИдентификаторОбученияПоВидуПлана(
				ДополнительныйПараметр,
				СтруктураОтвета,
				СтатусОбучения);
			Ответ.ВозвращаемоеЗначение = СтатусОбучения;
			Возврат Ответ;
		Иначе
			Ответ.ТекстОшибки = ТекстВоВремяЗапускаОбученияПроизошлаОшибка(СтатусОбучения);
			Возврат Ответ;
		КонецЕсли;
		
	ИначеЕсли ИмяДействия = "ОбновлениеИнформацииОСтатусеОбучения"
		И СтруктураОтвета = Неопределено Тогда
		Ответ.ТекстОшибки = ТекстПриПолученииДанныхПроизошлаОшибка();
	ИначеЕсли ИмяДействия = "ОбновлениеИнформацииОСтатусеОбучения"
		И СтруктураОтвета <> Неопределено Тогда
		
		Готовность = 0;
		
		СтатусОбучения = СтатусОбучается();
		Если ТипЗнч(СтруктураОтвета["status"]) = Тип("Строка") Тогда
			Если НРег(СтруктураОтвета["status"]) = "pending" Тогда
				СтатусОбучения = СтатусОжидаетОбучения();
			ИначеЕсли НРег(СтруктураОтвета["status"]) = "progress" Тогда
				СтатусОбучения = СтатусОбучается();
			ИначеЕсли НРег(СтруктураОтвета["status"]) = "finished" Тогда
				СтатусОбучения = СтатусГотовКПолучению();
				Готовность = 100;
			ИначеЕсли НРег(СтруктураОтвета["result"]) = "fail" Тогда
				СтатусОбучения = СтатусОшибкаОбучения();
			Иначе
				СтатусОбучения = СтатусНеизвестен();
			КонецЕсли;
		ИначеЕсли ТипЗнч(СтруктураОтвета["status"]) = Тип("Соответствие") Тогда
			Если НРег(СтруктураОтвета["result"]) = "fail" Тогда
				СтатусОбучения = СтатусОшибкаОбучения();
			ИначеЕсли СтруктураОтвета["status"]["progress"] > 0.1 Тогда
				СтатусОбучения = СтатусОбучается();
				Готовность = СтруктураОтвета["status"]["progress"] * 100;
				Готовность = ?(Готовность > 100, 100, Готовность);
			ИначеЕсли СтруктураОтвета["status"]["progress"] >= 100 Тогда
				СтатусОбучения = СтатусГотовКПолучению();
				Готовность = 100;
			Иначе
				СтатусОбучения = СтатусНеизвестен();
			КонецЕсли;
		ИначеЕсли Не ПустаяСтрока(ТекстОшибки) Тогда
			СтатусОбучения = СтатусОшибкаОбучения();
		КонецЕсли;
		
		РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ЗаполнитьИдентификаторОбученияПоВидуПлана(
			ДополнительныйПараметр.ВидПлана,
			ДополнительныйПараметр.ИдОбучения,
			СтатусОбучения,
			ТекстОшибки,
			Готовность);
		
		Ответ.ВозвращаемоеЗначение = СтатусОбучения;
		Возврат Ответ;
		
	ИначеЕсли ИмяДействия = "ПолучениеПрогнозаСервиса" Тогда
		
		СтатусОбучения = СтатусЗагружен();
		Готовность = 100;
		
		Если Не ЭтоИтерируемыйТип Тогда
			Ответ.ТекстОшибки = ТекстПриПолученииДанныхПроизошлаОшибка();
			Возврат Ответ;
		КонецЕсли;
		Если Не ПустаяСтрока(ТекстОшибки) Тогда
			РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ЗаполнитьИдентификаторОбученияПоВидуПлана(
				ДополнительныйПараметр.ВидПлана,
				ДополнительныйПараметр.ИдОбучения,
				СтатусОшибкаОбучения(),
				ТекстОшибки,
				Готовность);
			Возврат Ответ; // Возврат текста ошибки не требуется, ошибка будет выведена в интерфейс через метод выше.
		КонецЕсли;
		
		НастройкиВидаПлана = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДополнительныйПараметр.ВидПлана,
			"ТипПлана, ВариантПрогнозированияПоКатегориям");
		ПланПродажПоКатегориям = (НастройкиВидаПлана.ТипПлана = Перечисления.ТипыПланов.ПланПродажПоКатегориям);
		
		Если ПланПродажПоКатегориям И НастройкиВидаПлана.ВариантПрогнозированияПоКатегориям = 0 Тогда
			СервисПрогнозированияПереопределяемый.ЗаписатьПрогнозПродажПоКатегориям(СтруктураОтвета,
				ДополнительныйПараметр.ВидПлана,
				ДополнительныйПараметр.ИмяМодели,
				ДополнительныйПараметр.ИдОбучения,
				ДополнительныйПараметр.КачествоМодели);
		Иначе
			СервисПрогнозированияПереопределяемый.ЗаписатьПрогнозПродаж(СтруктураОтвета,
				ДополнительныйПараметр.ВидПлана,
				ДополнительныйПараметр.ИмяМодели,
				ДополнительныйПараметр.ИдОбучения,
				ДополнительныйПараметр.КачествоМодели);
		КонецЕсли;
			
		РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ЗаполнитьИдентификаторОбученияПоВидуПлана(
			ДополнительныйПараметр.ВидПлана,
			ДополнительныйПараметр.ИдОбучения,
			СтатусОбучения,
			ТекстОшибки,
			Готовность);
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Процедура ВосстановитьНастройкиПоШаблону(ТекущиеНастройки, ШаблонНастроек)
	
	Для Каждого КлючЗначение Из ШаблонНастроек Цикл
		
		ЕстьСвойство = ЕстьСвойствоСтруктурыСоответствия(ТекущиеНастройки, КлючЗначение.Ключ);
		
		Если ТипЗнч(КлючЗначение.Значение) = Тип("Структура")
			Или ТипЗнч(КлючЗначение.Значение) = Тип("Соответствие") Тогда
			
			Если ЕстьСвойство
				И ТипЗнч(КлючЗначение.Значение) <> ТипЗнч(ТекущиеНастройки[КлючЗначение.Ключ]) Тогда
				ТекущиеНастройки.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
			ИначеЕсли ЕстьСвойство Тогда
				ВосстановитьНастройкиПоШаблону(ТекущиеНастройки[КлючЗначение.Ключ], КлючЗначение.Значение);
			Иначе
				ТекущиеНастройки.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
			КонецЕсли;
			
		ИначеЕсли ЕстьСвойство
			И ТипЗнч(КлючЗначение.Значение) <> ТипЗнч(ТекущиеНастройки[КлючЗначение.Ключ]) Тогда
			ТекущиеНастройки.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		ИначеЕсли Не ЕстьСвойство Тогда
			ТекущиеНастройки.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Есть невыгруженный период.
// Проверяет, дошла ли ДатаАктуальности до текущей даты.
// 
// Параметры:
//  Коллекция - ПеречислениеСсылка.КоллекцииСервисаПрогнозированияПродаж - 
//  ДатаАктуальности - Дата, Неопределено - Переопределяет текущую позицию (курсор) выгрузки периодической коллекции.
// 
// Возвращаемое значение:
//  Булево - Есть невыгруженный период
Функция ЕстьНевыгруженныйПериод(Коллекция, ДатаАктуальности = Неопределено) Экспорт
	
	НастройкиСервиса = ПолучитьНастройкиСервиса();
	
	Если Коллекция <> Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПланыПродаж Тогда
		Если НастройкиСервиса.ВыгружатьОстаткиЗаВсеВремя Тогда
			ДатаОкончания = НачалоДня(ТекущаяДатаСеанса());
		Иначе
			ДатаОкончания = НастройкиСервиса.ДатаОкончанияВыгрузкиОстатковИПродаж;
		КонецЕсли;
	Иначе
		Если Не ЗначениеЗаполнено(НастройкиСервиса.СценарийПрогнозированияКонтрольныхПланов) Тогда
			Возврат Ложь; // Не указан сценарий экспертных планов продаж.
		КонецЕсли;
		Если НастройкиСервиса.ВыгружатьКонтрольныеПланыЗаВсеВремя Тогда
			ДатаОкончания = НачалоДня(ТекущаяДатаСеанса());
		Иначе
			ДатаОкончания = НастройкиСервиса.ДатаОкончанияВыгрузкиКонтрольныхПланов;
		КонецЕсли;
	КонецЕсли;
	
	Если ДатаОкончания < Дата(2000, 1, 1) Тогда
		ДатаОкончания = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Если ДатаАктуальности = Неопределено Тогда
		СводкаПоКоллекции= РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(Коллекция);
		ДатаАктуальности = СводкаПоКоллекции.ДатаАктуальности;
		Если ДатаАктуальности < Дата(2000, 1, 1) Тогда
			ДатаАктуальности = Дата(2000, 1, 1);
		КонецЕсли;
	КонецЕсли;
	
	Если НачалоДня(ДатаАктуальности) < НачалоДня(ДатаОкончания) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область Тексты

Функция ТекстПустойВидПлана()
	Возврат НСтр("ru='Не выбран вид плана.'");
КонецФункции

Функция ТекстНекорректныйВидПлана()
	Возврат НСтр("ru='Выбранный вид плана не настроен для работы с сервисом прогнозирования.'");
КонецФункции

Функция ТекстНеПолученИдентификаторМодели()
	Возврат НСтр("ru='Для выбранного вида плана не получен идентификатор модели.
	|Рекомендуется запустить обучение повторно.'");
КонецФункции

Функция ТекстВидПланаНеОбучен()
	ТекстОшибки = НСтр("ru='По выбранному виду плана не завершено или не запущено обучение.'");
	Возврат ТекстОшибки;
КонецФункции

Функция ТекстНетКоллекцийДляВыгрузки()
	Возврат НСтр("ru='Идентификаторы коллекций не известны. Требуется выполнить полную выгрузку данных.'");
КонецФункции

Функция ТекстОжиданиеЗавершенияОбученияМодели()
	Возврат НСтр("ru='По выбранному виду плана уже производится обучение. 
		|Дождитесь окончания и загрузите результат перед повторным запуском обучения.'");
КонецФункции

Функция ТекстНеНайденаИнформацияОбученияМодели()
	
	ТекстОшибки = НСтр("ru='По выбранному виду плана не найдена информация обучения модели.'");
	Возврат ТекстОшибки;
	
КонецФункции

Функция ТекстОбучениеНевозможноИдетВыгрузкаДанных()
	Возврат НСтр("ru='Невозможно запустить обучение поскольку в данный момент идет выгрузка данных на сервер.'");
КонецФункции

Функция ТекстМоделиУжеНазначенИдентификатор()
	Возврат НСтр("ru='Модели уже назначен идентификатор.'");
КонецФункции

Функция ТекстВоВремяЗапускаОбученияПроизошлаОшибка(ТекстОшибки)
	ТекстШаблона = НСтр("ru='Во время запуска обучения модели произошла ошибка:
		|%1'");
	Возврат СтрШаблон(ТекстШаблона, ТекстОшибки);
КонецФункции

Функция ТекстПриПолученииИдентификатораКоллекцииПроизошлаОшибка(ТекстОшибки, Коллекция)
	
	ТекстШаблона = НСтр("ru='Во время получения идентификатора коллекции ""%1"" произошла ошибка:
		|%2'");
	Возврат СтрШаблон(ТекстШаблона, Коллекция, ТекстОшибки);
	
КонецФункции

Функция ТекстПриУдаленииКоллекцииПроизошлаОшибка(ТекстОшибки, Коллекция)
	
	ТекстШаблона = НСтр("ru='Во время удаления ""%1"" для перевыгрузки данных произошла ошибка:
		|%2'");
	Возврат СтрШаблон(ТекстШаблона, Коллекция, ТекстОшибки);
	
КонецФункции

Функция ТекстПриПолученииИдентификатораВидаПланаПроизошлаОшибка(ТекстОшибки)
	ТекстШаблона = НСтр("ru='Во время получения идентификатора для вида плана произошла ошибка:
		|%1'");
	Возврат СтрШаблон(ТекстШаблона, ТекстОшибки);
КонецФункции

Функция ТекстТребуетсяСдвинутьДатуНачалаПрогнозаКДатеПродажи()
	ТекстШаблона = НСтр("ru = 'Недостаточно данных для построения прогноза. Продажи заканчиваются: %1. Прогноз запрошен с: %2.
						|Попробуйте добавить продажи, либо сдвинуть дату начала прогноза к дате продажи.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстТребуетсяДобавитьПродажиДляПостроенияПрогноза()
	ТекстШаблона = НСтр("ru = 'Недостаточно данных для построения прогноза. Прогноз запрошен с: %1.
						|Попробуйте добавить данные для построения прогноза.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстТребуетсяДобавитьПродажиДляПостроенияПрогнозаБезДаты()
	ТекстШаблона = НСтр("ru = 'Недостаточно данных для построения прогноза.
						|Попробуйте добавить продажи.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстНетПродажДоДатыЗапросаПрогноза()
	ТекстШаблона = НСтр("ru = 'Нет данных до даты запроса прогноза. Продажи начинаются с: %1. Прогноз запрошен с: %2.
						|Попробуйте добавить продажи, либо сдвинуть дату начала прогноза вперед.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстТребуетсяДнейДляПостроенияПрогноза()
	ТекстШаблона = НСтр("ru = 'Нет истории продаж достаточной длины для построения прогноза.
						|Минимальное требуемое количество различных дней продаж: %1.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстНедостаточноПравДляПользователя()
	ТекстШаблона = НСтр("ru = 'Недостаточно прав для пользователя с идентификатором %1.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстВоВремяОбращенияКСервисуПроизошлаОшибка(ТекстОшибки)
	ТекстШаблона = НСтр("ru='Во время обращения к сервису произошла ошибка: %1
		|Попробуйте еще раз. В случае повторения ошибки обратитесь в техподдержку.'");
	Возврат СтрШаблон(ТекстШаблона, ТекстОшибки);
КонецФункции

Функция ТекстДанныеАутентификацииНеНайдены()
	ТекстШаблона = НСтр("ru='Данные аутентификации не найдены. Проверьте подключение к сервису.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстТаймаутАутентификации()
	ТекстШаблона = НСтр("ru='Таймаут проверки аутентификации. Попробуйте снова.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстТаймаутВыгрузкиДанных()
	ТекстШаблона = НСтр("ru='Таймаут при выполнении выгрузки данных.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстТаймаутЗагрузкиДанных()
	ТекстШаблона = НСтр("ru='Таймаут при выполнении загрузки данных.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстВоВремяРегистрацииПриложенияПроизошлаОшибка(ТекстОшибки)
	ТекстШаблона = НСтр("ru='Во время регистрации приложения произошла ошибка:
		|%1
		|Проверьте аутентификационные данные и попробуйте снова.'");
	Возврат СтрШаблон(ТекстШаблона, ТекстОшибки);
КонецФункции

Функция ТекстСервисПринялНеВсеВыгружаемыеДанные(КоличествоВыгружено, КоличествоПринято)
	ТекстСообщения = НСтр("ru = 'Сервис прогнозирования принял данные частично. Выгружено: %1 Принято: %2'");
	Возврат СтрШаблон(ТекстСообщения, КоличествоВыгружено, КоличествоПринято);
КонецФункции

Функция ТекстСервисНеПринялДанные()
	ТекстШаблона = НСтр("ru='Сервис прогнозирования не принял выгружаемые данные.'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстНевозможноСформироватьJSON(ЗаписываемыйОбъект, ТекстОшибки)
	ТекстШаблона = НСтр("ru='Невозможно сформировать тело запроса JSON. Записываемый объект: %1 Описание ошибки: %2'");
	Возврат СтрШаблон(ТекстШаблона, ЗаписываемыйОбъект, ТекстОшибки);
КонецФункции

Функция ТекстНевозможноПрочитатьJSON(ЧитаемыйОбъект)
	ТекстШаблона = НСтр("ru='Невозможно прочитать тело запроса JSON. Читаемый объект(первые 150 символов): %1'");
	ЧитаемыйОбъектСтрока = Лев(Строка(ЧитаемыйОбъект), 150);
	Возврат СтрШаблон(ТекстШаблона, ЧитаемыйОбъектСтрока);
КонецФункции

Функция ТекстОшибкиФормированияОбъектаДляВыгрузки()
	Возврат НСтр("ru='Произошла ошибка формирования тела объекта JSON.'");
КонецФункции

Функция ТекстОшибкиПроизошлоЗацикливание()
	Возврат НСтр("ru='Произошло зацикливание.'");
КонецФункции

Функция ТекстКоллекцияПродажПустая()
	ТекстОшибки = НСтр("ru='Коллекция продаж в сервисе прогнозирования пустая, работа сервиса невозможна.
		|Проверьте наличие продаж в информационной базе и установленные фильтры на выгрузку, после чего повторите выгрузку данных.'");
	Возврат ТекстОшибки;
КонецФункции

Функция ТекстПриПолученииДанныхПроизошлаОшибка()
	ТекстОшибки = НСтр("ru='Произошла ошибка при получении данных.
	|Попробуйте еще раз позже. В случае повторения ошибки обратитесь в техподдержку.'");
	Возврат ТекстОшибки;
КонецФункции

Функция ТекстВыбраннаяПериодичностьНеПредусмотрена(Периодичность)
	ТекстШаблона = НСтр("ru='Выбранная периодичность не предусмотрена сервисом. Значение: %1'");
	Возврат СтрШаблон(ТекстШаблона, Периодичность);
КонецФункции

Функция ТекстНеНайденаНастройка(ИмяНастройки)
	ТекстШаблона = НСтр("ru='Не найдена настройка сервиса с именем %1'");
	Возврат СтрШаблон(ТекстШаблона, ИмяНастройки);
КонецФункции

Функция ТекстНеОтмеченаКВыгрузкеКолонкаКатегория()
	ТекстШаблона = НСтр("ru = 'Коллекция ""Категории товаров"" выгружена, но не используется.
	|Пожалуйста, отметьте к выгрузке реквизит ""Код категории"" в коллекции ""Товары"", или отключите выгрузку коллекции ""Категории товаров"".'");
	Возврат ТекстШаблона;
КонецФункции

Функция ТекстСерверПерегружен()
	
	ТекстШаблона = НСтр("ru = 'Сервер перегружен. Попробуйте позже.'");
	Возврат ТекстШаблона;
	
КонецФункции

Функция ТекстОжидаетсяПодключение()
	ТекстОшибки = НСтр("ru='Подключение к сервису прогнозирования в очереди. Попробуйте позже.'");
	Возврат ТекстОшибки;
КонецФункции

Функция ТекстНеПодключено()
	ТекстОшибки = НСтр("ru='Нет подключения к сервису прогнозирования. Проверьте авторизацию в помощнике подключения.'");
	Возврат ТекстОшибки;
КонецФункции

Функция ТекстОшибкиНеверныйЛогинПарольИТС()
	Возврат НСтр("ru = 'Неверный логин или пароль пользователя ИТС.'");
КонецФункции

Функция ТекстОшибкиНеУдалосьПолучитьМодульИнтернетПоддержкаПользователей()
	Возврат НСтр("ru = 'Не удалось получить модуль ""Интернет поддержка пользователей"". Обратитесь к администратору.'");
КонецФункции

Функция ТекстОшибкиНеУдалосьПолучитьТикет()
	Возврат НСтр("ru = 'Не удалось получить тикет.'");
КонецФункции

Функция ТекстОшибкиНеУказанСценарийПрогнозированияКонтрольныхПланов()
	
	Возврат НСтр("ru = 'В настройках сервиса прогнозирования не указан сценарий планирования для экспертных планов продаж.
		|Для возможности учета экспертных планов продаж при прогнозировании, установите сценарий планирования (экспертный) в настройках сервиса -
		|Помощник подключения к сервису - Источники данных - Экспертные планы продаж (на будущий период).'");
	
КонецФункции

Функция ТекстНеПолученИдентификаторОсновнойКоллекции()
	
	Возврат НСтр("ru = 'Не получен идентификатор обязательной коллекции ""Продажи"".
	|Необходимо произвести выгрузку данных в сервис.'");
	
КонецФункции

Функция ТекстНеПолученИдентификаторКоллекции(ИмяКоллекции)
	
	ТекстШаблона = НСтр("ru = 'Не получен идентификатор коллекции ""%1"".
		|Попробуйте запустить еще раз полную выгрузку данных.'");
	Возврат СтрШаблон(ТекстШаблона, ИмяКоллекции);
	
КонецФункции

Функция ТекстПриОчисткеИдентификатораВидаПланаПроизошлаОшибка(ТекстОшибки, ТекстВидПлана)
	
	ТекстШаблона = НСтр("ru = 'Во время очистки идентификатора модели у вида плана ""%1"", произошла ошибка:
		|%2'");
	Возврат СтрШаблон(ТекстШаблона, ТекстВидПлана, ТекстОшибки);
	
КонецФункции

Функция ТекстПриИсправленииНастроекВидаПланаПроизошлаОшибка(ТекстОшибки, ТекстВидПлана)
	
	ТекстШаблона = НСтр("ru = 'Во время установки исправлений в настройках прогнозирования у вида плана ""%1"", произошла ошибка:
		|%2'");
	Возврат СтрШаблон(ТекстШаблона, ТекстВидПлана, ТекстОшибки);
	
КонецФункции

Функция ТекстИдентификаторНеНайден()
	
	Возврат НСтр("ru = 'Идентификатор не найден.
	|Попробуйте еще раз позже. В случае повторения ошибки обратитесь в техподдержку.'");
	
КонецФункции

Функция ТекстНевозможноПрочитатьКонфигурациюКоллекции()
	
	Возврат НСтр("ru = 'Невозможно прочитать конфигурацию коллекции %1.
	|Выгрузите данные повторно. В случае повторения ошибки обратитесь в техподдержку.'");
	
КонецФункции

Функция ТекстНевозможноПрочитатьКоллекцию()
	
	Возврат НСтр("ru = 'Невозможно прочитать коллекцию %1.
	|Выгрузите данные повторно. В случае повторения ошибки обратитесь в техподдержку.'");
	
КонецФункции

Функция ТекстКонфигурацияМоделиНеЗадана()
	
	Возврат НСтр("ru = 'Конфигурация вида плана не обнаружена.
	|Запросите прогноз повторно. В случае повторения ошибки обратитесь в техподдержку.'");
	
КонецФункции

Функция ТекстНевозможноПрочитатьСохраненныйПрогноз()
	
	Возврат НСтр("ru = 'Невозможно прочитать сохраненный прогноз по модели %1.
	|Запросите прогноз повторно. В случае повторения ошибки обратитесь в техподдержку.'");
	
КонецФункции

Функция ТекстНевозможноПрочитатьВыбранныйПрогноз()
	
	Возврат НСтр("ru = 'Невозможно прочитать прогноз по выбранной модели прогнозирования.
	|Загрузите прогноз по другой модели.'");
	
КонецФункции

Функция ТекстНекорректнаяГранулярность()
	
	Возврат НСтр("ru = 'Затребован прогноз с большей детальностью периодичности продаж, чем доступно в коллекции продаж.
	|Выгрузите данные повторно. В случае повторения ошибки обратитесь в техподдержку.'");
	
КонецФункции

Функция ТекстНедостаточноБаланса()
	
	Возврат НСтр("ru = 'Недостаточно баланса для продолжения работы.
	|Если баланс в наличии попробуйте еще раз позже. При повторении проблем с балансом обратитесь в техподдержку.'");
	
КонецФункции

Функция ТекстТаймаутСервиса()
	Возврат НСтр("ru = 'Таймаут во время обработки на сервере. 
	|Попробуйте еще раз позже. В случае повторения ошибки обратитесь в техподдержку.'");
	
КонецФункции

#КонецОбласти

#КонецОбласти
