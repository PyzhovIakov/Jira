////////////////////////////////////////////////////////////////////////////////
// Подсистема "Работа с почтовыми сообщениями".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается перед открытием формы нового письма.
// Открытие формы может быть отменено изменением параметра СтандартнаяОбработка.
//
// Параметры:
//  ПараметрыОтправки    - см. РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо;
//  ОбработчикЗавершения - ОписаниеОповещения - описание процедуры, которая будет вызвана после завершения
//                                              отправки письма.
//  СтандартнаяОбработка - Булево - признак продолжения открытия формы нового письма после выхода из этой
//                                  процедуры. Если установить Ложь, форма письма открыта не будет.
Процедура ПередОткрытиемФормыОтправкиПисьма(ПараметрыОтправки, ОбработчикЗавершения, СтандартнаяОбработка) Экспорт
	
	Перем Отправитель, Получатель, Вложения, Тема, Текст, УдалятьФайлыПослеОтправки, Предмет;
	
	ПараметрыОтправки.Свойство("Отправитель", Отправитель);
	ПараметрыОтправки.Свойство("Получатель", Получатель);
	ПараметрыОтправки.Свойство("Тема", Тема);
	ПараметрыОтправки.Свойство("Текст", Текст);

	Если Не CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		// В спарках может оказаться обычный текст
		// например, при формировании подписи о вложениях письма.
		Текст = CRM_УправлениеЭлектроннойПочтой.ТекстВHTML(Текст);
	КонецЕсли;
	
	ПараметрыОтправки.Свойство("Вложения", Вложения);
	ПараметрыОтправки.Свойство("УдалятьФайлыПослеОтправки", УдалятьФайлыПослеОтправки);
	ПараметрыОтправки.Свойство("Основание", Предмет);
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда
		ПараметрыОтправки.Свойство("Предмет", Предмет);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Предмет) И ПараметрыОтправки.Свойство("ВладелецФайлов") Тогда
		ПараметрыОтправки.Свойство("ВладелецФайлов", Предмет);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	CRM_ВзаимодействияКлиент.CRM_ОткрытьФормуОтправкиПочтовогоСообщения(Неопределено,
		Получатель, Тема, Текст, Вложения, Предмет, ОбработчикЗавершения);
	
КонецПроцедуры

#КонецОбласти