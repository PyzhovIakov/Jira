
#Область СлужебныйПрограммныйИнтерфейс

Функция СформироватьСодержаниеДляПечати(Номенклатура, Характеристика) Экспорт
	НаименованиеНоменклатуры = ?(Пустаястрока(Номенклатура.НаименованиеПолное), Номенклатура.Наименование, Номенклатура.НаименованиеПолное);
	Если ЗначениеЗаполнено(Характеристика) Тогда
		НаименованиеХарактеристики = ?(Пустаястрока(Характеристика.НаименованиеПолное), Характеристика.Наименование, Характеристика.НаименованиеПолное);
	Иначе
		НаименованиеХарактеристики = "";
	КонецЕсли;	
	Содержание = НаименованиеНоменклатуры + ?(Пустаястрока(НаименованиеХарактеристики), "", " ("+НаименованиеХарактеристики+")");
	Возврат Содержание;
КонецФункции

#Область ДобавлениеРеквизитовВФормы

Процедура ДобавитьЭлементыФормыДляКонтакта(Форма) Экспорт
	
	ЦветРамки = Новый Цвет(221, 221, 221);
	
	Элемент = Форма.Элементы.Найти("ДолжностьПоВизитке");
	Если Элемент = Неопределено Тогда
		Элемент = Форма.Элементы.Вставить("ДолжностьПоВизитке", Тип("ПолеФормы"),
			Форма.Элементы.Найти("ГруппаСведения"), Форма.Элементы.Найти("CRM_Состояние"));
		Элемент.ПутьКДанным = "Объект.ДолжностьПоВизитке";
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.ЦветРамки = ЦветРамки;
		Элемент.АвтоМаксимальнаяШирина = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбменССайтомФормаУзлаПриСозданииНаСервере(Форма) Экспорт
	
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "CRM_ТекущийТриггерОбъекта") Тогда
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("CRM_ТекущийТриггерОбъекта", Новый ОписаниеТипов("СправочникСсылка.CRM_Триггеры")));
		
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	КонецЕсли;
	
	Если НЕ Форма.Объект.Ссылка.Пустая() Тогда
		Форма.CRM_ТекущийТриггерОбъекта = ОбменССайтомТриггерСозданияИнтереса(Форма.Объект.Ссылка);
	КонецЕсли;
	
	Гиперссылка = Форма.Элементы.Найти("CRM_Команда_Гиперссылка");
	Если Гиперссылка = Неопределено Тогда
		Гиперссылка = Форма.Элементы.Добавить("CRM_Команда_Гиперссылка", Тип("ДекорацияФормы"), Форма.Элементы.ГруппаЗаказыОсновныеНастройки );
	КонецЕсли;
	Гиперссылка.Заголовок = ОбменССайтомПредставлениеТриггера(Форма.CRM_ТекущийТриггерОбъекта);
		
КонецПроцедуры
	
Функция ОбменССайтомТриггерСозданияИнтереса(НастройкаОбменаССайтом) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	CRM_Триггеры.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.CRM_Триггеры КАК CRM_Триггеры
	                      |ГДЕ
	                      |	CRM_Триггеры.Действие = ЗНАЧЕНИЕ(Справочник.CRM_ДействияТриггеров.CRM_Модуль_ПривязкаЗаказовКИнтересам)
	                      |	И CRM_Триггеры.УсловиеСрабатывания = ЗНАЧЕНИЕ(Справочник.CRM_УсловияСрабатыванияТриггеров.CRM_Модуль_ПриЗаписиЗаказаКлиента)
	                      |	И НЕ CRM_Триггеры.ПометкаУдаления
	                      |	И CRM_Триггеры.Действует");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НастройкиДействия = Выборка.Ссылка.НастройкиДействий.Получить();
		Если ТипЗнч(НастройкиДействия) = Тип("Структура") И НастройкиДействия.Свойство("НастройкаОбменаССайтом")
			И НастройкиДействия.НастройкаОбменаССайтом = НастройкаОбменаССайтом Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
	КонецЦикла;
КонецФункции

Функция ОбменССайтомПредставлениеТриггера(Триггер) Экспорт
	КомпонентыЗаголовка = Новый Массив;
	Если ЗначениеЗаполнено(Триггер) Тогда
		КомпонентыЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'При загрузке заказов, будут создаваться интересы.'")+Символы.ПС));
		КомпонентыЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Текущие настройки:'")+Символы.ПС));
		КомпонентыЗаголовка.Добавить(Новый ФорматированнаяСтрока(Триггер.ПредставлениеНастроекДействия,
			,,, "e1cib/command/Справочник.CRM_Триггеры.Команда.CRM_Модуль_НастройкаТриггераДляОбменаССайтом"));
	Иначе
		КомпонентыЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Для создания интересов, при загрузке заказов, нужно настроить триггер.'")+" "));
		КомпонентыЗаголовка.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Настроить триггер.'"),
			,,, "e1cib/command/Справочник.CRM_Триггеры.Команда.CRM_Модуль_НастройкаТриггераДляОбменаССайтом"));
	КонецЕсли;
	Возврат Новый ФорматированнаяСтрока(КомпонентыЗаголовка);
КонецФункции

#КонецОбласти

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
