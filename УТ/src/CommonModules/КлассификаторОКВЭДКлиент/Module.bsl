///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ОКВЭД".
// ОбщийМодуль.КлассификаторОКВЭДКлиент.
//
// Клиентские функции по работе с ОКВЭД:
//  - открытие формы выбора значения из классификатора.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет открытие формы списка регистра сведений "КлассификаторОКВЕД" в режиме выбора.
// Для формирования структуры параметров по умолчанию, рекомендуется использовать функцию
// КлассификаторОКВЭДКлиент.НовыйПараметрыВыбора.
//
// Параметры:
//  ПараметрыФормы - Структура - настройки открываемой формы. см. КлассификаторОКВЭДКлиент.НовыйПараметрыВыбора.
//
Процедура Выбрать(ПараметрыФормы) Экспорт
	
	ПравильныеПараметры = НовыйПараметрыВыбора();
	ЗаполнитьЗначенияСвойств(ПравильныеПараметры, ПараметрыФормы);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора",        Истина);
	ПараметрыОткрытия.Вставить("Разрешенные",        ПравильныеПараметры.Разрешенные);
	ПараметрыОткрытия.Вставить("ТекущийКод",         ПравильныеПараметры.ТекущийКод);
	ПараметрыОткрытия.Вставить("МножественныйВыбор", ПравильныеПараметры.МножественныйВыбор);
	ПараметрыОткрытия.Вставить("СтрокаПоиска",       ПравильныеПараметры.СтрокаПоиска);
	
	ОткрытьФорму(
		"РегистрСведений.ДанныеОКВЭД.ФормаСписка",
		ПараметрыОткрытия,
		ПравильныеПараметры.Владелец,
		Новый УникальныйИдентификатор,
		,
		,
		ПравильныеПараметры.ОписаниеОповещенияОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

// Возвращает структуру, необходимую для использования в процедуре Выбрать.
//
// Возвращаемое значение:
//  Структура - параметры, которые можно использовать при открытии формы выбора из ОКВЭД:
//    * ОписаниеОповещенияОЗакрытии - ОписаниеОповещения, Неопределено - содержит описание процедуры, которая будет
//      вызвана при закрытии формы. В случае выбора значения в результат оповещения будет возвращена структура:
//        ** ВыбранныеЗначения - Массив из Структура - значения, которые выбрал пользователь:
//          *** Раздел - Строка - раздел классификатора, к которому относится найденное значение.
//          *** Код - Строка - код по классификатору.
//          *** Наименование - Строка - текстовое описание выбранного значения.
//          *** Представление - Строка - включает код и наименование.
//      Если параметр не указан, то по завершению никакая процедура вызвана не будет.
//      Значение по умолчанию - Неопределено.
//    * Владелец - Произвольный, Неопределено - владелец открываемой формы - форма, элемент управления другой формы,
//      либо Неопределено. Значение по умолчанию - Неопределено.
//    * Разрешенные - Булево - позволяет выбрать только значения, длина кода которого больше или равна 4 (без учета
//      точек). Значение по умолчанию - Ложь.
//    * МножественныйВыбор - Булево - Позволяет выбрать несколько значений. Значение по умолчанию - Ложь.
//    * ТекущийКод - Строка - код ранее выбранного значения. Может быть не указан. Используется для позиционирования в
//      списке. Значение по умолчанию - пустая строка.
//    * СтрокаПоиска - Строка - если заполнена, то при открытии формы будет выполнен поиск значений, код или
//      наименование которых содержит указанную строку. Значение по умолчанию - пустая строка.
//
Функция НовыйПараметрыВыбора() Экспорт
	
	Результат = Новый Структура();
	
	Результат.Вставить("ОписаниеОповещенияОЗакрытии", Неопределено);
	Результат.Вставить("Владелец",                    Неопределено);
	Результат.Вставить("Разрешенные",                 Ложь);
	Результат.Вставить("МножественныйВыбор",          Ложь);
	Результат.Вставить("ТекущийКод",                  "");
	Результат.Вставить("СтрокаПоиска",                "");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти