///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура выполняет добавление аудио-файла.
//
// Параметры:
//  УникальныйИдентификатор	 - УникальныйИдентификатор	 - Уникальный идентификатор формы.
//  РезультатАудиозаписи	 - ДвоичныеДанные	 - Результат аудиозаписи.
//  ПараметрыМодели			 - ПараметрыМоделиРаспознаванияРечи	 - Параметры модели распознавания речи.
//  ВариантИспользования	 - ВариантИспользованияРасположенияРаботыСРечью	 - Вариант использования расположения работы
//  	с речью.
//  ВремяНачало				 - Число	 - Время начала распознавания речи в милисекундах.
//  ВремяКонец				 - Число	 - Время окончания распознавания речи в милисекундах.
//
Процедура ДобавитьАудио(УникальныйИдентификатор, РезультатАудиозаписи, ПараметрыМодели, ВариантИспользования, ВремяНачало, ВремяКонец) Экспорт
	
	ИнициализацияПараметровПриложения(УникальныйИдентификатор);
	
	Структура = Новый Структура;
	Структура.Вставить("АудиоДанные", РезультатАудиозаписи);
	Структура.Вставить("ПараметрыМодели", ПараметрыМодели);
	Структура.Вставить("ВариантИспользования", ВариантИспользования);
	
	Структура.Вставить("Начало", ПредставлениеДаты(ВремяНачало));
	Структура.Вставить("Конец",  ПредставлениеДаты(ВремяКонец));
	Структура.Вставить("Длительность", Строка(ВремяКонец - ВремяНачало));

	ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор].Аудио.Добавить(Структура);
	
	КоличествоАудио = ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор].Аудио.Количество();
	КоличествоТекстов = ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор].Текст.Количество();
	
	Если КоличествоАудио > КоличествоТекстов Тогда
		ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор].Текст.Добавить(Новый Массив);
	КонецЕсли;
	
КонецПроцедуры

// Процедура добавляет результат распознавания
//
// Параметры:
//  УникальныйИдентификатор	 - УникальныйИдентификатор	 - Уникальный идентификатор формы.
//  РезультатРаспознавания	 - РезультатРаспознаванияРечи	 - Результат распознаваня речи.
//  ВремяНачало				 - Число	 - Время начала распознавания речи в милисекундах.
//  ВремяКонец				 - Число	 - Время окончания распознавания речи в милисекундах.
//
Процедура ДобавитьРезультатРаспознавания(УникальныйИдентификатор, РезультатРаспознавания, ВремяНачало, ВремяКонец) Экспорт
	
	ИнициализацияПараметровПриложения(УникальныйИдентификатор);
	
	НомерФайла = ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор].Аудио.Количество();
	Если ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор].Текст.Количество() <= НомерФайла Тогда
		ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор].Текст.Добавить(Новый Массив);
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("Данные", РезультатРаспознавания.ДанныеФраз[0].Фраза);
	Структура.Вставить("ДанныеПостобработка", CRM_ОбработкаРаспознанногоТекстаКлиентСервер.ТекстИзРезультатаРаспознавания(РезультатРаспознавания));
	Структура.Вставить("РаспознаваниеФразыЗавершено", РезультатРаспознавания.РаспознаваниеФразыЗавершено);
	Структура.Вставить("Начало", ПредставлениеДаты(ВремяНачало));
	Структура.Вставить("Конец",  ПредставлениеДаты(ВремяКонец));
	Структура.Вставить("Длительность", Строка(ВремяКонец - ВремяНачало));
	
	ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор].Текст[НомерФайла].Добавить(Структура);
	
КонецПроцедуры

// Процедура выполняет добавление исключения.
//
// Параметры:
//  УникальныйИдентификатор	 - УникальныйИдентификатор	 - Уникальный идентификатор формы.
//  ИнформацияОбОшибке		 - ИнформацияОбОшибке	 - Информация об ошибке.
//
Процедура ДобавитьИсключение(УникальныйИдентификатор, ИнформацияОбОшибке) Экспорт
	
	ИнициализацияПараметровПриложения(УникальныйИдентификатор);
	
	ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор].Исключения.Добавить(
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке)
	);
	
КонецПроцедуры

// Процедура выполняет очистку журнала.
//
// Параметры:
//  УникальныйИдентификатор	 - УникальныйИдентификатор	 - Уникальный идентификатор формы.
//
Процедура ОчиститьЖурнал(УникальныйИдентификатор) Экспорт
	
	Если Не ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"] = Неопределено Тогда
		Если Не ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор] = Неопределено Тогда
			ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"].Удалить(УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует письмо и открывает форму оформления жалобы на результат распознавания речи.
//
// Параметры:
//  УникальныйИдентификатор	 - УникальныйИдентификатор	 - Уникальный идентификатор формы.
//
Процедура СформироватьПисьмо(УникальныйИдентификатор) Экспорт
	
	ИнициализацияПараметровПриложения(УникальныйИдентификатор);
	
	Для Каждого Запись Из ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор].Аудио Цикл
		Если Запись.АудиоДанные <> Неопределено Тогда
			Если Запись.АудиоДанные.Размер() > 64*1024*1024 Тогда
				Запись.АудиоДанные = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Вложения = CRM_РаботаСРечьюЖурналированиеВызовСервера.СформироватьВложенияПисьма(
		ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор],
		УникальныйИдентификатор
	);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("СписокФайлов", Вложения);
	
	ОткрытьФорму("ОбщаяФорма.CRM_ПожаловатьсяНаРезультатРаспознаванияРечи", ПараметрыОтправки, , Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализацияПараметровПриложения(УникальныйИдентификатор)
	
	Если ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"] = Неопределено Тогда
		
		ПараметрыПриложения.Вставить("РаботаСРечью_ДанныеОтладки", Новый Соответствие);
		
	КонецЕсли;
	
	Если ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"][УникальныйИдентификатор] = Неопределено Тогда
		
		СтруктураРаспознанныхДанных = Новый Структура;
		СтруктураРаспознанныхДанных.Вставить("Текст", Новый Массив);
		СтруктураРаспознанныхДанных.Вставить("Аудио", Новый Массив);
		СтруктураРаспознанныхДанных.Вставить("Исключения", Новый Массив);
		
		ПараметрыПриложения["РаботаСРечью_ДанныеОтладки"].Вставить(УникальныйИдентификатор, СтруктураРаспознанныхДанных);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПредставлениеДаты(ВремяМилисекунд)
	
	Возврат Строка(Дата(1, 1, 1) + ВремяМилисекунд/1000) + "." + Формат(ВремяМилисекунд%1000, "ЧЦ=3; ЧВН=; ЧГ=0")
	
КонецФункции

#КонецОбласти