
#Область ПрограммныйИнтерфейс

#Область ПоискДублейПоЗначению

// Обрабатывает выбор таблицы результатов.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - Форма.
//	Элемент - ЭлементФормы - Элемент формы.
//	ВыбраннаяСтрока - СправочникСсылка - Выбранная строка.
//	Поле - ПолеФормы - Поле.
//	СтандартнаяОбработка - Булево - Признак стандартной обработки.
//
Процедура ОбработатьВыборТаблицыРезультатов(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрЧислоВхождений(Поле.Имя, "КомандаВыбрать") > 0 Тогда
		Элемент.Родитель.Скрыть();
		Форма.СтрокаПоиска = Элемент.ТекущиеДанные.ЗначениеПоиска;
		Форма.ВыполнитьПоиск(Истина);
		
		ПараметрыОтбораРезультатаПоиска = Новый Структура;
		
		Если ТипЗнч(Элемент.ТекущиеДанные.КлиентКонтакт) = Тип("СправочникСсылка.Партнеры") Тогда
			
			ПараметрыОтбораРезультатаПоиска.Вставить("Клиент", Элемент.ТекущиеДанные.КлиентКонтакт);
			
		ИначеЕсли ТипЗнч(Элемент.ТекущиеДанные.КлиентКонтакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров")
			ИЛИ ТипЗнч(Элемент.ТекущиеДанные.КлиентКонтакт) = Тип("СправочникСсылка.CRM_ПотенциальныеКлиенты") Тогда
			
			ПараметрыОтбораРезультатаПоиска.Вставить("Контакт", Элемент.ТекущиеДанные.КлиентКонтакт);
			
		КонецЕсли;
		
		СтрокиПозиционирования = Форма.РезультатыПоиска.НайтиСтроки(ПараметрыОтбораРезультатаПоиска);
		
		Для Каждого СтрПозиционирования Из СтрокиПозиционирования Цикл
			Форма.Элементы.РезультатыПоиска.ТекущаяСтрока = СтрПозиционирования.ПолучитьИдентификатор();
		КонецЦикла;
		
	Иначе			
		Если Элемент.ТекущиеДанные.Доступен Тогда
			ПоказатьЗначение(, Элемент.ТекущиеДанные.КлиентКонтакт);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Восстановливает отображение списка после полнотекстового поиска
//
// Параметры:
//  Форма	 - ФормаКлиентскогоПриложения	 - Форма
//
Процедура ВосстановитьОтображениеСпискаПослеПолнотекстовогоПоиска(Форма) Экспорт
	
	Если Форма.РасширенныйПоиск
		 И Строка(Форма.Элементы.Список.Отображение) <> Форма.РежимОтображенияДоПримененияПолнотекстовогоПоиска Тогда
		Если Форма.РежимОтображенияДоПримененияПолнотекстовогоПоиска = НСтр("ru = 'Иерархический список'") Тогда
			Форма.Элементы.Список.Отображение = ОтображениеТаблицы.ИерархическийСписок;
		ИначеЕсли Форма.РежимОтображенияДоПримененияПолнотекстовогоПоиска = НСтр("ru = 'Дерево'") Тогда
			Форма.Элементы.Список.Отображение = ОтображениеТаблицы.Дерево;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#КонецОбласти
