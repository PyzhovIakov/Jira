
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// +CRM
	Если Параметры.Свойство("РольИсполнителя") Тогда
		Роль = Параметры.РольИсполнителя;
	КонецЕсли;	
	Если Параметры.Свойство("ОсновнойОбъектАдресации") Тогда
		ОсновнойОбъектАдресации = Параметры.ОсновнойОбъектАдресации;
	КонецЕсли;	
	Если Параметры.Свойство("ДополнительныйОбъектАдресации") Тогда
		ДополнительныйОбъектАдресации = Параметры.ДополнительныйОбъектАдресации;
	КонецЕсли;
	// -CRM
		
	УстановитьТипыОбъектовАдресации();
	УстановитьСостояниеЭлементов();

	// +CRM
	//Если Параметры.ВыборОбъектаАдресации Тогда
	//	ТекущийЭлемент = Элементы.ОсновнойОбъектАдресации;
	//КонецЕсли;
	// -CRM
		
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// +CRM
	//Если ИспользуетсяБезОбъектовАдресации Тогда
	//	Возврат;
	//КонецЕсли;
	// -CRM
	
	ЗаданыТипыОсновногоОбъектаАдресации = ИспользуетсяСОбъектамиАдресации
		И ЗначениеЗаполнено(ТипыОсновногоОбъектаАдресации);
	ЗаданыТипыДополнительногоОбъектаАдресации = ИспользуетсяСОбъектамиАдресации 
		И ЗначениеЗаполнено(ТипыДополнительногоОбъектаАдресации);
	
	Если ЗаданыТипыОсновногоОбъектаАдресации И ОсновнойОбъектАдресации = Неопределено Тогда
	
		ОбщегоНазначения.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Поле ""%1"" не заполнено.';
				|en='The field ""%1"" is not filled in.'"),
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Роль, "ТипыОсновногоОбъектаАдресации")), ,
				"ОсновнойОбъектАдресации", , Отказ);
	ИначеЕсли ЗаданыТипыДополнительногоОбъектаАдресации И ДополнительныйОбъектАдресации = Неопределено Тогда
	
		ОбщегоНазначения.СообщитьПользователю( 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Поле ""%1"" не заполнено.';
				|en='The field ""%1"" is not filled in.'"),
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Роль, "ТипыДополнительногоОбъектаАдресации")), ,
			"ДополнительныйОбъектАдресации", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РольПриИзменении(Элемент)
	
	ОсновнойОбъектАдресации = Неопределено;
	ДополнительныйОбъектАдресации = Неопределено;
	УстановитьТипыОбъектовАдресации();
	УстановитьСостояниеЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура OKВыполнить()

	ОчиститьСообщения();
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;  
	КонецЕсли;
	
	РезультатВыбора = ПараметрыЗакрытия();
	
	// +CRM
	//ОповеститьОВыборе(РезультатВыбора);
	Закрыть(РезультатВыбора);
	// -CRM
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТипыОбъектовАдресации()
	
	ТипыОсновногоОбъектаАдресации = Неопределено;
	ТипыДополнительногоОбъектаАдресации = Неопределено;
	ИспользуетсяСОбъектамиАдресации = Ложь;
	ИспользуетсяБезОбъектовАдресации = Ложь;
	
	Если Не Роль.Пустая() Тогда
		СведенияОРоли = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Роль, 
			"ИспользуетсяСОбъектамиАдресации,ИспользуетсяБезОбъектовАдресации,ТипыОсновногоОбъектаАдресации,ТипыДополнительногоОбъектаАдресации");
		ИспользуетсяСОбъектамиАдресации = СведенияОРоли.ИспользуетсяСОбъектамиАдресации;
		ИспользуетсяБезОбъектовАдресации = СведенияОРоли.ИспользуетсяБезОбъектовАдресации;
		Если ИспользуетсяСОбъектамиАдресации Тогда
			ТипыОсновногоОбъектаАдресации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СведенияОРоли.ТипыОсновногоОбъектаАдресации, "ТипЗначения");
			ТипыДополнительногоОбъектаАдресации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СведенияОРоли.ТипыДополнительногоОбъектаАдресации, "ТипЗначения");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеЭлементов()

	ЗаданыТипыОсновногоОбъектаАдресации = ИспользуетсяСОбъектамиАдресации
		И ЗначениеЗаполнено(ТипыОсновногоОбъектаАдресации);
	ЗаданыТипыДополнительногоОбъектаАдресации = ИспользуетсяСОбъектамиАдресации 
		И ЗначениеЗаполнено(ТипыДополнительногоОбъектаАдресации);
	
	СведенияОРоли = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Роль, 
		"ТипыОсновногоОбъектаАдресации,ТипыДополнительногоОбъектаАдресации");
	
	// +CRM
	Если ЗаданыТипыОсновногоОбъектаАдресации И ЗаданыТипыДополнительногоОбъектаАдресации Тогда
		Элементы.ГруппаОбъектыАдресации.ТекущаяСтраница = Элементы.ГруппаДваОбъектаАдресации;
	ИначеЕсли ЗаданыТипыОсновногоОбъектаАдресации Тогда
		Элементы.ГруппаОбъектыАдресации.ТекущаяСтраница = Элементы.ГруппаОдинОбъектАдресации;
	Иначе	
		Элементы.ГруппаОбъектыАдресации.ТекущаяСтраница = Элементы.ГруппаНетОбъектовАдресации;
	КонецЕсли;
	
	Элементы.ОдинОсновнойОбъектАдресации.Заголовок = Строка(СведенияОРоли.ТипыОсновногоОбъектаАдресации);
	Элементы.ОдинОсновнойОбъектАдресации.ОграничениеТипа = ТипыОсновногоОбъектаАдресации;
	Элементы.ОдинОсновнойОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыОсновногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации;
	// -CRM
	
	Элементы.ОсновнойОбъектАдресации.Заголовок = Строка(СведенияОРоли.ТипыОсновногоОбъектаАдресации);
	// +CRM
	//Элементы.ОсновнойОбъектАдресации.Доступность = ЗаданыТипыОсновногоОбъектаАдресации;
	// -CRM
	Элементы.ОсновнойОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыОсновногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации;
	Элементы.ОсновнойОбъектАдресации.ОграничениеТипа = ТипыОсновногоОбъектаАдресации;
	
	Элементы.ДополнительныйОбъектАдресации.Заголовок = Строка(СведенияОРоли.ТипыДополнительногоОбъектаАдресации);
	// +CRM
	//Элементы.ДополнительныйОбъектАдресации.Доступность = ЗаданыТипыДополнительногоОбъектаАдресации;
	// -CRM
	Элементы.ДополнительныйОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыДополнительногоОбъектаАдресации
		И НЕ ИспользуетсяБезОбъектовАдресации;
	Элементы.ДополнительныйОбъектАдресации.ОграничениеТипа = ТипыДополнительногоОбъектаАдресации;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыЗакрытия()
	
	Результат = Новый Структура;
	Результат.Вставить("РольИсполнителя", Роль);
	Результат.Вставить("ОсновнойОбъектАдресации", ОсновнойОбъектАдресации);
	Результат.Вставить("ДополнительныйОбъектАдресации", ДополнительныйОбъектАдресации);
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Результат.ОсновнойОбъектАдресации)) И Результат.ОсновнойОбъектАдресации.Пустая() Тогда
		Результат.ОсновнойОбъектАдресации = Неопределено;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Результат.ДополнительныйОбъектАдресации)) И Результат.ДополнительныйОбъектАдресации.Пустая() Тогда
		Результат.ДополнительныйОбъектАдресации = Неопределено;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
