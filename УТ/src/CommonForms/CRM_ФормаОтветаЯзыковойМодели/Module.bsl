
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ИдентификаторДействия") Тогда
		Действие = Справочники.CRM_ДействияЯзыковыхМоделей.ПолучитьСсылку(
			Новый УникальныйИдентификатор(Параметры.ИдентификаторДействия));
		Элементы.ФормаПрименить.Видимость = Действие.ИзменяетТекст И Параметры.ИзменятьИсходныйТекст;
	Иначе	
		Отказ = Истина;
	КонецЕсли;
	
	Параметры.Свойство("ИмяРеквизита", ИмяРеквизита);
	Параметры.Свойство("ОбрабатываемыйТекст", ОбрабатываемыйТекст);

	Сервис = ХранилищеОбщихНастроек.Загрузить("CRM_СервисЯзыковыхМоделейПоУмолчанию");
	
	СвойЗапрос = (Действие = Справочники.CRM_ДействияЯзыковыхМоделей.СвойЗапрос);
	
	Если СвойЗапрос Тогда
		ОбрабатываемыйТекстHTML = "<br>";
		Элементы.ФормаОбработатьЗапрос.Видимость = Истина;
	Иначе
		Результат = CRM_РаботаСЯзыковымиМоделямиСервер.ВыполнитьЗапросКЯзыковойМодели(Сервис,
			Действие.СистемныйПромпт, ОбрабатываемыйТекст);
		Если ЗначениеЗаполнено(Результат.Ошибка) Тогда
			Ошибка = Результат.Ошибка;
		Иначе
			Ответ = Результат.Сообщение;
			ОбрабатываемыйТекстHTML = ОбрабатываемыйТекст;
			ОтветHTML = Ответ;
			CRM_ВзаимодействияКлиентСервер.ДобавитьНеобходимыеТэгиHTML(ОтветHTML);
		КонецЕсли;
	КонецЕсли;
	CRM_ВзаимодействияКлиентСервер.ДобавитьНеобходимыеТэгиHTML(ОбрабатываемыйТекстHTML);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(Ошибка) Тогда
		Закрыть();
		ВыполнитьОбработкуОповещения(ЭтотОбъект.ОписаниеОповещенияОЗакрытии,
			Новый Структура("Ошибка", Ошибка));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	ЭлементВладельца = ВладелецФормы.ТекущийЭлемент;
	Если ТипЗнч(ЭлементВладельца) = Тип("ПолеФормы") Тогда
		Если ЭлементВладельца.Вид = ВидПоляФормы.ПолеВвода Тогда
			Если ЭлементВладельца.ВыделенныйТекст <> "" Тогда
				ЭлементВладельца.ВыделенныйТекст = Ответ;
			Иначе	
				ЭлементВладельца.УстановитьГраницыВыделения(1, СтрДлина(ОбрабатываемыйТекст)+1);
				ЭлементВладельца.ВыделенныйТекст = Ответ;
			КонецЕсли;
		ИначеЕсли ЭлементВладельца.Вид = ВидПоляФормы.ПолеHTMLДокумента Тогда
			Выделение = ЭлементВладельца.Документ.defaultView.getSelection().getRangeAt(0);
			Если ЗначениеЗаполнено(Выделение.toString()) Тогда
				Выделение.deleteContents();
				ЭлементHTML = ЭлементВладельца.Документ.createElement("span");
				ЭлементHTML.innerHTML = Ответ;
				Выделение.insertNode(ЭлементHTML);
			Иначе
				СтруктураHTML = CRM_РаботаСHTML.РазложитьТелоHTML(ЭлементВладельца.Документ.body.innerHTML);
				Если ЗначениеЗаполнено(СтруктураHTML.Цитирование) Тогда
					ЭлементВладельца.Документ.body.innerHTML = Ответ + СтруктураHTML.Цитирование;
				Иначе
					ЭлементВладельца.Документ.body.innerHTML = Ответ;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура СкопироватьРезультат(Команда)
	
	Если Не СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		Возврат;
	КонецЕсли;
	
	ФорматДанныхHTML = СтандартныйФорматДанныхБуфераОбмена.HTML;
	ФорматДанныхТекст = СтандартныйФорматДанныхБуфераОбмена.Текст;
	Если Ждать СредстваБуфераОбмена.ПоддерживаетсяФорматДанных(ФорматДанныхHTML) Тогда
		
		ДанныеHTML = Новый ЭлементБуфераОбмена(ФорматДанныхHTML, Элементы.ПолеОтвета.Документ.body.innerHTML);
		ДанныеТекст = Новый ЭлементБуфераОбмена(ФорматДанныхТекст, Элементы.ПолеОтвета.Документ.body.innerText);
		
		ПомещаемыеДанные = Новый Массив;
		ПомещаемыеДанные.Добавить(ДанныеHTML);
		ПомещаемыеДанные.Добавить(ДанныеТекст);
		
		Ждать СредстваБуфераОбмена.ПоместитьДанныеАсинх(ПомещаемыеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбрабатываемыйТекстДокументСформирован(Элемент)
	Если СвойЗапрос Тогда
		CRM_РаботаСHTMLКлиент.ПолеHTMLДокументСформирован(ЭтотОбъект, Элемент, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработатьЗапросНаСервере(Запрос)
	Результат = CRM_РаботаСЯзыковымиМоделямиСервер.ВыполнитьЗапросКЯзыковойМодели(Сервис,
		"", Запрос);
	Если ЗначениеЗаполнено(Результат.Ошибка) Тогда
		Ошибка = Результат.Ошибка;
	Иначе
		Ответ = Результат.Сообщение;
		Ошибка = "";
		ОтветHTML = Ответ;
		CRM_ВзаимодействияКлиентСервер.ДобавитьНеобходимыеТэгиHTML(ОтветHTML);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗапрос(Команда)
	ОбработатьЗапросНаСервере(Элементы.ОбрабатываемыйТекст.Документ.body.innerText);
	Если ЗначениеЗаполнено(Ошибка) Тогда
		ПоказатьПредупреждение(, Ошибка);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

