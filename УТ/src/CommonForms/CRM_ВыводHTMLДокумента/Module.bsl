
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Размер") Тогда
		CRM_ОбщегоНазначенияСервер.СбросНастроекПоложенияОкна(ЭтотОбъект);
		Высота = Параметры.Размер.Высота;
		Ширина = Параметры.Размер.Ширина;
	КонецЕсли;
	
	Если Параметры.Свойство("ТекстHTML") Тогда
		ТекстДляВывода = Параметры.ТекстHTML;
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Если Параметры.Свойство("Кнопки") Тогда
		ДобавитьКнопки(Параметры.Кнопки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьКнопки(Кнопки)
	
	Для Каждого Кнопка Из Кнопки Цикл
		Команда = Команды.Добавить(Кнопка.Значение);
		Команда.Заголовок = Кнопка.Представление;
		Команда.Картинка = Кнопка.Картинка;
		Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;
		Команда.Действие = "Подключаемый_ВыполнениеКоманды";
		
		ЭлементКнопки = Элементы.Добавить(Кнопка.Значение, Тип("КнопкаФормы"), Элементы.ФормаКоманднаяПанель);
		ЭлементКнопки.ИмяКоманды = Кнопка.Значение;
		Если Кнопка.Пометка Тогда
			ЭлементКнопки.ЦветФона = ЦветаСтиля.CRM_ОсновнойГолубой;
			ЭлементКнопки.ЦветРамки = ЦветаСтиля.CRM_ОсновнойГолубой;
			ЭлементКнопки.ЦветТекста = ЦветаСтиля.ЦветФонаФормы;
		Иначе
			ЭлементКнопки.ЦветТекста = ЦветаСтиля.CRM_ОсновнойГолубой;
			ЭлементКнопки.ОтображениеФигуры = ОтображениеФигурыКнопки.Нет;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте 
Процедура Подключаемый_ВыполнениеКоманды(Команда)
	Закрыть(Команда.Имя);
КонецПроцедуры

#КонецОбласти
