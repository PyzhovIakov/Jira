
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресПубликацииИнформационнойБазыВИнтернете = ОбщегоНазначения.АдресПубликацииИнформационнойБазыВИнтернете();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СформироватьАдресСсылки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресПубликацииИнформационнойБазыВИнтернетеПриИзменении(Элемент)
	
	СформироватьАдресСсылки();

КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаОбъектПриИзменении(Элемент)
	
	СформироватьАдресСсылки();
	СформироватьПредставлениеСсылки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешняяСсылкаПриИзменении(Элемент)
	Элементы.АдресПубликацииИнформационнойБазыВИнтернете.Доступность = ВнешняяСсылка;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Вставить(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	
	Если ВнешняяСсылка И ПустаяСтрока(АдресПубликацииИнформационнойБазыВИнтернете) Тогда
		
		ТекстСообщения = НСтр("ru = 'Не указан адрес публикации информационной базы в интернете.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "АдресПубликацииИнформационнойБазыВИнтернете",, Отказ);
		
	КонецЕсли;
	
	Если ПустаяСтрока(СсылкаНаОбъект) Тогда
		
		ТекстСообщения = НСтр("ru = 'Не указана внутренняя ссылка на объект.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "СсылкаНаОбъект",, Отказ);
		
	КонецЕсли;
	
	Если Не Отказ Тогда
		СтруктураДанных = Новый Структура("Ссылка, ПредставлениеСсылки", СформированнаяСсылка, ПредставлениеСсылки);
		ОповеститьОВыборе(СтруктураДанных);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьАдресСсылки()
	
	Если Не СтрНачинаетсяС(СсылкаНаОбъект, "e1cib/") Тогда
		ПозицияНачалаСсылки = СтрНайти(СсылкаНаОбъект, "e1cib/");
		СсылкаНаОбъект = Сред(СсылкаНаОбъект, ПозицияНачалаСсылки);
	КонецЕсли;
	
	Если ВнешняяСсылка Тогда
		СформированнаяСсылка = АдресПубликацииИнформационнойБазыВИнтернете + "#"+ СсылкаНаОбъект;
	Иначе
		СформированнаяСсылка = СсылкаНаОбъект;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредставлениеСсылки()
	
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(СсылкаНаОбъект);
	ПредставлениеСсылки = ПолучитьПредставленияНавигационныхСсылок(МассивСсылок)[0];
	
КонецПроцедуры

#КонецОбласти
