
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипДокумента = Неопределено;
	Если Параметры.Свойство("ТипДокумента", ТипДокумента) Тогда
		Если ТипДокумента = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
			ОписаниеТипа = Метаданные.ОпределяемыеТипы.ДокументыОснованияЭТрН_ЭПД.Тип;	
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЭлектроннаяСопроводительнаяВедомость") Тогда
			ОписаниеТипа = Метаданные.ОпределяемыеТипы.ДокументыОснованияЭСВ_ЭПД.Тип;
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЭлектронныйДоговорФрахтования") Тогда
			ОписаниеТипа = Метаданные.ОпределяемыеТипы.ДокументыОснованияЭДФ_ЭПД.Тип;
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЭлектронныйЗаказЗаявка") Тогда
			ОписаниеТипа = Метаданные.ОпределяемыеТипы.ДокументыОснованияЭЗЗ_ЭПД.Тип;
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЭлектронныйЗаказНаряд") Тогда
			ОписаниеТипа = Метаданные.ОпределяемыеТипы.ДокументыОснованияЭЗН_ЭПД.Тип;
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
			ОписаниеТипа = Метаданные.ОпределяемыеТипы.ДокументыОснованияЭПЛ_ЭПД.Тип;	
		КонецЕсли;
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Документ", ОписаниеТипа, "Документы"));
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
		ДокументыОснованиеДокумент = Элементы.Добавить("ДокументыОснованиеДокумент", Тип("ПолеФормы"), Элементы.ДокументыОснование);
		ДокументыОснованиеДокумент.Вид = ВидПоляФормы.ПолеВвода;
		ДокументыОснованиеДокумент.ПутьКДанным = "Документы.Документ";
	КонецЕсли;
	
	МассивДокументов = Неопределено;
	Если Параметры.Свойство("МассивДокументов", МассивДокументов) Тогда
		Для Каждого Документ Из МассивДокументов Цикл
			НовСтр = Документы.Добавить();	
			НовСтр.Документ = Документ;	
		КонецЦикла;		
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоПросмотр") И Параметры.ТолькоПросмотр = Истина Тогда
		ТолькоПросмотр = Истина;
		Элементы.ДокументыОснование.ТолькоПросмотр = Истина;
		Элементы.ФормаОК.Видимость = Ложь;	
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОК(Команда)
	
	МассивДокументов = Новый Массив;
	Для Каждого СтрТаб Из Документы Цикл
		Если МассивДокументов.Найти(СтрТаб.Документ) = Неопределено Тогда
			МассивДокументов.Добавить(СтрТаб.Документ);
		КонецЕсли;	
	КонецЦикла;
	
	Закрыть(МассивДокументов);
	
КонецПроцедуры
