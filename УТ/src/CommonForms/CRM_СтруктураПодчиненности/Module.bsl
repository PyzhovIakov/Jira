
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОпределитьНастройки();
	
	Параметры.Свойство("ОбъектОтбора", ОсновнойОбъект);
	
	ИсходныйОбъект = ОсновнойОбъект;
	
	Если ЗначениеЗаполнено(ОсновнойОбъект) Тогда
		ОбновитьДеревоСтруктурыПодчиненности();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ВывестиСтруктуруПодчиненности();
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиДляТекущего(Команда)
	
	ТекущийОбъект = Элементы.ТаблицаОтчета.ТекущаяОбласть.Расшифровка;
	
	Если ЗначениеЗаполнено(ТекущийОбъект) Тогда
		ОсновнойОбъект = ТекущийОбъект;
	Иначе
		Возврат;
	КонецЕсли;
	
	ВывестиСтруктуруПодчиненности();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПометкуУдаления(Команда)
	
	ВыбранныеЭлементы = ВыбранныеЭлементы();
	
	Если ВыбранныеЭлементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Статистика = СтатистикаПоВыбраннымЭлементам(ВыбранныеЭлементы);
	
	Сценарий = СценарийИзмененияПометкиУдаления(ВыбранныеЭлементы, Статистика);
	Сценарий.Вставить("ВыбранныеЭлементы", ВыбранныеЭлементы);
	
	Обработчик = Новый ОписаниеОповещения("ВыполнитьСценарийИзмененияПометкиУдаления", ЭтотОбъект, Сценарий);
	ПоказатьВопрос(Обработчик, Сценарий.Вопрос, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура Провести(Команда)
	
	ИзменитьПроведениеДокументов(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПроведение(Команда)
	
	ИзменитьПроведениеДокументов(РежимЗаписиДокумента.ОтменаПроведения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//////////////////////////////////////////////////////////////////////////////////////////////
// Процедуры вывода в табличный документ.

// Выводит дерево подчиненности в табличный документ.
&НаСервере
Процедура ВывестиТабличныйДокумент()

	ТаблицаОтчета.Очистить();
	
	Макет = ПолучитьОбщийМакет("CRM_СтруктураПодчиненности");
	
	ВывестиРодительскиеЭлементыДерева(ДеревоРодительскиеОбъекты.ПолучитьЭлементы(), Макет, 1);
	ВывестиТекущийОбъект(Макет);
	ВывестиПодчиненныеЭлементыДерева(ДеревоПодчиненныеОбъекты.ПолучитьЭлементы(), Макет, 1);
	РеквизитФормыВЗначение("ДеревоРодительскиеОбъекты");
КонецПроцедуры

// Выводит строки дерева родительских объектов.
//
// Параметры:
//  СтрокиДерева  - ДанныеФормыКоллекцияЭлементовДерева - строки дерева
//                 которые выводятся в табличный документ.
//  Макет  - МакетТабличногоДокумента - макет, на основании которого
//           происходит вывод в табличный документ.
//  УровеньРекурсии - Число - уровень рекурсии процедуры.
//
&НаСервере
Процедура ВывестиРодительскиеЭлементыДерева(СтрокиДерева, Макет, УровеньРекурсии)
	
	Счетчик =  СтрокиДерева.Количество();
	Пока Счетчик > 0 Цикл
		
		ТекущаяСтрокаДерева = СтрокиДерева.Получить(Счетчик - 1);
		ПодчиненныеЭлементыСтрокиДерева = ТекущаяСтрокаДерева.ПолучитьЭлементы();
		ВывестиРодительскиеЭлементыДерева(ПодчиненныеЭлементыСтрокиДерева, Макет, УровеньРекурсии + 1);
		
		Для инд = 1 По УровеньРекурсии Цикл
			
			Если инд = УровеньРекурсии Тогда
				
				Если СтрокиДерева.Индекс(ТекущаяСтрокаДерева) < (СтрокиДерева.Количество() - 1) Тогда
					Область = Макет.ПолучитьОбласть("КоннекторВерхПравоНиз");
				Иначе	
					Область = Макет.ПолучитьОбласть("КоннекторПравоНиз");
				КонецЕсли;
				
			Иначе
				
				Если НеобходимостьВыводаВертикальногоКоннектора(УровеньРекурсии - инд + 1, ТекущаяСтрокаДерева, Ложь) Тогда
					Область = Макет.ПолучитьОбласть("КоннекторВерхНиз");
				Иначе
					Область = Макет.ПолучитьОбласть("Отступ");
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если инд = 1 Тогда
				ТаблицаОтчета.Вывести(Область);
			Иначе
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
		КонецЦикла;
		
		ВывестиПредставлениеИКартинку(ТекущаяСтрокаДерева, Макет, Ложь, Ложь);

		Счетчик = Счетчик - 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Выводит в табличный документ картинку, соответствующую статусу объекта и его представление.
//
&НаСервере
Процедура ВывестиПредставлениеИКартинку(СтрокаДерева, Макет, ЭтоТекущийОбъект = Ложь, ЭтоПодчиненный = Неопределено)
	
	МетаданныеОбъекта = СтрокаДерева.Ссылка.Метаданные();
	ЭтоДокумент       = ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта);
	
	Если ТипЗнч(СтрокаДерева.Ссылка) = Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс") Тогда
		
		ИмяОбласти = "БизнесПроцесс";
		
		Если НЕ СтрокаДерева.Ссылка.Стартован Тогда
			Если СтрокаДерева.Ссылка.ПометкаУдаления Тогда
				ИмяОбласти = ИмяОбласти + "НеСтартованПомеченНаУдаление";
			Иначе
				ИмяОбласти = ИмяОбласти + "НеСтартован";
			КонецЕсли;
		Иначе
			Если СтрокаДерева.Ссылка.Завершен Тогда
				Если СтрокаДерева.Ссылка.ПометкаУдаления Тогда
					ИмяОбласти = ИмяОбласти + "ЗавершенПомеченНаУдаление";
				Иначе
					ИмяОбласти = ИмяОбласти + "Завершен";
				КонецЕсли;
			Иначе
				Если СтрокаДерева.Ссылка.ПометкаУдаления Тогда
					ИмяОбласти = ИмяОбласти + "СтартованПомеченНаУдаление";
				Иначе
					ИмяОбласти = ИмяОбласти + "Стартован";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Вывод картинки
		Если ЭтоПодчиненный = Неопределено  Тогда
			Если ДеревоПодчиненныеОбъекты.ПолучитьЭлементы().Количество()
				 И ДеревоРодительскиеОбъекты.ПолучитьЭлементы().Количество()  Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти + "КоннекторВерхНиз");
			ИначеЕсли ДеревоПодчиненныеОбъекты.ПолучитьЭлементы().Количество() Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти + "КоннекторНиз");
			ИначеЕсли ДеревоРодительскиеОбъекты.ПолучитьЭлементы().Количество() Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти + "КоннекторВерх");
			Иначе
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти + "КоннекторВерх");
			КонецЕсли;
		ИначеЕсли ЭтоПодчиненный = Истина Тогда
			Если СтрокаДерева.ПолучитьЭлементы().Количество() Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти + "КоннекторЛевоНиз");
			Иначе
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			КонецЕсли;
		Иначе
			Если СтрокаДерева.ПолучитьЭлементы().Количество() Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти + "КоннекторЛевоВерх");
			Иначе
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипЗнч(СтрокаДерева.Ссылка) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		Если НЕ СтрокаДерева.Ссылка.Выполнена Тогда
			Если СтрокаДерева.Ссылка.ПометкаУдаления Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть("ЗадачаПомеченаНаУдаление");
			Иначе
				ОбластьКартинка = Макет.ПолучитьОбласть("Задача");
			КонецЕсли;
		Иначе
			Если СтрокаДерева.Ссылка.ПометкаУдаления Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть("ЗадачаВыполненаПомеченаНаУдаление");
			Иначе
				ОбластьКартинка = Макет.ПолучитьОбласть("ЗадачаВыполнена");
			КонецЕсли;
		КонецЕсли;
	Иначе
	// Вывод картинки
	Если СтрокаДерева.Проведен Тогда
		Если ЭтоТекущийОбъект Тогда
			Если ДеревоПодчиненныеОбъекты.ПолучитьЭлементы().Количество()
				 И ДеревоРодительскиеОбъекты.ПолучитьЭлементы().Количество()  Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть("ДокументПроведенКоннекторВерхНиз");
			ИначеЕсли ДеревоПодчиненныеОбъекты.ПолучитьЭлементы().Количество() Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть("ДокументПроведенКоннекторНиз");
			Иначе
				ОбластьКартинка = Макет.ПолучитьОбласть("ДокументПроведенКоннекторВерх");
			КонецЕсли;
		ИначеЕсли ЭтоПодчиненный = Истина Тогда
			Если СтрокаДерева.ПолучитьЭлементы().Количество() Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть("ДокументПроведенКоннекторЛевоНиз");
			Иначе
				ОбластьКартинка = Макет.ПолучитьОбласть("ДокументПроведен");
			КонецЕсли;
		Иначе
			Если СтрокаДерева.ПолучитьЭлементы().Количество() Тогда
				ОбластьКартинка = Макет.ПолучитьОбласть("ДокументПроведенКоннекторЛевоВерх");
			Иначе
				ОбластьКартинка = Макет.ПолучитьОбласть("ДокументПроведен");
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли СтрокаДерева.ПометкаУдаления Тогда
		Если ЭтоТекущийОбъект Тогда
			Если ДеревоПодчиненныеОбъекты.ПолучитьЭлементы().Количество()
				 И ДеревоРодительскиеОбъекты.ПолучитьЭлементы().Количество()  Тогда
				ИмяОбласти = ?( ЭтоДокумент, "ДокументПомеченНаУдалениеКоннекторВерхНиз",
					 "СправочникПВХПомеченНаУдалениеКоннекторВерхНиз");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			ИначеЕсли ДеревоПодчиненныеОбъекты.ПолучитьЭлементы().Количество() Тогда
				ИмяОбласти = ?( ЭтоДокумент, "ДокументПомеченНаУдалениеКоннекторНиз", "СправочникПВХПомеченНаУдалениеКоннекторНиз");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			Иначе
				ИмяОбласти = ?( ЭтоДокумент, "ДокументПомеченНаУдалениеКоннекторВерх",
					 "СправочникПВХПомеченНаУдалениеКоннекторВерх");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			КонецЕсли;
		ИначеЕсли ЭтоПодчиненный = Истина Тогда
			Если СтрокаДерева.ПолучитьЭлементы().Количество() Тогда
				ИмяОбласти = ?( ЭтоДокумент, "ДокументПомеченНаУдалениеКоннекторЛевоНиз",
					 "СправочникПВХПомеченНаУдалениеКоннекторЛевоНиз");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			Иначе
				ИмяОбласти = ?( ЭтоДокумент, "ДокументПомеченНаУдаление", "СправочникПВХПомеченНаУдалениеКоннекторЛево");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			КонецЕсли;
		Иначе
			Если СтрокаДерева.ПолучитьЭлементы().Количество() Тогда
				ИмяОбласти = ?( ЭтоДокумент, "ДокументПомеченНаУдалениеКоннекторЛевоВерх",
					 "СправочникПВХПомеченНаУдалениеКоннекторЛевоВерх");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			Иначе
				ИмяОбласти = ?( ЭтоДокумент, "ДокументПомеченНаУдаление", "СправочникПВХПомеченНаУдалениеКоннекторЛево");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЭтоТекущийОбъект Тогда
			Если ДеревоПодчиненныеОбъекты.ПолучитьЭлементы().Количество()
				 И ДеревоРодительскиеОбъекты.ПолучитьЭлементы().Количество()  Тогда
				ИмяОбласти = ?( ЭтоДокумент, "ДокументЗаписанКоннекторВерхНиз", "СправочникПВХКоннекторВерхНиз");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			ИначеЕсли ДеревоПодчиненныеОбъекты.ПолучитьЭлементы().Количество() Тогда
				ИмяОбласти = ?( ЭтоДокумент, "ДокументЗаписанКоннекторНиз", "СправочникПВХКоннекторНиз");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			Иначе
				ИмяОбласти = ?( ЭтоДокумент, "ДокументЗаписанКоннекторВерх", "СправочникПВХКоннекторВерх");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			КонецЕсли;
		ИначеЕсли ЭтоПодчиненный = Истина Тогда
			Если СтрокаДерева.ПолучитьЭлементы().Количество() Тогда
				ИмяОбласти = ?( ЭтоДокумент, "ДокументЗаписанКоннекторЛевоНиз", "СправочникПВХКоннекторЛевоНиз");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			Иначе
				ИмяОбласти = ?( ЭтоДокумент, "ДокументЗаписан", "СправочникПВХКоннекторЛево");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			КонецЕсли;
		Иначе
			Если СтрокаДерева.ПолучитьЭлементы().Количество() Тогда
				ИмяОбласти = ?( ЭтоДокумент, "ДокументЗаписанКоннекторЛевоВерх", "СправочникПВХКоннекторЛевоВерх");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			Иначе
				ИмяОбласти = ?( ЭтоДокумент, "ДокументЗаписан", "СправочникПВХКоннекторЛево");
				ОбластьКартинка = Макет.ПолучитьОбласть(ИмяОбласти);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	КонецЕсли;
	Если ЭтоТекущийОбъект Тогда
		ТаблицаОтчета.Вывести(ОбластьКартинка); 
	Иначе
		ТаблицаОтчета.Присоединить(ОбластьКартинка);
	КонецЕсли;
	
	// Вывод объекта
	ОбластьОбъект = Макет.ПолучитьОбласть(?(ЭтоТекущийОбъект, "ТекущийОбъект", "Объект"));
	ОбластьОбъект.Параметры.ПредставлениеОбъекта = СтрокаДерева.Представление;
	ОбластьОбъект.Параметры.Объект = СтрокаДерева.Ссылка;
	ТаблицаОтчета.Присоединить(ОбластьОбъект);
	
КонецПроцедуры

// Определяет необходимость вывода вертикального коннектора в  табличный документ.
//
// Параметры:
//  УровеньВверх  - Число - на каком количестве уровней выше находится 
//                 родитель от которого будет рисоваться вертикальный коннектор.
//  СтрокаДерева  - ДанныеФормыЭлементДерева - исходная строка дерева значений
//                  от которой ведется отсчет.
// Возвращаемое значение:
//   Булево   - необходимость вывода в области вертикального коннектора.
//
&НаСервере
Функция НеобходимостьВыводаВертикальногоКоннектора(УровеньВверх, СтрокаДерева, ИщемСредиПодчиненных = Истина)
	
	ТекущаяСтрока = СтрокаДерева;
	
	Для инд = 1 По УровеньВверх Цикл
		
		ТекущаяСтрока = ТекущаяСтрока.ПолучитьРодителя();
		Если инд = УровеньВверх Тогда
			ИскомыйРодитель = ТекущаяСтрока;
		ИначеЕсли инд = (УровеньВверх - 1) Тогда
			ИскомаяСтрока = ТекущаяСтрока;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИскомыйРодитель = Неопределено Тогда
		Если ИщемСредиПодчиненных Тогда
			ПодчиненныеЭлементыРодителя =  ДеревоПодчиненныеОбъекты.ПолучитьЭлементы(); 
		Иначе
			ПодчиненныеЭлементыРодителя =  ДеревоРодительскиеОбъекты.ПолучитьЭлементы();
		КонецЕсли;
	Иначе
		ПодчиненныеЭлементыРодителя =  ИскомыйРодитель.ПолучитьЭлементы(); 
	КонецЕсли;
	
	Возврат ПодчиненныеЭлементыРодителя.Индекс(ИскомаяСтрока) < (ПодчиненныеЭлементыРодителя.Количество() - 1);
	
КонецФункции

// Выводит в табличный документ строку с документом, для которого формируется структура подчиненности.
//
// Параметры:
//  Макет  - МакетТабличногоДокумента - макет, на основании которого формируется табличный документ.
&НаСервере
Процедура ВывестиТекущийОбъект(Макет)
	
	Выборка = ЗапросПоРеквизитамОбъектов(ОсновнойОбъект).Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ПереопределяемоеПредставление =
			CRM_СтруктураПодчиненностиПереопределяемый.ПредставлениеОбъектаДляВыводаВОтчет(Выборка);

		Если ПереопределяемоеПредставление <> Неопределено Тогда
			СтруктураРеквизитов = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Выборка.Владелец().Выгрузить()[0]);
			СтруктураРеквизитов.Представление = ПереопределяемоеПредставление;
			ВывестиПредставлениеИКартинку(СтруктураРеквизитов, Макет, Истина);
		Иначе
			СтруктураРеквизитов = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Выборка.Владелец().Выгрузить()[0]);
			СтруктураРеквизитов.Представление = ПредставлениеОбъектаДляВыводаВОтчет(Выборка);
			ВывестиПредставлениеИКартинку(СтруктураРеквизитов, Макет, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует представление документа для вывода в табличный документ.
//
// Параметры:
//  Выборка  - ВыборкаИзРезультатаЗапроса или ДанныеФормыЭлементДерева - набор данных
//             на основании которого формируется представление.
//
// Возвращаемое значение:
//   Строка   - сформированное представление.
//
&НаСервере
Функция ПредставлениеОбъектаДляВыводаВОтчет(Выборка)
	
	ПредставлениеОбъекта = Выборка.Представление;
	МетаданныеОбъекта = Выборка.Ссылка.Метаданные();
	
	Если ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта) Тогда
		Если (Выборка.СуммаДокумента <> 0) И (Выборка.СуммаДокумента <> NULL) Тогда
			ПредставлениеОбъекта = ПредставлениеОбъекта + " " + НСтр("ru='на сумму';en='to the amount of'") 
				+ " " + Выборка.СуммаДокумента + " " 
				+ Выборка.Валюта;
		КонецЕсли;
	Иначе
		ПредставлениеОбъекта = ПредставлениеОбъекта + " (" + МетаданныеОбъекта.ПредставлениеОбъекта + ")";
	КонецЕсли;
	
	Возврат ПредставлениеОбъекта;
	
КонецФункции

// Выводит строки дерева подчиненных документов.
//
// Параметры:
//  СтрокиДерева  - ДанныеФормыКоллекцияЭлементовДерева - строки дерева
//                 которые выводятся в табличный документ.
//  Макет  - МакетТабличногоДокумента - макет, на основании которого
//                 происходит вывод в табличный документ.
//  УровеньРекурсии - Число - уровень рекурсии процедуры.
//
&НаСервере
Процедура ВывестиПодчиненныеЭлементыДерева(СтрокиДерева, Макет, УровеньРекурсии)

	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		
		ЭтоТекущийОбъект = (СтрокаДерева.Ссылка = ОсновнойОбъект);
		ЭтоИсходныйОбъект = (СтрокаДерева.Ссылка = ИсходныйОбъект);
		ПодчиненныеЭлементыДерева = СтрокаДерева.ПолучитьЭлементы();
		
		// Вывод коннекторов
		Для инд = 1 По УровеньРекурсии Цикл
			Если УровеньРекурсии > инд Тогда
				
				Если НеобходимостьВыводаВертикальногоКоннектора(УровеньРекурсии - инд + 1, СтрокаДерева) Тогда
					Область = Макет.ПолучитьОбласть("КоннекторВерхНиз");
				Иначе
					Область = Макет.ПолучитьОбласть("Отступ");
					
				КонецЕсли;
			Иначе 
				
				Если СтрокиДерева.Количество() > 1 И (СтрокиДерева.Индекс(СтрокаДерева) <> (СтрокиДерева.Количество() - 1)) Тогда
					Область = Макет.ПолучитьОбласть("КоннекторВерхПравоНиз");
				Иначе
					Область = Макет.ПолучитьОбласть("КоннекторВерхПраво");
				КонецЕсли;
				
			КонецЕсли;	
			
			Область.Параметры.Документ = ?(ЭтоИсходныйОбъект, Неопределено, СтрокаДерева.Ссылка);
			
			Если инд = 1 Тогда
				ТаблицаОтчета.Вывести(Область);
			Иначе
				ТаблицаОтчета.Присоединить(Область);
			КонецЕсли;
			
		КонецЦикла;		
		
		ВывестиПредставлениеИКартинку(СтрокаДерева, Макет, Ложь, Истина);
		
		// Вывод подчиненных элементов дерева.
		ВывестиПодчиненныеЭлементыДерева(ПодчиненныеЭлементыДерева, Макет, УровеньРекурсии + 1);
		
	КонецЦикла;
	
КонецПроцедуры

// Инициирует вывод в табличный документ и отображает его по окончанию формирования.
&НаКлиенте
Процедура ВывестиСтруктуруПодчиненности()

	ОбновитьДеревоСтруктурыПодчиненности();

КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////////////////
// Процедуры построения дерева подчиненности документов.

&НаСервере
Процедура ОбновитьДеревоСтруктурыПодчиненности()

	Если ОсновнойДокументДоступен() Тогда
		СформироватьДеревьяДокументов();
		ВывестиТабличныйДокумент();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Документ, для которого сформирован отчет о структуре подчиненности, стал недоступен.';en='The document for which the dependence structure report is generated is no longer available.'"));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СформироватьДеревьяДокументов()

	ДеревоРодительскиеОбъекты.ПолучитьЭлементы().Очистить();
	ДеревоПодчиненныеОбъекты.ПолучитьЭлементы().Очистить();

	ВыведенныеОбъекты = Новый Соответствие;
	
	ВывестиРодительскиеОбъекты(ОсновнойОбъект, ДеревоРодительскиеОбъекты, ВыведенныеОбъекты);
	ВывестиПодчиненныеОбъекты(ОсновнойОбъект, ДеревоПодчиненныеОбъекты, ВыведенныеОбъекты);
	
КонецПроцедуры

&НаСервере
Функция ОсновнойДокументДоступен()
	// +CRM
	МетаданныеТипаОбъекта = ОсновнойОбъект.Метаданные();
	Если Метаданные.БизнесПроцессы.Содержит(МетаданныеТипаОбъекта) Тогда
		ИмяТаблицыОбъекта = "БизнесПроцесс." + МетаданныеТипаОбъекта.Имя;			 
	ИначеЕсли Метаданные.Задачи.Содержит(МетаданныеТипаОбъекта) Тогда
		ИмяТаблицыОбъекта = "Задача." + МетаданныеТипаОбъекта.Имя;
	ИначеЕсли Метаданные.Справочники.Содержит(МетаданныеТипаОбъекта) Тогда
		ИмяТаблицыОбъекта = "Справочник." + МетаданныеТипаОбъекта.Имя;
	ИначеЕсли Метаданные.Документы.Содержит(МетаданныеТипаОбъекта) Тогда
		ИмяТаблицыОбъекта = "Документ." + МетаданныеТипаОбъекта.Имя;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	// -CRM
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1
	|ИЗ
	// +CRM
	//|	" + ОбъектСсылка.Метаданные().ПолноеИмя() + " КАК Таб
	|	" + ИмяТаблицыОбъекта + " КАК Таб
	// -CRM
	|ГДЕ
	|	Таб.Ссылка = &ТекущийОбъект
	|");
	Запрос.УстановитьПараметр("ТекущийОбъект", ОсновнойОбъект);
	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции

// Параметры:
//  СписокОбъектов - Массив
//                 - ДокументСсылка
//                 - СправочникСсылка
//                 - ПланВидовХарактеристикСсылка
//
// Возвращаемое значение:
//   Запрос
//
&НаСервере
Функция ЗапросПоРеквизитамОбъектов(СписокОбъектов)
	
	ОбъектыПоТипу = Новый Соответствие;
	Если ТипЗнч(СписокОбъектов) = Тип("Массив") Тогда
		Для каждого ТекущийОбъект Из СписокОбъектов Цикл
			Объекты = ОбъектыПоТипу[ТекущийОбъект.Метаданные()];
			Если Объекты = Неопределено Тогда
				Объекты = Новый Массив;
				ОбъектыПоТипу[ТекущийОбъект.Метаданные()] = Объекты;
			КонецЕсли;
			Объекты.Добавить(ТекущийОбъект);
		КонецЦикла; 
	Иначе
		ОбъектыПоТипу[СписокОбъектов.Метаданные()] = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СписокОбъектов);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапросов = Новый Массив;
	
	Для каждого ТипОбъекта Из ОбъектыПоТипу Цикл

		ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	&Дата КАК Дата,
			|	Ссылка,
			|	&Проведен КАК Проведен,
			|	ПометкаУдаления,
			|	&Сумма КАК СуммаДокумента,
			|	&Валюта КАК Валюта,
			|	&Представление
			|ИЗ
			|	&ИмяТаблицы
			|ГДЕ
			|	Ссылка В (&Ссылка)
			|";
		
		МетаданныеОбъекта = ТипОбъекта.Ключ;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", МетаданныеОбъекта.ПолноеИмя());
		Если ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта) Тогда
			ИмяРеквизитаСумма    = ИмяРеквизитаДокумента(МетаданныеОбъекта, "СуммаДокумента");
			ИмяРеквизитаВалюта   = ИмяРеквизитаДокумента(МетаданныеОбъекта, "Валюта");
			ИмяРеквизитаПроведен = "Проведен";
			ИмяРеквизитаДата     = "Дата";
		// +CRM
		ИначеЕсли  ОбщегоНазначения.ЭтоБизнесПроцесс(МетаданныеОбъекта) Тогда
			ИмяРеквизитаСумма    = Неопределено;
			ИмяРеквизитаВалюта   = Неопределено;
			ИмяРеквизитаПроведен = "Стартован";
			ИмяРеквизитаДата     = "Дата";
		ИначеЕсли  ОбщегоНазначения.ЭтоЗадача(МетаданныеОбъекта) Тогда
			ИмяРеквизитаСумма    = Неопределено;
			ИмяРеквизитаВалюта   = Неопределено;
			ИмяРеквизитаПроведен = "Выполнена";
			ИмяРеквизитаДата     = "Дата";
		// -CRM
		Иначе
			ИмяРеквизитаСумма    = Неопределено;
			ИмяРеквизитаВалюта   = Неопределено;
			ИмяРеквизитаПроведен = "Ложь";
			ИмяРеквизитаДата     = "Ложь";
		КонецЕсли;
		
		ЗаменитьТекстЗапроса(ТекстЗапроса, МетаданныеОбъекта, "&Дата", ИмяРеквизитаДата, Истина);
		ЗаменитьТекстЗапроса(ТекстЗапроса, МетаданныеОбъекта, "&Проведен", ИмяРеквизитаПроведен, Истина);
		ЗаменитьТекстЗапроса(ТекстЗапроса, МетаданныеОбъекта, "&Сумма", ИмяРеквизитаСумма);
		ЗаменитьТекстЗапроса(ТекстЗапроса, МетаданныеОбъекта, "&Валюта", ИмяРеквизитаВалюта);
		
		// +CRM
		//ДопРеквизиты = РеквизитыДляПредставления(МетаданныеОбъекта.ПолноеИмя(), МетаданныеОбъекта.Имя);
		ДопРеквизиты = CRM_СтруктураПодчиненностиПереопределяемый.МассивРеквизитовОбъектаДляФормированияПредставления(
			МетаданныеОбъекта.Имя);
		// -CRM
		ТекстПредставление = "Представление КАК Представление"; // @query-part
		Для Индекс = 1 По 3 Цикл
			ТекстПредставление = ТекстПредставление + ",
				|	" + ?(ДопРеквизиты.Количество() >= Индекс, ДопРеквизиты[Индекс - 1], "NULL") 
				+ " КАК ДополнительныйРеквизит" + Индекс; // @query-part
		КонецЦикла;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Представление", ТекстПредставление);
		
		ИмяПараметра = "Ссылка" + СтрЗаменить(МетаданныеОбъекта.ПолноеИмя(), ".", "_");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Ссылка", "&" + ИмяПараметра);
		Запрос.УстановитьПараметр(ИмяПараметра, ТипОбъекта.Значение);
		
		Если ТекстыЗапросов.Количество() > 0 Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ"); // @query-part-1, @query-part-2
		КонецЕсли;

		ТекстыЗапросов.Добавить(ТекстЗапроса);
		
	КонецЦикла;
	
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС); // @query-part
	Возврат Запрос; 
	
КонецФункции

&НаСервере
Процедура ВывестиРодительскиеОбъекты(ТекущийОбъект, ДеревоРодитель, ВыведенныеОбъекты,
	СлужебныеОбъекты = Неопределено, ИндексСвязейОбъектов = Неопределено)
	
	МетаданныеОбъекта = ТекущийОбъект.Метаданные();
	СписокРеквизитов  = Новый Массив;
	
	Если СлужебныеОбъекты = Неопределено Тогда 
		СлужебныеОбъекты = Новый Соответствие;
	КонецЕсли;
	
	Если ИндексСвязейОбъектов = Неопределено Тогда 
		ИндексСвязейОбъектов = Новый Соответствие;
	КонецЕсли;
	
	Для Каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
		
		Если Не Метаданные.КритерииОтбора.СвязанныеДокументы.Состав.Содержит(Реквизит) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ТекущийТип Из Реквизит.Тип.Типы() Цикл
			
			МетаданныеРеквизита = МетаданныеТипаРеквизита(ТекущийТип);
			Если МетаданныеРеквизита.Метаданные = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначениеРеквизита = ТекущийОбъект[Реквизит.Имя];
			Если ЗначениеЗаполнено(ЗначениеРеквизита)
				И ТипЗнч(ЗначениеРеквизита) = ТекущийТип
				И ЗначениеРеквизита <> ТекущийОбъект
				И СписокРеквизитов.Найти(ЗначениеРеквизита) = Неопределено Тогда
				
				СписокРеквизитов.Добавить(ЗначениеРеквизита);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ТабличнаяЧасть Из МетаданныеОбъекта.ТабличныеЧасти Цикл
		
		ИменаРеквизитов = "";
		СодержимоеТЧ = ТекущийОбъект[ТабличнаяЧасть.Имя].Выгрузить(); // ТаблицаЗначений
		Для Каждого Реквизит Из ТабличнаяЧасть.Реквизиты Цикл

			Если Не Метаданные.КритерииОтбора.СвязанныеДокументы.Состав.Содержит(Реквизит) Тогда
				Продолжить;
			КонецЕсли;
				
			Для Каждого ТекущийТип Из Реквизит.Тип.Типы() Цикл
				МетаданныеРеквизита = МетаданныеТипаРеквизита(ТекущийТип);
				Если МетаданныеРеквизита.Метаданные = Неопределено Тогда
					Продолжить;
				КонецЕсли;
	
				ИменаРеквизитов = ИменаРеквизитов + ?(ИменаРеквизитов = "", "", ", ") + Реквизит.Имя;
				Прервать;
			КонецЦикла;
			
		КонецЦикла;
		
		СодержимоеТЧ.Свернуть(ИменаРеквизитов);
		Для Каждого КолонкаТЧ Из СодержимоеТЧ.Колонки Цикл
			
			Для Каждого СтрокаТЧ Из СодержимоеТЧ Цикл
			
				ЗначениеРеквизита = СтрокаТЧ[КолонкаТЧ.Имя];
				МетаданныеЗначения = МетаданныеТипаРеквизита(ТипЗнч(ЗначениеРеквизита));
				Если МетаданныеЗначения.Метаданные = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Если ЗначениеРеквизита = ТекущийОбъект
					Или СписокРеквизитов.Найти(ЗначениеРеквизита) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				СписокРеквизитов.Добавить(ЗначениеРеквизита);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если СписокРеквизитов.Количество() > 0 Тогда
		ВыводимыеОбъекты = ЗапросПоРеквизитамОбъектов(СписокРеквизитов).Выполнить().Выгрузить();
		ВыводимыеОбъекты.Сортировать("Дата");
		Для каждого ВыводимыйОбъект Из ВыводимыеОбъекты Цикл 
			
			Если ИндексСвязейОбъектов[ТекущийОбъект] = ВыводимыйОбъект.Ссылка Тогда 
				Продолжить;
			КонецЕсли;
			
			ИндексСвязейОбъектов[ТекущийОбъект] = ВыводимыйОбъект.Ссылка;
			
			НоваяСтрока = ДобавитьСтрокуВДерево(ДеревоРодитель, ВыводимыйОбъект, ВыведенныеОбъекты);			
			Если НоваяСтрока <> Неопределено
				И Не ДобавляемыйОбъектИмеетсяСредиРодителей(ДеревоРодитель, ВыводимыйОбъект.Ссылка) Тогда
				
				// @skip-check query-in-loop - Рекурсивный алгоритм обработки дерева.
				ВывестиРодительскиеОбъекты(ВыводимыйОбъект.Ссылка, НоваяСтрока, ВыведенныеОбъекты,
					СлужебныеОбъекты, ИндексСвязейОбъектов);
				
			ИначеЕсли СлужебныеОбъекты[ВыводимыйОбъект.Ссылка] = Неопределено Тогда 
				
				СлужебныеОбъекты[ВыводимыйОбъект.Ссылка] = Истина;
				// @skip-check query-in-loop - Рекурсивный алгоритм обработки дерева.
				ВывестиРодительскиеОбъекты(ВыводимыйОбъект.Ссылка, ДеревоРодитель, ВыведенныеОбъекты,
					СлужебныеОбъекты, ИндексСвязейОбъектов);
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ТипРеквизита - Тип
// 
// Возвращаемое значение:
//  Структура:
//   * Метаданные - ОбъектМетаданных
//   * ЭтоДокумент - Булево
//
&НаСервереБезКонтекста
Функция МетаданныеТипаРеквизита(ТипРеквизита)

	Результат = Новый Структура("Метаданные, ЭтоДокумент", Неопределено, Ложь); 
	
	МетаданныеРеквизита = Метаданные.НайтиПоТипу(ТипРеквизита);
	Если МетаданныеРеквизита = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(МетаданныеРеквизита) 
		Или Не ПравоДоступа("Просмотр", МетаданныеРеквизита) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.ЭтоДокумент = Метаданные.Документы.Содержит(МетаданныеРеквизита);
	Если Не Результат.ЭтоДокумент
		И Не Метаданные.Справочники.Содержит(МетаданныеРеквизита)
		И Не Метаданные.ПланыВидовХарактеристик.Содержит(МетаданныеРеквизита)
// +CRM
		И Не Метаданные.БизнесПроцессы.Содержит(МетаданныеРеквизита)
		И Не Метаданные.Задачи.Содержит(МетаданныеРеквизита)  Тогда
// -CRM
		Возврат Результат;
	КонецЕсли;
	Результат.Метаданные = МетаданныеРеквизита;
	Возврат Результат;

КонецФункции

// Определяет наличие документа среди родителей строки дерева, которая возможно будет добавлена.
//
// Параметры:
//  СтрокаРодитель  - ДанныеФормыДерево,ДанныеФормыЭлементДерева - родитель, для 
//                 которого предполагается добавить строку дерева.
//  ИскомыйОбъект  - Ссылка - ссылка на объект метаданных, на наличие которого выполняется проверка.
//
// Возвращаемое значение:
//   Булево   - Истина если найден, Ложь в обратном случае.
//
&НаСервере
Функция ДобавляемыйОбъектИмеетсяСредиРодителей(СтрокаРодитель, ИскомыйОбъект)
	
	Если ИскомыйОбъект = ОсновнойОбъект Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ТипЗнч(СтрокаРодитель) = Тип("ДанныеФормыДерево") Тогда
		Возврат Ложь; 
	КонецЕсли;
	
	ТекущийРодитель = СтрокаРодитель;
	Пока ТекущийРодитель <> Неопределено Цикл
		Если ТекущийРодитель.Ссылка = ИскомыйОбъект Тогда
		    Возврат Истина;
		КонецЕсли;
		ТекущийРодитель = ТекущийРодитель.ПолучитьРодителя();
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ЗаменитьТекстЗапроса(ТекстЗапроса, МетаданныеОбъекта, ЧтоЗаменять, ИмяРеквизита, НеИскатьВРеквизитах = Ложь)

	ЕстьРеквизит = НеИскатьВРеквизитах Или МетаданныеОбъекта.Реквизиты.Найти(ИмяРеквизита) <> Неопределено; 
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЧтоЗаменять, ?(ЕстьРеквизит, ИмяРеквизита,  "NULL"));

КонецПроцедуры

&НаСервере
Функция ОбъектыПоКритериюОтбора(ЗначениеКритерияОтбора)
	
	Если Не Метаданные.КритерииОтбора.СвязанныеДокументы.Тип.СодержитТип(ТипЗнч(ЗначениеКритерияОтбора))  Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СвязанныеДокументы.Ссылка КАК Ссылка
	|ИЗ
	|	КритерийОтбора.СвязанныеДокументы(&ЗначениеКритерияОтбора) КАК СвязанныеДокументы";
	
	Запрос.УстановитьПараметр("ЗначениеКритерияОтбора", ЗначениеКритерияОтбора);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Процедура ВывестиПодчиненныеОбъекты(ТекущийОбъект, ДеревоРодитель, ВыведенныеОбъекты,
	СлужебныеОбъекты = Неопределено, ИндексСвязейОбъектов = Неопределено)
	
	Таблица = ОбъектыПоКритериюОтбора(ТекущийОбъект);
	Если Таблица = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СлужебныеОбъекты = Неопределено Тогда 
		СлужебныеОбъекты = Новый Соответствие;
	КонецЕсли;
	
	Если ИндексСвязейОбъектов = Неопределено Тогда 
		ИндексСвязейОбъектов = Новый Соответствие;
	КонецЕсли;
	
	СписокОбъектов = Новый Массив;
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		
		ТекущаяСсылка = СтрокаТаблицы.Ссылка; // СправочникСсылка, ДокументСсылка
		МетаданныеОбъекта = ТекущаяСсылка.Метаданные();
		Если Не ПравоДоступа("Просмотр", МетаданныеОбъекта) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() И ТипЗнч(ТекущаяСсылка) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
			ЭтоЛичнаяЗадача = ТекущаяСсылка.CRM_Личная;
			ЭтоCRM_БизнесПроцесс = ТипЗнч(ТекущаяСсылка.БизнесПроцесс) = Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс");
			Если Не (ЭтоЛичнаяЗадача ИЛИ ЭтоCRM_БизнесПроцесс) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		СписокОбъектов.Добавить(ТекущаяСсылка);
	КонецЦикла;
	
	Если СписокОбъектов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВыводимыеОбъекты = ЗапросПоРеквизитамОбъектов(СписокОбъектов).Выполнить().Выгрузить();
	ВыводимыеОбъекты.Сортировать("Дата");
	
	Для каждого ВыводимыйОбъект Из ВыводимыеОбъекты Цикл
		
		Если ИндексСвязейОбъектов[ТекущийОбъект] = ВыводимыйОбъект.Ссылка Тогда 
			Продолжить;
		КонецЕсли;
		
		ИндексСвязейОбъектов[ТекущийОбъект] = ВыводимыйОбъект.Ссылка;
		
		НоваяСтрока = ДобавитьСтрокуВДерево(ДеревоРодитель, ВыводимыйОбъект, ВыведенныеОбъекты, Истина);
		Если НоваяСтрока <> Неопределено
			И Не ДобавляемыйОбъектИмеетсяСредиРодителей(ДеревоРодитель, ВыводимыйОбъект.Ссылка) Тогда
			
			// @skip-check query-in-loop - Рекурсивный алгоритм обработки дерева.
			ВывестиПодчиненныеОбъекты(ВыводимыйОбъект.Ссылка, НоваяСтрока, ВыведенныеОбъекты,
				СлужебныеОбъекты, ИндексСвязейОбъектов);
			
		ИначеЕсли СлужебныеОбъекты[ВыводимыйОбъект.Ссылка] = Неопределено Тогда 
			
			СлужебныеОбъекты.Вставить(ВыводимыйОбъект.Ссылка, Истина);
			// @skip-check query-in-loop - Рекурсивный алгоритм обработки дерева.
			ВывестиПодчиненныеОбъекты(ВыводимыйОбъект.Ссылка, ДеревоРодитель, ВыведенныеОбъекты,
				СлужебныеОбъекты, ИндексСвязейОбъектов);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьСтрокуВДерево(Родитель, Данные, ВыведенныеОбъекты, ЭтоПодчиненный = Ложь)
	
	УстановитьЧастотуВыводаОбъекта(Данные.Ссылка, ВыведенныеОбъекты, ЭтоПодчиненный);	
	Если Не ВыводитьТекущийОбъект(Родитель, Данные.Ссылка, ВыведенныеОбъекты, ЭтоПодчиненный) Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	НоваяСтрока = Родитель.ПолучитьЭлементы().Добавить();
	ОбщиеСвойства = "Ссылка, Представление, СуммаДокумента, Валюта, Проведен, ПометкаУдаления";
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Данные, ОбщиеСвойства);
	
	// +CRM
	//ПереопределенноеПредставление = ПредставлениеОбъектаДляВывода(Данные);
	ПереопределенноеПредставление =
		CRM_СтруктураПодчиненностиПереопределяемый.ПредставлениеОбъектаДляВыводаВОтчет(Данные);
	// -CRM
	Если ПереопределенноеПредставление <> Неопределено Тогда
		НоваяСтрока.Представление = ПереопределенноеПредставление;
	Иначе
		НоваяСтрока.Представление = ПредставлениеОбъектаДляВыводаВОтчет(Данные);
	КонецЕсли;
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаСервере
Функция ВыводитьТекущийОбъект(Родитель, ТекущийОбъект, ВыведенныеОбъекты, ЭтоПодчиненный, Отказ = Ложь)
	
	Выводился = ВыведенныеОбъекты[ТекущийОбъект];
	Если Выводился = Неопределено Тогда 
		Выводился = НовыеСвойстваЧастотыВыводаОбъекта();
	КонецЕсли;
	
	СвойстваОбъекта = НовыеСвойстваОбъекта();
	СвойстваОбъекта.ЭтоОсновной = (ТекущийОбъект = ОсновнойОбъект);
	СвойстваОбъекта.ЭтоПодчиненный = ЭтоПодчиненный;
	СвойстваОбъекта.Выводился = Выводился;
	
	Если СвойстваОбъекта.ЭтоОсновной
		Или ТекущийОбъектВыводилсяВВетке(Родитель, ТекущийОбъект) Тогда 
		
		Отказ = Истина;
	КонецЕсли;
	
	СтруктураПодчиненностиПереопределяемый.ПередВыводомСвязанногоОбъекта(ТекущийОбъект, СвойстваОбъекта, Отказ);
	
	Возврат Не Отказ;
	
КонецФункции

&НаСервере
Функция НовыеСвойстваОбъекта()
	
	СвойстваОбъекта = Новый Структура;
	СвойстваОбъекта.Вставить("ЭтоОсновной", Ложь);
	СвойстваОбъекта.Вставить("ЭтоСлужебный", Ложь);
	СвойстваОбъекта.Вставить("ЭтоПодчиненный", Ложь);
	СвойстваОбъекта.Вставить("Выводился", НовыеСвойстваЧастотыВыводаОбъекта());
	
	Возврат СвойстваОбъекта;
	
КонецФункции

&НаСервере
Функция ТекущийОбъектВыводилсяВВетке(Родитель, ТекущийОбъект, Выводился = Ложь)
	
	Если Родитель = Неопределено Тогда 
		Возврат Выводился;
	КонецЕсли;
	
	ЭтоСтрока = (ТипЗнч(Родитель) = Тип("ДанныеФормыЭлементДерева"));
	
	Если ЭтоСтрока И ТекущийОбъект = Родитель.Ссылка Тогда 
		Выводился = Истина;
	Иначе
		Строки = Родитель.ПолучитьЭлементы();
		
		Для Каждого Строка Из Строки Цикл 
			
			Если ТекущийОбъект = Строка.Ссылка Тогда 
				
				Выводился = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЭтоСтрока И Не Выводился Тогда 
			ТекущийОбъектВыводилсяВВетке(Родитель.ПолучитьРодителя(), ТекущийОбъект, Выводился);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Выводился;
	
КонецФункции

&НаСервере
Процедура УстановитьЧастотуВыводаОбъекта(ТекущийОбъект, ВыведенныеОбъекты, ЭтоПодчиненный)
	
	Выводился = ВыведенныеОбъекты[ТекущийОбъект];
	Если Выводился = Неопределено Тогда 
		Выводился = НовыеСвойстваЧастотыВыводаОбъекта();
	КонецЕсли;
	
	Выводился.Итого = Выводился.Итого + 1;
	Если ЭтоПодчиненный Тогда 
		Выводился.ВПодчиненных = Выводился.ВПодчиненных + 1;
	КонецЕсли;
	
	ВыведенныеОбъекты.Вставить(ТекущийОбъект, Выводился);
	
КонецПроцедуры

&НаСервере
Функция НовыеСвойстваЧастотыВыводаОбъекта()
	
	СвойстваЧастотыВывода = Новый Структура;
	СвойстваЧастотыВывода.Вставить("Итого", 0);
	СвойстваЧастотыВывода.Вставить("ВПодчиненных", 0);	
	Возврат СвойстваЧастотыВывода;
	
КонецФункции

&НаСервере
Функция ИмяРеквизитаДокумента(Знач МетаданныеОбъекта, Знач ИмяРеквизита) 
	
	ИменаРеквизитов = Настройки.Реквизиты[МетаданныеОбъекта.ПолноеИмя()];
	Если ИменаРеквизитов <> Неопределено Тогда
		Результат = ИменаРеквизитов[ИмяРеквизита];
		Возврат ?(Результат <> Неопределено, Результат, ИмяРеквизита);
	КонецЕсли;	
	
	// Для обратной совместимости.
	ИмяРеквизитаДокумента = CRM_СтруктураПодчиненностиПереопределяемый.ИмяРеквизитаДокумента(МетаданныеОбъекта.Имя,
		 ИмяРеквизита); 
	Если ИмяРеквизита = "СуммаДокумента" Тогда
		Возврат ?(ИмяРеквизитаДокумента = Неопределено, "СуммаДокумента", ИмяРеквизитаДокумента);
	ИначеЕсли ИмяРеквизита = "Валюта" Тогда
		Возврат ?(ИмяРеквизитаДокумента = Неопределено, "Валюта", ИмяРеквизитаДокумента);
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОпределитьНастройки()
	
	НастройкиПодсистемы = Новый Структура;
	НастройкиПодсистемы.Вставить("Реквизиты", Новый Соответствие);
	НастройкиПодсистемы.Вставить("РеквизитыДляПредставления", Новый Соответствие);
	СтруктураПодчиненностиПереопределяемый.ПриОпределенииНастроек(НастройкиПодсистемы);
	
	Настройки = НастройкиПодсистемы;

КонецПроцедуры

&НаСервере
Функция РеквизитыДляПредставления(Знач ПолноеИмяОбъектаМетаданных, Знач ИмяОбъектаМетаданных)
	
	Результат = Настройки.РеквизитыДляПредставления[ПолноеИмяОбъектаМетаданных];
	Если Результат <> Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Для обратной совместимости.
	Возврат СтруктураПодчиненностиПереопределяемый.МассивРеквизитовОбъектаДляФормированияПредставления(ИмяОбъектаМетаданных);

КонецФункции

&НаКлиенте
Процедура ТаблицаОтчетаВыбор(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Область.Расшифровка;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТипЗнч(ТекущиеДанные) = Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее") Тогда
			ОткрытьФорму("Документ.ЭлектронноеПисьмоВходящее.Форма.CRM_ФормаДокумента",
				 Новый Структура("Ключ", ТекущиеДанные),
				 ЭтотОбъект);
		ИначеЕсли ТипЗнч(ТекущиеДанные) = Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее") Тогда
			ОткрытьФорму("Документ.ЭлектронноеПисьмоИсходящее.Форма.CRM_ФормаДокумента",
				 Новый Структура("Ключ", ТекущиеДанные),
				 ЭтотОбъект);
		Иначе
			ПоказатьЗначение(, ТекущиеДанные);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////////////////
// Процедуры изменения состояния объектов.

&НаКлиенте
Функция ВыбранныеЭлементы()
	
	ВыбранныеЭлементы = Новый Массив;
	
	ВыделенныеОбласти = Элементы.ТаблицаОтчета.ПолучитьВыделенныеОбласти();
	
	Для Каждого ВыделеннаяОбласть Из ВыделенныеОбласти Цикл 
		
		Если ТипЗнч(ВыделеннаяОбласть) <> Тип("ОбластьЯчеекТабличногоДокумента") Тогда
			Продолжить;
		КонецЕсли;
		
		Границы = ГраницыВыделеннойОбласти(ВыделеннаяОбласть);
		
		Для НомерКолонки = Границы.Лево По Границы.Право Цикл
			
			Для НомерСтроки = Границы.Верх По Границы.Низ Цикл
				
				Ячейка = ТаблицаОтчета.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
				
				Если ЗначениеЗаполнено(Ячейка.Расшифровка) Тогда 
					ВыбранныеЭлементы.Добавить(Ячейка.Расшифровка);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ВыбранныеЭлементы;
	
КонецФункции

&НаКлиенте
Функция ГраницыВыделеннойОбласти(ВыделеннаяОбласть)
	
	Границы = Новый Структура("Лево, Верх, Низ, Право");
	ЗаполнитьЗначенияСвойств(Границы, ВыделеннаяОбласть);
	
	Если Границы.Лево = 0 Тогда 
		Границы.Лево = 1;
	КонецЕсли;
	
	Если Границы.Верх = 0 Тогда 
		Границы.Верх = 1;
	КонецЕсли;
	
	Если Границы.Низ = 0 Тогда 
		Границы.Низ = 1;
	КонецЕсли;
	
	Если Границы.Право = 0 Тогда 
		Границы.Право = 1;
	КонецЕсли;
	
	Возврат Границы;
	
КонецФункции

&НаКлиенте
Процедура ОповеститьСистемуОбИзменении(ВыбранныеЭлементы)
	
	Для Каждого Элемент Из ВыбранныеЭлементы Цикл 
		ОповеститьОбИзменении(Элемент);
	КонецЦикла;
	
КонецПроцедуры

// Изменение пометки удаления.

&НаСервереБезКонтекста
Функция СтатистикаПоВыбраннымЭлементам(ВыбранныеЭлементы)
	
	Статистика = Новый Структура;
	Статистика.Вставить("СПометкойНаУдаление", Новый Соответствие);
	Статистика.Вставить("БезПометкиНаУдаление", Новый Соответствие);
	
	СвойстваЭлементов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ВыбранныеЭлементы, "ПометкаУдаления, Представление");
	
	Для Каждого Элемент Из ВыбранныеЭлементы Цикл 
		
		СвойстваЭлемента = СвойстваЭлементов[Элемент];
		
		Если СвойстваЭлемента.ПометкаУдаления Тогда 
			Статистика.СПометкойНаУдаление.Вставить(Элемент, СвойстваЭлемента.Представление);
		Иначе
			Статистика.БезПометкиНаУдаление.Вставить(Элемент, СвойстваЭлемента.Представление);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Статистика;
	
КонецФункции

&НаКлиенте
Функция СценарийИзмененияПометкиУдаления(ВыбранныеЭлементы, СтатистикаПоВыбраннымЭлементам)
	
	Сценарий = Новый Структура;
	Сценарий.Вставить("Пометка", Ложь);
	Сценарий.Вставить("Вопрос", "");
	Сценарий.Вставить("Оповещение", "");
	Сценарий.Вставить("Ссылка", "");
	Сценарий.Вставить("Пояснение", "");
	
	КоличествоВыбранныхЭлементов = ВыбранныеЭлементы.Количество();
	КоличествоПомеченныхНаУдаление = СтатистикаПоВыбраннымЭлементам.СПометкойНаУдаление.Количество();
	
	Если КоличествоВыбранныхЭлементов = 1 Тогда 
		
		Элемент = ВыбранныеЭлементы[0];
		Сценарий.Ссылка = ПолучитьНавигационнуюСсылку(Элемент);
		
		Если КоличествоПомеченныхНаУдаление = 0 Тогда 
			
			ПредставлениеЭлемента = СтатистикаПоВыбраннымЭлементам.БезПометкиНаУдаление[Элемент];
			
			Сценарий.Пометка = Истина;
			Сценарий.Оповещение = НСтр("ru = 'Пометка удаления установлена';
										|en = 'Deletion mark is selected'");
			Сценарий.Пояснение = ПредставлениеЭлемента;
			Сценарий.Вопрос = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Пометить ""%1"" на удаление?';
					|en = 'Do you want to mark %1 for deletion?'"), ПредставлениеЭлемента);
			
		Иначе
			
			ПредставлениеЭлемента = СтатистикаПоВыбраннымЭлементам.СПометкойНаУдаление[Элемент];
			
			Сценарий.Оповещение = НСтр("ru = 'Пометка удаления снята';
										|en = 'Deletion mark is cleared'");
			Сценарий.Пояснение = ПредставлениеЭлемента;
			Сценарий.Вопрос = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Снять с ""%1"" пометку на удаление?';
					|en = 'Do you want to clear the deletion mark from ""%1""?'"), ПредставлениеЭлемента);
			
		КонецЕсли;
		
	Иначе
		
		Сценарий.Пояснение = Заголовок;
		
		Если КоличествоПомеченныхНаУдаление = 0 Тогда 
			
			Сценарий.Пометка = Истина;
			Сценарий.Вопрос = НСтр("ru = 'Пометить выделенные элементы на удаление?';
									|en = 'Mark the selected items for deletion?'");
			Сценарий.Оповещение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Пометка удаления установлена (%1)';
					|en = 'Deletion mark is set (%1)'"), КоличествоВыбранныхЭлементов);
		
		ИначеЕсли КоличествоПомеченныхНаУдаление = КоличествоВыбранныхЭлементов Тогда 
			
			Сценарий.Вопрос = НСтр("ru = 'Снять с выделенных элементов пометку на удаление?';
									|en = 'Clear marks for deletion of the selected items?'");
			Сценарий.Оповещение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Пометка удаления снята (%1)';
					|en = 'Deletion mark is cleared (%1)'"), КоличествоВыбранныхЭлементов);
			
		Иначе
			
			// Если часть выбранных элементов помечена на удаление, то система должна обработать только их, сняв пометку.
			ВыбранныеЭлементы.Очистить();
			
			Для Каждого Элемент Из СтатистикаПоВыбраннымЭлементам.СПометкойНаУдаление Цикл 
				ВыбранныеЭлементы.Добавить(Элемент.Ключ);
			КонецЦикла;
			
			Сценарий = СценарийИзмененияПометкиУдаления(ВыбранныеЭлементы, СтатистикаПоВыбраннымЭлементам);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Сценарий;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьСценарийИзмененияПометкиУдаления(Ответ, Сценарий) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	ИзменитьПометкуУдаленияЭлементов(Сценарий.ВыбранныеЭлементы, Сценарий.Пометка);
	
	ПоказатьОповещениеПользователя(
		Сценарий.Оповещение,
		Сценарий.Ссылка,
		Сценарий.Пояснение,
		БиблиотекаКартинок.Информация32);
	
	ОповеститьСистемуОбИзменении(Сценарий.ВыбранныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьПометкуУдаленияЭлементов(ВыбранныеЭлементы, Пометка)
	
	Для Каждого Элемент Из ВыбранныеЭлементы Цикл 
		
		ВыбранныйОбъект = Элемент.ПолучитьОбъект();
		ВыбранныйОбъект.УстановитьПометкуУдаления(Пометка);
		
	КонецЦикла;
	
	ОбновитьДеревоСтруктурыПодчиненности();
	
КонецПроцедуры

// Изменение проведения документа.

&НаКлиенте
Процедура ИзменитьПроведениеДокументов(Режим)
	
	ВыбранныеДокументы = ВыбранныеЭлементы();
	
	Если ВыбранныеДокументы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОбработанныеДокументы = ОбработанныеДокументы(ВыбранныеДокументы, Режим);
	КоличествоОбработанныхДокументов = ОбработанныеДокументы.Количество();
	
	Если КоличествоОбработанныхДокументов = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Если КоличествоОбработанныхДокументов = 1 Тогда 
		
		Документ = ВыбранныеДокументы[0];
		
		Оповещение = НСтр("ru = 'Изменение';
							|en = 'Update'");
		Ссылка = ПолучитьНавигационнуюСсылку(Документ);
		Пояснение = ОбработанныеДокументы[Документ];
		
	Иначе
		
		Ссылка = "";
		Пояснение = Заголовок;
		Оповещение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Изменение (%1)';
				|en = 'Change (%1)'"), КоличествоОбработанныхДокументов);
		
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		Оповещение,
		Ссылка,
		Пояснение,
		БиблиотекаКартинок.Информация32);
	
	ОповеститьСистемуОбИзменении(ВыбранныеДокументы);
	
КонецПроцедуры

&НаСервере
Функция ОбработанныеДокументы(ВыбранныеДокументы, Режим)
	
	ОбработанныеДокументы = Новый Соответствие;
	
	Индекс = ВыбранныеДокументы.ВГраница();
	
	Пока Индекс >= 0 Цикл 
		
		Если Не ОбщегоНазначения.ЭтоДокумент(ВыбранныеДокументы[Индекс].Метаданные()) Тогда 
			ВыбранныеДокументы.Удалить(Индекс);
		КонецЕсли;
		
		Индекс = Индекс - 1;
		
	КонецЦикла;
	
	Если ВыбранныеДокументы.Количество() = 0 Тогда 
		Возврат ОбработанныеДокументы;
	КонецЕсли;
	
	СвойстваДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ВыбранныеДокументы, "Проведен, Представление");
	
	Индекс = ВыбранныеДокументы.ВГраница();
	
	Пока Индекс >= 0 Цикл 
		
		Документ = ВыбранныеДокументы[Индекс];
		СвойстваДокумента = СвойстваДокументов[Документ];
	
		Если Режим = РежимЗаписиДокумента.ОтменаПроведения
			И Не СвойстваДокумента.Проведен Тогда 
			
			ВыбранныеДокументы.Удалить(Индекс);
			
		Иначе
			
			ВыбранныйОбъект = Документ.ПолучитьОбъект();
			ВыбранныйОбъект.Записать(Режим);
			
			ОбработанныеДокументы.Вставить(Документ, СвойстваДокумента.Представление);
			
		КонецЕсли;
		
		Индекс = Индекс - 1;
		
	КонецЦикла;
	
	ОбновитьДеревоСтруктурыПодчиненности();
	
	Возврат ОбработанныеДокументы;
	
КонецФункции

#КонецОбласти
