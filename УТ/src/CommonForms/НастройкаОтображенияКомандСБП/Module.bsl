///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Настройки = СистемаБыстрыхПлатежейСлужебный.НастройкиОтображенияКоманд();
	ИменаОбъектов = СистемаБыстрыхПлатежейСлужебный.ИменаДокументовСКомандой();
	Для Каждого ИмяОбъекта Из ИменаОбъектов Цикл
		Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(
			ИмяОбъекта);
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(
			ТипЗнч(Менеджер.ПустаяСсылка()));
		Пометка = СистемаБыстрыхПлатежейСлужебный.КомандаСБПИспользуется(
			ИмяОбъекта,
			Настройки);
		Объекты.Добавить(
			ИмяОбъекта,
			МетаданныеОбъекта.Синоним,
			Пометка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьНаСервере(Объекты);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Снять(Команда)
	
	УстановитьСнятьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Установить(Команда)
	
	УстановитьСнятьФлажки(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьНаСервере(Знач Объекты)
	
	Настройки = Новый Соответствие;
	Для Каждого Объект Из Объекты Цикл
		Настройки.Вставить(Объект.Значение, Объект.Пометка);
	КонецЦикла;
	
	СистемаБыстрыхПлатежейСлужебный.УстановитьНастройкиОтображенияКоманд(
		Настройки);
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСнятьФлажки(Пометка)
	
	Для Каждого ОбъектНастройки Из Объекты Цикл
		ОбъектНастройки.Пометка = Пометка;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти