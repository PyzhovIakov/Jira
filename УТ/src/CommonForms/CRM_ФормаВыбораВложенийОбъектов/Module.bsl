
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ОбрабатываемыеОбъекты")
			Или Параметры.ОбрабатываемыеОбъекты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоДобавленныхРеквизитов = 0;
	
	Для Каждого ОбрабатываемыйОбъект Из Параметры.ОбрабатываемыеОбъекты Цикл
		ВложенияОбъекта = Неопределено;
		
		Если ТипЗнч(ОбрабатываемыйОбъект) = Тип("ДокументСсылка.CRM_Интерес") Тогда
			ВложенияОбъекта = ПолучитьВложенияОбъекта("CRM_ИнтересПрисоединенныеФайлы", ОбрабатываемыйОбъект);
		КонецЕсли;
		
		Если ВложенияОбъекта = Неопределено Или ВложенияОбъекта.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяРеквизитаСпискаВложений		= "СписокВложений_" + Строка(КоличествоДобавленныхРеквизитов);
		ИмяЭлементаСтраницыВложений		= "Страница" + ИмяРеквизитаСпискаВложений;
		
		ИмяЭлементаПоляПометка	= "Пометка" + ИмяРеквизитаСпискаВложений;
		ИмяЭлементаПоляЗначения	= "Значение" + ИмяРеквизитаСпискаВложений;
		
		/////////////////////
		// РЕКВИЗИТЫ
		
		МассивДобавляемыхРеквизитов = Новый Массив;
		
		// СписокВложенийИнтереса
		РеквизитСписокВложений = Новый РеквизитФормы(ИмяРеквизитаСпискаВложений, Новый ОписаниеТипов("СписокЗначений"));
		МассивДобавляемыхРеквизитов.Добавить(РеквизитСписокВложений);
		
		ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);
		
		/////////////////////
		// ЭЛЕМЕНТЫ ФОРМЫ
		
		// Элементы.СтраницаСписокВложений_N
		ЭлементСтраницы = Элементы.Добавить(ИмяЭлементаСтраницыВложений, Тип("ГруппаФормы"), Элементы.СтраницыИнтересов);
		ЭлементСтраницы.Вид			= ВидГруппыФормы.Страница;
		ЭлементСтраницы.Заголовок	= ОбрабатываемыйОбъект.Номер;
		
		// Элементы.СписокВложений_N
		ЭлементСписка = Элементы.Добавить(ИмяРеквизитаСпискаВложений, Тип("ТаблицаФормы"), ЭлементСтраницы);
		ЭлементСписка.ИзменятьСоставСтрок		= Ложь;
		ЭлементСписка.ИзменятьПорядокСтрок		= Ложь;
		ЭлементСписка.ПутьКДанным				= ИмяРеквизитаСпискаВложений;
		ЭлементСписка.ПоложениеКоманднойПанели	= ПоложениеКоманднойПанелиЭлементаФормы.Нет;
		
		// Элементы.ПометкаСписокВложений_N
		НовоеПолеСписка = Элементы.Добавить(ИмяЭлементаПоляПометка, Тип("ПолеФормы"), ЭлементСписка);
		НовоеПолеСписка.Вид			= ВидПоляФормы.ПолеФлажка;
		НовоеПолеСписка.ПутьКДанным	= ИмяРеквизитаСпискаВложений + ".Пометка";
		
		// Элементы.ЗначениеСписокВложений_N
		НовоеПолеСписка = Элементы.Добавить(ИмяЭлементаПоляЗначения, Тип("ПолеФормы"), ЭлементСписка);
		НовоеПолеСписка.Вид				= ВидПоляФормы.ПолеВвода;
		НовоеПолеСписка.ПутьКДанным		= ИмяРеквизитаСпискаВложений + ".Значение";
		НовоеПолеСписка.ТолькоПросмотр	= Истина;
		
		/////////////////////
		// ЗАПОЛНЕНИЕ ДАННЫМИ
		
		СписокДляВложений = ЭтотОбъект[ИмяРеквизитаСпискаВложений];
		
		Для Каждого ЭлементВложения Из ВложенияОбъекта Цикл
			ИмяКартинкиФайла	= "CRM_ИконкаТипаФайла_" + Строка(ЭлементВложения.ИндексКартинки);
			КартинкаФайла		= БиблиотекаКартинок[ИмяКартинкиФайла];
			СписокДляВложений.Добавить(ЭлементВложения.Ссылка, , , КартинкаФайла);
		КонецЦикла;
		
		КоличествоДобавленныхРеквизитов = КоличествоДобавленныхРеквизитов + 1;
	КонецЦикла;
	
	// Если страница одна, то отображать страницы не имеет смысла.
	Если КоличествоДобавленныхРеквизитов = 1 Тогда
		Элементы.СтраницыИнтересов.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	Если КоличествоДобавленныхРеквизитов >= 1 Тогда
		Элементы.ГруппаПредупреждение.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбранныеФайлы = Новый Массив;
	
	Для НомерСписка = 0 По КоличествоДобавленныхРеквизитов - 1 Цикл
		ИмяРеквизитаСпискаВложений = "СписокВложений_" + Строка(НомерСписка);
		Для Каждого ЭлементСпискаЗначений Из ЭтотОбъект[ИмяРеквизитаСпискаВложений] Цикл
			Если ЭлементСпискаЗначений.Пометка Тогда
				ВыбранныеФайлы.Добавить(ЭлементСпискаЗначений.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если ВыбранныеФайлы.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не выбран хотя бы один файл'; en = 'At least one file is not selected'"));
		Возврат;
	КонецЕсли;
	
	Закрыть(ВыбранныеФайлы);
	
КонецПроцедуры // Выбрать()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьВложенияОбъекта(НаименованиеСправочникаВложений, СсылкаНаОбъект)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СправочникВложений.Ссылка КАК Ссылка,
	|	СправочникВложений.ИндексКартинки КАК ИндексКартинки
	|ИЗ
	|	Справочник.CRM_ИнтересПрисоединенныеФайлы КАК СправочникВложений
	|ГДЕ
	|	СправочникВложений.ЭтоГруппа = ЛОЖЬ
	|	И СправочникВложений.ВладелецФайла = &ВладелецФайла
	|	И СправочникВложений.ПометкаУдаления = ЛОЖЬ";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "CRM_ИнтересПрисоединенныеФайлы", НаименованиеСправочникаВложений);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВладелецФайла", СсылкаНаОбъект);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьВложенияОбъекта()

#КонецОбласти