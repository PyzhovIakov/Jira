
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	CRM_ОбщегоНазначенияСервер.СбросНастроекПоложенияОкна(ЭтотОбъект);
	
	МассивНовыхРеквизитов = Новый Массив;
	Если Параметры.Свойство("ПрефиксОбработки") Тогда
		ПрефиксОбработки = Параметры.ПрефиксОбработки;
	Иначе
		ПрефиксОбработки = "CRM_РаботаСМессенджером";
	КонецЕсли;

	Если Параметры.Свойство("МассивСкрываемых") Тогда
		МассивСкрываемых = Параметры.МассивСкрываемых;
	Иначе
		МассивСкрываемых = Новый Массив;
		МассивСкрываемых.Добавить("CRM_РаботаСМессенджеромСистемаВзаимодействия");
		МассивСкрываемых.Добавить("CRM_РаботаСМессенджеромB2BПортал");
		
		// Запрещены
		МассивСкрываемых.Добавить("CRM_РаботаСМессенджеромFacebook");
		МассивСкрываемых.Добавить("CRM_РаботаСМессенджеромInstagram");
		МассивСкрываемых.Добавить("CRM_РаботаСМессенджеромInstagramDirect");
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		МассивСкрываемых.Добавить("CRM_РаботаСМессенджеромTelegram");
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	// Получим список мессенджеров с логотипами
	СписокМессенджеров = Новый СписокЗначений;
	Для каждого Обработка Из Метаданные.Обработки Цикл
		
		Если МассивСкрываемых.Найти(Обработка.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрНайти(Обработка.Имя, ПрефиксОбработки) = 1 Тогда
			ТипМессенджера = СтрЗаменить(Обработка.Имя, ПрефиксОбработки, "");
			Если Обработка.Макеты.Найти("Иконка_32") <> Неопределено Тогда
				ДанныеКартинки = Обработки[Обработка.Имя].ПолучитьМакет("Иконка_32");
			Иначе
				ДанныеКартинки = Неопределено;
			КонецЕсли;
			Мессенджер = СписокМессенджеров.Добавить(Новый Структура("Представление, Логотип",
				 Обработка.Представление(), ДанныеКартинки),
				 ТипМессенджера);
			
			// создаем реквизиты для хранения адресов картинок
			РеквизитДляКартинки = Новый РеквизитФормы("АдресКартинкиМессенджера" + ТипМессенджера,
													   Новый ОписаниеТипов("Строка"));
			РеквизитНаименования = Новый РеквизитФормы("НаименованиеМессенджера" + ТипМессенджера,
													   Новый ОписаниеТипов("Строка"));
													   
													   МассивНовыхРеквизитов.Добавить(РеквизитДляКартинки);
			МассивНовыхРеквизитов.Добавить(РеквизитНаименования);
			
		КонецЕсли;
	КонецЦикла;
	
	// Добавим новые реквизиты в форму
	ИзменитьРеквизиты(МассивНовыхРеквизитов);
	
	СписокМессенджеров.СортироватьПоПредставлению();
	
	// Сгенерируем кнопки мессенджеров
	Для каждого Мессенджер Из СписокМессенджеров Цикл
		
		ГруппаМессенджера = Элементы.Добавить("ГруппаМессенджера" + Мессенджер.Представление,
			 Тип("ГруппаФормы"), Элементы.ГруппаКаналы);
		ГруппаМессенджера.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаМессенджера.ЦветФона = WebЦвета.Белый;
		ГруппаМессенджера.ОтображатьЗаголовок = Ложь;
		ГруппаМессенджера.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Лево;
		ГруппаМессенджера.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
		Если Мессенджер.Значение.Логотип <> Неопределено Тогда
			ЛоготипМессенджера = Элементы.Добавить("ЛоготипМессенджера" + Мессенджер.Представление,
				 Тип("ПолеФормы"), ГруппаМессенджера);
			ЛоготипМессенджера.Вид = ВидПоляФормы.ПолеКартинки;
			ЛоготипМессенджера.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			ЛоготипМессенджера.РастягиватьПоВертикали = Ложь;
			ЛоготипМессенджера.РастягиватьПоГоризонтали = Ложь;
			ЛоготипМессенджера.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
			
			ИмяРеквизитаКартинки = "АдресКартинкиМессенджера" + Мессенджер.Представление;
			ЛоготипМессенджера.ПутьКДанным = ИмяРеквизитаКартинки;
			ЭтотОбъект[ИмяРеквизитаКартинки] = ПоместитьВоВременноеХранилище(Мессенджер.Значение.Логотип);
		
			ЛоготипМессенджера.Высота = 2;
			ЛоготипМессенджера.Ширина = 4;
			ЛоготипМессенджера.Гиперссылка = Истина;
			ЛоготипМессенджера.УстановитьДействие("Нажатие", "ПодключаемыйЛоготипНажатие");
			ЛоготипМессенджера.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
			ЛоготипМессенджера.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
			
		КонецЕсли;
		
		КомандаКанала = Команды.Добавить("НазваниеМессенджера" + Мессенджер.Представление);
		КомандаКанала.Заголовок = Мессенджер.Значение.Представление;
		КомандаКанала.Действие = "ПодключаемыйЛоготипНажатие";
		КомандаКанала.Отображение = ОтображениеКнопки.Текст;
		
		ЭлементКомандыКанала = Элементы.Добавить("НазваниеМессенджера" + Мессенджер.Представление,
			 Тип("КнопкаФормы"), ГруппаМессенджера);
		ЭлементКомандыКанала.ИмяКоманды = КомандаКанала.Имя;
		ЭлементКомандыКанала.ОтображениеФигуры = ОтображениеФигурыКнопки.Нет;
		ЭлементКомандыКанала.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
		ЭлементКомандыКанала.Шрифт = Новый Шрифт(, "14", , , , Ложь);
		ЭлементКомандыКанала.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
		ЭлементКомандыКанала.РастягиватьПоГоризонтали = Ложь;
		ЭлементКомандыКанала.РастягиватьПоВертикали = Ложь;
		
		ДекорацияРазделитель = Элементы.Добавить("Разделитель" + Мессенджер.Представление,
			 Тип("ДекорацияФормы"), Элементы.ГруппаКаналы);
		ДекорацияРазделитель.Вид = ВидДекорацииФормы.Надпись;
		ДекорацияРазделитель.ЦветРамки = Новый Цвет(199, 199, 199);
		ДекорацияРазделитель.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.ЧертаСверху);
		ДекорацияРазделитель.АвтоМаксимальнаяШирина = Ложь;
		ДекорацияРазделитель.АвтоМаксимальнаяВысота = Ложь;
		ДекорацияРазделитель.Высота = 1;
		ДекорацияРазделитель.РастягиватьПоГоризонтали = Истина;
		ДекорацияРазделитель.РастягиватьПоВертикали = Ложь;
		ДекорацияРазделитель.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
		ДекорацияРазделитель.Шрифт = Новый Шрифт(, 1);
		
	КонецЦикла;

	// Для последнего уберем рамку
	ЛоготипМессенджера.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	ДекорацияРазделитель.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);

КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПодключаемыйЛоготипНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИмяМессенджера = СтрЗаменить(Элемент.Имя, "ЛоготипМессенджера", "");
	ИмяМессенджера = СтрЗаменить(ИмяМессенджера, "НазваниеМессенджера", "");
	Закрыть(ИмяМессенджера);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы
