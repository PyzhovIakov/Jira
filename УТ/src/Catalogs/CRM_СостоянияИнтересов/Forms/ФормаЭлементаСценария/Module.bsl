
#Область ОписаниеПеременных

&НаКлиенте
Перем ПеретаскиваемыйРеквизит;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьПодразделения = CRM_ОбщегоНазначенияПовтИсп.ИспользоватьПодразделения();
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Заголовок = Объект.Наименование + НСтр("ru=' (Сценарий)';en=' (Script)'");
	Иначе
		Заголовок = НСтр("ru='Сценарий (создание)';en='Script (creation)'");
	КонецЕсли;
	
	ИдентификаторМетаданныхИнтерес = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Документы.CRM_Интерес);
	СписокТиповУслуг.ЗагрузитьЗначения(Объект.ТипыУслуг.Выгрузить(, "ТипУслуги").ВыгрузитьКолонку("ТипУслуги"));
	СписокПодразделений.ЗагрузитьЗначения(Объект.Подразделения.Выгрузить(,
		 "Подразделение").ВыгрузитьКолонку("Подразделение"));
	Элементы.ДекорацияНастройкаСценария.Видимость = ЗначениеЗаполнено(Объект.Ссылка);
	Элементы.Подразделение.Видимость = ИспользоватьПодразделения;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ТипыУслуг.Очистить();
	Для каждого ЭлементСписка Из СписокТиповУслуг Цикл
		НовТип = ТекущийОбъект.ТипыУслуг.Добавить();
		НовТип.ТипУслуги = ЭлементСписка.Значение;
	КонецЦикла;
	ТекущийОбъект.Подразделения.Очистить();
	Для каждого ЭлементСписка Из СписокПодразделений Цикл
		НовТип = ТекущийОбъект.Подразделения.Добавить();
		НовТип.Подразделение = ЭлементСписка.Значение;
	КонецЦикла;
	Элементы.ДекорацияНастройкаСценария.Видимость = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗначениеЗаполнено(Объект.Ссылка) И ОписаниеОповещенияОЗакрытии <> Неопределено
		 И ТипЗнч(ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры) = Тип("Структура") Тогда
		ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры.Вставить("Сценарий", Объект.Ссылка);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияНастройкаСценарияНажатие(Элемент)
	ПараметрыОткрытия = Новый Структура("Сценарий", Объект.Ссылка);
	ОткрытьФорму("Обработка.CRM_НастройкаСценарияПродаж.Форма.Форма", ПараметрыОткрытия);
КонецПроцедуры

&НаКлиенте
Процедура ИндексЦветаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОповещениеНовое = Новый ОписаниеОповещения("ЦветНачалоВыбораЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("ТекущийЦвет", Объект.ИндексЦвета);
	ФормаВыбораЦвета = ОткрытьФорму("Справочник.CRM_Категории.Форма.ФормаВыбораЦвета", ПараметрыФормы, Элемент, , , ,
		ОповещениеНовое, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);	
КонецПроцедуры // ЦветНачалоВыбора()

&НаКлиенте
// Продолжение процедуры "ЦветНачалоВыбора"
//
Процедура ЦветНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ (Результат = Неопределено) И НЕ (Результат = КодВозвратаДиалога.Отмена) Тогда
		Объект.ИндексЦвета = Результат[0].Картинка;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
