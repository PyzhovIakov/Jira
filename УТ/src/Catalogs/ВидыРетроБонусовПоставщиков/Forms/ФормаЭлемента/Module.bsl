//@strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииЧтенииНаСервере();
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		
		ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
		
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура БезРасчетаПриИзменении(Элемент)
	
	Объект.ТипБонуса = ПредопределенноеЗначение("Перечисление.ТипыРетроБонусовПоставщиков.ПустаяСсылка");
	Объект.ПоказательТоваров = ПредопределенноеЗначение("Перечисление.ЦелевыеПоказателиРетроБонусовПоТоварам.НеИспользуется");
	Объект.ПериодичностьУсловий = ПредопределенноеЗначение("Перечисление.ПериодичностиРетроБонусов.ПустаяСсылка");
	УстановитьДоступныйСписокПоказателяПродаж(ЭтотОбъект);
	УстановитьДоступныйСписокБазРасчетаПродаж(ЭтотОбъект);
	Объект.УчитыватьНДС = Ложь;
	
	УстановитьДоступныйСписокСоставУчастников(ЭтотОбъект);
	УстановитьДоступныйСписокСоставТоваров(ЭтотОбъект);
	УстановитьДоступныйСписокОтборТоваров(ЭтотОбъект);
	Объект.ПериодичностьНачислений = ПредопределенноеЗначение("Перечисление.ПериодичностиРетроБонусов.Однократно");
	УстановитьДоступныйСписокПериодичностьНачислений(ЭтотОбъект);
	
	Объект.ПорядокРаспределения = РетроБонусыКлиентСервер.ПроверитьСброситьПорядокРаспределения(
		Объект.ПорядокРаспределения,
		Объект.БезРасчета,
		Объект.ТипБонуса,
		Объект.ПоказательТоваров,
		Объект.БазаРасчета);
	
	НастроитьЗависимыеЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипБонусаПриИзменении(Элемент)
	
	ТипБонусаПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипБонусаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательТоваровПриИзменении(Элемент)
	
	УстановитьДоступныйСписокБазРасчетаПродаж(ЭтотОбъект);
	УстановитьДоступныйСписокСоставТоваров(ЭтотОбъект);
	УстановитьДоступныйСписокОтборТоваров(ЭтотОбъект);
	
	Если Объект.ПоказательТоваров = ПредопределенноеЗначение("Перечисление.ЦелевыеПоказателиРетроБонусовПоТоварам.НеИспользуется") Тогда
		Объект.ПериодичностьУсловий = ПредопределенноеЗначение("Перечисление.ПериодичностиРетроБонусов.ПустаяСсылка");
	КонецЕсли;
	
	Объект.ПорядокРаспределения = РетроБонусыКлиентСервер.ПроверитьСброситьПорядокРаспределения(
		Объект.ПорядокРаспределения,
		Объект.БезРасчета,
		Объект.ТипБонуса,
		Объект.ПоказательТоваров,
		Объект.БазаРасчета);
	
	УстановитьДоступныйСписокПериодичностьНачислений(ЭтотОбъект);
	
	ПроверитьСброситьФлагВыполнениеУсловийПоПокупателям(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьУсловийПриИзменении(Элемент)
	
	УстановитьДоступныйСписокПериодичностьНачислений(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура БазаРасчетаПриИзменении(Элемент)
	
	Если Объект.БазаРасчета = ПредопределенноеЗначение("Перечисление.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены") Тогда
		
		Объект.УчитыватьНДС = Ложь;
		
	КонецЕсли;
	
	Объект.ПорядокРаспределения = РетроБонусыКлиентСервер.ПроверитьСброситьПорядокРаспределения(
		Объект.ПорядокРаспределения,
		Объект.БезРасчета,
		Объект.ТипБонуса,
		Объект.ПоказательТоваров,
		Объект.БазаРасчета);
	
	УстановитьДоступныйСписокСоставТоваров(ЭтотОбъект);
	УстановитьДоступныйСписокОтборТоваров(ЭтотОбъект);
	НастроитьЗависимыеЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СоставТоваровПриИзменении(Элемент)
	
	УстановитьДоступныйСписокОтборТоваров(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	ОбщегоНазначенияУТКлиент.РедактироватьПериод(
		Объект, 
		Новый Структура("ДатаНачала, ДатаОкончания", "НачалоДействия", "ОкончаниеДействия"));
		
КонецПроцедуры

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ПодключаемыеКоманды

// Параметры:
//   Команда - КомандаФормы - выполняемая команда
//
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ИнтеграцияС1СДокументооборотом

// Параметры:
//   Команда - КомандаФормы - вызванная команда.
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

// Конец ИнтеграцияС1СДокументооборотом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытийЭлементовШапкиФормыВспомогательные

&НаСервере
Процедура ТипБонусаПриИзмененииСервер()
	
	Объект.ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ПустаяСсылка();
	УстановитьЗаголовокПоказателяТоваров();
	
	УстановитьДоступныйСписокСоставУчастников(ЭтотОбъект);
	УстановитьДоступныйСписокПоказателяПродаж(ЭтотОбъект);
	УстановитьДоступныйСписокБазРасчетаПродаж(ЭтотОбъект);
	УстановитьДоступныйСписокПериодичностьНачислений(ЭтотОбъект);
	УстановитьДоступныйСписокСоставТоваров(ЭтотОбъект);
	УстановитьДоступныйСписокОтборТоваров(ЭтотОбъект);
	
	ПроверитьСброситьФлагВыполнениеУсловийПоПокупателям(ЭтотОбъект);
	
	Если Объект.ПоказательТоваров = ПредопределенноеЗначение("Перечисление.ЦелевыеПоказателиРетроБонусовПоТоварам.НеИспользуется") Тогда
		Объект.ПериодичностьУсловий = ПредопределенноеЗначение("Перечисление.ПериодичностиРетроБонусов.ПустаяСсылка");
	КонецЕсли;
	
	Объект.ПорядокРаспределения = РетроБонусыКлиентСервер.ПроверитьСброситьПорядокРаспределения(
		Объект.ПорядокРаспределения,
		Объект.БезРасчета,
		Объект.ТипБонуса,
		Объект.ПоказательТоваров,
		Объект.БазаРасчета);
	
	НастроитьЗависимыеЭлементыФормыНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УстановитьЗаголовокПоказателяТоваров()
	
	Если Объект.ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Закупки Тогда
		
		ЗаголовокПоля = НСтр("ru = 'Целевой показатель
						 |закупок'");
		
	ИначеЕсли Объект.ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Продажи Тогда
		
		ЗаголовокПоля = НСтр("ru = 'Целевой показатель
						 |продаж'");
		
	Иначе
		
		ЗаголовокПоля = "";
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Элементы.ПоказательТоваров.Имя, "Заголовок", ЗаголовокПоля);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьЗависимыеЭлементыФормы(ИзмененныйРеквизит = "")
	
	ДенежныеСредстваКлиентСервер.НастроитьЭлементыФормы(ЭтотОбъект, ИзмененныйРеквизит, РеквизитыФормы(ЭтотОбъект));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЗависимыеЭлементыФормыНаСервере(ИзмененныйРеквизит = "")
	
	ДенежныеСредстваКлиентСервер.НастроитьЭлементыФормы(ЭтотОбъект, ИзмененныйРеквизит, РеквизитыФормы(ЭтотОбъект));
	
КонецПроцедуры

// Параметры:
//  Форма - см. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаЭлемента
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступныйСписокПоказателяПродаж(Форма)
	
	Объект = Форма.Объект;
	
	ПараметрыОтбора = РетроБонусыКлиентСервер.НовыеПараметрыДоступныхПоказателейТоваров();
	ПараметрыОтбора.БезРасчета = Объект.БезРасчета;
	ПараметрыОтбора.ТипБонуса = Объект.ТипБонуса;
	
	МассивВыбора = РетроБонусыКлиентСервер.ДоступныйСписокПоказателейТоваров(ПараметрыОтбора);
	Форма.Элементы.ПоказательТоваров.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	РетроБонусыКлиентСервер.ПроверитьУстановитьЗначениеСпискаВыбора(Объект, "ПоказательТоваров", МассивВыбора);
	
КонецПроцедуры

// Установить доступный список баз расчета продаж
// 
// Параметры:
//  Форма - см. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаЭлемента
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступныйСписокБазРасчетаПродаж(Форма)
	
	Объект = Форма.Объект;
	
	ПараметрыОтбора = РетроБонусыКлиентСервер.НовыеПараметрыДоступныхБазРасчетаРетроБонусов();
	ПараметрыОтбора.БезРасчета = Объект.БезРасчета;
	ПараметрыОтбора.ПоказательТоваров = Объект.ПоказательТоваров;
	ПараметрыОтбора.ТипБонуса = Объект.ТипБонуса;
	
	МассивВыбора = РетроБонусыКлиентСервер.ДоступныйСписокБазРасчетаРетроБонусов(ПараметрыОтбора);
	Форма.Элементы.БазаРасчета.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	РетроБонусыКлиентСервер.ПроверитьУстановитьЗначениеСпискаВыбора(Объект, "БазаРасчета", МассивВыбора);
	
КонецПроцедуры

// Установить доступный список периодичности начислений
// 
// Параметры:
//  Форма - см. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаЭлемента
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступныйСписокПериодичностьНачислений(Форма)
	
	Объект = Форма.Объект;
	
	ПараметрыОтбора = РетроБонусыКлиентСервер.НовыеПараметрыДоступныхПериодичностейНачислений();
	ПараметрыОтбора.ПериодичностьУсловий = Объект.ПериодичностьУсловий;
	ПараметрыОтбора.ПоказательТоваров = Объект.ПоказательТоваров;
	ПараметрыОтбора.ТипБонуса = Объект.ТипБонуса;
	
	МассивВыбора = РетроБонусыКлиентСервер.ДоступныйСписокПериодичностьНачислений(ПараметрыОтбора);
	Форма.Элементы.ПериодичностьНачислений.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	РетроБонусыКлиентСервер.ПроверитьУстановитьЗначениеСпискаВыбора(Объект, "ПериодичностьНачислений", МассивВыбора);
	
КонецПроцедуры

// Параметры:
//  Форма - см. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаЭлемента
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступныйСписокСоставТоваров(Форма)
	
	Объект = Форма.Объект;
	
	ПараметрыОтбора = РетроБонусыКлиентСервер.НовыеПараметрыДоступныхСоставовТоваров();
	ПараметрыОтбора.БезРасчета = Объект.БезРасчета;
	ПараметрыОтбора.ИспользоватьСегментыНоменклатуры = Форма.ИспользуютсяСегментыНоменклатуры;
	ПараметрыОтбора.ПоказательТоваров = Объект.ПоказательТоваров;
	ПараметрыОтбора.БазаРасчета = Объект.БазаРасчета;
	ПараметрыОтбора.ТипБонуса = Объект.ТипБонуса;
	
	МассивВыбора = РетроБонусыКлиентСервер.ДоступныйСписокСоставТоваров(ПараметрыОтбора);
	Форма.Элементы.СоставТоваров.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	РетроБонусыКлиентСервер.ПроверитьУстановитьЗначениеСпискаВыбора(Объект, "СоставТоваров", МассивВыбора);
	
КонецПроцедуры

// Параметры:
//  Форма - см. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаЭлемента
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступныйСписокОтборТоваров(Форма)
	
	Объект = Форма.Объект;
	
	ПараметрыОтбора = РетроБонусыКлиентСервер.НовыеПараметрыДоступныхОтборовТоваров();
	ПараметрыОтбора.БезРасчета = Объект.БезРасчета;
	ПараметрыОтбора.ПоказательТоваров = Объект.ПоказательТоваров;
	ПараметрыОтбора.БазаРасчета = Объект.БазаРасчета;
	ПараметрыОтбора.СоставТоваров = Объект.СоставТоваров;
	ПараметрыОтбора.ТипБонуса = Объект.ТипБонуса;
	
	МассивВыбора = РетроБонусыКлиентСервер.ДоступныйСписокОтборТоваров(ПараметрыОтбора);
	Форма.Элементы.ОтборТоваров.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	РетроБонусыКлиентСервер.ПроверитьУстановитьЗначениеСпискаВыбора(Объект, "ОтборТоваров", МассивВыбора);
	
КонецПроцедуры

// Параметры:
//  Форма - см. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаЭлемента
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступныйСписокСоставУчастников(Форма)
	
	Объект = Форма.Объект;
	
	ПараметрыОтбора = РетроБонусыКлиентСервер.НовыеПараметрыДоступныхСоставовУчастников();
	ПараметрыОтбора.БезРасчета = Объект.БезРасчета;
	ПараметрыОтбора.ТипБонуса = Объект.ТипБонуса;
	
	МассивВыбора = РетроБонусыКлиентСервер.ДоступныйСписокСоставУчастников(ПараметрыОтбора);
	Форма.Элементы.СоставУчастников.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	РетроБонусыКлиентСервер.ПроверитьУстановитьЗначениеСпискаВыбора(Объект, "СоставУчастников", МассивВыбора);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыФормы(Форма)
	
	РеквизитыФормы = Новый Структура;
	РеквизитыФормы.Вставить("ВидРетроБонусаИспользуется");
	РеквизитыФормы.Вставить("ИспользуютсяСегментыНоменклатуры");
	
	ЗаполнитьЗначенияСвойств(РеквизитыФормы, Форма);
	
	Возврат РеквизитыФормы;
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	ВидРетроБонусаИспользуется = Справочники.ВидыРетроБонусовПоставщиков.ВидРетроБонусаИспользуется(Объект);
	
	ИспользуютсяСегментыНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСегментыНоменклатуры");
	
	ДенежныеСредстваСервер.УправлениеЭлементамиФормыПриЧтенииСозданииНаСервере(ЭтотОбъект);
	
	УстановитьДоступныйСписокПоказателяПродаж(ЭтотОбъект);
	УстановитьДоступныйСписокБазРасчетаПродаж(ЭтотОбъект);
	УстановитьДоступныйСписокПериодичностьНачислений(ЭтотОбъект);
	УстановитьДоступныйСписокСоставТоваров(ЭтотОбъект);
	УстановитьДоступныйСписокОтборТоваров(ЭтотОбъект);
	УстановитьДоступныйСписокСоставУчастников(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормыНаСервере();
	
	УстановитьЗаголовокПоказателяТоваров();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьСброситьФлагВыполнениеУсловийПоПокупателям(Форма)
	
	Объект = Форма.Объект;
	
	ДоступнаПроверкаПоПокупателям =
		(Объект.ТипБонуса = ПредопределенноеЗначение("Перечисление.ТипыРетроБонусовПоставщиков.Продажи")
		 И Объект.ПоказательТоваров <> ПредопределенноеЗначение("Перечисление.ЦелевыеПоказателиРетроБонусовПоТоварам.НеИспользуется"));
	Если НЕ ДоступнаПроверкаПоПокупателям Тогда
		
		Объект.ВыполнениеУсловийПоПокупателям = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы_ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды

// Подключаемый продолжить выполнение команды на сервере.
// 
// Параметры:
//  ПараметрыВыполнения - Структура -
//  ДополнительныеПараметры - Структура -
//
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СтандартныеПодсистемы_Свойства

// СтандартныеПодсистемы.Свойства
 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#КонецОбласти