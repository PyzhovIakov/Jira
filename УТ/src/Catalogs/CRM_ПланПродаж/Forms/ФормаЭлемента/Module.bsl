
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
	
		Возврат;
	
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Администратор = Объект.Администраторы.Добавить();
		Администратор.Пользователь = Пользователи.ТекущийПользователь();
		ПриЧтенииСозданииНаСервере();
	Иначе
		Элементы.ГруппаОсновное.Доступность = (Не ПланИспользуется());
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	CRM_СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры
 
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();

	CRM_МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ОтображатьДиапазонДат = Не Объект.ОтображатьНомерНедели;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИспользоватьСуммуПриИзменении(Неопределено);
	ИспользоватьКоличествоПриИзменении(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура СхемаПланаПродажПриИзмененииНаСервере()
	
	ДоступностьВарианта = ДоступенВыборВариантаФакта(Объект.СхемаПланаПродаж);
	Элементы.ВариантПолученияФакта.Доступность = ДоступностьВарианта;
	Если Не ДоступностьВарианта Тогда
		Объект.ВариантПолученияФакта = Перечисления.CRM_ВариантыПолученияФактаПродажи.ПоВыручке;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СхемаПланаПродажПриИзменении(Элемент)
	СхемаПланаПродажПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСуммуПриИзменении(Элемент)
	
	Элементы.ГруппаНастройкиСумма2.Доступность = Объект.ИспользоватьСумму;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьКоличествоПриИзменении(Элемент)
	
	Элементы.ГруппаНастройкиКоличество2.Доступность = Объект.ИспользоватьКоличество;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображениеНеделиПриИзменении(Элемент)
	
	Объект.ОтображатьНомерНедели = Не ОтображатьДиапазонДат;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьНомерНеделиПриИзменении(Элемент)
	
	ОтображатьДиапазонДат = Не Объект.ОтображатьНомерНедели;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПланИспользуется()
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	CRM_ПланПродаж.ПланПродаж КАК ПланПродаж
	                      |ИЗ
	                      |	РегистрСведений.CRM_ПланПродаж КАК CRM_ПланПродаж
	                      |ГДЕ
	                      |	CRM_ПланПродаж.ПланПродаж = &ПланПродаж");
	
	Запрос.УстановитьПараметр("ПланПродаж", Объект.Ссылка);
	Результат = Не Запрос.Выполнить().Пустой();
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДоступенВыборВариантаФакта(Схема)

	ТипыАналитикОплата = Новый Массив;
	ТипыАналитикОплата.Добавить(ПланыВидовХарактеристик.CRM_ТипыАналитикПланирования.Подразделение);
	ТипыАналитикОплата.Добавить(ПланыВидовХарактеристик.CRM_ТипыАналитикПланирования.Менеджеры);
	ТипыАналитикОплата.Добавить(ПланыВидовХарактеристик.CRM_ТипыАналитикПланирования.Партнеры);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	CRM_СхемаПланаПродажЭлементыСхемы.Разрез КАК Разрез
	                      |ИЗ
	                      |	Справочник.CRM_СхемаПланаПродаж.ЭлементыСхемы КАК CRM_СхемаПланаПродажЭлементыСхемы
	                      |ГДЕ
	                      |	CRM_СхемаПланаПродажЭлементыСхемы.Ссылка = &Схема
	                      |	И НЕ CRM_СхемаПланаПродажЭлементыСхемы.Разрез В (&ТипыАналитик)");
	Запрос.УстановитьПараметр("Схема", Схема);
	Запрос.УстановитьПараметр("ТипыАналитик", ТипыАналитикОплата);
	Если Не Запрос.Выполнить().Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;

	ТипыАналитикПартнер = Новый Массив;
	ТипыАналитикПартнер.Добавить(ПланыВидовХарактеристик.CRM_ТипыАналитикПланирования.Партнеры);

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	CRM_СхемаПланаПродажЭлементыСхемы.Разрез КАК Разрез
	                      |ИЗ
	                      |	Справочник.CRM_СхемаПланаПродаж.ЭлементыСхемы КАК CRM_СхемаПланаПродажЭлементыСхемы
	                      |ГДЕ
	                      |	CRM_СхемаПланаПродажЭлементыСхемы.Ссылка = &Схема
	                      |	И CRM_СхемаПланаПродажЭлементыСхемы.Разрез В (&ТипыАналитик)");
	Запрос.УстановитьПараметр("Схема", Схема);
	Запрос.УстановитьПараметр("ТипыАналитик", ТипыАналитикПартнер);
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
