#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает массив источников лидов сценария.
//
// Параметры:
//  СценарийИнтереса - СправочникСсылка.CRM_СостоянияИнтересов	 - Сценарий интереса.
// 
// Возвращаемое значение:
//   - Неопределено, Массив
//
Функция ИсточникиЛидовСценария(СценарийИнтереса) Экспорт
	
	Если Не ЗначениеЗаполнено(СценарийИнтереса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	CRM_ИсточникиПолученияЛидовСценарии.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.CRM_ИсточникиПолученияЛидов.CRM_Сценарии КАК CRM_ИсточникиПолученияЛидовСценарии
	                      |ГДЕ
	                      |	CRM_ИсточникиПолученияЛидовСценарии.Сценарий = &Сценарий
	                      |	И НЕ CRM_ИсточникиПолученияЛидовСценарии.Ссылка.ПометкаУдаления
	                      |	И CRM_ИсточникиПолученияЛидовСценарии.Ссылка.Включено
	                      |	И CRM_ИсточникиПолученияЛидовСценарии.Ссылка.CRM_ИсточникЛидов
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	CRM_УчетныеЗаписиМессенджеровСценарии.Ссылка
	                      |ИЗ
	                      |	Справочник.CRM_УчетныеЗаписиМессенджеров.CRM_Сценарии КАК CRM_УчетныеЗаписиМессенджеровСценарии
	                      |ГДЕ
	                      |	CRM_УчетныеЗаписиМессенджеровСценарии.Сценарий = &Сценарий
	                      |	И НЕ CRM_УчетныеЗаписиМессенджеровСценарии.Ссылка.ПометкаУдаления
	                      |	И CRM_УчетныеЗаписиМессенджеровСценарии.Ссылка.Включена
	                      |	И CRM_УчетныеЗаписиМессенджеровСценарии.Ссылка.CRM_ИсточникЛидов
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	УчетныеЗаписиЭлектроннойПочтыСценарии.Ссылка
	                      |ИЗ
	                      |	Справочник.УчетныеЗаписиЭлектроннойПочты.CRM_Сценарии КАК УчетныеЗаписиЭлектроннойПочтыСценарии
	                      |ГДЕ
	                      |	УчетныеЗаписиЭлектроннойПочтыСценарии.Сценарий = &Сценарий
	                      |	И НЕ УчетныеЗаписиЭлектроннойПочтыСценарии.Ссылка.ПометкаУдаления
	                      |	И УчетныеЗаписиЭлектроннойПочтыСценарии.Ссылка.CRM_ИсточникЛидов
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	УчетныеЗаписиЭлектроннойПочтыСценарии.Ссылка");

	Запрос.УстановитьПараметр("Сценарий", СценарийИнтереса);
	МассивИсточников = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Если ПроверитьОтборПоСценариюТелефония(СценарийИнтереса) Тогда
		МассивИсточников.Добавить(Неопределено);
	КонецЕсли;
	
	Возврат МассивИсточников;
	
КонецФункции

// Функция возвращает Истина, если отбор по сценарию "Телефония" установлен корректно.
//
// Параметры:
//  СценарийИнтереса - СправочникСсылка.CRM_СостоянияИнтересов	 - Сценарий интереса.
// 
// Возвращаемое значение:
//   - Булево
//
Функция ПроверитьОтборПоСценариюТелефония(СценарийИнтереса) Экспорт
	
	Если Не ЗначениеЗаполнено(СценарийИнтереса) Тогда
		Возврат Истина;
	КонецЕсли;
	
	СценарииОбработки = Константы.сфпНастройкиТелефонии_СценарииОбработки.Получить().Получить();
	Если СценарииОбработки = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	ИсточникОбращения = Новый Структура;
	ИсточникОбращения.Вставить("CRM_ИсточникЛидов", Константы.сфпНастройкиТелефонии_ИсточникЛидов.Получить());
	ИсточникОбращения.Вставить("СценарииОбработки",
		 Константы.сфпНастройкиТелефонии_СценарииОбработки.Получить().Получить());
	
	Если ИсточникОбращения.CRM_ИсточникЛидов Тогда
		
		Для Каждого СтрокаПравил Из ИсточникОбращения.СценарииОбработки Цикл
			Если СтрокаПравил.Сценарий = СценарийИнтереса Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли