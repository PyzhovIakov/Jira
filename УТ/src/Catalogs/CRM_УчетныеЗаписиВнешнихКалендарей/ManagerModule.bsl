#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеАвторизации(УчетнаяЗапись) Экспорт
	
	ДанныеАвторизации = Новый Структура;
	Если Не УчетнаяЗапись.Пустая() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Пароль = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(УчетнаяЗапись, "Пароль");
		УстановитьПривилегированныйРежим(Ложь);
		
		ДанныеАвторизации.Вставить("Сервер", УчетнаяЗапись.Сервер);
		ДанныеАвторизации.Вставить("Логин", УчетнаяЗапись.Наименование);
		ДанныеАвторизации.Вставить("Пароль", Пароль);
		ДанныеАвторизации.Вставить("КорневойКаталог", "");
		ДанныеАвторизации.Вставить("КаталогКалендарей",	УчетнаяЗапись.КаталогКалендарей);
		ДанныеАвторизации.Вставить("УчетнаяЗаписьВнешнегоКалендаря", УчетнаяЗапись.Ссылка);
		
	КонецЕсли;
	
	Возврат ДанныеАвторизации;
	
КонецФункции

Функция УчетныеЗаписиПользователя(Пользователь) Экспорт
	Возврат НастройкиУчетныхЗаписей(Пользователь);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НастройкиУчетныхЗаписей(Пользователь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	CRM_УчетныеЗаписиВнешнихКалендарей.Наименование КАК Наименование,
	|	CRM_УчетныеЗаписиВнешнихКалендарей.Сервер КАК Сервер,
	|	CRM_УчетныеЗаписиВнешнихКалендарей.Сервис КАК Сервис,
	|	CRM_УчетныеЗаписиВнешнихКалендарей.Статус КАК Статус,
	|	CRM_УчетныеЗаписиВнешнихКалендарей.Код КАК Код,
	|	CRM_УчетныеЗаписиВнешнихКалендарей.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.CRM_УчетныеЗаписиВнешнихКалендарей КАК CRM_УчетныеЗаписиВнешнихКалендарей
	|ГДЕ
	|	CRM_УчетныеЗаписиВнешнихКалендарей.Пользователь = &Пользователь
	|	И CRM_УчетныеЗаписиВнешнихКалендарей.Статус = ИСТИНА
	|	И CRM_УчетныеЗаписиВнешнихКалендарей.ПометкаУдаления = ЛОЖЬ");
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли