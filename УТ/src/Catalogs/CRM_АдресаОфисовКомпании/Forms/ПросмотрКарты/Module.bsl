////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
&НаКлиенте
Перем НаименованиеЭлемента;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	Карта = CRM_РаботаСКартамиСервер.ПолучитьОсновнуюКартуДляСистемы();
	ИспользованиеWebKit = Истина;
	Если Не Карта = Неопределено Тогда
		ИсточникКарты = Карта;
	Иначе			
		Отказ = Истина;
	КонецЕсли;
	
	Если Параметры.Режим = "ДляПросмотра" Тогда
		Режим = "ViewMarker";		
	ИначеЕсли Параметры.Режим = "ДляУказанияКоординат" Тогда
		Режим = "LabelEditor";
	Иначе
		ВызватьИсключение(НСтр("ru=' Неизвестный режим окрытия карты: '") + Параметры.Режим);
	КонецЕсли;
	
	Если Параметры.Свойство("СписокСсылок") Тогда
		СписокСсылок = Параметры.СписокСсылок;
		Маркер = Новый Структура("Широта, Долгота, Заголовок");
		Если СписокСсылок.Количество() > 0 Тогда
			Маркер.Широта  = СписокСсылок[0].Значение.Широта;
			Маркер.Долгота = СписокСсылок[0].Значение.Долгота;
		Иначе 
			ВызватьИсключение(НСтр("ru='Переданы не все параметры для открытия карты!'"));
		КонецЕсли;
	Иначе	
		ОбъектСсылка = Параметры.ОбъектСсылка;
		
		Подвал = "";
		Адрес = ОбъектСсылка.ПредставлениеАдреса;
		
		Маркер = Новый Структура("Широта, Долгота, Заголовок");
		Если Параметры.Свойство("Широта") И Параметры.Свойство("Долгота") Тогда
			Маркер.Широта  = Параметры.Широта;
			Маркер.Долгота = Параметры.Долгота;
		ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.CRM_АдресаОфисовКомпании") Тогда
			Маркер.Широта  = ОбъектСсылка.Широта;
			Маркер.Долгота = ОбъектСсылка.Долгота;
		Иначе 
			ВызватьИсключение(НСтр("ru='Переданы не все параметры для открытия карты!'"));
		КонецЕсли;
		
		Если Параметры.Свойство("Наименование") Тогда
			Наименование = Параметры.Наименование;
		Иначе
			Наименование = ОбъектСсылка.Наименование;
		КонецЕсли;
		
		Информация = Информация + CRM_РаботаСКартамиКлиентСервер.ПредставениеКоординат(Маркер.Широта, Маркер.Долгота) + ". ";
		Элементы.ДекорацияИнформация.Заголовок = Информация;
		
		ВызовДляКарты = Истина;
		
		Если Режим = "ViewMarker" Тогда
			Заг = ?(ВызовДляКарты, НСтр("ru='Центр карты'"), НСтр("ru='Просмотр карты'"));
			Если Не ПустаяСтрока(Наименование) Тогда
				Заг = Заг + " - " + Наименование;
			КонецЕсли;
			Заголовок = Заг; // заголовок формы
			Маркер.Заголовок = ?(ВызовДляКарты, Заголовок, Наименование); // заголовок маркера
		ИначеЕсли Режим = "LabelEditor" Тогда
			Заг = НСтр("ru='Выбор места на карте'");
			Если Не ПустаяСтрока(Наименование) Тогда
				Заг = Заг + " - " + Наименование;
			КонецЕсли;
			Заголовок = Заг; // заголовок формы
			Маркер.Заголовок = Наименование; // заголовок маркера
		Иначе
			ВызватьИсключение(НСтр("ru='Неизвестный режим окрытия карты: '") + Режим);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Режим = "LabelEditor" Тогда
		Элементы.ФормаКнопкаСохранить.КнопкаПоУмолчанию = Истина;
		Элементы.ФормаКнопкаРежим.Видимость = Ложь;
	Иначе
		Элементы.ФормаКнопкаСохранить.Видимость = Ложь;
	КонецЕсли;
	
	Если КартаЦентр = Неопределено Тогда
		КартаЦентр = CRM_РаботаСКартамиСервер.СохранитьЦентрКарты(CRM_РаботаСКартамиКлиент.ПолучитьКоординатыПользователя());
	КонецЕсли;
	
	Если Маркер.Широта = 0 И Маркер.Долгота = 0 Тогда		
		СтррКоординаты = КартаЦентр;
		Если ТипЗнч(СтррКоординаты) = Тип("Структура") Тогда
			Маркер.Широта  = СтррКоординаты.Широта;
			Маркер.Долгота = СтррКоординаты.Долгота;
		КонецЕсли;
	КонецЕсли;
	
	КартаОбновитьТекущийМакет();
	НаименованиеЭлемента = "ПолеБраузераWebKit";
КонецПроцедуры

&НаКлиенте
Процедура ПолеБраузераДокументСформирован(Элемент)	
	CRM_РаботаСКартамиКлиент.ПолеБраузераДокументСформирован(ИспользованиеWebKit, Элемент, ИсточникКарты, Режим);
	CRM_РаботаСКартамиКлиент.Таймер_Включить("ДобавитьМаркер", ЭтотОбъект);	
КонецПроцедуры

&НаКлиенте
Процедура ПолеБраузераПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	Если Режим = "LabelEditor" Тогда
		Если ИспользованиеWebKit Тогда
			СтрокаСКоординатами = ДанныеСобытия.Document.getElementById("СoordinatesСenter").innerHTML; 	
		Иначе
			Документ = Элементы.ПолеБраузера.Document;
			HTMLWindow = ?(Документ.parentWindow <> Неопределено, Документ.parentWindow, Документ.defaultView);
			
			#Если ВебКлиент Тогда
				СтрокаСКоординатами = Элементы.ПолеБраузера.Document.getElementById("СoordinatesСenter").innerHTML;
			#Иначе 
				СтрокаСКоординатами = HTMLWindow.name;
			#КонецЕсли 
		КонецЕсли;
	
		Если СтрНайти(СтрокаСКоординатами, "LabelEditor_") <> 0 Тогда  // Координаты получены
			СтрокаСКоординатами = СтрЗаменить(СтрокаСКоординатами, "LabelEditor_", "");
			КоординатыОбъектаИзменены(СтрокаСКоординатами);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные

&НаКлиенте
Функция КоординатыОбъектаИзменены(СтрокаСКоординатами)
	МассивКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСКоординатами, ",");
	
	Если МассивКоординат.Количество() <> 2 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Маркер.Вставить("НовШирота",  CRM_РаботаСКартамиКлиентСервер.СтрокуВЧисло(МассивКоординат[0]));
	Маркер.Вставить("НовДолгота", CRM_РаботаСКартамиКлиентСервер.СтрокуВЧисло(МассивКоординат[1]));
	Модифицированность = Истина;
	СтруктураКоординат = Новый Структура;
	СтруктураКоординат.Вставить("Долгота", Маркер.НовДолгота);
	СтруктураКоординат.Вставить("Широта", Маркер.НовШирота);
	Адрес = CRM_РаботаСКартамиСервер.ПолучитьАдресПоКоординатамОтВебСервиса(СтруктураКоординат);
	Элементы.ДекорацияИнформация.Заголовок = НСтр("ru='Новые координаты - '")
		+ CRM_РаботаСКартамиКлиентСервер.ПредставениеКоординат(Маркер.НовШирота, Маркер.НовДолгота);
		
	Возврат Истина;
КонецФункции

// Функция возвращает Истина, если координаты объекта были изменены на карте с оболочкой OpenLayers.
// Проблема с оболочкой OpenLayers - не срабатывает событие ПриНажатии() для объекта ПолеHTMLДокумента.
&НаКлиенте
Функция КоординатыИзмененыВOpenLayers()
	Если Режим <> "LabelEditor" Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		Документ = Элементы.ПолеБраузера.Document;
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	ОкноБраузера = Документ.parentWindow; // IE
    Если ОкноБраузера = Неопределено Тогда
        ОкноБраузера = Документ.defaultView; // Прочие браузеры
	КонецЕсли;
	
	СтрокаСКоординатами = ОкноБраузера.name;
	Если СтрНайти(СтрокаСКоординатами, "LabelEditor_OSM_") = 0 Тогда
		Возврат Ложь; // Координаты не получены
	КонецЕсли;
	СтрокаСКоординатами = СтрЗаменить(СтрокаСКоординатами, "LabelEditor_OSM_", "");
	
	Возврат КоординатыОбъектаИзменены(СтрокаСКоординатами);
КонецФункции

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
	Модифицированность = Ложь;
	Результат = Новый Структура("Широта, Долгота, Адрес", Маркер.НовШирота, Маркер.НовДолгота, Адрес);
	ОповеститьОВыборе(Результат);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВопросаПользователю(Результат, Параметр) Экспорт
	
	//апОпцииФормК.ПроверитьКодВозвратаДиалога(Результат);

	Если Параметр = "ЗаписатьИзменения" Тогда
		Если Результат = КодВозвратаДиалога.Да Тогда
			ЗаписатьИЗакрыть();
		ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
			Модифицированность = Ложь;
			Закрыть();
		КонецЕсли;
	ИначеЕсли Параметр = "..." Тогда 

	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ИзменитьИсточникКарты()
	
	КартаОбновитьТекущийМакет();	
	
КонецПроцедуры
        
&НаСервере
Функция ПолучитьОписаниеМетки(АдресСсылка)
	Возврат Справочники.CRM_АдресаОфисовКомпании.ПолучитьОписаниеМетки(АдресСсылка);
КонецФункции	

&НаКлиенте
Процедура КартаДобавитьМаркер(УстановитьТаймер = Ложь)
	Если СписокСсылок.Количество() > 0 Тогда
		Для Каждого АдресДляВывода Из СписокСсылок Цикл
			ОписаниеМетки =  ПолучитьОписаниеМетки(АдресДляВывода.Значение);
			Если ОписаниеМетки.Широта = 0 И ОписаниеМетки.Долгота = 0 Тогда
				Возврат;
			КонецЕсли;
			
			Попытка 
				
				Документ 		= Элементы[НаименованиеЭлемента].Document;    // ПолеБраузераWebKit
				ОкноБраузера 	= Документ.parentWindow; // IE
				Если ОкноБраузера = Неопределено Тогда
					ОкноБраузера = Документ.defaultView; // Прочие браузеры
				КонецЕсли;
				
				#Если ВебКлиент Тогда
					ОкноБраузера.name = CRM_РаботаСКартамиСервер.СформироватьПараметрРежимРедактора(Режим, ИсточникКарты);
				#КонецЕсли
				ОкноБраузера.setMarker(ОписаниеМетки.Широта, ОписаниеМетки.Долгота, ОписаниеМетки.Заголовок,
					 ОписаниеМетки.Описание, ОписаниеМетки.Подвал, ОписаниеМетки.Подсказка,
					 Ложь);
				
			Исключение
				
				Если УстановитьТаймер Тогда 
					CRM_РаботаСКартамиКлиент.Таймер_Включить("ДобавитьМаркер", ЭтотОбъект);	
				Иначе	
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При построении карты произошла ошибка. Возможно,
						| карта не загружена'"));	
				КонецЕсли;	
				
			КонецПопытки;
			
		КонецЦикла;	
		Маркер = Новый Структура("Широта, Долгота, Заголовок");
		Маркер.Широта = ОписаниеМетки.Широта;
		Маркер.Долгота = ОписаниеМетки.Долгота;
		КартаПерейтиКМаркеру(Ложь);
	Иначе	
		ОписаниеМетки =  ПолучитьОписаниеМетки(ОбъектСсылка);
		Если ОписаниеМетки.Широта = 0 И ОписаниеМетки.Долгота = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Попытка 
			
			Документ 		= Элементы[НаименованиеЭлемента].Document;    // ПолеБраузераWebKit
			ОкноБраузера 	= Документ.parentWindow; // IE
			Если ОкноБраузера = Неопределено Тогда
				ОкноБраузера = Документ.defaultView; // Прочие браузеры
			КонецЕсли;
			
			#Если ВебКлиент Тогда
				ОкноБраузера.name = CRM_РаботаСКартамиСервер.СформироватьПараметрРежимРедактора(Режим, ИсточникКарты);
			#КонецЕсли
			ОкноБраузера.setMarker(ОписаниеМетки.Широта, ОписаниеМетки.Долгота, ОписаниеМетки.Заголовок,
				 ОписаниеМетки.Описание, ОписаниеМетки.Подвал, ОписаниеМетки.Подсказка,
				 Истина);
			
		Исключение
			
			Если УстановитьТаймер Тогда 
				CRM_РаботаСКартамиКлиент.Таймер_Включить("ДобавитьМаркер", ЭтотОбъект);	
			Иначе	
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При построении карты произошла ошибка. Возможно,
					| карта не загружена'"));	
			КонецЕсли;	
			
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КартаПерейтиКМаркеру(УстановитьТаймер = Ложь)
	
	Попытка
		Документ = Элементы[НаименованиеЭлемента].Document;
	Исключение
		Возврат;
	КонецПопытки;
	
	Если Режим = "LabelEditor" И Маркер.Свойство("НовШирота")
		 И Маркер.НовШирота <> 0 Тогда // это означает, что маркер редактировался 
		Широта 	= Маркер.НовШирота;
		Долгота = Маркер.НовДолгота;
	Иначе
		Широта 	= Маркер.Широта;
		Долгота = Маркер.Долгота;
	КонецЕсли;
	
	Если Широта = 0 И Долгота = 0 Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Координаты не указаны. Сначала укажите объект на карте.'"));
		Возврат;
	КонецЕсли;
	
	ОкноБраузера = Документ.parentWindow; // IE
    Если ОкноБраузера = Неопределено Тогда
        ОкноБраузера = Документ.defaultView; // Прочие браузеры
	КонецЕсли;
	
	Если Не УстановитьТаймер Тогда 
		//Документ.parentWindow.GoToMarker(Широта, Долгота);
		ОкноБраузера.GoToMarker(Широта, Долгота);
	Иначе
		Попытка
			//Документ.parentWindow.GoToMarker(Широта, Долгота);
			ОкноБраузера.GoToMarker(Широта, Долгота);
		Исключение
			CRM_РаботаСКартамиКлиент.Таймер_Включить("ПерейтиКМаркеру", ЭтотОбъект);	
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Карта обновить текущий макет
//
&НаКлиенте
Процедура КартаОбновитьТекущийМакет()
	// Нужна промежуточная переменная, потому что если сразу записывать в ТекстСкрипта, то в платформе ниже 8.3.14 карта не подгружается
	ТекстМакета	= CRM_РаботаСКартамиСервер.ПолучитьТекстМакета(ИсточникКарты);	
	
	ТекстСкрипта = ТекстМакета;
	CRM_РаботаСКартамиКлиент.ЗаменитьСтрокуСкрипта(ТекстСкрипта, ИсточникКарты, Маркер.Широта, Маркер.Долгота, Режим);	
	
КонецПроцедуры

// Обработчик таймера. Запускается каждую секунду после включения таймера в процедурой Таймер_Включить()
&НаКлиенте
Процедура Таймер_Обработчик()
	Если ОбщегоНазначенияКлиент.ДатаСеанса() - ТаймерОжидания.Время > 5 Тогда 
		// Время с момента постановки события в очередь превысило 5 секунд - прекращаем обрабатывать все события.
		CRM_РаботаСКартамиКлиент.Таймер_Выключить(Истина, ЭтотОбъект);
	КонецЕсли;
	Событие =  ТаймерОжидания.Очередь[0];
	Попытка
		Если Событие = "ДобавитьМаркер" Тогда
			КартаДобавитьМаркер();
		ИначеЕсли Событие = "ПерейтиКМаркеру" Тогда
			КартаПерейтиКМаркеру();
		КонецЕсли;
	Исключение
		Возврат;
	КонецПопытки;
	CRM_РаботаСКартамиКлиент.Таймер_Выключить(Ложь, ЭтотОбъект);
КонецПроцедуры

#Область ОбработчикиКомандФормы

// Включение/выключение формы в режим редактора метки. Возможно только если изначально форма была открытя только для просмотра.
&НаКлиенте
Процедура КомандаФормаРежимРедактора(Команда)
	
	Режим = ?(Режим = "ViewMarker", "LabelEditor", "ViewMarker");
	
	Если Режим = "LabelEditor" Тогда
		Элементы.ФормаКнопкаСохранить.Видимость = Истина;
		Элементы.ФормаКнопкаСохранить.КнопкаПоУмолчанию = Истина;
	Иначе // Режим "ViewMarker"
		Элементы.ФормаКнопкаСохранить.Видимость = Ложь;
		Элементы.ФормаКнопкаЗакрыть.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	Элементы.ФормаКнопкаРежим.Пометка = (Режим = "LabelEditor");
	
	Документ = Элементы.ПолеБраузера.Document;	
	
	ИзменитьИсточникКарты();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаФормаЗакрыть(Команда)
	Если Режим = "LabelEditor" Тогда
		Если НЕ ИспользованиеWebKit Тогда
			КоординатыИзмененыВOpenLayers();
		КонецЕсли;
		Если Модифицированность Тогда
			Текст = НСтр("ru = 'Координаты были изменены, но не записаны. Перенести изменения?'");
			Оповещение = Новый ОписаниеОповещения("ОбработкаВопросаПользователю", ЭтотОбъект, "ЗаписатьИзменения");
			ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНетОтмена);
			Возврат; // закрыте формы только после получения ответа от пользователя в процедуре ОбработкаВопросаПользователю()
		КонецЕсли;
	КонецЕсли;
	Закрыть();		
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейтиКМаркеру(Команда)
	КартаПерейтиКМаркеру(Истина);
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписатьКоординаты(Команда)
	Если НЕ ИспользованиеWebKit Тогда
		КоординатыИзмененыВOpenLayers();
	КонецЕсли;
	Если Не Маркер.Свойство("НовШирота") Или Не Маркер.Свойство("НовДолгота") Тогда
		Текст = НСтр("ru='Новые координаты не были указаны. Укажите точку на карте.'"); 
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Неопределено, Текст, 
			РежимДиалогаВопрос.ОК);
		Возврат;
	Иначе
		ЗаписатьИЗакрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновитьКарту(Команда)
	
	КартаОбновитьТекущийМакет();
	
КонецПроцедуры

// ОбработчикиКомандФормы
#КонецОбласти
