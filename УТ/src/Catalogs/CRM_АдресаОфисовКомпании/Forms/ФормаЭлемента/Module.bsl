
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		Объект.Широта = ВыбранноеЗначение.Широта;
		Объект.Долгота = ВыбранноеЗначение.Долгота;
		Объект.ПредставлениеАдреса = ВыбранноеЗначение.Адрес;
		Модифицированность = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	Если Объект.Владелец.Пустая() Тогда
		Если Параметры.Свойство("Владелец") Тогда
			Объект.Владелец = Параметры.Владелец;
		Иначе
			Отказ = Истина;
		КонецЕсли;	
	КонецЕсли;	
	Если Объект.Широта = 0 И Объект.Долгота = 0 Тогда
		Объект.Широта = 55.751574;
		Объект.Долгота = 37.573856;
	КонецЕсли;	
	Карта = CRM_РаботаСКартамиСервер.ПолучитьОсновнуюКартуДляСистемы();
	Если Не Карта = Неопределено Тогда
		ИсточникКарты = Карта;
	Иначе			
		Отказ = Истина;
	КонецЕсли;
	ЗаполнитьСписокАдресов();
	ВызовДляКарты = Истина;
	ПроверитьКлючAPIНкаСервере();	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьКарту();
	НаименованиеЭлемента = "ПолеБраузераWebKit";
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбновитьКарту()
	ТекстСкрипта	= CRM_РаботаСКартамиСервер.ПолучитьТекстМакета(ИсточникКарты);	
	CRM_РаботаСКартамиКлиент.ЗаменитьСтрокуСкрипта(ТекстСкрипта, ИсточникКарты, Объект.Широта,
		 Объект.Долгота,
		 "LabelEditor");	
КонецПроцедуры

&НаКлиенте
Процедура ПолеБраузераДокументСформирован(Элемент)	
	CRM_РаботаСКартамиКлиент.ПолеБраузераДокументСформирован(Истина, Элемент, ИсточникКарты, "LabelEditor");
	CRM_РаботаСКартамиКлиент.Таймер_Включить("ДобавитьМаркер", ЭтотОбъект);	
КонецПроцедуры

&НаКлиенте
Процедура ПолеБраузераПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	СтрокаСКоординатами = ДанныеСобытия.Document.getElementById("СoordinatesСenter").innerHTML; 	
	
	Если СтрНайти(СтрокаСКоординатами, "LabelEditor_") <> 0 Тогда  // Координаты получены
		СтрокаСКоординатами = СтрЗаменить(СтрокаСКоординатами, "LabelEditor_", "");
		СтрокаСКоординатами = СтрЗаменить(СтрокаСКоординатами, "_initEnd", "");
		КоординатыОбъектаИзменены(СтрокаСКоординатами);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция КоординатыОбъектаИзменены(СтрокаСКоординатами)
	МассивКоординат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСКоординатами, ",");
	
	Если МассивКоординат.Количество() <> 2 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Объект.Широта = Число(МассивКоординат[0]);
	Объект.Долгота = Число(МассивКоординат[1]);
	Модифицированность = Истина;
	СтруктураКоординат = Новый Структура;
	СтруктураКоординат.Вставить("Долгота", Объект.Долгота);
	СтруктураКоординат.Вставить("Широта", Объект.Широта);
	Объект.ПредставлениеАдреса = CRM_РаботаСКартамиСервер.ПолучитьАдресПоКоординатамОтВебСервиса(СтруктураКоординат);
	//Элементы.ДекорацияИнформация.Заголовок = НСтр("ru='Новые координаты - '")
	//	+ CRM_РаботаСКартамиКлиентСервер.ПредставениеКоординат(Объект.Широта, Объект.Долгота);
		
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура Таймер_Обработчик()
	Если ОбщегоНазначенияКлиент.ДатаСеанса() - ТаймерОжидания.Время > 5 Тогда 
		// Время с момента постановки события в очередь превысило 5 секунд - прекращаем обрабатывать все события.
		CRM_РаботаСКартамиКлиент.Таймер_Выключить(Истина, ЭтотОбъект);
	КонецЕсли;
	Событие =  ТаймерОжидания.Очередь[0];
	Попытка
		Если Событие = "ДобавитьМаркер" Тогда
			КартаДобавитьМаркер();
		ИначеЕсли Событие = "ПерейтиКМаркеру" Тогда
			КартаПерейтиКМаркеру();
		КонецЕсли;
	Исключение
		Возврат;
	КонецПопытки;
	CRM_РаботаСКартамиКлиент.Таймер_Выключить(Ложь, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КартаДобавитьМаркер(УстановитьТаймер = Ложь)
	
	ОписаниеМетки =  ПолучитьОписаниеМетки();
	Если ОписаниеМетки.Широта = 0 И ОписаниеМетки.Долгота = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Попытка 
		
		Документ 		= Элементы.ПолеБраузераWebKit.Document;    // ПолеБраузераWebKit
		ОкноБраузера 	= Документ.parentWindow; // IE
		Если ОкноБраузера = Неопределено Тогда
			ОкноБраузера = Документ.defaultView; // Прочие браузеры
		КонецЕсли;
		
		#Если ВебКлиент Тогда
			ОкноБраузера.name = CRM_РаботаСКартамиСервер.СформироватьПараметрРежимРедактора(Режим, ИсточникКарты);
		#КонецЕсли
		ОкноБраузера.setMarker(ОписаниеМетки.Широта, ОписаниеМетки.Долгота, ОписаниеМетки.Заголовок,
			 ОписаниеМетки.Описание, ОписаниеМетки.Подвал, ОписаниеМетки.Подсказка,
			 Истина);
		КартаПерейтиКМаркеру(Ложь);
	Исключение
		
		Если УстановитьТаймер Тогда 
			CRM_РаботаСКартамиКлиент.Таймер_Включить("ДобавитьМаркер", ЭтотОбъект);	
		Иначе	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При построении карты произошла ошибка. Возможно,
				| карта не загружена'"));	
		КонецЕсли;	
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОписаниеМетки()
	ОписаниеМетки = Новый Структура;
	ОписаниеМетки.Вставить("Заголовок", "<a href = ""#"">" + Объект.Владелец.Наименование + "</a>");
	ОписаниеМетки.Вставить("Описание", "<b>" + Объект.Наименование + "</b>");
	ОписаниеМетки.Вставить("Подвал", "<b>Адрес: </b>" + Объект.ПредставлениеАдреса);
	ОписаниеМетки.Вставить("Подсказка");
	ОписаниеМетки.Вставить("Широта", ?(Объект.Широта = 0, 55.751574, Объект.Широта));
	ОписаниеМетки.Вставить("Долгота", ?(Объект.Долгота = 0, 37.573856, Объект.Долгота));
	ОписаниеМетки.Вставить("Наименование", Объект.Наименование);
	ОписаниеМетки.Вставить("Адрес", Объект.ПредставлениеАдреса);
	Возврат ОписаниеМетки;
КонецФункции

&НаКлиенте
Процедура КартаПерейтиКМаркеру(УстановитьТаймер = Ложь)
	
	Попытка
		Документ = Элементы.ПолеБраузераWebKit.Document;
	Исключение
		Возврат;
	КонецПопытки;
	
	Если Объект.Широта = 0 И Объект.Долгота = 0 Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru='Координаты не указаны. Сначала укажите объект на карте.'"));
		Возврат;
	КонецЕсли;
	
	ОкноБраузера = Документ.parentWindow; // IE
    Если ОкноБраузера = Неопределено Тогда
        ОкноБраузера = Документ.defaultView; // Прочие браузеры
	КонецЕсли;
	
	Если Не УстановитьТаймер Тогда 
		ОкноБраузера.GoToMarker(Объект.Широта, Объект.Долгота);
	Иначе
		Попытка
			ОкноБраузера.GoToMarker(Объект.Широта, Объект.Долгота);
		Исключение
			CRM_РаботаСКартамиКлиент.Таймер_Включить("ПерейтиКМаркеру", ЭтотОбъект);	
		КонецПопытки;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НайтиНаКарте(Команда)
	СтруктураАдреса = CRM_РаботаСКартамиСервер.ПолучтьКоординатыОтВебСервиса(Объект.ПредставлениеАдреса, Истина);
	Если ТипЗнч(СтруктураАдреса) = Тип("Структура") Тогда
		тмпКоординаты = СтруктураАдреса.Координаты;
		Поз = СтрНайти(тмпКоординаты, " ");
		тмпДолгота = Лев(тмпКоординаты, Поз - 1);
		тмпШирота = СтрЗаменить(тмпКоординаты, тмпДолгота + " ", "");
		Объект.Долгота = Число(тмпДолгота);
		Объект.Широта = Число(тмпШирота);
		Модифицированность = Истина;
		КартаДобавитьМаркер();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоАдресам(Команда)
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораАдреса", ЭтотОбъект);
	ПоказатьВыборИзМеню(Оповещение, СписокАдресов, Элементы.Заполнить);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораАдреса(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Объект.ПредставлениеАдреса = Результат.Значение;
		СтруктураАдреса = CRM_РаботаСКартамиСервер.ПолучтьКоординатыОтВебСервиса(Объект.ПредставлениеАдреса, Истина);
		Если ТипЗнч(СтруктураАдреса) = Тип("Структура") Тогда
			тмпКоординаты = СтруктураАдреса.Координаты;
			Поз = СтрНайти(тмпКоординаты, " ");
			тмпДолгота = Лев(тмпКоординаты, Поз - 1);
			тмпШирота = СтрЗаменить(тмпКоординаты, тмпДолгота + " ", "");
			Объект.Долгота = Число(тмпДолгота);
			Объект.Широта = Число(тмпШирота);
			Модифицированность = Истина;
			КартаДобавитьМаркер();
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокАдресов()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	CRM_АдресаОфисовКомпании.ПредставлениеАдреса КАК ПредставлениеАдреса
	|ИЗ
	|	Справочник.CRM_АдресаОфисовКомпании КАК CRM_АдресаОфисовКомпании
	|ГДЕ
	|	CRM_АдресаОфисовКомпании.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Владелец", Объект.Владелец);
	ТабАдресов = Запрос.Выполнить().Выгрузить();
	СтруктураПоиска = Новый Структура("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);	
	МассивСтрок = Объект.Владелец.КонтактнаяИнформация.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаАдреса Из МассивСтрок Цикл
		Если ТабАдресов.Найти(СтрокаАдреса.Представление, "ПредставлениеАдреса") = Неопределено
			 И СписокАдресов.НайтиПоЗначению(СтрокаАдреса.Представление) = Неопределено Тогда
			СписокАдресов.Добавить(СтрокаАдреса.Представление);
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры	

&НаКлиенте
Процедура ПредставлениеАдресаПриИзменении(Элемент)
	СтруктураАдреса = CRM_РаботаСКартамиСервер.ПолучтьКоординатыОтВебСервиса(Объект.ПредставлениеАдреса, Истина);
	Если ТипЗнч(СтруктураАдреса) = Тип("Структура") Тогда
		Попытка
			тмпКоординаты = СтруктураАдреса.Координаты;
			Поз = СтрНайти(тмпКоординаты, " ");
			тмпДолгота = Лев(тмпКоординаты, Поз - 1);
			тмпШирота = СтрЗаменить(тмпКоординаты, тмпДолгота + " ", "");
			Объект.Долгота = Число(тмпДолгота);
			Объект.Широта = Число(тмпШирота);
			Модифицированность = Истина;
			ОбновитьКарту();
			КартаДобавитьМаркер();
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не удалось получить координаты по адресу!'");
			Сообщение.Сообщить();
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКлючAPIНажатие(Элемент)
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияФормыНастроек", ЭтотОбъект);
	ОткрытьФорму("Обработка.CRM_ПанельАдминистрированияCRM.Форма.РазделКлиенты",
		 Новый Структура("УстановитьФокус, ГруппаНастроек", "CRM_КлючAPIКарт", "ГруппаПланированиеВстреч"),
		 ЭтотОбъект, УникальныйИдентификатор, , ,
		 ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыНастроек(Результат, ДопПараметры) Экспорт
	ПроверитьКлючAPIНкаСервере();
КонецПроцедуры

&НаСервере
Процедура ПроверитьКлючAPIНкаСервере()
	КлючAPI = Константы.CRM_КлючAPIКарт.Получить();
	Если Не ЗначениеЗаполнено(КлючAPI) Тогда
		Элементы.ДекорацияКлючAPI.Видимость = Истина;
		Если НЕ РольДоступна("ПолныеПрава") Тогда
			Элементы.ДекорацияКлючAPI.Доступность = Ложь;
			Элементы.ДекорацияКлючAPI.Заголовок =
				НСтр("ru='Необходимо ввести ключ для доступа к API Яндекс.Карт. обратитесь к Администратору'");
		КонецЕсли;
	Иначе
		Элементы.ДекорацияКлючAPI.Видимость = Ложь;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти
