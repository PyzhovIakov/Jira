
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Попытка
		Владелец = Параметры.Отбор.Владелец;
		Элементы.ФормаПоказатьАдресаНаКарте.Видимость = Истина;
		АдресПоУмолчанию = Владелец.CRM_ОсновнойАдрес;
		УстановитьОформлениеПоУмолчанию(АдресПоУмолчанию);
	Исключение
		Элементы.ФормаПоказатьАдресаНаКарте.Видимость = Ложь;
		Элементы.ФормаСделатьОсновным.Видимость = Ложь;
	КонецПопытки;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьАдресаНаКарте(Команда)
	ПараметрыОткрытия = Новый Структура("СписокСсылок, Режим", ПолучитьСписокАдресов(), "ДляПросмотра");
	ПараметрыОткрытия.Вставить("ОткрытаПоСценарию", Истина);
	ОткрытьФорму("Справочник.CRM_АдресаОфисовКомпании.Форма.ПросмотрКарты", ПараметрыОткрытия,
		 ЭтотОбъект,
		 УникальныйИдентификатор);
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокАдресов()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	CRM_АдресаОфисовКомпании.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.CRM_АдресаОфисовКомпании КАК CRM_АдресаОфисовКомпании
	|ГДЕ
	|	CRM_АдресаОфисовКомпании.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Владелец", Владелец);
	СписокСсылок = Новый СписокЗначений;
	СписокСсылок.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	Возврат СписокСсылок; 
КонецФункции	

&НаКлиенте
Процедура СделатьОсновным(Команда)
	ТекДанные = Элементы.Список.ТекущаяСтрока;
	Если ТекДанные <> Неопределено Тогда
		ИзменитьКарточкуОбъектаВладелецаИПоменятьОформлениеСписка(Владелец, ТекДанные);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ИзменитьКарточкуОбъектаВладелецаИПоменятьОформлениеСписка(ВладелецСсылка, НовыйАдресПоУмолчанию)
	
	ОбъектВладелец	= ВладелецСсылка.ПолучитьОбъект();
	ОбъектВладелец.CRM_ОсновнойАдрес = НовыйАдресПоУмолчанию;
	
	Попытка
		
		ОбъектВладелец.Записать();
		УстановитьОформлениеПоУмолчанию(НовыйАдресПоУмолчанию);
		
	Исключение
		
		ТекстСообщения = НСтр("ru='Не удалось поменять основной адрес в карточке владельца.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеПоУмолчанию(АдресПоУмолчанию)
	
	ЭлементыУО = Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы;
	МаксИндекс = ЭлементыУО.Количество() - 1;
	Для Ном = 0 По МаксИндекс Цикл
		ЭлементУсловногоОформления = ЭлементыУО[МаксИндекс - Ном];
		Если ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный" Тогда
			Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы.Удалить(ЭлементУсловногоОформления);
		КонецЕсли;
	КонецЦикла;
	
	ЭлементыУсловногоОформленияСписка	= Список.КомпоновщикНастроек.ФиксированныеНастройки.УсловноеОформление.Элементы;
	ЭлементУсловногоОформления			= ЭлементыУсловногоОформленияСписка.Добавить();
	
	ЭлементОтбора 						= ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение 		= Новый ПолеКомпоновкиДанных("Ссылка");
	ЭлементОтбора.ВидСравнения 			= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение 		= АдресПоУмолчанию;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(, , Истина));
	
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементУсловногоОформления.ИдентификаторПользовательскойНастройки = "Предустановленный";
	ЭлементУсловногоОформления.Представление = "Адреса по умолчанию";  
	
КонецПроцедуры

#КонецОбласти
