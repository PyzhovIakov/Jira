
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УчетнаяЗапись = Параметры.Отбор.Владелец;
	СписокВыбранныхПапок = Параметры.Отбор.ВыбранныеПапки;
	
	Заголовок = Заголовок + " (" + УчетнаяЗапись + ")";
	
	ЗаполнитьДеревоПапок(СписокВыбранныхПапок);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПапок(СписокВыбранныхПапок)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыбранныеПапки.Папка КАК Папка
		|ПОМЕСТИТЬ ВыбранныеПапки
		|ИЗ
		|	&ВыбранныеПапки КАК ВыбранныеПапки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПапкиЭлектронныхПисем.Ссылка КАК Папка,
		|	ВЫБОР
		|		КОГДА ВыбранныеПапки.Папка ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Выбрана
		|ИЗ
		|	Справочник.ПапкиЭлектронныхПисем КАК ПапкиЭлектронныхПисем
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВыбранныеПапки КАК ВыбранныеПапки
		|		ПО ПапкиЭлектронныхПисем.Ссылка = ВыбранныеПапки.Папка
		|ГДЕ
		|	ПапкиЭлектронныхПисем.Владелец = &Владелец
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПапкиЭлектронныхПисем.РеквизитДопУпорядочивания,
		|	Папка ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("Владелец", УчетнаяЗапись);
	
	ТаблицаВыбранныеПапки = Новый ТаблицаЗначений;
	ТаблицаВыбранныеПапки.Колонки.Добавить("Папка", Новый ОписаниеТипов("СправочникСсылка.ПапкиЭлектронныхПисем"));
	Для Каждого ЭлементСписка Из СписокВыбранныхПапок Цикл
		НС = ТаблицаВыбранныеПапки.Добавить();
		НС.Папка = ЭлементСписка.Значение;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ВыбранныеПапки", ТаблицаВыбранныеПапки);
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПапок");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ВыбранныеПапки = Новый Массив;
	КомандаВыбратьНаСервере(ВыбранныеПапки);
	
	Закрыть(ВыбранныеПапки);
	
КонецПроцедуры

&НаСервере
Процедура КомандаВыбратьНаСервере(ВыбранныеПапки)
	
	Дерево = РеквизитФормыВЗначение("ДеревоПапок");
	ВыбранныеПапкиДерева(ВыбранныеПапки, Дерево);
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПапок");
	
КонецПроцедуры

&НаСервере
Процедура ВыбранныеПапкиДерева(ВыбранныеПапки, Дерево)
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		Если СтрокаДерева.Выбрана Тогда
			ВыбранныеПапки.Добавить(СтрокаДерева.Папка);
		КонецЕсли;
		ВыбранныеПапкиДерева(ВыбранныеПапки, СтрокаДерева);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СнятьВсеФлаги(Дерево)
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		СтрокаДерева.Выбрана = Ложь;
		СнятьВсеФлаги(СтрокаДерева);
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаСнятьВсе(Команда)
	
	КомандаСнятьВсеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура КомандаСнятьВсеНаСервере()
	
	Дерево = РеквизитФормыВЗначение("ДеревоПапок");
	СнятьВсеФлаги(Дерево);
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПапок");
	
КонецПроцедуры

#КонецОбласти