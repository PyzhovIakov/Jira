#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Наименование <> Ссылка.Наименование 
		ИЛИ Клиент <> Ссылка.Клиент
		ИЛИ Поставщик <> Ссылка.Поставщик
		ИЛИ Конкурент <> Ссылка.Конкурент
		ИЛИ ПрочиеОтношения <> Ссылка.ПрочиеОтношения Тогда
		ДополнительныеСвойства.Вставить("ПерезаписатьПартнеров");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПерезаписатьПартнеров") Тогда
		ПараметрыПроцедуры = Новый Структура("ТипОтношений", Ссылка);
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru='Перезапись партнеров,
			| при изменении типа отношений.';en='Overwriting partners, when changing relation type.'") 
			+ Наименование;
		
		ДлительныеОперации.ВыполнитьВФоне("Справочники.CRM_ТипыОтношений.ПерезаписатьПартнеровСТипом",
			 ПараметрыПроцедуры,
			 ПараметрыВыполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли
