
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		МодульВерсионированиеОбъектов = ОбщегоНазначения.ОбщийМодуль("ВерсионированиеОбъектов");
		МодульВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.СтатусРешения	= Перечисления.CRM_СтатусыРешений.НаРассмотрении;
		Объект.Автор			= Пользователи.ТекущийПользователь();
		СтрОтвета = Объект.Ответы.Добавить();
		СтрОтвета.ИДСтроки = СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", "_");
		СтрОтвета.СтатусОтвета = Перечисления.CRM_СтатусыРешений.НаРассмотрении;
		
		Параметры.Свойство("Родитель", Объект.Родитель);
		
		Если Параметры.Свойство("ТекстОтвета") Тогда
			Объект.Наименование = Параметры.ТекстОтвета;
			
			СтрОтвета.НаименованиеОтвета = Объект.Наименование;
			Если Не ЗначениеЗаполнено(СтрОтвета.ВариантОтвета) Тогда
				СтрОтвета.ВариантОтвета = Объект.Наименование;
			КонецЕсли;
		КонецЕсли;

		Если Параметры.Свойство("ТекстВопроса") Тогда
			Объект.Наименование = Параметры.ТекстВопроса;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	СтрОтвета = ТекущийОбъект.Ответы[0];
	
	Если Не ЗначениеЗаполнено(СтрОтвета.ВариантОтвета) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнен текст ответа!'"), , "ВариантОтвета");
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.Родитель) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнена группа!'"), , "Объект.Родитель");
		Возврат;
	КонецЕсли;
	
	ДокументHTML = Взаимодействия.ПолучитьДокументHTMLИзОбычногоТекста(СтрОтвета.ВариантОтвета);
	СтрОтвета.ВариантОтветаHTML	= Взаимодействия.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	СтрОтвета.НаименованиеОтвета = Объект.Наименование;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.ЭтоНовый Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	CRM_РешенияПрисоединенныеФайлы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.CRM_РешенияПрисоединенныеФайлы КАК CRM_РешенияПрисоединенныеФайлы
		|ГДЕ
		|	CRM_РешенияПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла";
	
	Запрос.УстановитьПараметр("ВладелецФайла", ТекущийОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ЕстьВложения = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если Объект.Ответы.Количество() = 0 Тогда
		Объект.Ответы.Добавить();
	КонецЕсли;
	СтрОтвета = Объект.Ответы[0];
	Если Не ЗначениеЗаполнено(СтрОтвета.ВариантОтвета) Тогда
		СтрОтвета.ВариантОтвета = Объект.Наименование;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Параметры.CRM_КонтекстВызова = "ФормаБыстрыеОтветы" И Не Объект.Ссылка.Пустая() Тогда
		Если ЕстьВложения Тогда
			CRM_ЦентрМониторингаКлиент.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.АРМДиалоги.Удобство.БыстрыеОтветы.ЗаписьНовогоБыстрогоОтветаСФайлами");
		Иначе
			CRM_ЦентрМониторингаКлиент.ЗаписатьОперациюБизнесСтатистики("CRM_Статистика.АРМДиалоги.Удобство.БыстрыеОтветы.ЗаписьНовогоБыстрогоОтветаБезФайлов");
		КонецЕсли;
		
		Оповестить("ЗаписьНовогоОтветаВБазуЗнаний", Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
