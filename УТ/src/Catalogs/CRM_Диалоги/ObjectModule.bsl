#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Если Не ЗначениеЗаполнено(ДатаНачала) Тогда
			ДатаНачала = ТекущаяДатаСеанса();
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Автор) Тогда
			Автор = Пользователи.ТекущийПользователь();
		КонецЕсли;
		Статус = Перечисления.CRM_СтатусыДиалогов.Новый;
	Иначе
		Если ПометкаУдаления И Не Завершен Тогда
			Статус = Перечисления.CRM_СтатусыДиалогов.Закрыт;
			Справочники.CRM_Диалоги.ДобавитьДействие(
				ЭтотОбъект, Перечисления.CRM_ДействияСДиалогами.ЗавершенАвтоматически);
		КонецЕсли;
		
		Если Статус = Перечисления.CRM_СтатусыДиалогов.Закрыт И Статус <> Ссылка.Статус Тогда
			Если Не ЗначениеЗаполнено(ДатаЗавершения) Тогда
				ДатаЗавершения = ТекущаяДатаСеанса();
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Ответственный) Тогда
				Ответственный = Пользователи.ТекущийПользователь();
			КонецЕсли;
			Если Не Завершен Тогда
				Завершен = Истина;
			КонецЕсли;
		ИначеЕсли Статус = Перечисления.CRM_СтатусыДиалогов.ПринятВРаботу И Статус <> ссылка.Статус Тогда
			Если Не ЗначениеЗаполнено(ДатаПринятия) Тогда
				ДатаПринятия = ТекущаяДатаСеанса();
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Ответственный) Тогда
				Ответственный = Пользователи.ТекущийПользователь();
			КонецЕсли;
		ИначеЕсли Статус = Перечисления.CRM_СтатусыДиалогов.ПереданДляОзнакомления Тогда
			
		Иначе
			Если (ЗначениеЗаполнено(Ответственный) И Ответственный <> Ссылка.Ответственный)
				Или (Не ЗначениеЗаполнено(Ответственный) И ЗначениеЗаполнено(Ссылка.Ответственный)
					 И ЗначениеЗаполнено(CRM_РольОтветственного)) Тогда
				Статус = Перечисления.CRM_СтатусыДиалогов.ВозвращенВОчередь;
				НовДействие = ДействияСДиалогом.Добавить();
				НовДействие.Дата = ТекущаяДатаСеанса();
				НовДействие.Действие = Перечисления.CRM_ДействияСДиалогами.ВозвращенВОчередь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	CRM_КлиентыСервер.ПередЗаписьюОбъектаОбщегоЖурнала(ЭтотОбъект, Отказ, Неопределено, Неопределено);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	ДатаНачала = ТекущаяДатаСеанса();
	ДатаПринятия = ТекущаяДатаСеанса();
	ДатаЗавершения = Дата(1, 1, 1);
	Завершен = Ложь;
	Наименование = "";
	ДействияСДиалогом.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли
