#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьПредопределенныеЭлементы(Элемент = Неопределено) Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	CRM_ДействияЯзыковыхМоделей.Ссылка КАК Ссылка,
	                      |	CRM_ДействияЯзыковыхМоделей.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
	                      |ИЗ
	                      |	Справочник.CRM_ДействияЯзыковыхМоделей КАК CRM_ДействияЯзыковыхМоделей
	                      |ГДЕ
	                      |	НЕ CRM_ДействияЯзыковыхМоделей.ЭтоГруппа
	                      |	И CRM_ДействияЯзыковыхМоделей.Предопределенный
	                      |	И ИСТИНА");
	
	Если Элемент <> Неопределено Тогда
		Если Элемент.ЭтоГруппа Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИСТИНА", "CRM_ДействияЯзыковыхМоделей.Родитель = &Родитель");
			Запрос.УстановитьПараметр("Родитель", Элемент);
		Иначе 
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИСТИНА", "CRM_ДействияЯзыковыхМоделей.Ссылка = &Ссылка");
			Запрос.УстановитьПараметр("Ссылка", Элемент);
		КонецЕсли;
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТабДок = ПолучитьМакет("ПредопределенныеПромпты");
	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабДок.Область(1, 1,
		 ТабДок.ВысотаТаблицы,
		 ТабДок.ШиринаТаблицы));
	Построитель.Выполнить();
	ТаблицаДанных = Построитель.Результат.Выгрузить();
	
	Пока Выборка.Следующий() Цикл
		Строка = ТаблицаДанных.Найти(Выборка.ИмяПредопределенныхДанных, "ИмяПредопределенного");
		Если Строка <> Неопределено Тогда
			Действие = Выборка.Ссылка.ПолучитьОбъект();
			Действие.СистемныйПромпт = Строка.СистемныйПромпт;
			Действие.ИзменяетТекст = Строка.ИзменяетТекст;
			Действие.Используется = Истина;
			Действие.Иконка = Строка.Иконка;
			Если Действие.РеквизитДопУпорядочивания = 0 Тогда
				Действие.РеквизитДопУпорядочивания = Число(Действие.Код);
			КонецЕсли;
			Действие.Записать();
		КонецЕсли;
	КонецЦикла
	
КонецПроцедуры

Функция ПромптПредопределенного(ИмяПредопределенного) Экспорт

	ТабДок = ПолучитьМакет("ПредопределенныеПромпты");
	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабДок.Область(1, 1,
		 ТабДок.ВысотаТаблицы,
		 ТабДок.ШиринаТаблицы));
	Построитель.Выполнить();
	ТаблицаДанных = Построитель.Результат.Выгрузить();

	Строка = ТаблицаДанных.Найти(ИмяПредопределенного, "ИмяПредопределенного");
	Если Строка <> Неопределено Тогда
		Возврат Строка.СистемныйПромпт;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
