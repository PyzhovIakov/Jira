#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция УсловиеВыполнено(Условие, Обращение) Экспорт
	
	Если Условие = БезУсловия Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Условие.ВидУсловия = Перечисления.CRM_ВидыУсловийОбработкиОбращений.СписокСлов Тогда
		Возврат УсловиеНаСписокСловВыполнено(Условие, Обращение);
	ИначеЕсли Условие.ВидУсловия = Перечисления.CRM_ВидыУсловийОбработкиОбращений.МеткаПомощника Тогда
		Возврат УсловиеНаМеткуКлассификацииВыполнено(Условие, Обращение);
	ИначеЕсли Условие.ВидУсловия = Перечисления.CRM_ВидыУсловийОбработкиОбращений.АдресОтправителя Тогда
		Возврат УсловиеНаАдресОтправителяВыполнено(Условие, Обращение);
	ИначеЕсли Условие.ВидУсловия = Перечисления.CRM_ВидыУсловийОбработкиОбращений.СписокТелефонныхНомеров Тогда
		Возврат УсловиеНаНомераТелефоновВыполнено(Условие, Обращение);
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция УсловиеНаСписокСловВыполнено(Условие, Обращение)
	
	Если (Условие.ИскомыеСтроки.Количество() = 0) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Поиск по содержанию
	МетаданныеОбъекта = Обращение.Метаданные();
	ТекстДляПоиска = "";
	
	Если CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("Тема", МетаданныеОбъекта) Тогда
		ТекстДляПоиска = ТекстДляПоиска + " " + Обращение["Тема"];
	КонецЕсли;
	Если CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("Комментарий", МетаданныеОбъекта) Тогда
		ТекстДляПоиска = ТекстДляПоиска + " " + Обращение["Комментарий"];
	КонецЕсли;
	Если CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("Текст", МетаданныеОбъекта) Тогда
		ТекстДляПоиска = ТекстДляПоиска + " " + Обращение["Текст"];
	КонецЕсли;
	Если CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("ТекстСообщения", МетаданныеОбъекта) Тогда
		ТекстДляПоиска = ТекстДляПоиска + " " + Обращение["ТекстСообщения"];
	КонецЕсли;
	Если CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("Описание", МетаданныеОбъекта) Тогда
		ТекстДляПоиска = ТекстДляПоиска + " " + Обращение["Описание"];
	КонецЕсли;
	
	Если СокрЛП(ТекстДляПоиска) = "" Тогда
		Возврат Ложь;
	КонецЕсли;
	ТекстДляПоиска = ВРег(ТекстДляПоиска);
	
	ДлинаСтроки = СтрДлина(ТекстДляПоиска);
	Разделители = " ,.?!""'%()<>*" + Символы.ВК + Символы.ВТаб + Символы.НПП + Символы.ПС + Символы.ПФ + Символы.Таб;
	Для каждого ИскомаяСтрока Из Условие.ИскомыеСтроки Цикл
		Позиция = СтрНайти(ТекстДляПоиска, ВРег(СокрЛП(ИскомаяСтрока.Строка)));
		Если Позиция > 0 Тогда
			Если ИскомаяСтрока.ИскатьКакСлово Тогда
				Пока Позиция > 0 Цикл
					ИскатьСледующее = Ложь;
					Если Позиция > 1 И СтрНайти(Разделители, Сред(ТекстДляПоиска, Позиция - 1, 1)) = 0 Тогда
						ИскатьСледующее = Истина;
					КонецЕсли;
					Если Не ИскатьСледующее Тогда
						КонПозиция = Позиция + СтрДлина(ИскомаяСтрока.Строка);
						Если КонПозиция < ДлинаСтроки И СтрНайти(Разделители, Сред(ТекстДляПоиска, КонПозиция, 1)) = 0 Тогда
							ИскатьСледующее = Истина;
						КонецЕсли;
					КонецЕсли;
					Если Не ИскатьСледующее Тогда
						Возврат Истина;
					КонецЕсли;
					Позиция = СтрНайти(ТекстДляПоиска, ВРег(СокрЛП(ИскомаяСтрока.Строка)), , Позиция + 1);
				КонецЦикла;
			Иначе
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция УсловиеНаМеткуКлассификацииВыполнено(Условие, Обращение)
	
	Если Не Обращение.ДополнительныеСвойства.Свойство("МеткаКлассификации") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Условие.Метка = Обращение.ДополнительныеСвойства.МеткаКлассификации Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция УсловиеНаАдресОтправителяВыполнено(Условие, Обращение)
	
	Если (Условие.ИскомыеСтроки.Количество() = 0) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Поиск по содержанию
	МетаданныеОбъекта = Обращение.Метаданные();
	ТекстДляПоиска = "";
	
	Если CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("ОтправительАдрес", МетаданныеОбъекта) Тогда
		ТекстДляПоиска = ТекстДляПоиска + Обращение["ОтправительАдрес"];
	КонецЕсли;
	
	Если СокрЛП(ТекстДляПоиска) = "" Тогда
		Возврат Ложь;
	КонецЕсли;
	ТекстДляПоиска = ВРег(ТекстДляПоиска);
	
	Для каждого ИскомаяСтрока Из Условие.ИскомыеСтроки Цикл
		Если ЗначениеЗаполнено(ИскомаяСтрока.Строка) И СтрНайти(ТекстДляПоиска, ВРег(СокрЛП(ИскомаяСтрока.Строка))) > 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция УсловиеНаНомераТелефоновВыполнено(Условие, Обращение)
	
	Если (Условие.ИскомыеСтроки.Количество() = 0) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Поиск по номерам
	МетаданныеОбъекта = Обращение.Метаданные();
	ТекстДляПоиска = "";
	
	Если CRM_ОбщегоНазначенияСервер.ЕстьРеквизитДокумента("АбонентКакСвязаться", МетаданныеОбъекта) Тогда
		ТекстДляПоиска = ТекстДляПоиска + Обращение["АбонентКакСвязаться"];
	КонецЕсли;
	
	ПоследниеЦифрыТелефонногоНомера = Константы.сфпПоследниеЦифрыТелефонногоНомера.Получить();
	
	СтруктураНомера = сфпСофтФонПроСервер.сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(ТекстДляПоиска);
	ТекстДляПоиска = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
		+ СтруктураНомера.НомерТелефона,
		 ПоследниеЦифрыТелефонногоНомера);
	
	Если СокрЛП(ТекстДляПоиска) = "" Тогда
		Возврат Ложь;
	КонецЕсли;
	ТекстДляПоиска = ВРег(ТекстДляПоиска);
	
	Для каждого ИскомаяСтрока Из Условие.ИскомыеСтроки Цикл
		СтруктураНомера = сфпСофтФонПроСервер.сфпЗаполнитьСтруктуруПолейПоПредставлениюТелефон(ИскомаяСтрока.Строка);
		ИскомыйНомер = Прав(СтрЗаменить(СтруктураНомера.КодСтраны, "+", "") + СтруктураНомера.КодГорода 
			+ СтруктураНомера.НомерТелефона,
			 ПоследниеЦифрыТелефонногоНомера);
		Если ЗначениеЗаполнено(ИскомыйНомер) И СтрНайти(ТекстДляПоиска, СокрЛП(ИскомыйНомер)) > 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли
