
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		Если Объект.Предопределенный Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Изменение элемента невозможно.'"), , , , Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ИсточникОткрытияТелефония") Тогда
		
		Объект.ВидУсловия = Перечисления.CRM_ВидыУсловийОбработкиОбращений.СписокТелефонныхНомеров;
		Список = Новый СписокЗначений;
		Список.Добавить(Перечисления.CRM_ВидыУсловийОбработкиОбращений.СписокТелефонныхНомеров);
	Иначе
		Список = Новый СписокЗначений;
		Список.Добавить(Перечисления.CRM_ВидыУсловийОбработкиОбращений.СписокСлов);
		Список.Добавить(Перечисления.CRM_ВидыУсловийОбработкиОбращений.АдресОтправителя);
		Список.Добавить(Перечисления.CRM_ВидыУсловийОбработкиОбращений.МеткаПомощника);
	КонецЕсли;
	
	НовыйМассив = Новый Массив();
	ПараметрВыбора = Новый ПараметрВыбора("Отбор.Ссылка", Список);
	НовыйМассив.Добавить(ПараметрВыбора);
	
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	Элементы.ВидУсловия.ПараметрыВыбора = НовыеПараметры;
	
	Если Объект.ВидУсловия = Перечисления.CRM_ВидыУсловийОбработкиОбращений.СписокСлов Тогда
		Элементы.СтраницыВидыУсловий.ТекущаяСтраница = Элементы.СтраницаСписокСлов;
	ИначеЕсли Объект.ВидУсловия = Перечисления.CRM_ВидыУсловийОбработкиОбращений.МеткаПомощника Тогда
		Элементы.СтраницыВидыУсловий.ТекущаяСтраница = Элементы.СтраницаМеткаПомощника;
	ИначеЕсли Объект.ВидУсловия = Перечисления.CRM_ВидыУсловийОбработкиОбращений.АдресОтправителя Тогда
		Элементы.СтраницыВидыУсловий.ТекущаяСтраница = Элементы.СтраницаАдресОтправителя;
	ИначеЕсли Объект.ВидУсловия = Перечисления.CRM_ВидыУсловийОбработкиОбращений.СписокТелефонныхНомеров Тогда
		Элементы.СтраницыВидыУсловий.ТекущаяСтраница = Элементы.СтраницаСписокТелефонов;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Элементы.ГруппаЭлементовЗаполненияПоРазделителю.Скрыта() Тогда
		Элементы.ГруппаЭлементовЗаполненияПоРазделителю.Показать();
		ПодключитьОбработчикОжидания("ЗакрытьПанельЗаполненияПоРазделителю", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры // Заполнить()

&НаКлиенте
Процедура ПеренестиСловаПоРазделителю(Команда)
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(РазделительТекста) Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("en = 'Split is not selected.'; ru = 'Не выбран разделить.'"), , "РазделительТекста");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекстЗаполненияПоРазделителю) Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("en = 'The text is not filled in.'; ru = 'Не заполнен текст.'"), , "ТекстЗаполненияПоРазделителю");
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ИскомыеСтроки.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПеренестиСловаПоРазделителюЗавершение", ЭтотОбъект);
		
		СписокКнопокВопроса = Новый СписокЗначений;
		СписокКнопокВопроса.Добавить("Заменить",	НСтр("en = 'Replace'; ru = 'Заменить'"));
		СписокКнопокВопроса.Добавить("Дополнить",	НСтр("en = 'Supplement'; ru = 'Дополнить'")); 
		СписокКнопокВопроса.Добавить("Отмена",		НСтр("en = 'Cancel'; ru = 'Отмена'"));
		
		ПоказатьВопрос(
			ОписаниеОповещения,
			НСтр("en = 'The table is not empty. Should I replace or supplement it?';
				| ru = 'Таблица не пустая. Выполнить замену или дополнить?'"),
			СписокКнопокВопроса, , ,
			НСтр("en = 'Attention!'; ru = 'Внимание!'"));
	Иначе
		ПеренестиСловаПоРазделителюЗавершение("Заменить", Неопределено);
	КонецЕсли;
	
КонецПроцедуры // ПеренестиСловаПоРазделителю()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидУсловияПриИзменении(Элемент)
	
	Объект.Метка = Неопределено;
	Объект.ИскомыеСтроки.Очистить();
	
	Если Объект.ВидУсловия = ПредопределенноеЗначение("Перечисление.CRM_ВидыУсловийОбработкиОбращений.СписокСлов") Тогда
		Элементы.СтраницыВидыУсловий.ТекущаяСтраница = Элементы.СтраницаСписокСлов;
	ИначеЕсли Объект.ВидУсловия = ПредопределенноеЗначение("Перечисление.CRM_ВидыУсловийОбработкиОбращений.МеткаПомощника") Тогда
		Элементы.СтраницыВидыУсловий.ТекущаяСтраница = Элементы.СтраницаМеткаПомощника;
	ИначеЕсли Объект.ВидУсловия = ПредопределенноеЗначение("Перечисление.CRM_ВидыУсловийОбработкиОбращений.АдресОтправителя") Тогда
		Элементы.СтраницыВидыУсловий.ТекущаяСтраница = Элементы.СтраницаАдресОтправителя;
	ИначеЕсли Объект.ВидУсловия = ПредопределенноеЗначение("Перечисление.CRM_ВидыУсловийОбработкиОбращений.СписокТелефонныхНомеров") Тогда
		Элементы.СтраницыВидыУсловий.ТекущаяСтраница = Элементы.СтраницаСписокТелефонов;
	КонецЕсли;
	
	Если Объект.ВидУсловия = ПредопределенноеЗначение("Перечисление.CRM_ВидыУсловийОбработкиОбращений.СписокСлов")
			Или Объект.ВидУсловия = ПредопределенноеЗначение("Перечисление.CRM_ВидыУсловийОбработкиОбращений.АдресОтправителя") Тогда
		
		Элементы.ГруппаЭлементовЗаполненияПоРазделителю.Доступность = Истина;
	Иначе
		Элементы.ГруппаЭлементовЗаполненияПоРазделителю.Доступность = Ложь;
		Элементы.ГруппаЭлементовЗаполненияПоРазделителю.Скрыть();
	КонецЕсли;
	
КонецПроцедуры // ВидУсловияПриИзменении()

&НаКлиенте
Процедура ВидУсловияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры // ВидУсловияОчистка()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Объект.ВидУсловия <> Перечисления.CRM_ВидыУсловийОбработкиОбращений.МеткаПомощника 
		И Объект.ИскомыеСтроки.Количество() = 0 Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнен список искомых слов!'"), , , "Объект.ИскомыеСтроки");
		Возврат;
	КонецЕсли;
		
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаКлиенте
Процедура ИскомыеСтрокиСтрокаПриИзменении(Элемент)
	
	Элемент.Родитель.ТекущиеДанные.Строка = СокрЛП(Элемент.Родитель.ТекущиеДанные.Строка);
	
КонецПроцедуры // ИскомыеСтрокиСтрокаПриИзменении()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗакрытьПанельЗаполненияПоРазделителю()
	
	Если Элементы.ГруппаЭлементовЗаполненияПоРазделителю.Скрыта() Тогда
		ТекущийЭлемент = ?(
			Объект.ВидУсловия = ПредопределенноеЗначение("Перечисление.CRM_ВидыУсловийОбработкиОбращений.СписокСлов"),
			Элементы.ИскомыеСтрокиЗаполнить,
			Элементы.ИскомыеСтроки1Заполнить);
	Иначе
		ПодключитьОбработчикОжидания("ЗакрытьПанельЗаполненияПоРазделителю", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры // ЗакрытьПанельЗаполненияПоРазделителю()

&НаКлиенте
Процедура ПеренестиСловаПоРазделителюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат = "Отмена" Тогда
			Возврат;
		КонецЕсли;
		
		ИспользуемыйРазделитель = ?(РазделительТекста <> "ПереводСтроки", РазделительТекста, Символы.ПС);
		
		СловаИзТекста = СтрРазделить(ТекстЗаполненияПоРазделителю, ИспользуемыйРазделитель, Ложь);
		
		ТекстЗаполненияПоРазделителю = "";
		
		Если Результат <> "Дополнить" Тогда
			Объект.ИскомыеСтроки.Очистить();
		КонецЕсли;
		
		Для Каждого ТекущееСловаИзТекста Из СловаИзТекста Цикл
			НоваяСтрока = Объект.ИскомыеСтроки.Добавить();
			НоваяСтрока.Строка = ТекущееСловаИзТекста;
		КонецЦикла;
		
		Элементы.ГруппаЭлементовЗаполненияПоРазделителю.Скрыть();
	КонецЕсли;
	
КонецПроцедуры // ПеренестиСловаПоРазделителюЗавершение()

#КонецОбласти
