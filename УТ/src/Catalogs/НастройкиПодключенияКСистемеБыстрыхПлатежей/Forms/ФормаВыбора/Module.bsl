///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КорневаяГруппа = Неопределено;
	Если ЗначениеЗаполнено(Параметры.ВариантНастройки) Тогда
		ВариантНастройки = Перечисления.ВариантыНастройкиСБП[Параметры.ВариантНастройки];
		КорневаяГруппа = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ГруппаВариантНастройки(
			ВариантНастройки,
			Ложь,
			Ложь);
	КонецЕсли;
	
	УстановитьУсловноеОформление(КорневаяГруппа);
	
	Результат = ОбновлениеИнформационнойБазы.ОбъектОбработан(
		"Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей");
	
	Если Не Результат.Обработан Тогда
		ТолькоПросмотр = Истина;
		Элементы.Список.Доступность = Ложь;
		ОбщегоНазначения.СообщитьПользователю(
			Результат.ТекстИсключения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Отображение) Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы[Параметры.Отображение];
	КонецЕсли;
	
	Если Параметры.СверкаВзаиморасчетовc2b Тогда
		
		КлючиПоиска = Новый Структура;
		КлючиПоиска.Вставить("СверкаВзаиморасчетовc2b", Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"ИдентификаторУчастника",
			СистемаБыстрыхПлатежейСлужебный.УчастникиСБППоНастройкам(КлючиПоиска),
			ВидСравненияКомпоновкиДанных.ВСписке,
			НСтр("ru = 'Идентификатор участника'"),
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Родитель",
			Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ГруппаВариантНастройки(
				Перечисления.ВариантыНастройкиСБП.c2b),
			ВидСравненияКомпоновкиДанных.ВИерархии,
			НСтр("ru = 'Сценарий оплаты'"),
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбновитьДанныеФормы",
		ЭтотОбъект);
	
	ПараметрыПодключения = СистемаБыстрыхПлатежейКлиент.НовыйПараметрыПодключения(
		Неопределено,
		Неопределено);
	
	СистемаБыстрыхПлатежейКлиент.ПодключитьСистемуБыстрыхПлатежей(
		ПараметрыПодключения,
		ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление(КорневаяГруппа)
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОформлениеОтбор = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОформлениеОтбор = ОформлениеОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОформлениеОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОформлениеОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОформлениеОтбор.ПравоеЗначение = Истина;
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста",
		Метаданные.ЭлементыСтиля.ЦветНеАктивнойСтроки.Значение);
	
	ОформлениеОтбор = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОформлениеОтбор = ОформлениеОтбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОформлениеОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Используется");
	ОформлениеОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОформлениеОтбор.ПравоеЗначение = Ложь;
	
	Если ЗначениеЗаполнено(КорневаяГруппа) Тогда
		
		ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
		ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Истина );
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Отображать",Истина );
		
		ОтборПоГруппе = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоГруппе.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
		ОтборПоГруппе.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;
		ОтборПоГруппе.ПравоеЗначение = КорневаяГруппа;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеФормы(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти