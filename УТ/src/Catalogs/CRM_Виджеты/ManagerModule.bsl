////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции модуля менеджера "Справочники.CRM_Виджеты".
//
////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Заполнить предопределенные виджеты
//
Процедура ЗаполнитьПредопределенныеВиджеты() Экспорт
	
	Выборка = Справочники.CRM_Виджеты.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		ВиджетОбъект = Выборка.ПолучитьОбъект();
		ВиджетОбъект.ТипИсточникаДанных = Перечисления.CRM_ТипыИсточниковДанныхВиджетов.КлючевойПоказатель;
		
		Если Выборка.Ссылка = Справочники.CRM_Виджеты.ВоронкаПродаж Тогда
			ВиджетОбъект.ТипИсточникаДанных = Перечисления.CRM_ТипыИсточниковДанныхВиджетов.ВоронкаПродаж;
		ИначеЕсли Выборка.Ссылка = Справочники.CRM_Виджеты.ДинамикаПродаж Тогда
			ВиджетОбъект.ТипИсточникаДанных = Перечисления.CRM_ТипыИсточниковДанныхВиджетов.ДинамикаПродаж;
		ИначеЕсли Выборка.Ссылка = Справочники.CRM_Виджеты.ДебиторскаяЗадолженность Тогда
			ВиджетОбъект.ТипИсточникаДанных = Перечисления.CRM_ТипыИсточниковДанныхВиджетов.ДебиторскаяЗадолженность;
		ИначеЕсли Выборка.Ссылка = Справочники.CRM_Виджеты.ДинамикаСтатусовКлиентов Тогда
			ВиджетОбъект.ТипИсточникаДанных = Перечисления.CRM_ТипыИсточниковДанныхВиджетов.ДинамикаСтатусовКлиентов;
		ИначеЕсли Выборка.Ссылка = Справочники.CRM_Виджеты.СтатистикаТелефонныхЗвонков Тогда
			ВиджетОбъект.ТипИсточникаДанных = Перечисления.CRM_ТипыИсточниковДанныхВиджетов.СтатистикаТелефонныхЗвонков;
		ИначеЕсли Выборка.Ссылка = Справочники.CRM_Виджеты.ТрудозатратыВремениПоВидамРабот Тогда
			ВиджетОбъект.ТипИсточникаДанных = Перечисления.CRM_ТипыИсточниковДанныхВиджетов.ТрудозатратыВремениПоВидамРабот;
		КонецЕсли;
		
		ВиджетОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПредопределенныеВиджеты()

// Функция для получения переопределяемых виджетов.
//
// Возвращаемое значение:
//   Массив   - Переопределяемые виджеты.
//
Функция ПолучитьПереопределяемыеВиджеты() Экспорт
	
	Переопределяемые = Новый Массив;
	
	Переопределяемые.Добавить(Справочники.CRM_Виджеты.ВыполнениеПланаПродаж);
	Переопределяемые.Добавить(Справочники.CRM_Виджеты.ВыполнениеПланаПродажКол);
	
	Возврат Переопределяемые;
	
КонецФункции // ПолучитьПереопределяемыеВиджеты()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	Если CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		УстановитьНастройкиВариантовОтчета(Настройки, Метаданные.Отчеты["CRM_АнализПродажПланФакт"]);
		УстановитьНастройкиВариантовОтчета(Настройки, Метаданные.Отчеты["CRM_ДебиторскаяЗадолженностьПоСрокамДолга"]);
	КонецЕсли;
	
	УстановитьНастройкиВариантовОтчета(Настройки, Метаданные.Отчеты.CRM_ДинамикаСтатусовКлиентов);
	УстановитьНастройкиВариантовОтчета(Настройки, Метаданные.Отчеты.CRM_АнализТрудозатрат);
	
КонецПроцедуры // НастроитьВариантыОтчетов()

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьНастройкиВариантовОтчета(Настройки, Отчет)
	
	// Отчет.
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Отчет);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
	// Данные виджета.
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "ДанныеВиджета");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	НастройкиВарианта.Включен = Ложь;
	
	// Расшифровка.
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, "ДанныеВиджетаРасшифровка");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	НастройкиВарианта.Включен = Ложь;
	
КонецПроцедуры // УстановитьНастройкиВариантовОтчета()

#КонецОбласти

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////