#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий


Процедура ПередЗаписью(Отказ)
	Если СпособУстановки = Перечисления.CRM_СпособыУстановкиСтатусаКлиента.Автоматически Тогда
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.CRM_РасчетСтатусовРаботыСКлиентами);
		Если Не ОбщегоНазначения.РазделениеВключено() Тогда
			ПараметрыЗадания.Вставить("ИмяМетода", Метаданные.РегламентныеЗадания.CRM_РасчетСтатусовРаботыСКлиентами.ИмяМетода);
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		
		СписокЗаданий = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыЗадания);
		Если СписокЗаданий.Количество() = 0 Тогда
			ПараметрыЗадания.Вставить("Использование", Истина);
			РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
		Иначе
			ПараметрыЗадания = Новый Структура("Использование", Истина);
			Для Каждого Задание Из СписокЗаданий Цикл
				РегламентныеЗаданияСервер.ИзменитьЗадание(Задание, ПараметрыЗадания);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если СпособУстановки = Перечисления.CRM_СпособыУстановкиСтатусаКлиента.Вручную Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	CRM_СтатусыРаботыСКлиентом.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.CRM_СтатусыРаботыСКлиентом КАК CRM_СтатусыРаботыСКлиентом
	                      |ГДЕ
	                      |	CRM_СтатусыРаботыСКлиентом.СпособУстановки = &СпособУстановки
	                      |	И CRM_СтатусыРаботыСКлиентом.Ссылка <> &Ссылка
	                      |	И НЕ CRM_СтатусыРаботыСКлиентом.ПометкаУдаления");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("СпособУстановки", СпособУстановки);
	Если СпособУстановки = Перечисления.CRM_СпособыУстановкиСтатусаКлиента.Автоматически Тогда
		Запрос.Текст = Запрос.Текст + "
		| И CRM_СтатусыРаботыСКлиентом.ШаблонРасчета = &ШаблонРасчета";
		Запрос.УстановитьПараметр("ШаблонРасчета", ШаблонРасчета);
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Отказ = Истина;
		Если СпособУстановки = Перечисления.CRM_СпособыУстановкиСтатусаКлиента.Автоматически Тогда
			Сообщение = НСтр("ru = 'Уже есть элемент с таким шаблоном расчета'");
		Иначе
			Сообщение = НСтр("ru = 'Нельзя создавать более одного элемента со способом установки ""Для нового клиента""!'");
		КонецЕсли;
		ОбщегоНазначения.СообщитьПользователю(Сообщение);
	КонецЕсли;
	
	Если СпособУстановки = Перечисления.CRM_СпособыУстановкиСтатусаКлиента.Автоматически 
		И ШаблонРасчета = "" Тогда
		Отказ = Истина;
		Сообщение = НСтр("ru = 'Не заполнен шаблон расчета!'");
		ОбщегоНазначения.СообщитьПользователю(Сообщение);
	КонецЕсли; 
	
КонецПроцедуры


#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли
