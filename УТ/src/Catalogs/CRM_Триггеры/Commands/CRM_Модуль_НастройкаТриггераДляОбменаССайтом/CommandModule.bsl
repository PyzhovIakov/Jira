
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Форма = АктивнаяФорма();
	Если Форма.ИмяФормы = "ПланОбмена.ОбменССайтом.Форма.ФормаУзла" Тогда
		Узел = Форма.Объект.Ссылка;
		Если ЗначениеЗаполнено(Узел) Тогда
			ПараметрыФормы = Новый Структура;
			Если ЗначениеЗаполнено(Форма.CRM_ТекущийТриггерОбъекта) Тогда
				ПараметрыФормы.Вставить("Ключ", Форма.CRM_ТекущийТриггерОбъекта);
			Иначе
				НастройкиНовогоТриггера = Новый Структура;
				НастройкиНовогоТриггера.Вставить("УсловиеСрабатывания", 
					ПредопределенноеЗначение("Справочник.CRM_УсловияСрабатыванияТриггеров.CRM_Модуль_ПриЗаписиЗаказаКлиента"));
				НастройкиНовогоТриггера.Вставить("Действие",
					ПредопределенноеЗначение("Справочник.CRM_ДействияТриггеров.CRM_Модуль_ПривязкаЗаказовКИнтересам"));
				НастройкиДействий = Новый Структура("НастройкаОбменаССайтом", Узел);
				НастройкиНовогоТриггера.Вставить("НастройкиДействий", НастройкиДействий);
				НастройкиНовогоТриггера.Вставить("ОткрыватьНастройкиДействия", Истина);
				ПараметрыФормы.Вставить("НастройкиНовогоТриггера", НастройкиНовогоТриггера);
			КонецЕсли;
			Оповещение = Новый ОписаниеОповещения("ПослеНастройкиТриггера", ЭтотОбъект, Форма);
			ОткрытьФорму("Справочник.CRM_Триггеры.Форма.ФормаЭлемента", ПараметрыФормы, Форма,,,, Оповещение);
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Настроить триггер создания интересов можно только после записи настроек обмена.'"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция АктивнаяФорма()
	Окна = ПолучитьОкна();
	Для каждого Окно из Окна Цикл
		Для каждого Форма из Окно.Содержимое Цикл
			Если Форма.ВводДоступен() Тогда
				Возврат Форма;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецФункции

&НаКлиенте
Процедура ПослеНастройкиТриггера(Результат, Форма) Экспорт
	Форма.CRM_ТекущийТриггерОбъекта = CRM_Модуль_РаботаСФормамиСервер.ОбменССайтомТриггерСозданияИнтереса(Форма.Объект.Ссылка);
	Гиперссылка = Форма.Элементы.Найти("CRM_Команда_Гиперссылка");
	Если Гиперссылка <> Неопределено Тогда
		Гиперссылка.Заголовок = CRM_Модуль_РаботаСФормамиСервер.ОбменССайтомПредставлениеТриггера(Форма.CRM_ТекущийТриггерОбъекта);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
