
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Отбор.Свойство("ОбъектДействия") Тогда
		ОбъектДействия = Параметры.Отбор.ОбъектДействия;
		ОбновитьОтборПоОбъектуДействия();
			
		СтандартнаяОбработка = Ложь;
	Иначе
		МассивИсключаемых = Справочники.CRM_УсловияСрабатыванияТриггеров.МассивИсключаемыхПоФОПредопределенных();
		Если МассивИсключаемых.Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка",
				 МассивИсключаемых, ВидСравненияКомпоновкиДанных.НеВСписке, ,
				 Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ОбъектДействия) Тогда
		ОбновитьОтборПоОбъектуДействия();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьОтборПоОбъектуДействия()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	УсловияСрабатывания.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.CRM_УсловияСрабатыванияТриггеров КАК УсловияСрабатывания
	|ГДЕ
	|	УсловияСрабатывания.ОбъектДействия = &ОбъектОбработки
	|	И НЕ УсловияСрабатывания.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УсловияСрабатыванияОбъектыОбработки.Ссылка
	|ИЗ
	|	Справочник.CRM_УсловияСрабатыванияТриггеров.ОбъектыОбработки КАК УсловияСрабатыванияОбъектыОбработки
	|ГДЕ
	|	УсловияСрабатыванияОбъектыОбработки.ОбъектОбработки = &ОбъектОбработки
	|	И НЕ УсловияСрабатыванияОбъектыОбработки.Ссылка.ПометкаУдаления");
	
	Запрос.УстановитьПараметр("ОбъектОбработки", ОбъектДействия);
	ДанныеДляОтбора = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка",
		ДанныеДляОтбора, ВидСравненияКомпоновкиДанных.ВСписке, , Истина);
	
КонецПроцедуры

#КонецОбласти
