#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		Наименование = Событие;
		Для каждого Строка Из ОбъектыОбработки Цикл
			Наименование = Наименование + ?(Строка.НомерСтроки = 1, ": ", "; ") + Строка.ОбъектОбработки;
		КонецЦикла;
	КонецЕсли;
	
	Если Ссылка <> Справочники.CRM_УсловияСрабатыванияТриггеров.ПоРасписанию Тогда
		ОбъектыОбработкиПредставление = "";
		Для каждого Строка Из ОбъектыОбработки Цикл
			ОбъектыОбработкиПредставление = ОбъектыОбработкиПредставление 
				+ ?(ОбъектыОбработкиПредставление = "", "", "; ") + Строка.ОбъектОбработки.Синоним;
		КонецЦикла;
	Иначе
		ОбъектыОбработкиПредставление = Наименование;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если СервисныйВызов Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ОбъектыОбработки"));
	Иначе
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ИдентификаторВызова"));
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли
