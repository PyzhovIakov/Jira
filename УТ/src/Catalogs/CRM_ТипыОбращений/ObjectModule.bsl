#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	Если Наименование <> Ссылка.Наименование Тогда
		ДополнительныеСвойства.Вставить("СтароеНаименование", Ссылка.Наименование);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Перем НаборДопРеквизитов; 
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.ЭтоНовый Тогда
		
		НаборДопРеквизитов = Справочники.НаборыДополнительныхРеквизитовИСведений.СоздатьЭлемент();
		НаборДопРеквизитов.Родитель = УправлениеСвойствами.НаборСвойствПоИмени("Документ_CRM_Интерес");
		НаборДопРеквизитов.Используется = Истина;
		
	ИначеЕсли ДополнительныеСвойства.Свойство("СтароеНаименование") Тогда
		
		СсылкаНаДопРеквизит =
			Справочники.НаборыДополнительныхРеквизитовИСведений.НайтиПоНаименованию(ДополнительныеСвойства.СтароеНаименование);
		Если ЗначениеЗаполнено(СсылкаНаДопРеквизит) Тогда
			НаборДопРеквизитов = СсылкаНаДопРеквизит.ПолучитьОбъект();
		КонецЕсли;
		
	КонецЕсли;
	
	Если НаборДопРеквизитов <> Неопределено Тогда
		НаборДопРеквизитов.Наименование = Наименование;
		НаборДопРеквизитов.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли
