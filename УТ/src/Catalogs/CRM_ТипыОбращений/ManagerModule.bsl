
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет предопределенные данные.
//
// Параметры:
//  Параметры - Структура - Параметры выполнения.
//
Процедура ЗаполнитьПредопределенныеДанные(Параметры = Неопределено) Экспорт
	
	ТипИнтерес = Интерес.ПолучитьОбъект();
	ТипИнтерес.ИндексКартинки = 0;
	ТипИнтерес.РеквизитДопУпорядочивания = 0;
	ТипИнтерес.Записать();
	
	ТипОбращение = Обращение.ПолучитьОбъект();
	ТипОбращение.ИндексКартинки = 14;
	ТипОбращение.РеквизитДопУпорядочивания = 1;
	ТипОбращение.Записать();
	
	Если Параметры <> Неопределено Тогда
		Параметры.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	ТолькоПоддержка = Параметры.Свойство("ТолькоПоддержка");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТипыОбращений.Ссылка КАК Ссылка,
	|	ТипыОбращений.ИндексКартинки КАК ИндексКартинки
	|ИЗ
	|	Справочник.CRM_ТипыОбращений КАК ТипыОбращений
	|ГДЕ
	|	НЕ ТипыОбращений.ПометкаУдаления";
	
	Если ТолькоПоддержка Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	И НЕ ТипыОбращений.Ссылка = ЗНАЧЕНИЕ(Справочник.CRM_ТипыОбращений.Интерес)";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипыОбращений.РеквизитДопУпорядочивания";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка, , , БиблиотекаКартинок["CRM_ТипОбращения" + Выборка.ИндексКартинки]);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
