// @strict-types


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ХешАдресаСервиса = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(АдресСервиса);
	
	УчетнаяЗапись = Справочники.УчетныеЗаписиОблачногоЭДО.НайтиПоАдресуСервиса(АдресСервиса);
	Если ЗначениеЗаполнено(УчетнаяЗапись)
		И УчетнаяЗапись <> Ссылка Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Учетная запись облачного ЭДО с адресом сервиса ""%1"" уже существует'"),
			АдресСервиса);
		ОбщегоНазначенияБЭД.ЗаписатьВЖурналРегистрации(ТекстСообщения,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСКонтрагентами);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыЭДО = РегистрыСведений.НастройкиОблачногоЭДО.ИдентификаторыЭДОУчетнойЗаписи(Ссылка);
	Если Не ЗначениеЗаполнено(ИдентификаторыЭДО) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого ИдентификаторЭДО Из ИдентификаторыЭДО Цикл
		УчетныеЗаписиЭДО.УдалитьУчетнуюЗапись(ИдентификаторЭДО);
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
