////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции модуля формы "Справочники.CRM_ВариантыВиджетов.Форма.ФормаЭлемента".
//
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("НастройкиСоздания") Тогда
		
		НастройкиСоздания = Параметры.НастройкиСоздания;
		
		Объект.Виджет = Параметры.Виджет;
		Элементы.Виджет.Доступность = Ложь;
		
	КонецЕсли;
	

	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ЭтоАдминистратор = Пользователи.ЭтоПолноправныйПользователь(ТекущийПользователь, Истина);
	
	Если ЭтоАдминистратор Тогда
		Элементы.Автор.Вид = ВидПоляФормы.ПолеВвода;
	Иначе
		Если Не Объект.Ссылка.Пустая() Тогда
			ДоступенВсем = Не Объект.ТолькоДляАвтора;
			Если ДоступенВсем И Объект.Автор <> ТекущийПользователь Тогда
				ЭтотОбъект.Доступность = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Автор = ТекущийПользователь;
		Объект.Используется = Истина;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НастройкиСоздания <> Неопределено Тогда
		
		ТекущийОбъект.НастройкиВарианта = Новый ХранилищеЗначения(НастройкиСоздания);
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписьюНаСервере()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьНастройки(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для открытия настроек необходимо записать объект.'"));
		Возврат;
	КонецЕсли;
	
	НастройкиВарианта = CRM_РаботаСВиджетамиВызовСервера.ПолучитьНастройкиВариантаВиджета(
		Объект.Ссылка);
	
	ТипВиджета = CRM_ОбщегоНазначенияСервер.ЗначениеРеквизитаОбъекта(
		Объект.Виджет, "ТипИсточникаДанных");
	
	ДанныеВиджета = Новый Структура;
	ДанныеВиджета.Вставить("ВариантВиджета", Объект.Ссылка);
	ДанныеВиджета.Вставить("Настройки", НастройкиВарианта);
	ДанныеВиджета.Вставить("ТипВиджета", ТипВиджета);
	
	ОписаниеОповещенияОбИзмененииНастроек = Новый ОписаниеОповещения(
		"ПриИзмененииНастроекВариантаВиджета", ЭтотОбъект);
	
	CRM_РаботаСВиджетамиКлиент.ОткрытьНастройкиВиджета(
		ЭтотОбъект, ДанныеВиджета, ОписаниеОповещенияОбИзмененииНастроек);
	
КонецПроцедуры // ОткрытьНастройки()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ПриИзмененииНастроекВариантаВиджета(ДанныеВиджета, ДополнительныеПараметры) Экспорт
	
	Прочитать();
	
КонецПроцедуры // ПриИзмененииНастроекВариантаВиджета()

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////