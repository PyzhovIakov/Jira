#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы.

// Процедура заполнения шаблона для анкетирования при первом старте и обновлении.
//
Процедура CRM_ЗаполнитьШаблонДляАнкетирования() Экспорт
	
	ШаблонРассылкиДляАнкетированияОбъект = Справочники.CRM_ШаблоныРассылки.ШаблонДляАнкетирования.ПолучитьОбъект();
	
	// АПК:1223-выкл отключаем проверку на использование местоимений "Вы", "Вас" и пр.
	ТекстШаблона = "%УВАЖАЕМЫЙ/УВАЖАЕМАЯ% %ПОЛУЧАТЕЛЬ_ИМЯ_ОТЧЕСТВО%" + "!" + Символы.ПС;
	ТекстШаблона = ТекстШаблона + Символы.ПС;
	ТекстШаблона = ТекстШаблона 
		+ "Предлагаем Вам принять участие в анкетировании, проводимом компанией ""<НАИМЕНОВАНИЕ ОРГАНИЗАЦИИ>""" 
		+ Символы.ПС;
	ТекстШаблона = ТекстШаблона + "Ваше мнение очень важно для нас!" + Символы.ПС;
	ТекстШаблона = ТекстШаблона + Символы.ПС;
	ТекстШаблона = ТекстШаблона 
		+ "Для начала анкетирования Вам необходимо перейти по указанной ниже ссылке и зайти в Личный кабинет." 
		+ Символы.ПС;
	ТекстШаблона = ТекстШаблона + Символы.ПС;
	ТекстШаблона = ТекстШаблона + "%АДРЕС_WEB_ДОСТУПА%" + Символы.ПС;
	ТекстШаблона = ТекстШаблона + Символы.ПС;
	ТекстШаблона = ТекстШаблона 
		+ "Для входа в Личный кабинет Вам необходимо будет ввести имя пользователя и пароль, указанные ниже:" 
		+ Символы.ПС;
	ТекстШаблона = ТекстШаблона + "%ИМЯ_РЕСПОНДЕНТА%" + Символы.ПС;
	ТекстШаблона = ТекстШаблона + "%ПАРОЛЬ_РЕСПОНДЕНТА%" + Символы.ПС;
	ТекстШаблона = ТекстШаблона + Символы.ПС;
	ТекстШаблона = ТекстШаблона + "P.S. В случае, если в Вашем web-браузере будут выдаваться сообщения о блокировке "
				  + "активных элементов при входе в Личный кабинет, Вам необходимо добавить в список "
				  + "надежных адресов указанную выше ссылку и перезапустить web-браузер." + Символы.ПС;
	ТекстШаблона = ТекстШаблона + Символы.ПС;
	// АПК:1223-вкл
	
	ТекстШаблонаHTML = CRM_ОбщегоНазначенияКлиентСервер.ПреобразоватьТекстВHTML(ТекстШаблона);
	
	ШаблонРассылкиДляАнкетированияОбъект.Текст = ТекстШаблонаHTML;
	
	ШаблонРассылкиДляАнкетированияОбъект.Записать();
	
КонецПроцедуры

#Область ПереходНаНовуюВерсию

Процедура ОбновитьОбъектСправочника(ВыборкаДетальныеЗаписи) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	CRM_ШаблоныРассылкиПрисоединенныеФайлы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.CRM_ШаблоныРассылкиПрисоединенныеФайлы КАК CRM_ШаблоныРассылкиПрисоединенныеФайлы
	|ГДЕ
	|	CRM_ШаблоныРассылкиПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла";
	Запрос.УстановитьПараметр("ВладелецФайла", Справочники.CRM_ШаблоныРассылки.ШаблонОценкиОбращения);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Об = Выборка.Ссылка.ПолучитьОбъект();
		Об.Удалить();
	КонецЦикла;	
	СправочникОбъект = Неопределено;
	
	КаталогКартинок = КаталогВременныхФайлов();
	
	СправочникОбъект = Справочники.CRM_ШаблоныРассылки.ШаблонОценкиОбращения.ПолучитьОбъект();
	ВременныйТекстHTML = Справочники.CRM_ШаблоныРассылки.ПолучитьМакет("ШаблонОценкиОбращения").ПолучитьТекст();
	
	ДДАрхива = Справочники.CRM_ШаблоныРассылки.ПолучитьМакет("ШаблонОценкиОбращенияАрхивСмайликов");
	ПолныйПутьКАрхиву = КаталогКартинок + "img.zip";
	ДДАрхива.Записать(ПолныйПутьКАрхиву);
	
	ЧтениеZipФайла = Новый ЧтениеZipФайла(ПолныйПутьКАрхиву);
	ЧтениеZipФайла.ИзвлечьВсе(КаталогКартинок, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
	
	ДокументHTML = Взаимодействия.ПолучитьОбъектДокументHTMLИзТекстаHTML(ВременныйТекстHTML);
	
	Для каждого Картинка Из ДокументHTML.Картинки Цикл
		АтрибутИсточникКартинки = Картинка.Атрибуты.ПолучитьИменованныйЭлемент("src");
		
		ПутьККартинке = СтрЗаменить(АтрибутИсточникКартинки.Значение, "/", "\");
		ПолныйПутьККартинке = "data:image/png;base64,";
		
		Если СтрНайти(ПутьККартинке, "smile1") > 0 Тогда
			ПолныйПутьККартинке = КаталогКартинок + "smile1.png";
		ИначеЕсли СтрНайти(ПутьККартинке, "smile2") > 0 Тогда
			ПолныйПутьККартинке = КаталогКартинок + "smile2.png";
		ИначеЕсли СтрНайти(ПутьККартинке, "smile3") > 0 Тогда
			ПолныйПутьККартинке = КаталогКартинок + "smile3.png";
		ИначеЕсли СтрНайти(ПутьККартинке, "smile4") > 0 Тогда
			ПолныйПутьККартинке = КаталогКартинок + "smile4.png";
		ИначеЕсли СтрНайти(ПутьККартинке, "smile5") > 0 Тогда
			ПолныйПутьККартинке = КаталогКартинок + "smile5.png";
		КонецЕсли;	
		
		ВременныйТекстHTML = СтрЗаменить(ВременныйТекстHTML, АтрибутИсточникКартинки.Значение, ПолныйПутьККартинке);
	КонецЦикла;
	
	СправочникОбъект.Тема = "Шаблон оценки обращения";
	
	ТаблицаСоответствийИменВложенийИдентификаторам = Новый ТаблицаЗначений;
	ТаблицаСоответствийИменВложенийИдентификаторам.Колонки.Добавить("ИмяФайла");
	ТаблицаСоответствийИменВложенийИдентификаторам.Колонки.Добавить("ИдентификаторФайлаДляHTML");
	ТаблицаСоответствийИменВложенийИдентификаторам.Колонки.Добавить("Картинка");
	
	ДокументHTML = Взаимодействия.ПолучитьОбъектДокументHTMLИзТекстаHTML(ВременныйТекстHTML);
	
	Для каждого Картинка Из ДокументHTML.Картинки Цикл
		АтрибутИсточникКартинки = Картинка.Атрибуты.ПолучитьИменованныйЭлемент("src");
		
		ПолныйПутьККартинке = СтрЗаменить(АтрибутИсточникКартинки.Значение, "/", "\");
		ФайлКартинки = Новый Файл(ПолныйПутьККартинке);
		
		Если ФайлКартинки.Существует() Тогда
			
			МассивНайденых = ТаблицаСоответствийИменВложенийИдентификаторам.НайтиСтроки(Новый Структура("ИмяФайла",
				 АтрибутИсточникКартинки.Значение));
			Если МассивНайденых.Количество() > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаСоответствийИменВложенийИдентификаторам.Добавить();
			НоваяСтрока.ИмяФайла = АтрибутИсточникКартинки.Значение;
			НоваяСтрока.ИдентификаторФайлаДляHTML = Новый УникальныйИдентификатор;
			НоваяСтрока.Картинка = Новый Картинка(ПолныйПутьККартинке);
			
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаСоответствийИменВложенийИдентификаторам.Количество() > 0 Тогда
		ТаблицаСоответствийИменВложенийИдентификаторам.Индексы.Добавить("ИмяФайла");
		
		Для каждого Картинка Из ДокументHTML.Картинки Цикл
			
			АтрибутИсточникКартинки = Картинка.Атрибуты.ПолучитьИменованныйЭлемент("src");
			
			НайденнаяСтрока = ТаблицаСоответствийИменВложенийИдентификаторам.Найти(АтрибутИсточникКартинки.ТекстовоеСодержимое,
				 "ИмяФайла");
			Если НайденнаяСтрока <> Неопределено Тогда
				
				НовыйАтрибутКартинки = АтрибутИсточникКартинки.КлонироватьУзел(Ложь);
				НовыйАтрибутКартинки.ТекстовоеСодержимое = Строка("cid:" + НайденнаяСтрока.ИдентификаторФайлаДляHTML);
				Картинка.Атрибуты.УстановитьИменованныйЭлемент(НовыйАтрибутКартинки);
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	ТекстHTML = Взаимодействия.ПолучитьТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	СправочникОбъект.Текст = ТекстHTML;
	
	Для каждого Вложение Из ТаблицаСоответствийИменВложенийИдентификаторам Цикл
		
		ИмяФайлаВложения = "_" + СтрЗаменить(Вложение.ИдентификаторФайлаДляHTML, "-", "_") + "." + Вложение.Картинка.Формат();
		
		ДвоичныеДанныеКартинки = Вложение.Картинка.ПолучитьДвоичныеДанные();
		
		ПараметрыВложения = Новый Структура;
		ПараметрыВложения.Вставить("ИмяФайла", ИмяФайлаВложения);
		ПараметрыВложения.Вставить("Размер", ДвоичныеДанныеКартинки.Размер());
		
		АдресКартинкиВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеКартинки,
			 Вложение.ИдентификаторФайлаДляHTML);
		ПрисоединенныйФайл = CRM_УправлениеЭлектроннойПочтой.ЗаписатьВложениеЭлектронногоПисьмаИзВременногоХранилища(
		СправочникОбъект.Ссылка,
		АдресКартинкиВоВременномХранилище,
		ПараметрыВложения);
		
		Если ПрисоединенныйФайл <> Неопределено Тогда
			ПрисоединенныйФайлОбъект = ПрисоединенныйФайл.ПолучитьОбъект();
			ПрисоединенныйФайлОбъект.ИДФайлаЭлектронногоПисьма = Вложение.ИдентификаторФайлаДляHTML;
			ПрисоединенныйФайлОбъект.Записать();
		КонецЕсли;
		
	КонецЦикла;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
