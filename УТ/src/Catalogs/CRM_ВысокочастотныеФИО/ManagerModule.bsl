#Область СлужебныеПроцедурыИФункции

Функция ФИОПоНаименованию(Наименование) Экспорт
	
	Строки = СтрРазделить(Наименование, " ", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВысокочастотныеФИО.Наименование КАК Наименование,
	               |	ВысокочастотныеФИО.Фамилия КАК Фамилия,
	               |	ВысокочастотныеФИО.Имя КАК Имя,
	               |	ВысокочастотныеФИО.Отчество КАК Отчество
	               |ИЗ
	               |	Справочник.CRM_ВысокочастотныеФИО КАК ВысокочастотныеФИО
	               |ГДЕ
	               |	ВысокочастотныеФИО.Наименование В(&Строки)";
	
	Запрос.УстановитьПараметр("Строки", Строки);
	
	ФИО = Новый Структура("Фамилия,Имя,Отчество");
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	
	МаксимальныйИндекс =  ТаблицаЗначений.Количество() - 1;
	Для ИндексПеребора = 0 По МаксимальныйИндекс Цикл
		ИндексСтроки = МаксимальныйИндекс - ИндексПеребора;
		ТекущаяСтрока = ТаблицаЗначений[ИндексСтроки];
		Если ТекущаяСтрока.Фамилия Тогда
			СтруктураОтбораОтчество = Новый Структура("Наименование, Отчество", ТекущаяСтрока.Наименование, Истина);
			ИсключаемыеСтрокиОтчество = ТаблицаЗначений.НайтиСтроки(СтруктураОтбораОтчество);
			
			СтруктураОтбораИмя = Новый Структура("Наименование, Имя", ТекущаяСтрока.Наименование, Истина);
			ИсключаемыеСтрокиИмя = ТаблицаЗначений.НайтиСтроки(СтруктураОтбораИмя);
		
			Если ИсключаемыеСтрокиОтчество.Количество() > 0 Или ИсключаемыеСтрокиИмя.Количество() > 0 Тогда
				ТаблицаЗначений.Удалить(ИндексСтроки);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		Если СтрокаТаблицы.Фамилия Тогда
			ФИО.Фамилия = СтрокаТаблицы.Наименование;
		ИначеЕсли СтрокаТаблицы.Имя Тогда 
			ФИО.Имя = СтрокаТаблицы.Наименование;
		ИначеЕсли СтрокаТаблицы.Отчество Тогда 
			ФИО.Отчество = СтрокаТаблицы.Наименование;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ФИО;
КонецФункции

Процедура ДополнитьНедостающиеФИОПоНаименованию(Наименование, Фамилия, Имя, Отчество) Экспорт 
	
	Если Не ЗначениеЗаполнено(Фамилия) ИЛИ Не ЗначениеЗаполнено(Имя) ИЛИ Не ЗначениеЗаполнено(Отчество) Тогда
		ФИОНаименование = Наименование;
		ФИОИзНаименования = Новый Структура("Фамилия,Имя,Отчество");
		ПервоеСлово = ВыделитьЧастьНаименованияКонтактногоЛица(ФИОНаименование);
		ФИОИзНаименования.Фамилия = ПервоеСлово;
		ФИОИзНаименования.Имя = ВыделитьЧастьНаименованияКонтактногоЛица(ФИОНаименование);
		ФИОИзНаименования.Отчество = ВыделитьЧастьНаименованияКонтактногоЛица(ФИОНаименование);
		
		ФИОИзНаименованияДляСверки = Новый Структура;
		Для Каждого ЭлементСтруктуры Из ФИОИзНаименования Цикл
			ФИОИзНаименованияДляСверки.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
		КонецЦикла;
		
		ФИО = ФИОПоНаименованию(Наименование);
		ПовторИмени = "";
		Если ФИОИзНаименования.Фамилия = ФИОИзНаименования.Отчество Или
			ФИОИзНаименования.Фамилия = ФИОИзНаименования.Имя Тогда
			ПовторИмени = ФИОИзНаименования.Фамилия;
		ИначеЕсли ФИОИзНаименования.Отчество = ФИОИзНаименования.Имя Тогда
			ПовторИмени = ФИОИзНаименования.Отчество;
		КонецЕсли;
		Если ЗначениеЗаполнено(ПовторИмени) И ФИО.Отчество = ПовторИмени Тогда
			ФИО.Фамилия = ФИО.Отчество;
		ИначеЕсли ЗначениеЗаполнено(ПовторИмени) И ФИО.Имя = ПовторИмени Тогда
			ФИО.Фамилия = ФИО.Имя;
		КонецЕсли;
		
		КопияФИОДляСверки = Новый Структура;
		Для Каждого ЭлементСтруктуры Из ФИО Цикл
			КопияФИОДляСверки.Вставить(ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
		КонецЦикла;
		
		Для Каждого Строка Из ФИОИзНаименованияДляСверки Цикл
			Для Каждого СтрокаФИО Из КопияФИОДляСверки Цикл
				Если СтрокаФИО.Значение = Строка.Значение Тогда
					ФИОИзНаименованияДляСверки.Удалить(Строка.Ключ);
					КопияФИОДляСверки.Удалить(СтрокаФИО.Ключ);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Если ФИОИзНаименованияДляСверки.Количество() = 1 Тогда
			Для Каждого ЭлементСтруктуры Из ФИО Цикл
				Если ЭлементСтруктуры.Значение = Неопределено Тогда
					КлючСтруктуры = ЭлементСтруктуры.Ключ;
					Для Каждого ЭлементСтруктуры2 Из ФИОИзНаименованияДляСверки Цикл
						ФИО[КлючСтруктуры] = ЭлементСтруктуры2.Значение;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Фамилия) И ЗначениеЗаполнено(ФИО.Фамилия) Тогда 
			Фамилия = ФИО.Фамилия;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Имя) И ЗначениеЗаполнено(ФИО.Имя) Тогда 
			Имя = ФИО.Имя;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Отчество) И ЗначениеЗаполнено(ФИО.Отчество) Тогда
			Отчество = ФИО.Отчество;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Отчество) И Не ЗначениеЗаполнено(ФИО.Отчество) Тогда
			Если ЗначениеЗаполнено(ФИО.Фамилия) И ЗначениеЗаполнено(ФИО.Имя) Тогда
				Отчество = ФИОИзНаименования.Отчество;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ВыделитьЧастьНаименованияКонтактногоЛица(ИсходнаяСтрока)
	Буфер = СокрЛ(ИсходнаяСтрока);
	ПозицияПослПробела = СтрНайти(Буфер, " ");
	Если ПозицияПослПробела = 0 Тогда
		ИсходнаяСтрока = "";
		Возврат Буфер;
	КонецЕсли;
	ВыделенноеСлово = СокрЛП(Лев(Буфер, ПозицияПослПробела));
	ИсходнаяСтрока = Сред(ИсходнаяСтрока, ПозицияПослПробела + 1);
	Возврат ВыделенноеСлово;
КонецФункции

#Область ПереходНаНовуюВерсию

Процедура ЗаполнитьВысокочастотныеФИО(Параметры = Неопределено) Экспорт
	
	Макет = Справочники.CRM_ВысокочастотныеФИО.ПолучитьМакет("Макет");
	ПостроительЗапроса = Новый ПостроительЗапроса;
	ПостроительЗапроса.ИсточникДанных = Новый ОписаниеИсточникаДанных(Макет.Область(1, 1,
		 Макет.ВысотаТаблицы,
		 Макет.ШиринаТаблицы));
	ПостроительЗапроса.Выполнить();
	
	ТаблицаФИО = ПостроительЗапроса.Результат.Выгрузить();
	
	Для Каждого СтрокаТаблицаФИО Из ТаблицаФИО Цикл
	
		ЭтоФамилия = Ложь;
		ЭтоИмя = Ложь;
		ЭтоОтчество = Ложь;
		
		Если СтрокаТаблицаФИО.Фамилия = "Да" Тогда
			ЭтоФамилия = Истина;
		ИначеЕсли СтрокаТаблицаФИО.Имя = "Да" Тогда
			ЭтоИмя = Истина;
		Иначе
			ЭтоОтчество = Истина;
		КонецЕсли;
		
		НовыйЭлемент = Справочники.CRM_ВысокочастотныеФИО.СоздатьЭлемент();
		НовыйЭлемент.Наименование = СокрЛП(СтрокаТаблицаФИО.Наименование);
		НовыйЭлемент.Фамилия = ЭтоФамилия;
		НовыйЭлемент.Имя = ЭтоИмя;
		НовыйЭлемент.Отчество = ЭтоОтчество;
		НовыйЭлемент.Записать();
		
	КонецЦикла;
	
	Если Параметры <> Неопределено Тогда
		Параметры.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
