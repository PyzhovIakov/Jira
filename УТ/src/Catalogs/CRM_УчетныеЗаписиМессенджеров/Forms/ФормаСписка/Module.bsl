
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолучитьЛоготипыМессенджеров();
	
	// Проверка наличия учетных записей
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	CRM_УчетныеЗаписиМессенджеров.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.CRM_УчетныеЗаписиМессенджеров КАК CRM_УчетныеЗаписиМессенджеров
	|ГДЕ
	|	НЕ CRM_УчетныеЗаписиМессенджеров.ПометкаУдаления
	|	И CRM_УчетныеЗаписиМессенджеров.Предопределенный = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НетУчетныхЗаписей = Истина;
	Иначе
		НетУчетныхЗаписей = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("ОткрытьВыборКаналаДиалогов") Тогда
		ОткрытьВыборКаналаДиалогов = Истина;
	КонецЕсли;
	
	// Убирает B2B портал из списка
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, 
	"Ссылка", 
	Справочники.CRM_УчетныеЗаписиМессенджеров.B2BПортал, 
	ВидСравненияКомпоновкиДанных.НеРавно,
	,
	Истина,
	РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НетУчетныхЗаписей Или ОткрытьВыборКаналаДиалогов Тогда
		Элементы.Список.ДобавитьСтроку();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭтоПерваяУчетнаяЗапись()
	Выборка = Справочники.CRM_УчетныеЗаписиМессенджеров.Выбрать();
	Количество = 0;
	Пока Выборка.Следующий() Цикл
		Количество = Количество + 1;
	КонецЦикла;
	Возврат (Количество - 1);
КонецФункции

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	Если ЭтоПерваяУчетнаяЗапись() Тогда
		ЗаписьВыполнена =
			CRM_НачалоРаботыСПрограммойВызовСервера.ЗаписатьНастроенныйРазделРешения(ПредопределенноеЗначение("Перечисление.CRM_РазделыНастройкиРешения.Диалоги"));
		Если ЗаписьВыполнена Тогда
			Оповестить("ОбновитьНастроенныеРазделы");
			CRM_РабочийСтолКлиент.ПереинициализироватьРабочийСтолКлиент("Обработка.CRM_Мессенджер.Форма.ФормаМессенджера");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.CRM_ВыборКаналаДиалогов", , ЭтотОбъект, , , , ОписаниеОповещения,
		 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗавершение(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ПараметрыФормы = Новый Структура("ТипМессенджера", Результат);
		ОткрытьФорму("Справочник.CRM_УчетныеЗаписиМессенджеров.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьЛоготипыМессенджеров()
	СписокМессенджеров = CRM_РаботаСМессенджерамиСервер.ПолучитьСписокМессенджеров();
	Для каждого Мессенджер Из СписокМессенджеров Цикл
		СтрокаКартинки = ПоместитьВоВременноеХранилище(Мессенджер.Картинка, УникальныйИдентификатор);
		НовЛого = ЛоготипыМессенджеров.Добавить();
		НовЛого.ТипМессенджера = Мессенджер.Значение;
		НовЛого.Логотип = СтрокаКартинки;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
