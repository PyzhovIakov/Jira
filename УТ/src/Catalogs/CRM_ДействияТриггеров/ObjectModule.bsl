#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если РежимОтладки Тогда
		Если НЕ Пользователи.ТекущийПользователь().Служебный Тогда
			Обработка = CRM_ТриггерыСервер.ВернутьОбработкуДействия(Новый Структура("Действие", ЭтотОбъект));
		КонецЕсли;
	Иначе	
		ОбработкаПроверки = ОбработкаДействия.Получить();
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("epf");
		Если ОбработкаПроверки = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не выбрана обработка действия! Запись невозможна!';
				|en='Action processor is not selected! Processing impossible!'"));
			Отказ = Истина;
			Возврат;
		Иначе
			ОбработкаПроверки.Записать(ИмяВременногоФайла);
		КонецЕсли;
		
		ПараметрыЗащиты = Новый("ОписаниеЗащитыОтОпасныхДействий" + "");
		ПараметрыЗащиты.ПредупреждатьОбОпасныхДействиях = Ложь;                                            
		
		Данные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		АдресХранилища = ПоместитьВоВременноеХранилище(Данные, Новый УникальныйИдентификатор());
		
		Обработка =  ВнешниеОбработки.Создать(ВнешниеОбработки.Подключить(АдресХранилища, , Ложь, ПараметрыЗащиты), Ложь);
		
		Попытка
			УдалитьФайлы(ИмяВременногоФайла);
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка выполнения'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	ОбъектыДействия = "";
	Если Обработка <> Неопределено И Обработка.Метаданные().Реквизиты.Найти("ОбъектДействия") <> Неопределено Тогда
		ОписаниеТипаОбъектаДействия = Обработка.Метаданные().Реквизиты.ОбъектДействия.Тип;
		ОписаниеТипов = Новый ХранилищеЗначения(ОписаниеТипаОбъектаДействия);
		
		МассивОбъектов = Новый Массив;
		Для каждого Тип Из ОписаниеТипаОбъектаДействия.Типы() Цикл
			ИдентификаторМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип);
			Если МассивОбъектов.Найти(ИдентификаторМетаданных) = Неопределено Тогда
				МассивОбъектов.Добавить(ИдентификаторМетаданных);
			КонецЕсли;
		КонецЦикла;
		Если МассивОбъектов.Количество() > 0 Тогда
			Для каждого Объект Из МассивОбъектов Цикл
				ОбъектыДействия = ОбъектыДействия + ?(ОбъектыДействия = "", "", "; ") + Объект.Синоним;
			КонецЦикла;
		Иначе
			ОбъектыДействия = НСтр("ru='Произвольный';en='The arbitrary'");
		КонецЕсли;
	Иначе
		ОписаниеТипов = Новый ХранилищеЗначения(Неопределено);
		ОбъектыДействия = НСтр("ru='Не определен';en='Not Stated'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте.';en='Invalid call of object on client.'");
#КонецЕсли
