
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		Параметры.Свойство("ИмяСервиса", Объект.ИмяСервиса);
	ИначеЕсли Объект.Предопределенный Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Изменение данного элемента не предусмотрено!'"));
		Возврат;
	КонецЕсли;
		
	Логотип = ПоместитьВоВременноеХранилище(CRM_ИнтеграцияССервисамиРассылок.КартинкаСервиса(
		Новый Структура("ИмяСервиса", Объект.ИмяСервиса), "Картинка_128"), УникальныйИдентификатор);
		
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(CRM_ИнтеграцияССервисамиРассылок.ТекстЗаголовка(Объект.ИмяСервиса));
	ТекстЗаголовка = СериализаторXDTO.ПрочитатьJSON(ЧтениеJSON, Тип("ФорматированнаяСтрока"));
	
	Элементы.ПериодОбновленияСтатусов.Видимость =
		CRM_ИнтеграцияССервисамиРассылок.ИспользуетсяОбновлениеСтатусов(Объект.ИмяСервиса);
	
	УправлениеВидимостьюЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	СтруктураНастроек = ТекущийОбъект.ХранилищеНастроек.Получить();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ХранилищеНастроек = Новый ХранилищеЗначения(СтруктураНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	Если Записать() Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Настройка(Команда)
	Если ЗначениеЗаполнено(Объект.ИмяСервиса) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаЗавершение", ЭтотОбъект);
		ОткрытьФорму("Обработка.CRM_РаботаССервисомРассылок" + Строка(Объект.ИмяСервиса) 
			+ ".Форма.ФормаНастройки", СтруктураНастроек,
				ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Модифицированность = Истина;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не выбран сервис рассылок!'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НастройкаЗавершение(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		СтруктураНастроек = Результат;
		Объект.Включен = Истина;
		УправлениеВидимостьюЭлементов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Отключить(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтключитьЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Сервис рассылок будет отключен, с удалением всех сделанных настроек.
                                             |Продолжить?'"), РежимДиалогаВопрос.ОКОтмена, , , НСтр("ru = 'Отключение сервиса рассылок'"));
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьЗавершение(Результат, ДопПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.ОК Тогда
		СтруктураНастроек = Неопределено;
		УправлениеВидимостьюЭлементов();
		Если НЕ Объект.Ссылка.Пустая() Тогда
			Записать();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеВидимостьюЭлементов()
	Элементы.Настройка.Заголовок = ?(СтруктураНастроек = Неопределено, НСтр("ru = 'Подключить'"), НСтр("ru = 'Изменить'"));
	Элементы.Отключить.Видимость = (СтруктураНастроек <> Неопределено);
КонецПроцедуры

#КонецОбласти 
