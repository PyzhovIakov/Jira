#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПолучитьЛоготипыСервисов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.CRM_ВыборКаналаДиалогов", 
		Новый Структура("ПрефиксОбработки, Заголовок", "CRM_РаботаССервисомРассылок", НСтр("ru = 'Выбор сервиса рассылки'")),
		ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗавершение(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ПараметрыФормы = Новый Структура("ИмяСервиса", Результат);
		ОткрытьФорму("Справочник.CRM_СервисыРассылок.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьЛоготипыСервисов()
	СписокСервисов = CRM_ИнтеграцияССервисамиРассылок.ПолучитьСписокСервисов();
	Для каждого Сервис Из СписокСервисов Цикл
		СтрокаКартинки = ПоместитьВоВременноеХранилище(Сервис.Картинка, УникальныйИдентификатор);
		НовЛого = ЛоготипыСервисов.Добавить();
		НовЛого.ИмяСервиса = Сервис.Значение;
		НовЛого.Логотип = СтрокаКартинки;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
