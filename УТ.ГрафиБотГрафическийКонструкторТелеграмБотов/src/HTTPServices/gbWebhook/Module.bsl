#Область ОбработчикиСобытий

Функция Webhook_post(Запрос)
	Перем Ответ;
	
	Попытка
		Ответ = Webhook_post_Исполняющая(Запрос);
	Исключение
		гбСервер.ОповеститьОбОшибке(Перечисления.гбСобытия.НеобработаннаяОшибка,
			ИнформацияОбОшибке(), Запрос.ПолучитьТелоКакСтроку());
		ПодробноеОписаниеОшибки = Неопределено;
		гбСервер.ПолучитьОписаниеОшибкиИзДанныхОшибки(ИнформацияОбОшибке(), , ПодробноеОписаниеОшибки);
		Ответ = Новый HTTPСервисОтвет(500);
		Ответ.УстановитьТелоИзСтроки(ПодробноеОписаниеОшибки);
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция Webhook_get(Запрос)
	Возврат default_get(Запрос);
КонецФункции

Функция default_get(Запрос)
	ОписаниеСистемы = гбСерверПовтИсп.ПолучитьМетаданныеРасширения("Синоним, АвторскиеПрава, АдресИнформацииОКонфигурации");
	Информация = СтрШаблон("<a href=""%2"">Подсистема ""%3""</a><br />
		|Компания: <a href=""%2"">%2</a><br />
		|Автор: <a href=""%2"">%1</a>", 
	    ОписаниеСистемы.АвторскиеПрава,
		ОписаниеСистемы.АдресИнформацииОКонфигурации,
		ОписаниеСистемы.Синоним);
	ЗаголовкиHTTP = Новый Соответствие;
	ЗаголовкиHTTP.Вставить("Content-Type", "text/html; charset=utf-8");
	
	Ответ = Новый HTTPСервисОтвет(200, , ЗаголовкиHTTP);
	Ответ.УстановитьТелоИзСтроки(Информация, , ИспользованиеByteOrderMark.НеИспользовать);
	Возврат Ответ;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Webhook_post_Исполняющая(Запрос)
	ВходящийТокен = Запрос.ПараметрыURL["token"];
	
	Попытка
		Бот = гбСервер.ПолучитьБотаПоТокену(ВходящийТокен);
	Исключение
		гбСервер.ОповеститьОбОшибке(Перечисления.гбСобытия.НеобработаннаяОшибка,
			ИнформацияОбОшибке(), Запрос.ПолучитьТелоКакСтроку());
		ПодробноеОписаниеОшибки = Неопределено;
		гбСервер.ПолучитьОписаниеОшибкиИзДанныхОшибки(ИнформацияОбОшибке(), , ПодробноеОписаниеОшибки);
		Ответ = Новый HTTPСервисОтвет(404);
		Ответ.УстановитьТелоИзСтроки(ПодробноеОписаниеОшибки);
		
		Возврат Ответ;
	КонецПопытки;

	Обновление = гбСервер.ОбъектИз_JSON(Запрос.ПолучитьТелоКакСтроку());
    гбСервер.ОбработатьВходящееОбновление(Бот, Обновление);
	
	Возврат Новый HTTPСервисОтвет(200);
	
КонецФункции

#КонецОбласти

