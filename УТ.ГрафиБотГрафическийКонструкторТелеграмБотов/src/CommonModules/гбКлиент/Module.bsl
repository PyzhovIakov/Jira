#Область ПрограммныйИнтерфейс


// Подключает обработчики сообщений системы взаимодействия
//
Процедура ПодключитьОбработчикиСообщенийСистемыВзаимодействия() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикПослеОтправкиСообщения", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикПослеОтправкиСообщения(ОписаниеОповещения);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикДействияСообщения", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикДействияСообщения(ОписаниеОповещения);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Процедура вызывается после интерактивной отправки сообщения в обсуждение
//
// Параметры:
//  Сообщение				 - СообщениеСистемыВзаимодействия	 - Сообщение
//  Обсуждение				 - ОбсуждениеСистемыВзаимодействия	 - Обсуждение
//  ДополнительныеПараметры	 - Произвольный	 - не используется
//
Процедура ОбработчикПослеОтправкиСообщения(Сообщение, Обсуждение, ДополнительныеПараметры) Экспорт
	
	Если гбКлиентСервер.ЭтоУникальныйИдентификатор(Обсуждение.Ключ) тогда
		гбСервер.ОтправитьСообщениеСистемыВзаимодействияВЧатТелеграм(Сообщение.Идентификатор, Сообщение.Текст, Обсуждение.Ключ);
	КонецЕсли;

КонецПроцедуры


// Процедура - Обработчик действия сообщения. Вызывает переход к нужному элементу блок-схемы бота
//
// Параметры:
//  Сообщение				 - СообщениеСистемыВзаимодействия	 - Сообщение
//  Действие				 - Структура	 - Структура со свойствами Бот, Чат, ИмяЭлемента
//  ДополнительныеПараметры	 - Произвольный	 - не используется
//
Процедура ОбработчикДействияСообщения(Сообщение, Действие, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Действие) = Тип("Структура") 
		и Действие.Свойство("Бот") и Действие.Свойство("Чат") и Действие.Свойство("ИмяЭлемента") тогда
		
		гбСервер.ПерейтиКЭлементуБлокСхемы(Действие.Бот, Действие.Чат, Действие.ИмяЭлемента);
				
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

