
&НаСервереБезКонтекста
Функция ПолучитьСписокРасширенийНаСервере()
	СписокРасширений = Новый СписокЗначений;
	ИмяРасширенияКонструктора = гбСерверПовтИсп.ПолучитьМетаданныеРасширения("Имя").Имя;
	
	Для Каждого Расширение из РасширенияКонфигурации.Получить(, ИсточникРасширенийКонфигурации.СеансАктивные) цикл
		Если Расширение.Имя = ИмяРасширенияКонструктора тогда
			Продолжить;
		ИначеЕсли ЗначениеЗаполнено(Справочники.гбРасширения.НайтиПоКоду(Расширение.Имя)) тогда
			Продолжить;
		КонецЕсли;
		СписокРасширений.Добавить(Расширение.Имя, Расширение.Синоним);
	КонецЦикла;
	Возврат СписокРасширений;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если не ЗначениеЗаполнено(Объект.Ссылка) тогда
		СписокРасширений = ПолучитьСписокРасширенийНаСервере();
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораРасширения", ЭтаФорма);
		Если СписокРасширений.Количество() = 0 тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
		ИначеЕсли СписокРасширений.Количество() = 1 тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, СписокРасширений[0]);
		Иначе
			СписокРасширений.ПоказатьВыборЭлемента(ОписаниеОповещения);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораРасширения(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено тогда
		Закрыть();
		ПоказатьПредупреждение(, "Нет доступных расширений конфигурации");
		Возврат;
	КонецЕсли;
	
	Объект.Код = Результат.Значение;
	Объект.Наименование = Результат.Представление;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ОбновлениеРасширения");
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	гбХранилище.ПриСозданииНаСервере_ФормаЭлемента(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	ОбработкаОповещенияНаСервере(ИмяСобытия);
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияНаСервере(ИмяСобытия)
	гбХранилище.ОбработкаОповещения_ФормаЭлемента(ЭтаФорма, ИмяСобытия);
КонецПроцедуры

