


Процедура ПередЗаписью(Отказ)
	Если не ДополнительныеСвойства.Свойство("ОбновлениеРасширения") тогда
		Возврат;
	ИначеЕсли ПометкаУдаления тогда
		Возврат;
	ИначеЕсли не гбХранилище.ВозможноРедактированиеОбъекта(Ссылка) тогда
		//Отказ = Истина;
		Сообщить("Изменения расширения не синхронизированы, изменения заблокированы: " + Код);
		Возврат;
	КонецЕсли;

	Расширения = РасширенияКонфигурации.Получить(Новый Структура("Имя", СокрЛП(Код)));
	
	Если Расширения.Количество() <> 1 тогда
		ПометкаУдаления = Истина;
	Иначе
		Расширение = Расширения[0];

		Код = Расширение.Имя;
		Наименование = Расширение.Синоним;
		Версия = Расширение.Версия;
		ХешСумма = гбХранилище.Хеш(Расширение.ХешСумма);

		Данные = Новый ХранилищеЗначения(Расширение.Получитьданные());
		
		СтруктураСвойств = Новый Структура( 
			"БезопасныйРежим, ЗащитаОтОпасныхДействий, ИспользуетсяВРаспределеннойИнформационнойБазе, ОбластьДействия");
		
		ЗаполнитьЗначенияСвойств(СтруктураСвойств, Расширение);
		
		Свойства = Новый ХранилищеЗначения(СтруктураСвойств);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ДополнительныеСвойства.Свойство("ОбновлениеРасширения") тогда
		Возврат;
	ИначеЕсли ПометкаУдаления тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("Имя", СокрЛП(Код));
	Расширения = РасширенияКонфигурации.Получить(Отбор);
	
	Если Расширения.Количество() <> 1 тогда
		Расширение = РасширенияКонфигурации.Создать();
	Иначе
		Расширение = Расширения[0];
	КонецЕсли;
	
	Если ХешСумма <> гбХранилище.Хеш(Расширение.ХешСумма) тогда
		Расширение.Записать(Данные.Получить());
		
		Расширения = РасширенияКонфигурации.Получить(Отбор);
		Расширение = Расширения[0];
	КонецЕсли;
	
	СтруктураСвойств = Свойства.Получить();
	Если СтруктураСвойств <> Неопределено тогда
		Для Каждого КлючЗначение из СтруктураСвойств цикл
			Если ЗначениеВСтрокуВнутр(КлючЗначение.Значение) <> ЗначениеВСтрокуВнутр(Расширение[КлючЗначение.Ключ]) тогда
				ЗаполнитьЗначенияСвойств(Расширение, СтруктураСвойств);
				Расширение.Записать();
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
