
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Перем ТекстПредупреждения;
	
	Если не ВыполнитьПроверкиНаСервере(ПараметрКоманды, ТекстПредупреждения) тогда
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодтвержденияПродолжения", ЭтотОбъект, ПараметрКоманды);
	ПоказатьВопрос(ОписаниеОповещения, "Объекты блок-схемы будут помещены в хранилище.
		|Объекты уже существующие в хранилище, будут получены из хранилища.", РежимДиалогаВопрос.ОКОтмена);

КонецПроцедуры


&НаКлиенте
Процедура ПослеПодтвержденияПродолжения(Результат, ПараметрКоманды) Экспорт
	
	Если Результат <> КодВозвратаДиалога.ОК тогда
		Возврат;
	КонецЕсли;
	
	СписокПодключений = ПолучитьСписокПодключений(ПараметрКоманды);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораПодключения", ЭтотОбъект, ПараметрКоманды);
	Если СписокПодключений.Количество() = 0 тогда
		ПоказатьПредупреждение(, "Доступные подключения к хранилищу отсутсвуют");
		Возврат;
	ИначеЕсли СписокПодключений.Количество() = 1 тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, СписокПодключений[0]);
	Иначе
		СписокПодключений.ПоказатьВыборЭлемента(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПроверкиНаСервере(БлокСхема, ТекстПредупреждения)
	ТекстПредупреждения = "";
	Запись = РегистрыСведений.гбИспользуемыеХранилищаБлокСхем.СоздатьМенеджерЗаписи();
	Запись.БлокСхема = БлокСхема;
	Запись.Прочитать();
	Если БлокСхема.ЭтоГруппа тогда
		ТекстПредупреждения	= "Операция не поддерживается для групп"
	ИначеЕсли БлокСхема.ЭтоВложеннаяБлокСхема тогда
		ТекстПредупреждения = "Подключение к хранилищу вложенных блок-схем не требуется";
	ИначеЕсли Запись.Выбран() тогда
		ТекстПредупреждения = "Блок-схема уже подключена к хранилищу: " + Запись.Хранилище;
	КонецЕсли;
	
	Возврат ПустаяСтрока(ТекстПредупреждения);
КонецФункции


&НаКлиенте
Процедура ПослеВыбораПодключения(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено тогда
		Возврат;
	КонецЕсли;
	
	
	гбХранилище.ПодключитьБлокСхемуКХранилищу(ДополнительныеПараметры, Результат.Значение);

	//Сообщить(СтрШаблон("Подключение выполнено: %1", Результат.Значение));
	ПоказатьПредупреждение(, СтрШаблон("Блок-схема успешно подключена к хранилищу: %1", Результат.Значение));
	
	ОповеститьОбИзменении(ТипЗнч(ДополнительныеПараметры));	

	Оповестить("ГрафиБот:ИзменениеСостоянияХранилищаБлокСхем");
КонецПроцедуры
	
&НаСервере
Функция ПолучитьСписокПодключений(БлокСхема)
	СписокПодключений = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	гбХранилищаБлокСхем.Ссылка КАК Значение
		|ИЗ
		|	Справочник.гбХранилищаБлокСхем КАК гбХранилищаБлокСхем
		|ГДЕ
		|	НЕ гбХранилищаБлокСхем.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СписокПодключений.Добавить(), ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	Возврат СписокПодключений;
	
КонецФункции