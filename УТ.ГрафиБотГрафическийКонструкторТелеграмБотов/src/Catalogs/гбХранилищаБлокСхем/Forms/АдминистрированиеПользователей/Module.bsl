
&НаКлиенте
Процедура ПользователиПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ТекущиеДанные = Пользователи.НайтиПоИдентификатору(Элементы.Пользователи.ТекущаяСтрока);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Хранилище", Хранилище);
	ПараметрыФормы.Вставить("ИмяПользователя", ТекущиеДанные.ИмяПользователя);
	ПараметрыФормы.Вставить("Администрирование", ТекущиеДанные.Администрирование);
	ПараметрыФормы.Вставить("ЗахватОбъектов", ТекущиеДанные.ЗахватОбъектов);
	ОткрытьФорму("Справочник.гбХранилищаБлокСхем.Форма.ИзменитьПользователя", ПараметрыФормы,,,,,
	    Новый ОписаниеОповещения("ПослеИзмененияПользователя", ЭтаФорма));
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьСписок(Неопределено);
КонецПроцедуры

&НаСервере
Процедура ПользователиПередУдалениемНаСервере()
	ХранилищеОбъект = Хранилище.ПолучитьОбъект();
	
	ТекущиеДанные = Пользователи.НайтиПоИдентификатору(Элементы.Пользователи.ТекущаяСтрока);
	
	гбХранилище.ВыполнитьУдаленно(ХранилищеОбъект, "ИзменитьПользователя", 
		ХранилищеОбъект.ИмяПользователя, ХранилищеОбъект.Пароль,
		ТекущиеДанные.ИмяПользователя, , , , , Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПользователиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	Попытка

		ПользователиПередУдалениемНаСервере();
		ОбновитьСписок(Неопределено);
		
	Исключение
		ПоказатьПредупреждение(, ОписаниеОшибки());
	КонецПопытки;

КонецПроцедуры

&НаКлиенте
Процедура ПользователиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Хранилище", Хранилище);
	ОткрытьФорму("Справочник.гбХранилищаБлокСхем.Форма.ИзменитьПользователя", ПараметрыФормы,,,,,
	    Новый ОписаниеОповещения("ПослеИзмененияПользователя", ЭтаФорма));
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияПользователя(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено тогда
		Возврат;
	КонецЕсли;

	ОбновитьСписок(Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	ХранилищеОбъект = Хранилище.ПолучитьОбъект();
	ТекИмяПользователя = ХранилищеОбъект.ИмяПользователя;
	Список = гбХранилище.ВыполнитьУдаленно(ХранилищеОбъект, "СписокПользователей", 
			ХранилищеОбъект.ИмяПользователя, ХранилищеОбъект.Пароль);
			
	Пользователи.Очистить();
	Для Каждого ЭлементСписка из Список Цикл
		НоваяСтрока = Пользователи.Добавить();
		НоваяСтрока.ИмяПользователя = ЭлементСписка.Представление;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементСписка.Значение);
	КонецЦикла;	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Хранилище = Параметры.Хранилище;
КонецПроцедуры
