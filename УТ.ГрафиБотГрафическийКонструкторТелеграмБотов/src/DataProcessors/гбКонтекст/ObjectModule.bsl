#Если Сервер тогда

#Область ПрограммныйИнтерфейс

// Выполняет фрагмент кода, который передается ему в качестве строкового значения
//
// Параметры:
//  __Текст__	 - Строка	 - Строка, содержащая текст исполняемого кода
//
Процедура __Выполнить__(__Текст__) Экспорт
	ч = ЭтотОбъект;
	
	Выполнить(__Текст__);
	
КонецПроцедуры

// Вычисляет переданное выражение. 
//
// Параметры:
//  __Текст__	 - Строка	 - Выражение, которое требуется вычислить.
// 
// Возвращаемое значение:
//  Произвольный - Результат вычисления выражения.
//
Функция __Вычислить__(__Текст__) Экспорт
	ч = ЭтотОбъект;
	
	Возврат Вычислить(__Текст__);
	
КонецФункции

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

// Отправляет текст в чат
//
// Параметры:
//  ТекстСообщения	 - Строка	 -  Текст отправляемого сообщения
//	Параметры	- Структура	- Дополнительные параметры, например КлавиатураСообщения(СписокЗначений)(не обязательно).
// 
// Возвращаемое значение:
//  Структура - Данные отправленного сообщения
//
Функция ОтправитьТекст(ТекстСообщения, Знач Параметры = Неопределено) Экспорт
	Возврат гбСервер.ОтправитьСообщение(Бот, Чат, ТекстСообщения, Параметры);
КонецФункции

// Отправляет самоудаляемый через 5 секунд статус в чат
//
// Параметры:
//  Состояние	 - Строка	 -  Текст отправляемого сообщения
//			typing for text messages
//			upload_photo for photos
//			upload_video for videos
//			record_video for video recording
//			upload_audio for audio files
//			record_audio for audio file recording
//			upload_document for general files
//			find_location for location data
//			upload_video_note for video notes
//			record_video_note for video note recording
// 
// Возвращаемое значение:
//  Структура - Данные отправленного сообщения
//
Функция ОтправитьСостояние(Состояние) Экспорт
	Возврат гбСервер.ОтправитьСостояние(Бот, Чат, Состояние);
КонецФункции

// Отправляет файл в чат
//
// Параметры:
//  Файл		 - 	Файл|Строка|ДвоичныеДанные|СписокЗначений - Файл, ИД файла, имя файла или двоичные данные отправляемого файла.
//  ТекстСообщения	 - 	Строка - Подпись сообщения (не обязательно)
//	Параметры	- Структура	- Дополнительные параметры, например КлавиатураСообщения(СписокЗначений)(не обязательно).
// 
// Возвращаемое значение:
//  Структура - Данные отправленного сообщения
//
Функция ОтправитьФайл(Файл, ТекстСообщения = Неопределено, Знач Параметры = Неопределено) Экспорт
	Параметры = гбСервер.СкопироватьСтруктуру(Параметры); 
	Параметры.Вставить("Текст", ТекстСообщения);
	Возврат гбСервер.ОтправитьФайл(Бот, Чат, Файл, Параметры);	
КонецФункции

// Отправляет фото в чат
//
// Параметры:
//  Файл		 - 	Файл|Строка|Картинка|СписокЗначений - Файл, ИД файла, имя файла или данные фото.
//  ТекстСообщения	 - 	Строка - Подпись сообщения (не обязательно)
//	Параметры	- Структура	- Дополнительные параметры, например КлавиатураСообщения(СписокЗначений)(не обязательно).
// 
// Возвращаемое значение:
//  Структура - Данные отправленного сообщения
//
Функция ОтправитьФото(Файл, ТекстСообщения = Неопределено, Знач Параметры = Неопределено) Экспорт
	Параметры = гбСервер.СкопироватьСтруктуру(Параметры); 
	Параметры.Вставить("Текст", ТекстСообщения);
	Возврат гбСервер.ОтправитьФайл(Бот, Чат, Файл, Параметры, Перечисления.гбТипыФайлов.Фото);	
КонецФункции

// Отправляет видео в чат
//
// Параметры:
//  Файл		 - 	Файл|Строка||ДвоичнныеДанные|СписокЗначений - Файл, ИД файла, имя файла или данные видео.
//  ТекстСообщения	 - 	Строка - Подпись сообщения (не обязательно)
//	Параметры	- Структура	- Дополнительные параметры, например КлавиатураСообщения(СписокЗначений)(не обязательно).
// 
// Возвращаемое значение:
//  Структура - Данные отправленного сообщения
//
Функция ОтправитьВидео(Файл, ТекстСообщения = Неопределено, Знач Параметры = Неопределено) Экспорт
	Параметры = гбСервер.СкопироватьСтруктуру(Параметры); 
	Параметры.Вставить("Текст", ТекстСообщения);
	Возврат гбСервер.ОтправитьФайл(Бот, Чат, Файл, Параметры, Перечисления.гбТипыФайлов.Видео);	
КонецФункции

// Отправляет аудио в чат
//
// Параметры:
//  Файл		 - 	Файл|Строка|ДвоичнныеДанные|СписокЗначений - Файл, ИД файла, имя файла или данные аудио.
//  ТекстСообщения	 - 	Строка - Подпись сообщения (не обязательно)
//	Параметры	- Структура	- Дополнительные параметры, например КлавиатураСообщения(СписокЗначений)(не обязательно).
// 
// Возвращаемое значение:
//  Структура - Данные отправленного сообщения
//
Функция ОтправитьАудио(Файл, ТекстСообщения = Неопределено, Знач Параметры = Неопределено) Экспорт
	Параметры = гбСервер.СкопироватьСтруктуру(Параметры); 
	Параметры.Вставить("Текст", ТекстСообщения);
	Возврат гбСервер.ОтправитьФайл(Бот, Чат, Файл, Параметры, Перечисления.гбТипыФайлов.Аудио);	
КонецФункции

// Отправляет контакт в чат
//
// Параметры:
//  Имя				 - 	Строка - Имя контактного лица
//  НомерТелефона	 - 	Строка - Номер телефона
//  ТекстСообщения	 - 	Строка - Подпись сообщения (не обязательно)
//	Параметры	- Структура	- Дополнительные параметры, например КлавиатураСообщения(СписокЗначений)(не обязательно).
// 
// Возвращаемое значение:
//  Структура - Данные отправленного сообщения
//
Функция ОтправитьКонтакт(Имя, НомерТелефона, ТекстСообщения = Неопределено, Знач Параметры = Неопределено) Экспорт
	Если не ПустаяСтрока(ТекстСообщения) тогда
		Сообщение = ОтправитьТекст(ТекстСообщения);	
	КонецЕсли;
	
	Параметры = гбСервер.СкопироватьСтруктуру(Параметры); 
	Параметры.Вставить("ОтветНа", Сообщение);
	
	Возврат гбСервер.ОтправитьКонтакт(Бот, Чат, 
		Новый Структура("НомерТелефона, Имя", НомерТелефона, Имя), Параметры);	
КонецФункции 

// Отправляет позицию в чат
//
// Параметры:
//  Широта			 - 	Число - Широта
//  Долгота			 - 	Число - Долгота
//  ТекстСообщения	 - 	Строка - Подпись сообщения (не обязательно)
//	Параметры	- Структура	- Дополнительные параметры, например КлавиатураСообщения(СписокЗначений)(не обязательно).
// 
// Возвращаемое значение:
//  Структура - Данные отправленного сообщения
//
Функция ОтправитьПозицию(Широта, Долгота, ТекстСообщения = Неопределено, Знач Параметры = Неопределено) Экспорт
	Если не ПустаяСтрока(ТекстСообщения) тогда
		Сообщение = ОтправитьТекст(ТекстСообщения);	
	КонецЕсли;
	
	Параметры = гбСервер.СкопироватьСтруктуру(Параметры); 
	Параметры.Вставить("ОтветНа", Сообщение);
	Возврат гбСервер.ОтправитьПозицию(Бот, Чат, 
		Новый Структура("Широта, Долгота", Широта, Долгота), Параметры);	
КонецФункции

// Определяет есть ли команда с указанным именем в последних полученных данных
//
// Параметры:
//  Команда			 - 	Строка - Имя команды
//  Параметр			 - 	Произвольный - возвращает параметр команды (не обязательно)
// 
// Возвращаемое значение:
//  Булево - Есть / нет
//
Функция ЕстьКомандаВДанных(Команда, Параметр = Неопределено) Экспорт
	Возврат ТипЗнч(Данные) = Тип("ФиксированнаяСтруктура")
		и Данные.Свойство("Команды") и Данные.Команды.Свойство(Команда, Параметр);
КонецФункции

// Определяет есть ли команды в последних полученных данных
//
// Возвращаемое значение:
//  Булево - Есть / нет
//
Функция ЕстьКомандыВДанных() Экспорт
	Возврат ТипЗнч(Данные) = Тип("ФиксированнаяСтруктура") и Данные.Свойство("Команды") и Данные.Команды.Количество();
КонецФункции

// Определяет есть ли файл в последних полученных данных
// 
// Возвращаемое значение:
//  Булево - Есть / нет
//
Функция ЕстьФайлВДанных() Экспорт
	Возврат ТипЗнч(Данные) = Тип("ФиксированнаяСтруктура") и Данные.Свойство("Файл");
КонецФункции

// Определяет есть ли документ в последних полученных данных
// 
// Возвращаемое значение:
//  Булево - Есть / нет
//
Функция ЕстьДокументВДанных() Экспорт
	Возврат ТипЗнч(Данные) = Тип("ФиксированнаяСтруктура") и Данные.Свойство("Документ");
КонецФункции

// Определяет есть ли фото в последних полученных данных
// 
// Возвращаемое значение:
//  Булево - Есть / нет
//
Функция ЕстьФотоВДанных() Экспорт
	Возврат ТипЗнч(Данные) = Тип("ФиксированнаяСтруктура") и Данные.Свойство("Фото");
КонецФункции

// Определяет есть ли видео в последних полученных данных
// 
// Возвращаемое значение:
//  Булево - Есть / нет
//
Функция ЕстьВидеоВДанных() Экспорт
	Возврат ТипЗнч(Данные) = Тип("ФиксированнаяСтруктура") и Данные.Свойство("Видео");
КонецФункции

// Получает файл из последних полученных данных
// 
// Возвращаемое значение:
//  Строка - Имя полученного файла
//
Функция ПолучитьФайлИзДанных() Экспорт
	Перем ИмяФайла;
	Данные.Файл.Свойство("file_name", ИмяФайла);
	Возврат гбСервер.ПолучитьФайлПоИд(Бот, Чат, Данные.Файл.file_id, ИмяФайла);
КонецФункции

// Подключает оператора к чату
//
// Параметры:
//  ИмяПользователя	 - Строка	 - Имя пользователя информационной базы (оператора)
//  ТекстСообщения	 - Строка	 - Текст отправляемого оператору сообщения (не обязательно)
//
Процедура ПодключитьОператора(ИмяПользователя, ТекстСообщения = Неопределено) Экспорт
	гбСервер.ОтправитьСообщениеОператору(Бот, Чат, ИмяПользователя, ТекстСообщения);
КонецПроцедуры

// Подключает оператора к чату
//
// Параметры:
//  ИмяПользователя	 - Строка	 - Имя пользователя информационной базы (оператора)
//  ТекстСообщения	 - Строка	 - Текст отправляемого оператору сообщения (не обязательно)
//
Процедура ОтправитьСообщениеОператору(ИмяПользователя, ТекстСообщения = Неопределено) Экспорт
	гбСервер.ОтправитьСообщениеОператору(Бот, Чат, ИмяПользователя, ТекстСообщения);
КонецПроцедуры

// Возвращает представление значения. Структуры, массивы, списки раскрывает по свойствам рекурсивно
// 
// Параметры:
//  Значение			 - 	Произвольный - значение для получения представления.
//
// Возвращаемое значение:
//  Строка - представление данных
//
Функция ПредставлениеЗначения(Значение) Экспорт
	Возврат гбСервер.ПредставлениеЗначения(Значение);
КонецФункции

// Закрепляет сообщение
//
// Параметры:
//  Сообщение	 - Структура|Число	 - закрепляемое сообщение
//
Процедура СообщениеЗакрепить(Сообщение) Экспорт
	
	гбСервер.ЗакрепитьСообщение(Бот, Чат, Сообщение);
	
КонецПроцедуры

// Открепляет сообщение
//
// Параметры:
//  Сообщение	 - Структура|Число	 - открепляемое сообщение
//
Процедура СообщениеОткрепить(Сообщение) Экспорт
	
	гбСервер.ОткрепитьСообщение(Бот, Чат, Сообщение);
	
КонецПроцедуры

// Открепляет все сообщение
//
Процедура СообщенияОткрепитьВсе() Экспорт
	
	гбСервер.ОткрепитьВсеСообщения(Бот, Чат);
	
КонецПроцедуры

// Удаляет сообщение
//
// Параметры:
//  Сообщение	 - Структура|Число	 - удаляемое сообщение
//
Процедура СообщениеУдалить(Сообщение) Экспорт
	
	гбСервер.УдалитьСообщение(Бот, Чат, Сообщение);
	
КонецПроцедуры

// Планирование автоудаления сообщения
//
// Параметры:
//  Сообщение	 - Структура|Число	 - удаляемое сообщение
//  УдалитьПосле	 - Дата|Число	 - Когда или через сколько секунд удалить
//
Процедура СообщениеАвтоудаление(Сообщение, УдалитьПосле) Экспорт
	
	гбСервер.АвтоудалениеСообщения(Чат, Сообщение, УдалитьПосле);
	
КонецПроцедуры

// Получает последнее отправленное сообщение
// 
// Возвращаемое значение:
//  Структура|Массив - последнее отправленное сообщение
//
Функция СообщениеОтправленное() Экспорт
	Возврат гбСервер.ОтправленноеСообщение();
КонецФункции

// Удаляет клавиатуру ответа
//
Процедура КлавиатураОтветаУдалить() Экспорт
	
	гбСервер.УдалитьКлавиатуруОтвета(Бот, Чат, Истина);
	
КонецПроцедуры

// Удаляет клавиатуру ответа
//
Процедура КлавиатураОтветаВосстановить() Экспорт
	
	гбСервер.ВосстановитьКлавиатуруОтвета(Бот, Чат, Истина);
	
КонецПроцедуры



// Получает список команд бота
//
// Возвращаемое значение:
//  СписокЗначений - команды бота, значение элементов - имя команды, представлеие - описание 
//
Функция КомандыБотаПолучить() Экспорт
	Возврат гбСервер.ПолучитьКомандыБота(Бот);
КонецФункции


// Устанавливает список команд бота
//
// Параметры:
//  СписокКоманд - СписокЗначений - команды бота, значение элементов - имя команды, представлеие - описание 
//
Процедура КомандыБотаУстановить(СписокКоманд) Экспорт
	гбСервер.УстановитьКомандыБота(Бот, СписокКоманд);
КонецПроцедуры


// Очищает список команд бота
//
Процедура КомандыБотаОчистить() Экспорт
	гбСервер.ОчиститьКомандыБота(Бот);
КонецПроцедуры

#КонецОбласти

#КонецЕсли
