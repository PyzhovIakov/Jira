
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Пользователи.Ссылка как Пользователь,
		|	ЛОЖЬ как Пометка
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи";
	
	РезультатЗапроса = Запрос.Выполнить();	
	ДетальныеЗаписи = РезультатЗапроса.Выгрузить();	
	СписокПользователей.Загрузить(ДетальныеЗаписи);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СкопироватьНастройки(Команда)
	СкопироватьНастройкиНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура СкопироватьНастройкиНаСервере()
	Если Не ЗначениеЗаполнено(Оригинал) ИЛИ Не ЗначениеЗаполнено(Доска)Тогда
		ОбщегоНазначения.СообщитьПользователю("Введите образец и доску");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	cbr_НастройкиПользователейКолонокКанБан.Номер КАК Номер,
		|	cbr_НастройкиПользователейКолонокКанБан.НастройкиКолонки,
		|	cbr_НастройкиПользователейКолонокКанБан.Наименование,
		|	cbr_НастройкиПользователейКолонокКанБан.Цвет
		|ИЗ
		|	РегистрСведений.cbr_НастройкиПользователейКолонокКанБан КАК cbr_НастройкиПользователейКолонокКанБан
		|ГДЕ
		|	cbr_НастройкиПользователейКолонокКанБан.Пользователь = &Пользователь
		|	И cbr_НастройкиПользователейКолонокКанБан.Доска = &Доска
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номер";
	Запрос.УстановитьПараметр("Пользователь", Оригинал);
	Запрос.УстановитьПараметр("Доска", Доска);
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	Для Каждого Пользователь Из СписокПользователей Цикл
		Если Не Пользователь.Пометка Тогда
			Продолжить;
		КонецЕсли;

		Выборка.Сбросить();
		Пока Выборка.Следующий() Цикл
			НовыеНастройки = Выборка.НастройкиКолонки.Получить();
			ИзменитьОтборКомпоновщика(Пользователь.Пользователь, НовыеНастройки.Отбор.Элементы);
			
			РегистрыСведений.cbr_НастройкиПользователейКолонокКанБан.ЗаписатьКолонкуПользователя(
				Пользователь.Пользователь, Доска, новый ХранилищеЗначения(НовыеНастройки), Выборка.Наименование, Выборка.Цвет, Выборка.Номер);
		КонецЦикла;
	КонецЦикла;
	ОбщегоНазначения.СообщитьПользователю("Готово");
КонецПроцедуры

&НаСервере
Процедура ИзменитьОтборКомпоновщика(Пользователь, Настройки)
	Для Каждого ЭлементОтбора Из Настройки Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если ЭлементОтбора.ПравоеЗначение = Оригинал Тогда
				ЭлементОтбора.ПравоеЗначение = Пользователь;
			КонецЕсли;
		Иначе
			ИзменитьОтборКомпоновщика(Пользователь, ЭлементОтбора.Элементы);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

