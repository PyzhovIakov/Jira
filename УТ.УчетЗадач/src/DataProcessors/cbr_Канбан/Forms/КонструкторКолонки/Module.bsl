#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Доска = Параметры.Доска;
	Номер = Параметры.Номер;
	
	ЦветКолонки = Неопределено;
	Если Параметры.Свойство("Цвет", ЦветКолонки) Тогда
		Если ЗначениеЗаполнено(ЦветКолонки) Тогда
			Цвет = ЦветКолонки;
		КонецЕсли;
	КонецЕсли;

	ПараметрНаименование = Неопределено;
	Если Параметры.Свойство("Наименование", ПараметрНаименование) Тогда
		Если ЗначениеЗаполнено(ПараметрНаименование) Тогда
			Наименование = ПараметрНаименование;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.БезНастроек) Тогда
			БезНастроек = Параметры.БезНастроек;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьПоУмолчанию(Команда)
	Если ЗначениеЗаполнено(Наименование) Тогда
		СохранитьПоУмолчаниюНаСервере();
		Закрыть();
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Введите наименование");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьКонструктор(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	Если ЗначениеЗаполнено(Наименование) Тогда
		ПроверитьКолонкиПользователя();
		СохранитьНаСервере();
		Закрыть();
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Введите наименование");
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьКолонкиПользователя()
	Пользователь = Пользователи.АвторизованныйПользователь();

	Набор = РегистрыСведений.cbr_НастройкиПользователейКолонокКанБан.СоздатьНаборЗаписей();
	Набор.Отбор.Доска.Установить(Доска);
	Набор.Отбор.Пользователь.Установить(Пользователь);
	Набор.Прочитать();

	Если Не Набор.Количество() Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	cbr_НастройкиПользователейКолонокКанБан.Номер,
		|	cbr_НастройкиПользователейКолонокКанБан.НастройкиКолонки,
		|	cbr_НастройкиПользователейКолонокКанБан.Наименование
		|ИЗ
		|	РегистрСведений.cbr_НастройкиПользователейКолонокКанБан КАК cbr_НастройкиПользователейКолонокКанБан
		|ГДЕ
		|	cbr_НастройкиПользователейКолонокКанБан.Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|	И cbr_НастройкиПользователейКолонокКанБан.Доска = &Доска";

		Запрос.УстановитьПараметр("Доска", Доска);

		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();

		Пока Выборка.Следующий() Цикл
			Запись = Набор.Добавить();
			Запись.Доска = Доска;
			Запись.Пользователь = Пользователь;
			Запись.Наименование = Выборка.Наименование;
			Запись.Номер = Выборка.Номер;
			Запись.НастройкиКолонки = Выборка.НастройкиКолонки;
		КонецЦикла;

	КонецЕсли;

	Набор.Записать();
КонецПроцедуры

&НаСервере
Процедура СохранитьПоУмолчаниюНаСервере()
	ЗаписьНастройки = Новый ХранилищеЗначения(Колонка.КомпоновщикНастроек.ПолучитьНастройки());

	РегистрыСведений.cbr_НастройкиПользователейКолонокКанБан.ЗаписатьКолонкуПоУмолчанию(Доска, ЗаписьНастройки,
		Наименование, Цвет);
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	ЗаписьПользователь = Пользователи.АвторизованныйПользователь();
	ЗаписьНастройки = Новый ХранилищеЗначения(Колонка.КомпоновщикНастроек.ПолучитьНастройки());

	РегистрыСведений.cbr_НастройкиПользователейКолонокКанБан.ЗаписатьКолонкуПользователя(ЗаписьПользователь, Доска,
		ЗаписьНастройки, Наименование, Цвет, Номер);
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере()
	Если Не БезНастроек Тогда
		Настройки = Параметры.НастройкиКолонки.ПолучитьНастройки();
		Колонка.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	Иначе
		ТипЭлементаПорядка = Тип("ЭлементПорядкаКомпоновкиДанных");
		Колонка.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[1].Элементы.Очистить();
		ЭлементПорядка = Колонка.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[1].Элементы.Добавить(
			ТипЭлементаПорядка);
		ЭлементПорядка.Поле = Новый ПолеКомпоновкиДанных("Приоритет");
		ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ЭлементПорядка.Использование = Истина;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти