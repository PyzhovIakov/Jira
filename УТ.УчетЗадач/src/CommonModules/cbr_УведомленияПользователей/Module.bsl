#Область ПрограммныйИнтерфейс
// Отправляет уведомление с указанными параметрами выбранному пользователю
// Параметры:
// 		СвойстваУведомления - Структура
//			Пользователь - СправочникСсылка.Пользователи,
//			Описание - Строка,
//			СрокНапоминания - Дата,
//			ВремяСобытия - Дата,
//			СпособУстановкиВремениНапоминания - ПеречислениеСсылка.СпособыУстановкиВремениНапоминания,
//			ИнтервалВремениНапоминания - Число,
//			Источник - ДокументСсылка.cbr_Задача,
//			Идентификатор - Строка.
//		ПринудительныеУведомления - Структура
//			База - Булево,
//			Битрикс - Булево,
//			Телеграм - Булево,
//			ЭлектроннаяПочта - Булево.
Процедура ОтправитьУведомлениеПользователю(СвойстваУведомления, ПринудительныеУведомления) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_ПользовательскиеНастройки.ВидНастройки КАК ВидНастройки,
	|	cbr_ПользовательскиеНастройки.Значение КАК Значение
	|ПОМЕСТИТЬ вт_Настройки
	|ИЗ
	|	РегистрСведений.cbr_ПользовательскиеНастройки КАК cbr_ПользовательскиеНастройки
	|ГДЕ
	|	cbr_ПользовательскиеНастройки.Пользователь = &Пользователь
	|	И cbr_ПользовательскиеНастройки.ВидНастройки В
	|	(ЗНАЧЕНИЕ(ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.База),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.ЭлектроннаяПочта),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.Битрикс),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.Телеграм))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(вт_Настройки.Значение, ЛОЖЬ) КАК Значение,
	|	cbr_ВидыПользовательскихНастроек.Ссылка КАК ВидНастройки
	|ИЗ
	|	ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек КАК cbr_ВидыПользовательскихНастроек
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_Настройки КАК вт_Настройки
	|		ПО cbr_ВидыПользовательскихНастроек.Ссылка = вт_Настройки.ВидНастройки
	|ГДЕ
	|	cbr_ВидыПользовательскихНастроек.Ссылка В (ЗНАЧЕНИЕ(ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.База),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.ЭлектроннаяПочта),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.Битрикс),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.Телеграм))";

	Запрос.УстановитьПараметр("Пользователь", СвойстваУведомления.Пользователь);
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		Если Выборка.ВидНастройки = ПредопределенноеЗначение(
			"ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.ЭлектроннаяПочта") И (Выборка.Значение
			Или ПринудительныеУведомления.ЭлектроннаяПочта) Тогда

		КонецЕсли;

		Если Выборка.ВидНастройки = ПредопределенноеЗначение(
			"ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.Телеграм") И (Выборка.Значение
			Или ПринудительныеУведомления.Телеграм) Тогда
			СвойстваУведомления.Описание = ДобавитьСсылкиНаПредметУведомленияКТекстуУведомления(
				СвойстваУведомления.Описание, СвойстваУведомления.Источник, Истина, Истина, Истина);
			cbr_РаботаСTelegram.ОтправитьСообщениеПользователюПоСсылке(СвойстваУведомления.Пользователь,
				СвойстваУведомления.Описание, СвойстваУведомления.Источник);
		КонецЕсли;

		Если Выборка.ВидНастройки = ПредопределенноеЗначение(
			"ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.Битрикс") И (Выборка.Значение
			Или ПринудительныеУведомления.Битрикс) Тогда

		КонецЕсли;

		Если Выборка.ВидНастройки = ПредопределенноеЗначение(
			"ПланВидовХарактеристик.cbr_ВидыПользовательскихНастроек.База") И (Выборка.Значение
			Или ПринудительныеУведомления.База) Тогда
			НапоминанияПользователяСлужебный.ПодключитьНапоминание(СвойстваУведомления, Ложь, Ложь);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьСсылкиНаПредметУведомленияКТекстуУведомления(ТекстУведомления, ПредметУведомления,
	ДобавитьПредставлениеДокумента = Ложь, ДобавитьНавигационнуюСсылку = Ложь, ДобавитьВнешнююСсылку = Ложь)
	Если ТекстУведомления = "" Или ПредметУведомления = Неопределено Тогда
		Возврат ТекстУведомления;
	КонецЕсли;

	РазделительДобавлен = Ложь;
	Разделитель = Символы.ПС + "..." + Символы.ПС;
	ПредставлениеСсылки = "";
	ВнешняяНавигационаяСсылка = "";
	НавигационаяСсылка = "";

	Если ДобавитьПредставлениеДокумента Тогда
		ПредставлениеСсылки = Строка(ПредметУведомления);

		Если Не РазделительДобавлен Тогда
			ТекстУведомления = ТекстУведомления + Разделитель;
			РазделительДобавлен = Истина;
		Иначе
			ТекстУведомления = ТекстУведомления + Символы.ПС;
		КонецЕсли;

		ТекстУведомления = ТекстУведомления + ПредставлениеСсылки;
	КонецЕсли;

	Если ДобавитьВнешнююСсылку Тогда
		ВнешняяНавигационаяСсылка = ПолучитьВнешнююНавигационнуюСсылку(ПредметУведомления);
		Если Не ВнешняяНавигационаяСсылка = ПредставлениеСсылки Тогда
			ВнешняяНавигационаяСсылка = "Внешняя навигационая ссылка - "+"""["+ВнешняяНавигационаяСсылка+"]""";
			
			Если Не РазделительДобавлен Тогда
				ТекстУведомления = ТекстУведомления + Разделитель;
				РазделительДобавлен = Истина;
			Иначе
				ТекстУведомления = ТекстУведомления + Символы.ПС;
			КонецЕсли;

			ТекстУведомления = ТекстУведомления + ВнешняяНавигационаяСсылка;
		КонецЕсли;
	КонецЕсли;

	Если ДобавитьНавигационнуюСсылку Тогда
		НавигационаяСсылка = ПолучитьНавигационнуюСсылку(ПредметУведомления);
		
		НавигационаяСсылка = "Навигационная ссылка - "+"""["+НавигационаяСсылка+"]""";
		
		Если Не РазделительДобавлен Тогда
			ТекстУведомления = ТекстУведомления + Разделитель;
			РазделительДобавлен = Истина;
		Иначе
			ТекстУведомления = ТекстУведомления + Символы.ПС;
		КонецЕсли;

		ТекстУведомления = ТекстУведомления + НавигационаяСсылка;
		
	КонецЕсли;
	
	Возврат ТекстУведомления;

КонецФункции
#КонецОбласти