
#Область  ПрограммныйИнтерфейс
// Процедура регламентного задания на проверку задач
Процедура ПроверкаВыполненияЭтаповКонтроля() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	cbr_КонтрольНадЭтапамиЗадач.Запрос КАК Запрос,
		|	cbr_КонтрольНадЭтапамиЗадач.Ссылка КАК Ссылка,
		|	cbr_КонтрольНадЭтапамиЗадачПараметры.Параметр,
		|	cbr_КонтрольНадЭтапамиЗадачПараметры.Значение,
		|	cbr_КонтрольНадЭтапамиЗадач.Длительность
		|ИЗ
		|	Справочник.cbr_КонтрольНадЭтапамиЗадач КАК cbr_КонтрольНадЭтапамиЗадач
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.cbr_КонтрольНадЭтапамиЗадач.Параметры КАК cbr_КонтрольНадЭтапамиЗадачПараметры
		|		ПО cbr_КонтрольНадЭтапамиЗадач.Ссылка = cbr_КонтрольНадЭтапамиЗадачПараметры.Ссылка
		|ИТОГИ
		|	МАКСИМУМ(Запрос)
		|ПО
		|	Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ОчиститьРегистрСведенийКонтроля();
	Пока Выборка.Следующий() Цикл
		Попытка
			 ВыполнениеЗапросаИзСправочника(Выборка, Неопределено);		
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Ошибка Выполнения");
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры

// Функция для получения массива текущих этапов задачи
// Параметры:
//		Задача - ДокументСсылка.cbr_Задача - Задача, для которой тебуется выполнить запрос. При пустом значении запрос выполнится без отбора
// Возвращаемое значение:
//		Массив Из Строка - Сообщения о текущих этапах
Функция ПолучитьТекущиеЭтапыЗадачи(Задача = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	cbr_КонтрольНадЭтапамиЗадач.Запрос КАК Запрос,
		|	cbr_КонтрольНадЭтапамиЗадач.Ссылка КАК Ссылка,
		|	cbr_КонтрольНадЭтапамиЗадачПараметры.Параметр,
		|	cbr_КонтрольНадЭтапамиЗадачПараметры.Значение,
		|	cbr_КонтрольНадЭтапамиЗадач.Длительность
		|ИЗ
		|	Справочник.cbr_КонтрольНадЭтапамиЗадач КАК cbr_КонтрольНадЭтапамиЗадач
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.cbr_КонтрольНадЭтапамиЗадач.Параметры КАК cbr_КонтрольНадЭтапамиЗадачПараметры
		|		ПО cbr_КонтрольНадЭтапамиЗадач.Ссылка = cbr_КонтрольНадЭтапамиЗадачПараметры.Ссылка
		|ИТОГИ
		|	МАКСИМУМ(Запрос)
		|ПО
		|	Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	МассивСообщений = Новый Массив;
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
 	Пока Выборка.Следующий() Цикл
		Попытка
			 РезультатЗапросаЭлемента = ВыполнениеЗапросаИзСправочника(Выборка, Задача);	
			 
			 Если ЗначениеЗаполнено(РезультатЗапросаЭлемента) Тогда
			 	МассивСообщений.Добавить(РезультатЗапросаЭлемента);
			 КонецЕсли;
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Ошибка получения этапов задачи");
		КонецПопытки;
	КонецЦикла;
	
	Возврат МассивСообщений;
КонецФункции
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ОчиститьРегистрСведенийКонтроля()
	Набор = РегистрыСведений.cbr_ПросроченныеДействияПоЗадачам.СоздатьНаборЗаписей();
	Набор.Прочитать();
	Набор.Очистить();
	Набор.Записать();
КонецПроцедуры

Функция ВыполнениеЗапросаИзСправочника(СправочникКонтроля, Задача = Неопределено)
	Запрос = Новый Запрос;
	Запрос.Текст = СправочникКонтроля.Запрос;
	ВыборкаПараметровКонтроля = СправочникКонтроля.Выбрать();
	Пока ВыборкаПараметровКонтроля.Следующий() Цикл
		Запрос.УстановитьПараметр(ВыборкаПараметровКонтроля.Параметр, ВыборкаПараметровКонтроля.Значение);
	КонецЦикла;
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	Если ЗначениеЗаполнено(Задача) Тогда
		Запрос.УстановитьПараметр("Задача", Задача);
		Запрос.УстановитьПараметр("Длительность", 0);
		
		РезультатЗапросаСправочника = Запрос.Выполнить();
		ВыборкаЗапроса = РезультатЗапросаСправочника.Выбрать();
		Если ВыборкаЗапроса.Следующий() Тогда
			РезультатЗапроса = ОбработкаРезультатаЗапроса(ВыборкаЗапроса, СправочникКонтроля.Длительность, Задача,
				СправочникКонтроля.Ссылка);
			
			Возврат РезультатЗапроса;
		КонецЕсли;
	Иначе
		Запрос.УстановитьПараметр("Задача", Null);
		Запрос.УстановитьПараметр("Длительность", СправочникКонтроля.Длительность);
	
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			РуководительПользователя = cbr_ОбработчикиФормыЗадачиСервер.РуководительПользователя(Выборка.Пользователь);
			
			Запись = РегистрыСведений.cbr_ПросроченныеДействияПоЗадачам.СоздатьМенеджерЗаписи();
			Запись.Задача = Выборка.Ссылка;
			Запись.Пользователь = Выборка.Пользователь;
			Запись.Этап = СправочникКонтроля.Ссылка;
			Запись.Руководитель = РуководительПользователя;
			Запись.Записать();
		КонецЦикла;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Функция ОбработкаРезультатаЗапроса(ВыборкаЗапроса, Длительность, Задача, Этап)
	ПрефиксСтроки = "Текущий этап";
	СуффиксСтроки = "осталось";
	
	ОсталосьМинут = (Длительность - ВыборкаЗапроса.ВремяСНачалаЭтапа) / 60;
	
	Если ОсталосьМинут < 0 Тогда
		ОсталосьМинут = - ОсталосьМинут;
		ПрефиксСтроки = "Просрочен этап";
		СуффиксСтроки = "на";
	КонецЕсли;
	
	СтрокаСрока = СтрШаблон("%1 ч. %2 мин.", Формат(ОсталосьМинут / 60, "ЧДЦ=0;"), Формат(ОсталосьМинут % 60, "ЧДЦ=0;"));
	НазваниеЭтапа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Этап, "Наименование");
	СтрокаСообщения = СтрШаблон("%1: %2, %3 %4", ПрефиксСтроки, НазваниеЭтапа, СуффиксСтроки, СтрокаСрока);
	
	Возврат СтрокаСообщения;
КонецФункции
#КонецОбласти