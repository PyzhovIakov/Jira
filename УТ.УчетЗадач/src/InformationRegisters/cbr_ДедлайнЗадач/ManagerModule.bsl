#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции
Процедура СоздатьЗаписьСогласования(Задача, Согласующий, Дедлайн, КомментарийОценки, 
	ПричинаПереноса = Неопределено) Экспорт
	НачатьТранзакцию();
	Попытка
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Задача = Задача;
		МенеджерЗаписи.КомментарийОценки = КомментарийОценки;
		МенеджерЗаписи.Период = ТекущаяДатаСеанса();
		МенеджерЗаписи.Согласующий = Согласующий;
		МенеджерЗаписи.Согласовант = Пользователи.ТекущийПользователь();
		МенеджерЗаписи.Дедлайн = Дедлайн;
		Если ПричинаПереноса <> Неопределено Тогда
			МенеджерЗаписи.ПричинаПереноса = ПричинаПереноса;
		КонецЕсли;
		МенеджерЗаписи.Записать();
		
		МенеджерЗаписи = РегистрыСведений.cbr_СобытияПоЗадачам.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
		МенеджерЗаписи.АвторСобытия = Пользователи.АвторизованныйПользователь();
		МенеджерЗаписи.Задача = Задача;
		МенеджерЗаписи.Событие = СтрШаблон("Отправлен на согласование дедлайн %1", Формат(Дедлайн, "ДФ=dd.MM.yyyy;"));
		МенеджерЗаписи.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
КонецПроцедуры

Процедура ИзменитьПоследнююЗаписьПоЗадаче(Задача, Отказ, Согласовано, КомментарийОтказа = Неопределено) Экспорт
	НачатьТранзакцию();
	Попытка
		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Задача.Установить(Задача);
		Набор.Прочитать();
		Запись = Набор[Набор.Количество() - 1];
		Запись.Согласовано = Согласовано;
		Запись.Отказ = Отказ;
		Если КомментарийОтказа <> Неопределено Тогда
			Запись.КомментарийОтказа = КомментарийОтказа;
		КонецЕсли;
		Набор.Записать();
		
		МенеджерЗаписи = РегистрыСведений.cbr_СобытияПоЗадачам.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
		МенеджерЗаписи.АвторСобытия = Пользователи.АвторизованныйПользователь();
		МенеджерЗаписи.Задача = Задача;
		ТекстСобытия = "Оценка дедлайна по задаче";
		Если Отказ Тогда
			ТекстСобытия = ТекстСобытия + " не согласована";
		ИначеЕсли Согласовано Тогда
			ТекстСобытия = ТекстСобытия + " согласована";
		КонецЕсли;
		МенеджерЗаписи.Событие = ТекстСобытия;
		МенеджерЗаписи.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
КонецПроцедуры

Процедура ОтменитьНесогласованные(Задача, Согласующий) Экспорт
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Задача.Установить(Задача);
	Набор.Прочитать();
	
	Для Каждого Запись Из Набор Цикл
		Если Запись.Согласующий = Согласующий И Не Запись.Согласовано И Не Запись.Отказ Тогда
			Запись.Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Набор.Записать();
КонецПроцедуры
#КонецОбласти
#КонецЕсли