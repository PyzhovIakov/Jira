#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции
Процедура СоздатьЗаписьСогласования(Задача, ТипОценки, Длительность, КомментарийОценки, Согласующий,	
	ЧасыОт = Неопределено, ЧасыДо = Неопределено) Экспорт
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Задача = Задача;
	МенеджерЗаписи.ТипОценки = ТипОценки;
	Если ЧасыОт <> Неопределено Тогда
		МенеджерЗаписи.От = ЧасыОт;
	КонецЕсли;
	Если ЧасыДо <> Неопределено Тогда
		МенеджерЗаписи.До = ЧасыДо;
	КонецЕсли;
	МенеджерЗаписи.Длительность = Длительность;
	МенеджерЗаписи.КомментарийОценки = КомментарийОценки;
	МенеджерЗаписи.Период = ТекущаяДатаСеанса();
	МенеджерЗаписи.Согласующий = Согласующий;
	МенеджерЗаписи.Согласовант = Пользователи.ТекущийПользователь();
	МенеджерЗаписи.Записать();
	
	МенеджерЗаписи = РегистрыСведений.cbr_СобытияПоЗадачам.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
	МенеджерЗаписи.АвторСобытия = Пользователи.АвторизованныйПользователь();
	МенеджерЗаписи.Задача = Задача;
	ЧасыОценки = "";
	Если ЧасыОт <> Неопределено И ЧасыДо <> Неопределено Тогда
		ЧасыОценки =  СтрШаблон("оценка от %1 до %2 часов", ЧасыОт, ЧасыДо);
	ИначеЕсли ЧасыДо <> Неопределено Тогда
		ЧасыОценки = СтрШаблон("оценка %1 часов", Строка(ЧасыДо));
	КонецЕсли;
	МенеджерЗаписи.Событие = СтрШаблон("Отправлена на согласование %1 %2", ТипОценки, ЧасыОценки);
	МенеджерЗаписи.Записать();
КонецПроцедуры

Процедура ИзменитьПоследнююЗаписьПоЗадаче(Задача, Отказ, Согласовано, КомментарийОтказа = Неопределено) Экспорт
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Задача.Установить(Задача);
	Набор.Прочитать();
	Запись = Набор[Набор.Количество() - 1];
	Запись.Согласовано = Согласовано;
	Запись.Отказ = Отказ;
	Если КомментарийОтказа <> Неопределено Тогда
		Запись.КомментарийОтказа = КомментарийОтказа;
	КонецЕсли;
	Набор.Записать();
	
	МенеджерЗаписи = РегистрыСведений.cbr_СобытияПоЗадачам.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Дата = ТекущаяДатаСеанса();
	МенеджерЗаписи.АвторСобытия = Пользователи.АвторизованныйПользователь();
	МенеджерЗаписи.Задача = Задача;
	ТекстСобытия = "Оценка часов по задаче";
	Если Отказ Тогда
		ТекстСобытия = ТекстСобытия + " не согласована";
	Иначе
		ТекстСобытия = ТекстСобытия + " согласована";
	КонецЕсли;
	МенеджерЗаписи.Событие = ТекстСобытия;
	МенеджерЗаписи.Записать();
КонецПроцедуры
#КонецОбласти
#КонецЕсли