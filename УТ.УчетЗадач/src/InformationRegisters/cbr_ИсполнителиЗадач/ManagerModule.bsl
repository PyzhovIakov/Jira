#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область СлужебныеПроцедурыИФункции
// Установить взята в работу для последнего исполнителя
// Параметры:
//  Задача - ДокументСсылка.cbr_Задача
//  Исполнитель - СправочникСсылка.Пользователи.
//				- СправочникСсылка.cbr_Роли. 
//				- СправочникСсылка.cbr_ГруппыДоступаКЗадачам. 
//  Взята - Булево
//	ПричинаОтказа - СправочникСсылка.cbr_ПричиныОтказаОтЗадачи
Процедура ИзменениеПоследнейЗаписи(Задача, Исполнитель, Взята, ПричинаОтказа = Неопределено) Экспорт 
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Задача.Установить(Задача);
	Набор.Прочитать();
	
	Запись = Набор[Набор.Количество() - 1];	
	Если Исполнитель <> Запись.Исполнитель Тогда
		Набор.Записать();
		ЗаписатьИсполнителя(Задача, Исполнитель, Взята);
	Иначе
		Запись.ЗадачаВзята = Взята;
		Запись.ПричинаОтказа = ПричинаОтказа;
		Набор.Записать();
	КонецЕсли;
		
КонецПроцедуры

// Создать запись в регистр сведений
// Параметры:
//  Задача - ДокументСсылка.cbr_Задача
//  Исполнитель - СправочникСсылка.Пользователи.
//				- СправочникСсылка.cbr_Роли. 
//				- СправочникСсылка.cbr_ГруппыДоступаКЗадачам. 
//  Взята - Булево
Процедура ЗаписатьИсполнителя(Задача, Исполнитель, Взята = Ложь) Экспорт
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Исполнитель = Исполнитель;
	МенеджерЗаписи.Задача = Задача;
	МенеджерЗаписи.Период = ТекущаяДатаСеанса();
	МенеджерЗаписи.ЗадачаВзята = Взята;
	МенеджерЗаписи.Записать();
КонецПроцедуры
#КонецОбласти
#КонецЕсли