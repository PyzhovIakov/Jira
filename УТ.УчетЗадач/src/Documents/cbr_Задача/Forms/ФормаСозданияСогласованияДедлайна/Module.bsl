#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.Задача) Тогда
		Задача = Параметры.Задача;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_ДедлайнЗадачСрезПоследних.Задача,
	|	cbr_ДедлайнЗадачСрезПоследних.Согласовано,
	|	cbr_ДедлайнЗадачСрезПоследних.КомментарийОтказа,
	|	cbr_ДедлайнЗадачСрезПоследних.Отказ
	|ПОМЕСТИТЬ вт_Перенос
	|ИЗ
	|	РегистрСведений.cbr_ДедлайнЗадач.СрезПоследних(, Задача = &Задача) КАК cbr_ДедлайнЗадачСрезПоследних
	|ГДЕ
	|	cbr_ДедлайнЗадачСрезПоследних.Согласовант = &Согласовант
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	cbr_ОценкаЧасовСрезПоследних.Длительность,
	|	cbr_ОценкаЧасовСрезПоследних.Задача
	|ПОМЕСТИТЬ вт_Длительность
	|ИЗ
	|	РегистрСведений.cbr_ОценкаЧасов.СрезПоследних(, Задача = &Задача) КАК cbr_ОценкаЧасовСрезПоследних
	|ГДЕ
	|	cbr_ОценкаЧасовСрезПоследних.Согласовано
	|	И cbr_ОценкаЧасовСрезПоследних.Согласовант = &Согласовант
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	cbr_ДедлайнЗадач.Задача,
	|	СУММА(ВЫБОР
	|		КОГДА НЕ cbr_ДедлайнЗадач.КомментарийОценки ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ вт_СогласованоПоЗадаче
	|ИЗ
	|	РегистрСведений.cbr_ДедлайнЗадач КАК cbr_ДедлайнЗадач
	|ГДЕ
	|	cbr_ДедлайнЗадач.Задача = &Задача
	|	И cbr_ДедлайнЗадач.Согласовано
	|СГРУППИРОВАТЬ ПО
	|	cbr_ДедлайнЗадач.Задача
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_Длительность.Длительность,
	|	ЕСТЬNULL(вт_Перенос.Согласовано, ЛОЖЬ) КАК Согласовано,
	|	вт_Перенос.КомментарийОтказа,
	|	ЕСТЬNULL(вт_Перенос.Отказ, ЛОЖЬ) КАК Отказ,
	|	ЕСТЬNULL(вт_СогласованоПоЗадаче.Количество, 0) КАК Количество
	|ИЗ
	|	вт_Длительность КАК вт_Длительность
	|		ПОЛНОЕ СОЕДИНЕНИЕ вт_Перенос КАК вт_Перенос
	|		ПО вт_Перенос.Задача = вт_Длительность.Задача
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_СогласованоПоЗадаче КАК вт_СогласованоПоЗадаче
	|		ПО вт_Длительность.Задача = вт_СогласованоПоЗадаче.Задача";

	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("Согласовант", Пользователи.АвторизованныйПользователь());

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	Элементы.ПричинаПереноса.Видимость = Ложь;
	Если Выборка.Следующий() Тогда
		Если Выборка.Отказ Тогда
			КомментарийОтказа = Выборка.КомментарийОтказа;
		КонецЕсли;
		Если Выборка.Количество > 0 Тогда
			Элементы.Длительность.Видимость = Ложь;
			Элементы.ДатаСтарта.Видимость = Ложь;
			Элементы.ПричинаПереноса.Видимость = Истина;
			ПереносДедлайна = Истина;
		КонецЕсли;
		Длительность = Выборка.Длительность;
	КонецЕсли;
	ДатаСтарта = ТекущаяДатаСеанса();
	ЧислоСекВДне = 86400;
	Дедлайн = ДатаСтарта + (Длительность * ЧислоСекВДне);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ДатаСтартаПриИзменении(Элемент)
	ЧислоСекВДне = 86400;
	Дедлайн = ДатаСтарта + (Длительность * ЧислоСекВДне);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Отменить(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаСогласование(Команда)
	ОтправитьНаСогласованиеНаСервере();
	Закрыть();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтправитьНаСогласованиеНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_Задача.Постановщик,
	|	cbr_Задача.Постановщик ССЫЛКА Справочник.Партнеры КАК ЭтоПартнер,
	|	cbr_Задача.ГлавныйМенеджер
	|ИЗ
	|	Документ.cbr_Задача КАК cbr_Задача
	|ГДЕ
	|	cbr_Задача.Ссылка = &Задача";

	Запрос.УстановитьПараметр("Задача", Задача);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	Согласующий = Неопределено;
	Если Выборка.Следующий() Тогда
		Если Выборка.ЭтоПартнер Тогда
			Согласующий = Выборка.ГлавныйМенеджер;
		Иначе
			Согласующий = Выборка.Постановщик;
		КонецЕсли;
	КонецЕсли;
	Если ПереносДедлайна Тогда
		РегистрыСведений.cbr_ДедлайнЗадач.СоздатьЗаписьСогласования(Задача, Согласующий, Дедлайн, КомментарийОценки,
			ПричинаПереноса);
	Иначе
		РегистрыСведений.cbr_ДедлайнЗадач.СоздатьЗаписьСогласования(Задача, Согласующий, Дедлайн, КомментарийОценки);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	cbr_ДедлайнЗадачСрезПоследних.Согласовант КАК Согласовант,
		|	cbr_ДедлайнЗадачСрезПоследних.Согласующий КАК Согласующий
		|ИЗ
		|	РегистрСведений.cbr_ДедлайнЗадач.СрезПоследних(, Задача = &Задача) КАК cbr_ДедлайнЗадачСрезПоследних";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если Выборка.Согласовант = Выборка.Согласующий Тогда
			РегистрыСведений.cbr_ДедлайнЗадач.ИзменитьПоследнююЗаписьПоЗадаче(Задача, Ложь, Истина);		
		КонецЕсли;
	КонецЕсли;

	Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.ОтправкаДедлайнаНаСогласование");
	cbr_ОбработкаТриггеровВызовСервера.ВызовТриггера(Подписка, Задача);
КонецПроцедуры
#КонецОбласти