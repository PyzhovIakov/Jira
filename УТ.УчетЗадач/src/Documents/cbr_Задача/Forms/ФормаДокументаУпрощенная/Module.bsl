#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПараметрГлавная = Неопределено;
	Если Параметры.Свойство("ГлавнаяЗадача", ПараметрГлавная) Тогда
		Объект.ГлавнаяЗадача = ПараметрГлавная;
	КонецЕсли;
	
	ПараметрИсточникПараметров = Неопределено;
	Если Параметры.Свойство("ИсточникПараметров", ПараметрИсточникПараметров) Тогда
		ИсточникПараметров = ПараметрИсточникПараметров;
		cbr_ОбработчикиФормыЗадачиСервер.ЗаполнитьНаследуемыеПараметры(ЭтотОбъект, ПараметрИсточникПараметров);
		
		Параметры.Свойство("Исполнитель", Исполнитель);
	КонецЕсли;
	
	//ЗагрузитьЦветаМеток();
	
	ДокОбъект = РеквизитФормыВЗначение("Объект");
	Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.ПриСозданииНаСервереФормыЗадачи");
	cbr_ОбработкаТриггеров.ВызовТриггера(Подписка, Объект.Ссылка);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) И ПараметрИсточникПараметров = Неопределено Тогда
		cbr_ОбработчикиФормыЗадачиСервер.ЗаполнитьПользовательскиеНастройки(ЭтотОбъект);
	КонецЕсли;
	
	Выборка = ПолучениеВыборкиМеток();
	Пока Выборка.Следующий() Цикл
		cbr_ЭлементыФормы.СоздатьКоманду(ЭтотОбъект,
										   "МеткаКоманда" + Выборка.Код,
										   Выборка.Наименование,
										   "ПрисвоитьЗначениеМетке");
		
		НоваяКнопка = cbr_ЭлементыФормы.СздКнопка(ЭтотОбъект,
													"МеткаКоманда" + Выборка.Код,
													Элементы.Метки,
													Выборка.Наименование,
													"МеткаКоманда" + Выборка.Код);
		НоваяКнопка.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Лево;
		Элементы.Переместить(Элементы["МеткаКоманда" + Выборка.Код], Элементы.Метки, Элементы.Ещё);
		НоваяКнопка.Фигура = ФигураКнопки.Овал;
		
		Цвет = Выборка.ЦветRGB;
		Если ЗначениеЗаполнено(Цвет) Тогда
			Фон = СтрРазделить(Цвет, ",");
			//@skip-check new-color
			НоваяКнопка.ЦветФона = Новый Цвет(Фон[0], Фон[1], Фон[2]);
		КонецЕсли;

		СтрокаТаблицы = ТаблицаМеток.Добавить();
		СтрокаТаблицы.Команда = "МеткаКоманда" + Выборка.Код;
		СтрокаТаблицы.Код = Выборка.Код;

	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ПараметрыЗаписи.Свойство("НеСохранять") Тогда
		Отказ = Истина;
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
КонецПроцедуры



&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	cbr_ДвиженияПоРегистрам.ДвижениеИсполнителиЗадач(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ОписаниеЗадачи = Новый ХранилищеЗначения(ОписаниеЗадачиДокумент);
КонецПроцедуры
#КонецОбласти

&НаКлиенте
Процедура МеткиПриИзменении(Элемент)
	ЗагрузитьЦветаМеток();
КонецПроцедуры


#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОткрытьПолнуюФорму(Команда)	
	АдресОписанияЗадачи = ПоместитьВоВременноеХранилище(ОписаниеЗадачиДокумент);
	
	ПараметрыФормы = Новый Структура("НаименованиеЗадачи,ГлавнаяЗадача,ИсточникПараметров,ОписаниеЗадачи,Метка,Ключ",
		Объект.Наименование, Объект.ГлавнаяЗадача, ИсточникПараметров, АдресОписанияЗадачи, Объект.Метка, Объект.Ссылка);
		
	ФормаДок = ПолучитьФорму("Документ.cbr_Задача.Форма.ФормаДокумента", ПараметрыФормы);
	
	Если ОписаниеОповещенияОЗакрытии <> Неопределено Тогда
		ДанныеФормы = ФормаДок.Объект;
		
		ПараметрыДоп = Новый Структура("ФормаДок", ФормаДок);
		НовыйООЗ = Новый ОписаниеОповещения(ОписаниеОповещенияОЗакрытии.ИмяПроцедуры, ОписаниеОповещенияОЗакрытии.Модуль, 
			ПараметрыДоп);
	
		СоздатьГлавнуюЗадачуНаСервере(ДанныеФормы);
		ФормаДок.ОписаниеОповещенияОЗакрытии = НовыйООЗ;
	КонецЕсли;

	ФормаДок.Открыть();

	ОписаниеОповещенияОЗакрытии = Неопределено;
	ПараметрыЗакрытия = Новый Структура("НеСохранять", Истина);
	//@skip-check invocation-form-event-handler
	ПередЗаписью(Ложь, ПараметрыЗакрытия);
КонецПроцедуры

&НаКлиенте
Процедура МеткаЕщё(Команда)
	ВызовВыбораМеток();
КонецПроцедуры

&НаКлиенте
Процедура КонтекстноеМенюИзменить(Команда)
	ВызовВыбораМеток();
КонецПроцедуры

&НаКлиенте
Процедура КонтекстноеМенюОчистить(Команда)
	ОчисткаМетокНаФормеНаСервере();	
	Объект.Метка = Неопределено;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция СоздатьГлавнуюЗадачуНаСервере(ДанныеФормы)	
	Док = Документы.cbr_Задача.СоздатьДокумент();
	Док.УстановитьСсылкуНового(Объект.Ссылка);
	ЗначениеВДанныеФормы(Док, ДанныеФормы);
	Возврат  Объект.Ссылка; 
КонецФункции
#КонецОбласти

#Область Метки
&НаСервере
Функция ПолучениеВыборкиМеток()
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(Центр_МеткиЗадач.Наименование) КАК Наименование,
		|	Центр_МеткиЗадач.ЦветRGB,
		|	ПРЕДСТАВЛЕНИЕ(Центр_МеткиЗадач.Код) КАК Код
		|ИЗ
		|	Справочник.cbr_МеткиЗадач КАК Центр_МеткиЗадач
		|ГДЕ
		|	Центр_МеткиЗадач.ВидимостьНаФорме = ИСТИНА";
	
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции

&НаКлиенте
Процедура ПрисвоитьЗначениеМетке(Команда)
	ПараметрыМетки = Новый Структура("Команда", Команда.Имя);
	СтрокаМетки = ТаблицаМеток.НайтиСтроки(ПараметрыМетки);
	
	ИмяКоманды = Команда.Имя;
	ОчиститьОформлениеКнопок();
	Элементы[ИмяКоманды].ЦветРамки = Новый Цвет(0, 0, 0);	
	ПрисвоитьЗначениеМеткеНаСервере(СтрокаМетки[0].Код);
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОформлениеКнопок()
	Для Каждого СтрокаМетки Из ТаблицаМеток Цикл
		ИмяКоманды = СтрокаМетки.Команда;
		Элементы[ИмяКоманды].ЦветРамки = Новый Цвет();
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПрисвоитьЗначениеМеткеНаСервере(КодМетки)	
	Объект.Метка = Справочники.cbr_МеткиЗадач.НайтиПоКоду(КодМетки);
КонецПроцедуры

&НаКлиенте
Процедура ВызовВыбораМеток()
	ОписаниеОповещенияОЗакрытииФормыВыбора = Новый ОписаниеОповещения("ПослеВыбораМетки", ЭтотОбъект);
	ОткрытьФорму("Справочник.cbr_МеткиЗадач.ФормаВыбора",,,,,,ОписаниеОповещенияОЗакрытииФормыВыбора,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораМетки(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;

	ПослеВыбораМеткиНаСервере(Результат);
	Объект.Метка = Результат;
	Элементы.Ещё.Видимость = Ложь;	
КонецПроцедуры

&НаСервере
Процедура ПослеВыбораМеткиНаСервере(МеткаСсылка)
	Для Каждого СтрокаМетки Из ТаблицаМеток Цикл
		ИмяКоманды = СтрокаМетки.Команда;
		Элементы.Удалить(Элементы[ИмяКоманды]);
	КонецЦикла;
	
	Элемент = Элементы.Найти("ДекорацияМетки");
	Если Элемент <> Неопределено Тогда
		Элементы.Удалить(Элемент);
	КонецЕсли;
	
	ТаблицаМеток.Очистить();
	
	КодМетки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МеткаСсылка, "Код");
	RGBМетки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МеткаСсылка, "ЦветRGB");
	НаименованиеМетки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МеткаСсылка, "Наименование");
	
	ПараметрыМетки = Новый Структура("Команда", "МеткаКоманда" + КодМетки);

	ДекорацияМетки = Элементы.Добавить("ДекорацияМетки", Тип("ДекорацияФормы"), Элементы.Метки);
	ДекорацияМетки.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияМетки.Заголовок = НаименованиеМетки;

	Цвет = RGBМетки;
	Если ЗначениеЗаполнено(Цвет) Тогда
		Фон = СтрРазделить(Цвет, ",");
			//@skip-check new-color
		ДекорацияМетки.ЦветФона = Новый Цвет(Фон[0], Фон[1], Фон[2]);
	КонецЕсли;
	
	cbr_ЭлементыФормы.СздКнопка(ЭтотОбъект,
								  "КонтекстноеМенюОчистить",
								  Элементы["ДекорацияМетки"].КонтекстноеМеню,
								  "Очистить",
								  "КонтекстноеМенюОчистить");
	
	cbr_ЭлементыФормы.СздКнопка(ЭтотОбъект,
								  "КонтекстноеМенюИзменить",
								  Элементы["ДекорацияМетки"].КонтекстноеМеню,
								  "Изменить",
								  "КонтекстноеМенюИзменить");
	
	Элемент = Элементы.Найти("ВыборМеткиКнопка");
	Если Элемент <> Неопределено Тогда
		Элементы.Удалить(Элемент);
	КонецЕсли;						  																																	
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьЦветаМеток()
	ОчиститьОформлениеМеток();

	Для Каждого Метка Из Объект.Метки Цикл
		Цвет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Метка.Метка, "ЦветRGB");

		Если ЗначениеЗаполнено(Цвет) Тогда
			Фон = СтрРазделить(Цвет, ",");

			ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	    	ЭлементОформления.Использование = Истина;
	    	//@skip-check new-color
	    	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(Фон[0], Фон[1], Фон[2]));

			ЭлементУсловия                = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		    ЭлементУсловия.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.Метки.Метка");
		    ЭлементУсловия.ПравоеЗначение = Метка.Метка;
		    ЭлементУсловия.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;   
		    ЭлементУсловия.Использование  = Истина;

		    ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		    ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Метки");
	    КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОчиститьОформлениеМеток() 
	МассивКУдалению = Новый Массив;
	Для Каждого Настройка Из УсловноеОформление.Элементы Цикл
		Если Настройка.Отбор.Элементы[0].ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Метки.Метка")
			И Настройка.Отбор.Элементы[0].ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
			И Настройка.Поля.Элементы[0].Поле = Новый ПолеКомпоновкиДанных("Метки")
			И Настройка.Оформление.Элементы[0].Параметр = Новый ПараметрКомпоновкиДанных("ЦветФона") Тогда
			МассивКУдалению.Добавить(Настройка);
		КонецЕсли;
	КонецЦикла;

	Для Каждого Настрйока Из МассивКУдалению Цикл
		УсловноеОформление.Элементы.Удалить(Настрйока);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОчисткаМетокНаФормеНаСервере()
	Элемент = Элементы.Найти("ДекорацияМетки");
	Если Элемент <> Неопределено Тогда
		Элементы.Удалить(Элемент);
	КонецЕсли;
	
	cbr_ЭлементыФормы.СздКнопка(ЭтотОбъект, "ВыборМеткиКнопка", Элементы.Метки, "Метки", "МеткаЕщё", 2);	
КонецПроцедуры
#КонецОбласти
