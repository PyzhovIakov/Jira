
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Задача", Задача) Тогда
		СписокПользователей = cbr_ОбработчикиФормыЗадачиВызовСервера.ПользователиВыбраннойЗадачи(Задача);
		
		Для Каждого Пользователь Из СписокПользователей Цикл
			СтрокаТаблицы = Пользователи.Добавить();
			СтрокаТаблицы.Пользователь = Пользователь;
		КонецЦикла;
	Иначе
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсех(Команда)
	Для Каждого СтрокаТаблицы Из Пользователи Цикл
		СтрокаТаблицы.ИндикаторВыбора = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыбор(Команда)
	Для Каждого СтрокаТаблицы Из Пользователи Цикл
		СтрокаТаблицы.ИндикаторВыбора = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Уведомить(Команда)
	УведомитьНаСервере();
	
	Закрыть();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура УведомитьНаСервере()
	Для Каждого СтрокаТаблицы Из Пользователи Цикл
		Если СтрокаТаблицы.ИндикаторВыбора Тогда
			УведомитьПользователя(СтрокаТаблицы.Пользователь);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры	

&НаСервере
Процедура УведомитьПользователя(Пользователь)
	ТекущаяДата = ТекущаяДатаСеанса();
	ТекстНапоминания = СтрШаблон("Задача требует внимания");
	ПериодичностьНапоминания = ПредопределенноеЗначение(
		"Перечисление.СпособыУстановкиВремениНапоминания.Периодически");
	
	СвойстваУведомления = Новый Структура;
	СвойстваУведомления.Вставить("Пользователь", Пользователь);
	СвойстваУведомления.Вставить("Описание", ТекстНапоминания);
	СвойстваУведомления.Вставить("СрокНапоминания", ТекущаяДата);
	СвойстваУведомления.Вставить("ВремяСобытия", ТекущаяДата);
	СвойстваУведомления.Вставить("СпособУстановкиВремениНапоминания", ПериодичностьНапоминания);
	СвойстваУведомления.Вставить("ИнтервалВремениНапоминания", 60);
	СвойстваУведомления.Вставить("Источник", Задача);
	СвойстваУведомления.Вставить("Идентификатор", "");
	
	ПринудительныеУведомления = Новый Структура;
	ПринудительныеУведомления.Вставить("База", Истина);
	ПринудительныеУведомления.Вставить("Битрикс", Ложь);
	ПринудительныеУведомления.Вставить("Телеграм", Ложь);
	ПринудительныеУведомления.Вставить("ЭлектроннаяПочта", Ложь);
	
	cbr_УведомленияПользователей.ОтправитьУведомлениеПользователю(СвойстваУведомления,
		ПринудительныеУведомления);
КонецПроцедуры
#КонецОбласти