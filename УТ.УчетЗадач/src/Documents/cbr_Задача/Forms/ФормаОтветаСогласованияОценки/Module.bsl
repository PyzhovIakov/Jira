
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.Задача) Тогда
		Задача = Параметры.Задача;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_ОценкаЧасовСрезПоследних.До,
	|	cbr_ОценкаЧасовСрезПоследних.КомментарийОценки,
	|	cbr_ОценкаЧасовСрезПоследних.Период,
	|	cbr_ОценкаЧасовСрезПоследних.Согласовант,
	|	cbr_ОценкаЧасовСрезПоследних.Длительность,
	|	cbr_ОценкаЧасовСрезПоследних.От,
	|	cbr_ОценкаЧасовСрезПоследних.ТипОценки
	|ИЗ
	|	РегистрСведений.cbr_ОценкаЧасов.СрезПоследних(, Задача = &Задача) КАК cbr_ОценкаЧасовСрезПоследних
	|ГДЕ
	|	НЕ cbr_ОценкаЧасовСрезПоследних.Отказ
	|	И НЕ cbr_ОценкаЧасовСрезПоследних.Согласовано
	|	И cbr_ОценкаЧасовСрезПоследних.Согласующий = &Согласующий";

	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("Согласующий", Пользователи.АвторизованныйПользователь());
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		Период = Выборка.Период;
		КомментарийСогласования = Выборка.КомментарийОценки;
		Если Выборка.ТипОценки = ПредопределенноеЗначение("Перечисление.cbr_ТипОценкиЧасов.Точная") Тогда
			Часы = Строка(Выборка.До);
		ИначеЕсли Выборка.ТипОценки = ПредопределенноеЗначение("Перечисление.cbr_ТипОценкиЧасов.Рамочная") Тогда
			Часы = Строка(Выборка.От) + " - " + Строка(Выборка.До);
		Иначе
			Часы = Строка(Выборка.ТипОценки);
		КонецЕсли;		
		Длительность = Выборка.Длительность;
		Согласовант = Выборка.Согласовант;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Согласовано(Команда)
	СогласованоНаСервере();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Отказ(Команда)
	ОповещениеООтмене = Новый ОписаниеОповещения("ОбработкаВопросаОтказ", ЭтотОбъект);
	ПоказатьВводСтроки(ОповещениеООтмене, "", "Введите причину отказа", , Истина);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СогласованоНаСервере()
	Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.СогласованиеОценки");
		cbr_ОбработкаТриггеров.ВызовТриггера(Подписка, Задача);
	РегистрыСведений.cbr_ОценкаЧасов.ИзменитьПоследнююЗаписьПоЗадаче(Задача, Ложь, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВопросаОтказ(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ОтказНаСервере(Результат);
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОтказНаСервере(Результат)
	Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.СогласованиеОценки");
		cbr_ОбработкаТриггеров.ВызовТриггера(Подписка, Задача);
	РегистрыСведений.cbr_ОценкаЧасов.ИзменитьПоследнююЗаписьПоЗадаче(Задача, Истина, Ложь, Результат);
КонецПроцедуры
#КонецОбласти