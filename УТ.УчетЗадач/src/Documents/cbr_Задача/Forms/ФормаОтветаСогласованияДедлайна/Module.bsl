#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.Задача) Тогда
		Задача = Параметры.Задача;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_ДедлайнЗадачСрезПоследних.Период,
	|	cbr_ДедлайнЗадачСрезПоследних.Согласовант,
	|	cbr_ДедлайнЗадачСрезПоследних.Дедлайн,
	|	cbr_ДедлайнЗадачСрезПоследних.КомментарийОценки
	|ИЗ
	|	РегистрСведений.cbr_ДедлайнЗадач.СрезПоследних(, Задача = &Задача) КАК cbr_ДедлайнЗадачСрезПоследних
	|ГДЕ
	|	НЕ cbr_ДедлайнЗадачСрезПоследних.Отказ
	|	И НЕ cbr_ДедлайнЗадачСрезПоследних.Согласовано
	|	И cbr_ДедлайнЗадачСрезПоследних.Согласующий = &Согласующий";

	Запрос.УстановитьПараметр("Задача", Задача);
	Запрос.УстановитьПараметр("Согласующий", Пользователи.АвторизованныйПользователь());
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		Период = Выборка.Период;
		КомментарииСогласования = Выборка.КомментарийОценки;
		Дедлайн = Выборка.Дедлайн;
		Согласовант = Выборка.Согласовант;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Согласовано(Команда)
	СогласованоНаСервере();
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Отказ(Команда)
	ОповещениеООтмене = Новый ОписаниеОповещения("ОбработкаВопросаОтказ", ЭтотОбъект);
	ПоказатьВводСтроки(ОповещениеООтмене, "", "Введите причину отказа", , Истина);

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СогласованоНаСервере()
	РегистрыСведений.cbr_ДедлайнЗадач.ИзменитьПоследнююЗаписьПоЗадаче(Задача, Ложь, Истина);
	Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.СогласованиеДедлайна");
	cbr_ОбработкаТриггеровВызовСервера.ВызовТриггера(Подписка, Задача);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВопросаОтказ(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ОтказНаСервере(Результат);
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОтказНаСервере(Результат)
	РегистрыСведений.cbr_ДедлайнЗадач.ИзменитьПоследнююЗаписьПоЗадаче(Задача, Истина, Ложь, Результат);
	Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.СогласованиеДедлайна");
	cbr_ОбработкаТриггеровВызовСервера.ВызовТриггера(Подписка, Задача);
КонецПроцедуры
#КонецОбласти