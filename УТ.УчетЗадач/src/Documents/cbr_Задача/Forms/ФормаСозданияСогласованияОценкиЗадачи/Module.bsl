#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.Задача) Тогда
		Задача = Параметры.Задача;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_ОценкаЧасовСрезПоследних.КомментарийОтказа,
	|	cbr_ОценкаЧасовСрезПоследних.ТипОценки,
	|	cbr_ОценкаЧасовСрезПоследних.От,
	|	cbr_ОценкаЧасовСрезПоследних.До
	|ИЗ
	|	РегистрСведений.cbr_ОценкаЧасов.СрезПоследних(, Задача = &Задача) КАК cbr_ОценкаЧасовСрезПоследних
	|ГДЕ
	|	cbr_ОценкаЧасовСрезПоследних.Отказ";

	Запрос.УстановитьПараметр("Задача", Задача);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		КомментарийОтвет = Выборка.КомментарийОтказа;
		ТипОценкиОтвет = Выборка.ТипОценки;
		
		Элементы.ТипОценкиОтвет.Видимость = Истина;
		Элементы.ЧасыОценки.Видимость = Истина;
		Элементы.КомментарийОтвет.Видимость = Истина;
		
		Если Выборка.ТипОценки = ПредопределенноеЗначение("Перечисление.cbr_ТипОценкиЧасов.Точная") Тогда
			ЧасыОценки = Выборка.До;
		ИначеЕсли Выборка.ТипОценки = ПредопределенноеЗначение("Перечисление.cbr_ТипОценкиЧасов.Рамочная") Тогда
			ЧасыОценки = "от " + Выборка.От + " до " + Выборка.До;
		Иначе
			Элементы.ЧасыОценки.Видимость = Ложь;	
		КонецЕсли;
	Иначе
		Элементы.ТипОценкиОтвет.Видимость = Ложь;
		Элементы.ЧасыОценки.Видимость = Ложь;
		Элементы.КомментарийОтвет.Видимость = Ложь;
	КонецЕсли;

	ЕдиницыИзмеренияДлительности = ПредопределенноеЗначение("Перечисление.cbr_ЕдиницыИзмеренийВремени.День");
	ЕдиницыИзмеренияЧасов = ПредопределенноеЗначение("Перечисление.cbr_ЕдиницыИзмеренийВремени.Час");
	ТипОценки = ПредопределенноеЗначение("Перечисление.cbr_ТипОценкиЧасов.Точная");
	ВидимостьОценкиЧасов();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ТипОценкиПриИзменении(Элемент)
	ВидимостьОценкиЧасов();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Отменить(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаСогласование(Команда)
	ОтправитьНаСогласованиеНаСервере();
	Закрыть();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ВидимостьОценкиЧасов()
	Если ТипОценки = ПредопределенноеЗначение("Перечисление.cbr_ТипОценкиЧасов.Точная") Тогда
		Элементы.До.Заголовок = "Часы";
		Элементы.От.Видимость = Ложь;
		Элементы.До.Видимость = Истина;
		Элементы.ЕдиницыИзмеренияЧасов.Видимость = Истина;
		Элементы.КомментарийОценки.МаксимальнаяШирина = 47;
	ИначеЕсли ТипОценки = ПредопределенноеЗначение("Перечисление.cbr_ТипОценкиЧасов.Рамочная") Тогда
		Элементы.До.Заголовок = "До";
		Элементы.От.Видимость = Истина;
		Элементы.До.Видимость = Истина;
		Элементы.ЕдиницыИзмеренияЧасов.Видимость = Истина;
		От = 1;
		Элементы.КомментарийОценки.МаксимальнаяШирина = 62;
	Иначе
		Элементы.От.Видимость = Ложь;
		Элементы.До.Видимость = Ложь;
		Элементы.ЕдиницыИзмеренияЧасов.Видимость = Ложь;
		Элементы.КомментарийОценки.МаксимальнаяШирина = 42;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОтправитьНаСогласованиеНаСервере()
	ДоВРегистр = 0;
	ОтВРегистр = 0;
	ДлительностьВРегистр = 0;

	ЧасовВДне = 8;
	ДнейВНеделе = 7;
	Если ЕдиницыИзмеренияДлительности = ПредопределенноеЗначение("Перечисление.cbr_ЕдиницыИзмеренийВремени.Неделя") Тогда
		ДлительностьВРегистр = Длительность * ДнейВНеделе;
	Иначе
		ДлительностьВРегистр = Длительность;
	КонецЕсли;

	Если ЕдиницыИзмеренияЧасов = ПредопределенноеЗначение("Перечисление.cbr_ЕдиницыИзмеренийВремени.День") Тогда
		ДоВРегистр = До * ЧасовВДне;
		ОтВРегистр = От * ЧасовВДне;
	Иначе
		ДоВРегистр = До;
		ОтВРегистр = От;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_Задача.Постановщик,
	|	cbr_Задача.Постановщик ССЫЛКА Справочник.Партнеры КАК ЭтоПартнер,
	|	cbr_Задача.ГлавныйМенеджер
	|ИЗ
	|	Документ.cbr_Задача КАК cbr_Задача
	|ГДЕ
	|	cbr_Задача.Ссылка = &Задача";

	Запрос.УстановитьПараметр("Задача", Задача);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	Согласующий = Неопределено;
	Если Выборка.Следующий() Тогда
		Если Выборка.ЭтоПартнер Тогда
			Согласующий = Выборка.ГлавныйМенеджер;
		Иначе
			Согласующий = Выборка.Постановщик;
		КонецЕсли;
	КонецЕсли;

	Если ТипОценки = ПредопределенноеЗначение("Перечисление.cbr_ТипОценкиЧасов.Точная") Тогда
		РегистрыСведений.cbr_ОценкаЧасов.СоздатьЗаписьСогласования(Задача, ТипОценки, ДлительностьВРегистр,
			КомментарийОценки, Согласующий, , ДоВРегистр);
	ИначеЕсли ТипОценки = ПредопределенноеЗначение("Перечисление.cbr_ТипОценкиЧасов.Рамочная") Тогда
		РегистрыСведений.cbr_ОценкаЧасов.СоздатьЗаписьСогласования(Задача, ТипОценки, ДлительностьВРегистр,
			КомментарийОценки, Согласующий, ОтВРегистр, ДоВРегистр);
	Иначе
		РегистрыСведений.cbr_ОценкаЧасов.СоздатьЗаписьСогласования(Задача, ТипОценки, ДлительностьВРегистр,
			КомментарийОценки, Согласующий);
	КонецЕсли;

	Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.ОтправкаОценкиНаСогласование");
	cbr_ОбработкаТриггеровВызовСервера.ВызовТриггера(Подписка, Задача);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	cbr_ИсполнителиЗадачСрезПоследних.Исполнитель,
		|	cbr_Задача.ГлавныйМенеджер,
		|	cbr_Задача.Постановщик
		|ИЗ
		|	РегистрСведений.cbr_ИсполнителиЗадач.СрезПоследних(, Задача = &Задача) КАК cbr_ИсполнителиЗадачСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.cbr_Задача КАК cbr_Задача
		|		ПО cbr_ИсполнителиЗадачСрезПоследних.Задача = cbr_Задача.Ссылка
		|ГДЕ
		|	cbr_Задача.Ссылка = &Задача";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если Выборка.ГлавныйМенеджер = Выборка.Исполнитель ИЛИ Выборка.Постановщик = Выборка.Исполнитель Тогда
			РегистрыСведений.cbr_ОценкаЧасов.ИзменитьПоследнююЗаписьПоЗадаче(Задача, Ложь, Истина);
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры
#КонецОбласти