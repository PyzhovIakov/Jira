#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОсновнаяЗадача = Неопределено;
	Если Параметры.Свойство("ОсновнаяЗадача", ОсновнаяЗадача) Тогда
		Задача = ОсновнаяЗадача;

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	cbr_ВопросыПоЗадачам.Отправитель,
		|	МАКСИМУМ(cbr_ВопросыПоЗадачам.Дата) КАК Дата
		|ПОМЕСТИТЬ вт_ПоследнийВопрос
		|ИЗ
		|	РегистрСведений.cbr_ВопросыПоЗадачам КАК cbr_ВопросыПоЗадачам
		|ГДЕ
		|	cbr_ВопросыПоЗадачам.Задача = &Задача
		|	И cbr_ВопросыПоЗадачам.Адресат = &Адресат
		|	И cbr_ВопросыПоЗадачам.ВидВопроса = ЗНАЧЕНИЕ(Перечисление.cbr_ВидыВопросов.Вопрос)
		|СГРУППИРОВАТЬ ПО
		|	cbr_ВопросыПоЗадачам.Отправитель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	cbr_ВопросыПоЗадачам.ТекстВопроса,
		|	вт_ПоследнийВопрос.Отправитель,
		|	вт_ПоследнийВопрос.Дата
		|ИЗ
		|	РегистрСведений.cbr_ВопросыПоЗадачам КАК cbr_ВопросыПоЗадачам
		|		ПРАВОЕ СОЕДИНЕНИЕ вт_ПоследнийВопрос КАК вт_ПоследнийВопрос
		|		ПО вт_ПоследнийВопрос.Отправитель = cbr_ВопросыПоЗадачам.Отправитель
		|		И вт_ПоследнийВопрос.Дата = cbr_ВопросыПоЗадачам.Дата
		|		И cbr_ВопросыПоЗадачам.Задача = &Задача
		|		И cbr_ВопросыПоЗадачам.Адресат = &Адресат
		|		И cbr_ВопросыПоЗадачам.ВидВопроса = ЗНАЧЕНИЕ(Перечисление.cbr_ВидыВопросов.Вопрос)";

		Запрос.УстановитьПараметр("Задача", Задача);
		Запрос.УстановитьПараметр("Адресат", Пользователи.АвторизованныйПользователь());

		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();

		Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.ТекстВопроса) Тогда
			Адресат = Выборка.Отправитель;
			ТекстВопроса = Выборка.ТекстВопроса;
			Дата = Выборка.Дата;
		Иначе
			Отказ = Истина;

			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Вопросы к вам по данной задаче отсутствуют";
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОтправитьВопрос(Команда)
	Отказ = Ложь;

	Если Не ЗначениеЗаполнено(ТекстОтвета) Тогда
		Отказ = Истина;

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Введите текст вопроса";
		Сообщение.Поле = "ТекстНовогоВопроса";
		Сообщение.Сообщить();
	КонецЕсли;

	Если Не Отказ Тогда
		ОтправитьВопросНаСервере();
		Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.ОтветНаВопросПоЗадаче");
		cbr_ОбработкаТриггеровВызовСервера.ВызовТриггера(Подписка, Задача);
		ВладелецФормы.ПослеОтветаНаВопрос();
		Закрыть();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ОтправитьВопросНаСервере()
	cbr_ДвиженияПоРегистрам.ДвижениеВопросыПоЗадачам(Задача, Адресат, ТекстОтвета, Истина);
КонецПроцедуры
#КонецОбласти