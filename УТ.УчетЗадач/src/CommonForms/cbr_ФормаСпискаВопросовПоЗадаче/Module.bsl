#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОсновнаяЗадача = Неопределено;
	Если Параметры.Свойство("ОсновнаяЗадача", ОсновнаяЗадача) Тогда
		Задача = ОсновнаяЗадача;
		СписокДоступныхАдресатов = АдресатыВыбраннойЗадачи();

		Для Каждого АдресатСписка Из СписокДоступныхАдресатов Цикл
			Элементы.Адресат.СписокВыбора.Добавить(АдресатСписка);
		КонецЦикла;

		Постановщик = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнаяЗадача, "Постановщик");

		Если ТипЗнч(Постановщик) = Тип("СправочникСсылка.Пользователи") Тогда
			Адресат = Постановщик;
		Иначе
			ОсновнойМенеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "ГлавныйМенеджер");
			Адресат = ОсновнойМенеджер;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ЗадачаПриИзменении(Элемент)
	СписокДоступныхАдресатов = АдресатыВыбраннойЗадачи();

	Для Каждого АдресатСписка Из СписокДоступныхАдресатов Цикл
		Элементы.Адресат.СписокВыбора.Добавить(АдресатСписка);
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОтправитьВопрос(Команда)
	Отказ = Ложь;

	Если Не ЗначениеЗаполнено(Задача) Тогда
		Отказ = Истина;

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбрана задача";
		Сообщение.Поле = "Задача";
		Сообщение.Сообщить();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Адресат) Тогда
		Отказ = Истина;

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбран адресат";
		Сообщение.Поле = "Адресат";
		Сообщение.Сообщить();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ТекстНовогоВопроса) Тогда
		Отказ = Истина;

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Введите текст вопроса";
		Сообщение.Поле = "ТекстНовогоВопроса";
		Сообщение.Сообщить();
	КонецЕсли;

	Если Не Отказ Тогда
		ОтправитьВопросНаСервере();
		Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.СозданиеВопросаПоЗадаче");
		cbr_ОбработкаТриггеровВызовСервера.ВызовТриггера(Подписка, Задача);
		Закрыть();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция АдресатыВыбраннойЗадачи()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_ЗадачаНаблюдатели.Пользователь КАК Пользователь
	|ПОМЕСТИТЬ вт_НесгруппированныеПользователи
	|ИЗ
	|	Документ.cbr_Задача.Наблюдатели КАК cbr_ЗадачаНаблюдатели
	|ГДЕ
	|	cbr_ЗадачаНаблюдатели.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	cbr_ЗадачаНаблюдатели.Пользователь
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	cbr_ИсполнителиЗадачСрезПоследних.Исполнитель
	|ИЗ
	|	РегистрСведений.cbr_ИсполнителиЗадач.СрезПоследних(, Задача = &Ссылка
	|	И Исполнитель ССЫЛКА Справочник.Пользователи) КАК cbr_ИсполнителиЗадачСрезПоследних
	|СГРУППИРОВАТЬ ПО
	|	cbr_ИсполнителиЗадачСрезПоследних.Исполнитель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	cbr_Задача.Автор
	|ИЗ
	|	Документ.cbr_Задача КАК cbr_Задача
	|ГДЕ
	|	cbr_Задача.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	cbr_Задача.Автор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА cbr_Задача.Постановщик ССЫЛКА Справочник.Пользователи
	|			ТОГДА cbr_Задача.Постановщик
	|	КОНЕЦ
	|ИЗ
	|	Документ.cbr_Задача КАК cbr_Задача
	|ГДЕ
	|	cbr_Задача.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА cbr_Задача.Постановщик ССЫЛКА Справочник.Пользователи
	|			ТОГДА cbr_Задача.Постановщик
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	cbr_ГруппыДоступаКЗадачамПользователи.Пользователь
	|ИЗ
	|	РегистрСведений.cbr_ИсполнителиЗадач.СрезПоследних(, Задача = &Ссылка
	|	И Исполнитель ССЫЛКА Справочник.cbr_ГруппыДоступаКЗадачам) КАК cbr_ИсполнителиЗадачСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.cbr_ГруппыДоступаКЗадачам.Пользователи КАК cbr_ГруппыДоступаКЗадачамПользователи
	|		ПО cbr_ГруппыДоступаКЗадачамПользователи.Пользователь = cbr_ИсполнителиЗадачСрезПоследних.Исполнитель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_НесгруппированныеПользователи.Пользователь
	|ИЗ
	|	вт_НесгруппированныеПользователи КАК вт_НесгруппированныеПользователи
	|СГРУППИРОВАТЬ ПО
	|	вт_НесгруппированныеПользователи.Пользователь";

	Запрос.УстановитьПараметр("Ссылка", Задача);

	Выборка =  Запрос.Выполнить().Выбрать();

	МассивКандидатов = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Пользователь) И Выборка.Пользователь
			<> Пользователи.АвторизованныйПользователь() Тогда
			МассивКандидатов.Добавить(Выборка.Пользователь);
		КонецЕсли;
	КонецЦикла;

	ОсновнойМенеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "ГлавныйМенеджер");
	Если ОсновнойМенеджер <> Справочники.Пользователи.ПустаяСсылка() Тогда
		МассивКандидатов.Добавить(ОсновнойМенеджер);
	КонецЕсли;

	Возврат МассивКандидатов;
КонецФункции

&НаСервере
Процедура ОтправитьВопросНаСервере()
	cbr_ДвиженияПоРегистрам.ДвижениеВопросыПоЗадачам(Задача, Адресат, ТекстНовогоВопроса);
КонецПроцедуры
#КонецОбласти