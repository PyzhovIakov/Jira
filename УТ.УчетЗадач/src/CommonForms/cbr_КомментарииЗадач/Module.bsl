#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПараметрЗадачаИсточник = Неопределено;
	Если Параметры.Свойство("ЗадачаИсточник", ПараметрЗадачаИсточник) Тогда
		ЗадачаИсточник = ПараметрЗадачаИсточник;
		ОтобразитьЧат();
	Иначе
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Отправить(Команда)
	ОтправитьНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
#Область Чат
&НаСервере
Процедура ОтправитьНаСервере()
	cbr_ДвиженияПоРегистрам.ДвижениеКомментарииЗадач(ЗадачаИсточник, Новый ХранилищеЗначения(Комментарий));

	Комментарий.Удалить();
	ОтобразитьЧат();
КонецПроцедуры

&НаСервере
Процедура ОтобразитьЧат()
	Если КоличествоКомментариев > 0 Тогда
		Для к = 1 По КоличествоКомментариев Цикл
			cbr_ЭлементыФормы.УдлРеквизитИЭлемент(ЭтотОбъект, "Комментарий" + к);
		КонецЦикла;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_КомментарииЗадач.Автор,
	|	cbr_КомментарииЗадач.Комментарий,
	|	cbr_КомментарииЗадач.Период КАК Период
	|ИЗ
	|	РегистрСведений.cbr_КомментарииЗадач КАК cbr_КомментарииЗадач
	|ГДЕ
	|	cbr_КомментарииЗадач.Задача = &Задача
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";

	Запрос.УстановитьПараметр("Задача", ЗадачаИсточник);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	КоличествоКомментариев = Выборка.Количество();

	Индекс = 1;

	ТекущийПользователь = Пользователи.АвторизованныйПользователь();

	Пока Выборка.Следующий() Цикл
		cbr_ЭлементыФормы.СоздатьРеквизит(ЭтотОбъект, "Комментарий" + Индекс,
			Новый ОписаниеТипов("ФорматированныйДокумент"));

		СтруктураСвойств = Новый структура;
		СтруктураСвойств.Вставить("ТолькоПросмотр", Истина);
		СтруктураСвойств.Вставить("Высота", 5);
		СтруктураСвойств.Вставить("Ширина", 70);
		СтруктураСвойств.Вставить("РастягиватьПоГоризонтали", Ложь);
		СтруктураСвойств.Вставить("РастягиватьПоВертикали", Ложь);
		Если Выборка.Автор = ТекущийПользователь Тогда
			СтруктураСвойств.Вставить("ГоризонтальноеПоложениеВГруппе", ГоризонтальноеПоложениеЭлемента.Право);
		Иначе
			СтруктураСвойств.Вставить("ГоризонтальноеПоложениеВГруппе", ГоризонтальноеПоложениеЭлемента.Лево);
		КонецЕсли;
		ЗаголовокКомментария = Строка(Выборка.Автор) + " " + Выборка.Период;
		cbr_ЭлементыФормы.СздПоле(ЭтотОбъект, "Комментарий" + Индекс, Элементы.ГруппаРабочаяОбласть,
			ЗаголовокКомментария, , "Комментарий" + Индекс, СтруктураСвойств);
		ЭтотОбъект["Комментарий" + Индекс] = Выборка.Комментарий.Получить();
		Индекс = Индекс + 1;
	КонецЦикла;

КонецПроцедуры
#КонецОбласти
#КонецОбласти