#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОсновнаяЗадача = Неопределено;
	Если Параметры.Свойство("ОсновнаяЗадача", ОсновнаяЗадача) Тогда
		Задача = ОсновнаяЗадача;
		СписокВопросов.Параметры.УстановитьЗначениеПараметра("Задача", Задача);
		СписокДоступныхАдресатов = cbr_ОбработчикиФормыЗадачиСервер.ПользователиВыбраннойЗадачи(Задача);

		Для Каждого АдресатСписка Из СписокДоступныхАдресатов Цикл
			Элементы.Адресат.СписокВыбора.Добавить(АдресатСписка);
		КонецЦикла;

		Постановщик = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнаяЗадача, "Постановщик");

		Если ТипЗнч(Постановщик) = Тип("СправочникСсылка.Пользователи") Тогда
			Адресат = Постановщик;
		Иначе
			ОсновнойМенеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "ГлавныйМенеджер");
			Адресат = ОсновнойМенеджер;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ЗадачаПриИзменении(Элемент)
	СписокДоступныхАдресатов = cbr_ОбработчикиФормыЗадачиСервер.ПользователиВыбраннойЗадачи(Задача);;

	Для Каждого АдресатСписка Из СписокДоступныхАдресатов Цикл
		Элементы.Адресат.СписокВыбора.Добавить(АдресатСписка);
	КонецЦикла;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОтправитьВопрос(Команда)
	Отказ = Ложь;

	Если Не ЗначениеЗаполнено(Задача) Тогда
		Отказ = Истина;

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбрана задача";
		Сообщение.Поле = "Задача";
		Сообщение.Сообщить();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Адресат) Тогда
		Отказ = Истина;

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не выбран адресат";
		Сообщение.Поле = "Адресат";
		Сообщение.Сообщить();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ТекстНовогоВопроса) Тогда
		Отказ = Истина;

		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Введите текст вопроса";
		Сообщение.Поле = "ТекстНовогоВопроса";
		Сообщение.Сообщить();
	КонецЕсли;

	Если Не Отказ Тогда
		ОтправитьВопросНаСервере();
		Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.СозданиеВопросаПоЗадаче");
		cbr_ОбработкаТриггеровВызовСервера.ВызовТриггера(Подписка, Задача);
		Закрыть();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтправитьВопросНаСервере()
	cbr_ДвиженияПоРегистрам.ДвижениеВопросыПоЗадачам(Задача, Адресат, ТекстНовогоВопроса);
КонецПроцедуры
#КонецОбласти