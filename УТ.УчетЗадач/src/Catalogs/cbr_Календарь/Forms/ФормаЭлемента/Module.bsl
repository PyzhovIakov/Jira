#Область ОписаниеПеременных

&НаКлиенте
Перем ТекущаяЗаписьКалендаря;

&НаКлиенте
Перем ТекущаяДатаНачала;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьУсловноеОформление();
	ИспользоватьНапоминанияПользователя = ПолучитьФункциональнуюОпцию("ИспользоватьНапоминанияПользователя");
	Если ИспользоватьНапоминанияПользователя Тогда
		СрокНапоминанияПоУмолчанию = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиНапоминаний", "СрокНапоминанияПоУмолчанию", 15);
		УстанавливатьНапоминаниеАвтоматически = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиНапоминаний", "УстанавливатьНапоминаниеАвтоматически", Истина);
	КонецЕсли;

	ПрочитатьЦвет();
	ПрочитатьОтработана();
	Если ЗначениеЗаполнено(Объект.ДатаНачала) И ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		НачальноеЗначениеДатаНачала = Объект.ДатаНачала;
		НачальноеЗначениеДатаОкончания = Объект.ДатаОкончания;
	КонецЕсли;

	Если Объект.Состояние = Перечисления.cbr_СостояниеЗаписиРабочегоКалендаря.Запланированна Тогда

		Если ОтображатьКнопку() Тогда

			Элементы.СостояниеВработе.Видимость =  Истина;
            Элементы.СостояниеПоПлану.Видимость =  Истина; 
			
		КонецЕсли;

	ИначеЕсли Объект.Состояние = Перечисления.cbr_СостояниеЗаписиРабочегоКалендаря.ВРаботе Тогда
		Элементы.СостояниеЗавершенно.Видимость =  Истина;
	КонецЕсли; 	
	
	ИнициализироватьКалендарь();                                    

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьДоступностьЭлементовФормы();
	ВывестиДлительностьСобытия();
	ЗаполнитьСпискиВыбора();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_НапоминанияПользователя_Документооборот" И Источник = Объект.Ссылка
		 И ИспользоватьНапоминанияПользователя Тогда

		Напоминание = Параметр;

		УстановитьДоступностьЭлементовФормы();

	ИначеЕсли ИмяСобытия = "Удаление_НапоминанияПользователя_Документооборот" И Источник = Объект.Ссылка
		И ИспользоватьНапоминанияПользователя Тогда

		Напоминание = Неопределено;
		
		УстановитьДоступностьЭлементовФормы();

	ИначеЕсли ИмяСобытия = "Запись_ЗаписьКалендаря" Тогда

		Если (ТипЗнч(Параметр) = Тип("СправочникСсылка.cbr_Календарь") И Параметр = Объект.Ссылка И Источник
			<> УникальныйИдентификатор) Или (ТипЗнч(Параметр) = Тип("Массив") И Параметр.Найти(Объект.Ссылка) <> Неопределено
			И Источник <> УникальныйИдентификатор) Тогда

			Прочитать();
			УстановитьДоступностьЭлементовФормы();

		КонецЕсли;

	КонецЕсли;

	Если ИмяСобытия = "Запись_ЗаписьКалендаря" Тогда

		Если Источник = УникальныйИдентификатор Тогда
			Возврат;
		КонецЕсли;

		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			Если Параметр.Количество() <> 0 Тогда
				ТекущаяЗаписьКалендаря = Параметр[0];
				ТекущаяДатаНачала = Неопределено;
			КонецЕсли;
		КонецЕсли;

		ОбновитьОтображениеКалендарьКлиент(Параметр);

	КонецЕсли;

	Если ИмяСобытия = "Запись_ЗанятостьФизЛица" Или ИмяСобытия = "Запись_Мероприятие" Или ИмяСобытия = "Запись_ДоступноеВремяФизЛица"
		Или ИмяСобытия = "Запись_Отсутствие" Или ИмяСобытия = "Запись_НастройкиКалендаря" Тогда

		ОбновитьОтображениеКалендарьКлиент();

	КонецЕсли;

	Если ИмяСобытия = "ИзменениеДатыТекущейЗаписи" И Источник = УникальныйИдентификатор Тогда
		ЗаполнитьЗначенияСвойств(Объект, Параметр);
		ПриИзмененииДаты(Ложь);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ПрочитатьЦвет();
	ПрочитатьОтработана();
	УстановитьВидимостьЭлементовФормы();
	ОбновитьОтображениеКалендарь();

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ЗначениеЗаполнено(ПовторяющеесяСобытие) И ЗначениеЗаполнено(ПовторяющеесяСобытиеДатаИсключения) Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ПовторяющеесяСобытие", ПовторяющеесяСобытие);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ДатаИсключения", ПовторяющеесяСобытиеДатаИсключения);
	КонецЕсли;

	ТекущийОбъект.ДополнительныеСвойства.Вставить("УстановитьНапоминание", УстановитьНапоминание);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ИнтервалВремениСтрокой", ИнтервалВремениСтрокой);

	ПараметрыЗаписи.Вставить("ЭтоНовыйОбъект", Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка));

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	cbr_КалендарьСервер.ПослеЗаписиСобытияКалендаря(ТекущийОбъект.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Подписка = ПредопределенноеЗначение(
		"Перечисление.cbr_ПодпискаДляТриггера.ПослеЗаписиСобытияКалендаря");
	cbr_ОбработкаТриггеровВызовСервера.ВызовТриггера(Подписка, Объект.Предмет);

	ВыполнитьОповещениеОбИзменении();
	УстановитьДоступностьЭлементовФормы();

	ТекстОповещения = ?(ПараметрыЗаписи.ЭтоНовыйОбъект, НСтр("ru = 'Создание:'"), НСтр("ru = 'Изменение:'"));
	ПоказатьОповещениеПользователя(
		ТекстОповещения, ПолучитьНавигационнуюСсылку(Объект.Ссылка), Строка(Объект.Ссылка),
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
#Область ОбработчикиСобытийПланировщика

&НаКлиенте
Процедура ПланировщикВыбор(Элемент, СтандартнаяОбработка)

	cbr_КалендарьКлиент.ОбработкаВыбораЭлемента(Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)

	cbr_КалендарьКлиент.ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)

	cbr_КалендарьКлиент.ОбработкаПередНачаломРедактированиемЭлемента(Элемент, НовыйЭлемент, СтандартнаяОбработка,
		Объект.Сотрудник);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, Значения, Текст, СтандартнаяОбработка)

	cbr_КалендарьКлиент.ОбработкаПередСозданием(
		Элемент, Начало, Конец, Значения, Текст, СтандартнаяОбработка, Ложь, Объект.Сотрудник, НастройкиОтображения,
		УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ)

	cbr_КалендарьКлиент.ОбработкаПередУдалениемЭлемента(Элемент, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриАктивизации(Элемент, СтандартнаяОбработка)

	cbr_КалендарьКлиент.ОбработкаПриАктивизации(Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)

	cbr_КалендарьКлиент.ОбработкаОкончанияРедактированияЭлемента(
		Элемент, НовыйЭлемент, ОтменаРедактирования, Планировщик, НастройкиОтображения, УникальныйИдентификатор,
		Объект.Сотрудник);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура СотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)

	cbr_КалендарьКлиент.СотрудникОбработкаВыбора(Объект, "Сотрудник", ВыбранноеЗначение, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти
&НаКлиенте
Процедура ТипЗаписиКалендаряПриИзменении(Элемент)
	ТипЗаписиКалендаряПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ОтработанаПриИзменении(Элемент)

	ПриИзмененииОтработана();
	УстановитьДоступностьЭлементовФормы();
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ВыполнитьОповещениеОбИзменении();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)

	ПриИзмененииДаты();

КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Объект.ДатаНачала = Дата(1, 1, 1);
	Объект.ДатаОкончания = Дата(1, 1, 1);

	ПриИзмененииДаты();

КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяПриИзменении(Элемент)

	ПриИзмененииДаты();

КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Элемент.СписокВыбора;

КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Объект.ДатаНачала = Дата(1, 1, 1);
	Объект.ДатаОкончания = Дата(1, 1, 1);

	ПриИзмененииДаты();

КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаВремяАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)

	ПриИзмененииДаты();

КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Объект.ДатаНачала = Дата(1, 1, 1);
	Объект.ДатаОкончания = Дата(1, 1, 1);

	ПриИзмененииДаты();

КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяПриИзменении(Элемент)

	ПриИзмененииДаты();

КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Элемент.СписокВыбора;

КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Объект.ДатаНачала = Дата(1, 1, 1);
	Объект.ДатаОкончания = Дата(1, 1, 1);

	ПриИзмененииДаты();

КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияВремяАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВесьДеньПриИзменении(Элемент)

	cbr_КалендарьКлиент.ПриИзмененииВесьДень(
		Объект, НачальноеЗначениеДатаНачала, НачальноеЗначениеДатаОкончания, ОтображатьВремяС);

	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;

	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	УстановитьДоступностьЭлементовФормы();
	ОбновитьОтображениеТекущейЗаписиВКалендаре();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьНапоминаниеПриИзменении(Элемент)

	УстановитьДоступностьЭлементовФормы();

КонецПроцедуры

&НаКлиенте
Процедура ИнтервалВремениСтрокойОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если ВыбранноеЗначение = НСтр("ru = 'Другое...'") Тогда

		СтандартнаяОбработка = Ложь;

		ПараметрыФормы = Новый Структура;

		ПараметрыФормы.Вставить("СпособУстановкиВремениНапоминания", СпособУстановкиВремениНапоминания);
		ПараметрыФормы.Вставить("ИнтервалВремениСтрокой", ИнтервалВремениСтрокой);
		ПараметрыФормы.Вставить("ВремяНапоминания", ?(СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
			"Перечисление.СпособыУстановкиВремениНапоминания.ВУказанноеВремя"), ВремяНапоминания, Объект.ДатаНачала));

		ОписаниеОповещения = Новый ОписаниеОповещения("ИнтервалВремениСтрокойОбработкаВыбораЗавершение", ЭтотОбъект);
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		Результат = ОткрытьФорму("РегистрСведений.НапоминанияПользователя.Форма.НапоминаниеДокументооборот",
			ПараметрыФормы, , , , , ОписаниеОповещения, РежимОткрытияОкна);

	ИначеЕсли ИнтервалВремениСтрокой <> ВыбранноеЗначение Тогда

		ИзмененоНапоминание = Истина;
		СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
			"Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета");

	Иначе

		СтандартнаяОбработка = Ложь;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПовторятьНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность Тогда
		Записать();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	ИнициализироватьКалендарь();

КонецПроцедуры

&НаКлиенте
Процедура СотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	//СтандартнаяОбработка = Ложь;
	//cbr_КалендарьКлиент.ВыбратьСотрудникаИзАдреснойКниги(Элемент, Объект.Сотрудник);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНапомнитьНажатие(Элемент)

	УстановитьНапоминание = Не УстановитьНапоминание;
	УстановитьДоступностьЭлементовФормы();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПланДня

&НаКлиенте
Процедура ПланДняВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	cbr_КалендарьКлиент.ОбработкаВыбораПланДня(
		Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)

	cbr_КалендарьКлиент.ОбработкаНачалаПеретаскиванияПланДня(
		Элемент, ПараметрыПеретаскивания, Выполнение, НастройкиОтображения);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)

	cbr_КалендарьКлиент.ОбработкаПередНачаломДобавленияПланДня(
		Элемент, Отказ, Копирование, Родитель, Группа, НастройкиОтображения);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередНачаломИзменения(Элемент, Отказ)

	cbr_КалендарьКлиент.ОбработкаПередНачаломИзмененияПланДня(Элементы.ПланДня, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПередУдалением(Элемент, Отказ)

	cbr_КалендарьКлиент.ОбработкаПередУдалениемПланДня(Элементы.ПланДня, ПланДня, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	cbr_КалендарьКлиент.ОбработкаПеретаскиванияПланДня(
		Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле, НастройкиОтображения);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПриАктивизацииСтроки(Элемент)

	cbr_КалендарьКлиент.ОбработкаАктивизацииОбластиПланДня(
		Элемент, ТекущаяЗаписьКалендаря, ТекущаяДатаНачала);

КонецПроцедуры

&НаКлиенте
Процедура ПланДняПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)

	cbr_КалендарьКлиент.ОбработкаПроверкиПеретаскиванияПланДня(
		Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле, НастройкиОтображения);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СостояниеПоПлану(Команда)
	СостояниеВработеСервер(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СостояниеВработе(Команда)

	СостояниеВработеСервер(Истина);
	ПриИзмененииДаты(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЗавершенно(Команда)

	СостояниеЗавершенноСервер();  
	
	ОповещениеООтмене = новый ОписаниеОповещения("ОбработкаВопросаРезультата",ЭтотОбъект, );
	
	ПоказатьВводСтроки(ОповещениеООтмене,"","Запишите результат...",,Истина);

КонецПроцедуры 

&НаКлиенте
Процедура ПолучитьОписаниеИзСправочника(Команда)

	ОткрытьФорму("Справочник.cbr_КалендарьОписание.ФормаВыбора", , , , , ,
		Новый ОписаниеОповещения("ПриВыбореОписания", ЭтотОбъект));

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКалендарь(Команда)

	ПоказатьКалендарьНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписать(Команда)

	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;

	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ЗакрытьПослеЗаписи", Ложь);
	Записать(ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкаУдаления(Команда)

	Если Модифицированность = Истина Или (Не ЗначениеЗаполнено(Объект.Ссылка)) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьПометкаУдаленияЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр(
			"ru = 'Для установки отметки удаления необходимо записать внесенные вами изменения. Записать данные?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		Возврат;
	КонецЕсли;

	cbr_КалендарьКлиент.УстановитьПометкуУдаления(Объект.Ссылка, Не Объект.ПометкаУдаления);

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьПолеСотрудник(Команда)

	ОтображатьПолеСотрудник = Не ОтображатьПолеСотрудник;
	ОтображатьПолеСотрудникСервер();

КонецПроцедуры

&НаКлиенте
Процедура ОтработатьПланДня(Команда)

	cbr_КалендарьКлиент.ОтработатьВыделенныеЗаписиКалендаряПланДня(Элементы.ПланДня, ПланДня);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ИнтервалВремениСтрокойОбработкаВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Результат.СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
		"Перечисление.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета") Тогда

		Если УстановитьНапоминание = Ложь Или ИнтервалВремениСтрокой <> Результат.ИнтервалВремениСтрокой Тогда
			УстановитьНапоминание = Истина;
			ИзмененоНапоминание = Истина;
		КонецЕсли;
		СпособУстановкиВремениНапоминания = Результат.СпособУстановкиВремениНапоминания;
		ИнтервалВремениСтрокой = Результат.ИнтервалВремениСтрокой;
		ВремяНапоминания = Дата(1, 1, 1);

	ИначеЕсли Результат.СпособУстановкиВремениНапоминания = ПредопределенноеЗначение(
		"Перечисление.СпособыУстановкиВремениНапоминания.ВУказанноеВремя") Тогда

		Если УстановитьНапоминание = Ложь Или ВремяНапоминания <> Результат.ВремяНапоминания Тогда
			УстановитьНапоминание = Истина;
			ИзмененоНапоминание = Истина;
		КонецЕсли;
		СпособУстановкиВремениНапоминания = Результат.СпособУстановкиВремениНапоминания;
		ВремяНапоминания = Результат.ВремяНапоминания;
		ИнтервалВремениСтрокой = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'в %2 %1'"), Формат(ВремяНапоминания, "ДЛФ=D"), Формат(ВремяНапоминания, "ДФ=ЧЧ:мм"));

	КонецЕсли;

	УстановитьДоступностьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкаУдаленияЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;

	Записать();

	cbr_КалендарьКлиент.УстановитьПометкуУдаления(Объект.Ссылка, Не Объект.ПометкаУдаления);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	cbr_КалендарьСервер.УстановитьУсловноеОформлениеПланДня(УсловноеОформление);

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДаты(ИзменятьДатуНачалаИОкончания = Истина)

	ПроверитьОтсутствие = Истина;
	ИзмененоВремя = Истина;

	cbr_КаленарьКлиентСервер.СкорректироватьДатуНачалаИОкончания(
		Объект.ДатаНачала, Объект.ДатаОкончания, Объект.ВесьДень, НачальноеЗначениеДатаНачала,
		НачальноеЗначениеДатаОкончания, , , , , ИзменятьДатуНачалаИОкончания);

	ЗаполнитьСпискиВыбора();
	ВывестиДлительностьСобытия();
	УстановитьДоступностьЭлементовФормы();
	ОбновитьОтображениеТекущейЗаписиВКалендаре();

КонецПроцедуры

&НаСервере
Процедура ПоказатьКалендарьНаСервере()

	ПоказыватьКалендарь = Не ПоказыватьКалендарь;
	cbr_КалендарьСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьКалендарьВЗаписиКалендаря", ПоказыватьКалендарь);
	ИнициализироватьКалендарь();

КонецПроцедуры

&НаСервере
Процедура ОтображатьПолеСотрудникСервер()

	cbr_КалендарьСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьПолеСотрудник", ОтображатьПолеСотрудник);
	УстановитьВидимостьЭлементовФормы();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовФормы()

	Элементы.ДатаНачалаВремя.Видимость = Не Объект.ВесьДень;
	Элементы.ДатаОкончанияВремя.Видимость = Не Объект.ВесьДень;

	Если ИспользоватьНапоминанияПользователя Тогда
		Элементы.ИнтервалВремениСтрокой.Доступность = УстановитьНапоминание;
		Элементы.ГруппаНапоминаниеОтносительноДатыНачала.Видимость = Не Объект.ПометкаУдаления;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()

	Элементы.Предмет.Видимость = Истина;
	Элементы.ПовторениеСтрокой.Видимость = Повторять;

	Элементы.Автор.Видимость = Истина;

	Элементы.ФормаЗаписатьИЗакрыть.Видимость = Не ТолькоПросмотр;
	Элементы.ФормаЗаписатьИЗакрыть.КнопкаПоУмолчанию = Не ТолькоПросмотр;
	Элементы.ФормаЗаписать.Видимость = Не ТолькоПросмотр;
	Элементы.ФормаУстановитьПометкаУдаления.Видимость = Не ТолькоПросмотр;
	Элементы.ФормаЗакрыть.Видимость = ТолькоПросмотр;
	Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = ТолькоПросмотр;

КонецПроцедуры

&НаКлиенте
Процедура ВывестиДлительностьСобытия()

	ДлительностьСтр = "";

	Если Не ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		Возврат;
	КонецЕсли;

	ДлительностьСек = Объект.ДатаОкончания - Объект.ДатаНачала;

	Если Объект.ВесьДень Тогда
		ДлительностьСек = ДлительностьСек + 1;
	КонецЕсли;

	Дней = Цел(ДлительностьСек / 86400); // 86400 - число секунд в сутках
	ПодписьДней = cbr_КаленарьКлиентСервер.ПолучитьПодписьДней(Дней);

	Часов = Цел((ДлительностьСек - Дней * 86400) / 3600); // 86400 - число секунд в сутках
	ПодписьЧасов = cbr_КаленарьКлиентСервер.ПолучитьПодписьЧасов(Часов);

	Минут = Цел((ДлительностьСек - Дней * 86400 - Часов * 3600) / 60); // 86400 - число секунд в сутках
	ПодписьМинут = cbr_КаленарьКлиентСервер.ПолучитьПодписьМинут(Минут);

	Если Дней > 0 Тогда
		ДлительностьСтр = ДлительностьСтр + Строка(Дней) + " " + ПодписьДней;
	КонецЕсли;

	Если Часов > 0 Тогда

		Если Дней > 0 Тогда
			ДлительностьСтр = ДлительностьСтр + " ";
		КонецЕсли;

		ДлительностьСтр = ДлительностьСтр + Строка(Часов) + " " + ПодписьЧасов;
	КонецЕсли;

	Если Минут > 0 Тогда

		Если Дней > 0 Или Часов > 0 Тогда
			ДлительностьСтр = ДлительностьСтр + " ";
		КонецЕсли;

		ДлительностьСтр = ДлительностьСтр + Строка(Минут) + " " + ПодписьМинут;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСпискиВыбора()
	
	// Заполнение списка выбора даты начала
	Элементы.ДатаНачалаВремя.СписокВыбора.Очистить();

	Если ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		ТекДата = НачалоДня(Объект.ДатаНачала);
	Иначе
		ТекДата = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса());
	КонецЕсли;

	Для Инд = 1 По 48 Цикл

		Элементы.ДатаНачалаВремя.СписокВыбора.Добавить(ТекДата, Формат(ТекДата, "ДФ=ЧЧ:мм"));
		Если Объект.ДатаНачала > ТекДата И Объект.ДатаНачала < ТекДата + 1800 Тогда
			Элементы.ДатаНачалаВремя.СписокВыбора.Добавить(Объект.ДатаНачала, Формат(Объект.ДатаНачала, "ДФ=ЧЧ:мм"));
		КонецЕсли;

		ТекДата = ТекДата + 1800;

	КонецЦикла;
	
	// Заполнение списка выбора даты окончания
	Элементы.ДатаОкончанияВремя.СписокВыбора.Очистить();

	СобытиеВПределахОдногоДня = ЗначениеЗаполнено(Объект.ДатаНачала) И (НачалоДня(Объект.ДатаНачала) = НачалоДня(
		Объект.ДатаОкончания) Или Не ЗначениеЗаполнено(Объект.ДатаОкончания)) И Объект.ДатаНачала < Объект.ДатаОкончания;

	Если СобытиеВПределахОдногоДня Тогда

		ТекДата = cbr_КаленарьКлиентСервер.КонецПолучаса(Объект.ДатаНачала);
		Если Объект.ДатаОкончания > ТекДата - 1800 И Объект.ДатаОкончания < ТекДата Тогда
			Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(Объект.ДатаОкончания, Формат(Объект.ДатаОкончания,
				"ДФ=ЧЧ:мм"));
		КонецЕсли;

	ИначеЕсли ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		ТекДата = НачалоДня(Объект.ДатаОкончания);
	Иначе
		ТекДата = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса());
	КонецЕсли;

	Для Инд = 1 По 48 Цикл

		Если СобытиеВПределахОдногоДня И ТекДата > КонецДня(Объект.ДатаНачала) Тогда
			Прервать;
		КонецЕсли;

		Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(ТекДата, Формат(ТекДата, "ДФ=ЧЧ:мм"));
		Если Объект.ДатаОкончания > ТекДата И Объект.ДатаОкончания < ТекДата + 1800 Тогда
			Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(Объект.ДатаОкончания, Формат(Объект.ДатаОкончания,
				"ДФ=ЧЧ:мм"));
		КонецЕсли;
		ТекДата = ТекДата + 1800;

	КонецЦикла;

	Элементы.ДатаОкончанияВремя.СписокВыбора.Добавить(ТекДата, "00:00");

КонецПроцедуры

&НаСервере
Процедура ПрочитатьЦвет()

	Если Не ЗначениеЗаполнено(Объект.Ссылка) И Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(
		Параметры.ЗначениеКопирования) Тогда

	ИначеЕсли Не ЗначениеЗаполнено(Объект.Ссылка) И Параметры.Свойство("Основание") И ТипЗнч(Параметры.Основание) = Тип(
		"Структура") И Параметры.Основание.Свойство("ПовторяющеесяСобытие") И ЗначениеЗаполнено(
		Параметры.Основание.ПовторяющеесяСобытие) Тогда		
	Иначе
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОповещениеОбИзменении()

	ИзмененныеЗаписиКалендаря = Новый Массив;
	ИзмененныеЗаписиКалендаря.Добавить(Объект.Ссылка);
	Если ЗначениеЗаполнено(ПовторяющеесяСобытие) И ЗначениеЗаполнено(ПовторяющеесяСобытиеДатаИсключения) Тогда
		ИзмененныеЗаписиКалендаря.Добавить(ПовторяющеесяСобытие);
		ПовторяющеесяСобытие = Неопределено;
		ПовторяющеесяСобытиеДатаИсключения = Неопределено;
	КонецЕсли;
	Оповестить("Запись_ЗаписьКалендаря", ИзмененныеЗаписиКалендаря, УникальныйИдентификатор);

КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОтработана()

	Если Отработана Тогда
		УстановитьНапоминание = Ложь;
		Цвет = Перечисления.ЦветаРабочегоКалендаря.Нет;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПрочитатьОтработана()

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;

	Отработана = cbr_КалендарьСервер.ЗаписьКалендаряОтработана(Объект.Ссылка);
	Элементы.Описание.ОтображениеПредупрежденияПриРедактировании = ?(Отработана,
		ОтображениеПредупрежденияПриРедактировании.Отображать, ОтображениеПредупрежденияПриРедактировании.НеОтображать);

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКалендарь()

	ЧасовойПоясПользователя = cbr_КалендарьСервер.ЧасовойПоясПользователя(
		Объект.Сотрудник);

	ЧасовойПоясТекущегоПользователя = cbr_КалендарьСервер.ЧасовойПоясПользователя(Пользователи.ТекущийПользователь());
	СкрытьПринудительно = ЧасовойПоясПользователя <> ЧасовойПоясТекущегоПользователя;

	Если СкрытьПринудительно Тогда
		ПоказыватьКалендарь = Ложь;
	Иначе
		ПоказыватьКалендарь = cbr_КалендарьСервер.ПолучитьПерсональнуюНастройку(
			"ОтображатьКалендарьВЗаписиКалендаря");
	КонецЕсли;

	Если ПоказыватьКалендарь Тогда
		КлючСохраненияПоложенияОкна = "СКалендарем";
	Иначе
		КлючСохраненияПоложенияОкна = "БезКалендаря";
	КонецЕсли;

	Элементы.ГруппаКалендарь.Видимость = ПоказыватьКалендарь;
	Элементы.ФормаПоказатьКалендарь.Видимость = Не СкрытьПринудительно;
	Элементы.ФормаПоказатьКалендарь.Пометка = ПоказыватьКалендарь;
	Элементы.ГруппаЗаписьКалендаря.РастягиватьПоГоризонтали = Не ПоказыватьКалендарь;

	НастройкиОтображения = cbr_КалендарьСервер.ПолучитьНастройкиОтображения();
	ОбновитьОтображениеКалендарь();

КонецПроцедуры

&НаСервере
Процедура ОбновитьНастройкиОтображения()

	НастройкиОтображения.ФизЛица.Очистить();
	НастройкиОтображения.ФизЛица.Добавить(Объект.Сотрудник);

	НастройкиОтображения.ОтобразитьТекущуюЗапись = Истина;
	НастройкиОтображения.ЗапретСоздания = Истина;
	НастройкиОтображения.ЗапретИзменения = ТолькоПросмотр;
	НастройкиОтображения.ПланДняСкрытьПустые = Истина;
	НастройкиОтображения.ТекущаяЗаписьСсылка = Объект.Ссылка;
	НастройкиОтображения.ТекущаяЗаписьДатаНачала = Объект.ДатаНачала;
	НастройкиОтображения.ТекущаяЗаписьДатаОкончания = Объект.ДатаОкончания;
	НастройкиОтображения.ТекущаяЗаписьВесьДень = Объект.ВесьДень;
	Если Объект.ДатаНачала = Неопределено Тогда
		НачалоОтображаемогоПериода = НачалоДня(ТекущаяДатаСеанса());
	Иначе
		НачалоОтображаемогоПериода = НачалоДня(Объект.ДатаНачала);
	КонецЕсли;

	ОтображаемаяДата = НачалоОтображаемогоПериода;
	КонецОтображаемогоПериода = НачалоДня(Объект.ДатаОкончания);
	ОтображаемыйПериод = (КонецОтображаемогоПериода - НачалоОтображаемогоПериода) / 86400 + 1;
	НастройкиОтображения.ОтображаемаяДата = ОтображаемаяДата;
	НастройкиОтображения.ВыделенныеДаты.Очистить();
	КоличествоДат = 0;
	Пока ОтображаемаяДата <= КонецОтображаемогоПериода Цикл
		НастройкиОтображения.ВыделенныеДаты.Добавить(ОтображаемаяДата);
		ОтображаемаяДата = ОтображаемаяДата + 86400;
		КоличествоДат = КоличествоДат + 1;
		Если КоличествоДат > 31 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ОтображаемыйПериод <= 3 Тогда
		ПериодОтображения = ПредопределенноеЗначение("Перечисление.cbr_ПериодОтображенияРабочегоКалендаря.День");
		Элементы.ГруппаКалендарь.ТекущаяСтраница = Элементы.ГруппаПланировщик;
	Иначе
		ПериодОтображения = ПредопределенноеЗначение("Перечисление.cbr_ПериодОтображенияРабочегоКалендаря.ПланДня");
		Элементы.ГруппаКалендарь.ТекущаяСтраница = Элементы.ГруппаПланДня;
	КонецЕсли;
	НастройкиОтображения.ПериодОтображения = ПериодОтображения;

	НастройкиОтображения.ИсключенияЗанятости.Добавить(НастройкиОтображения.ТекущаяЗаписьСсылка);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиОтображенияКлиент()

	НастройкиОтображения.ФизЛица.Очистить();
	НастройкиОтображения.ФизЛица.Добавить( Объект.Сотрудник);

	НастройкиОтображения.ОтобразитьТекущуюЗапись = Истина;
	НастройкиОтображения.ЗапретСоздания = Истина;
	НастройкиОтображения.ПланДняСкрытьПустые = Истина;
	НастройкиОтображения.ТекущаяЗаписьСсылка = Объект.Ссылка;
	НастройкиОтображения.ТекущаяЗаписьДатаНачала = Объект.ДатаНачала;
	НастройкиОтображения.ТекущаяЗаписьДатаОкончания = Объект.ДатаОкончания;
	НастройкиОтображения.ТекущаяЗаписьВесьДень = Объект.ВесьДень;

	НачалоОтображаемогоПериода = НачалоДня(Объект.ДатаНачала);
	ОтображаемаяДата = НачалоОтображаемогоПериода;
	КонецОтображаемогоПериода = НачалоДня(Объект.ДатаОкончания);
	ОтображаемыйПериод = (КонецОтображаемогоПериода - НачалоОтображаемогоПериода) / 86400 + 1;
	НастройкиОтображения.ОтображаемаяДата = ОтображаемаяДата;
	НастройкиОтображения.ВыделенныеДаты.Очистить();
	КоличествоДат = 0;
	Пока ОтображаемаяДата <= КонецОтображаемогоПериода Цикл
		НастройкиОтображения.ВыделенныеДаты.Добавить(ОтображаемаяДата);
		ОтображаемаяДата = ОтображаемаяДата + 86400;
		КоличествоДат = КоличествоДат + 1;
		Если КоличествоДат > 31 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ОтображаемыйПериод <= 3 Тогда
		ПериодОтображения = ПредопределенноеЗначение("Перечисление.cbr_ПериодОтображенияРабочегоКалендаря.День");
		Элементы.ГруппаКалендарь.ТекущаяСтраница = Элементы.ГруппаПланировщик;
	Иначе
		ПериодОтображения = ПредопределенноеЗначение("Перечисление.cbr_ПериодОтображенияРабочегоКалендаря.ПланДня");
		Элементы.ГруппаКалендарь.ТекущаяСтраница = Элементы.ГруппаПланДня;
	КонецЕсли;
	НастройкиОтображения.ПериодОтображения = ПериодОтображения;

	Если Не ЗначениеЗаполнено(НастройкиОтображения.ТекущаяЗаписьСсылка) И ЗначениеЗаполнено(ПовторяющеесяСобытие) Тогда
		НастройкиОтображения.ТекущаяЗаписьСсылка = ПовторяющеесяСобытие;
	КонецЕсли;
	НастройкиОтображения.ИсключенияЗанятости.Добавить(НастройкиОтображения.ТекущаяЗаписьСсылка);

КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеКалендарь()

	Если Не ПоказыватьКалендарь Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Объект.ДатаНачала = ТекущаяДатаСеанса();
	КонецЕсли;

	Элементы.Планировщик.Доступность = Истина;
	Элементы.ПланДня.Доступность = Истина;

	ОбновитьНастройкиОтображения();

	Если ПериодОтображения = Перечисления.cbr_ПериодОтображенияРабочегоКалендаря.ПланДня Тогда
		ПланДняЗначение = РеквизитФормыВЗначение("ПланДня");
		cbr_КалендарьСервер.ОтобразитьПланДня(ПланДняЗначение, НастройкиОтображения);
		ЗначениеВРеквизитФормы(ПланДняЗначение, "ПланДня");
	Иначе
		cbr_КалендарьСервер.ОтобразитьКалендарь(Планировщик, НастройкиОтображения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеКалендарьКлиент(Параметр = Неопределено)

	Если Не ПоказыватьКалендарь Тогда
		Возврат;
	КонецЕсли;

	Если Параметр <> Неопределено И ПериодОтображения <> ПредопределенноеЗначение(
		"Перечисление.cbr_ПериодОтображенияРабочегоКалендаря.ПланДня") Тогда
		cbr_КалендарьКлиент.ОбновитьЗаписиКалендаряВПланировщике(
			Планировщик, НастройкиОтображения, Параметр);
		ОбновитьОтображениеТекущейЗаписиВКалендаре();
		Возврат;
	КонецЕсли;

	СвернутыеЭлементы = cbr_КалендарьКлиент.ПолучитьСвернутыеЭлементыПланаДня(
		Элементы.ПланДня, ПланДня);
	ОбновитьОтображениеКалендарь();
	cbr_КалендарьКлиент.ВосстановитьСостояниеПланаДня(
		Элементы.ПланДня, ПланДня, СвернутыеЭлементы, ТекущаяЗаписьКалендаря, ТекущаяДатаНачала);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеТекущейЗаписиВКалендаре()

	Если Не ПоказыватьКалендарь Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Элементы.Планировщик.Доступность = Ложь;
		Элементы.ПланДня.Доступность = Ложь;
		Возврат;
	КонецЕсли;

	Элементы.Планировщик.Доступность = Истина;
	Элементы.ПланДня.Доступность = Истина;

	СтарыйПериодОтображения = ПериодОтображения;
	ОбновитьНастройкиОтображенияКлиент();
	Если ПериодОтображения <> СтарыйПериодОтображения Тогда
		ОбновитьОтображениеКалендарьКлиент();
	ИначеЕсли ПериодОтображения = ПредопределенноеЗначение("Перечисление.cbr_ПериодОтображенияРабочегоКалендаря.День") Тогда
		cbr_КаленарьКлиентСервер.ОбновитьТекущуюЗапись(Планировщик, НастройкиОтображения);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ТипЗаписиКалендаряПриИзмененииСервер()
	Элементы.ГруппаЗадача.Видимость = Объект.ТипЗаписиКалендаря = Перечисления.cbr_ТипЗаписиКалендаря.Задача;
КонецПроцедуры

&НаСервере
Процедура СостояниеЗавершенноСервер()
	Объект.Состояние = Перечисления.cbr_СостояниеЗаписиРабочегоКалендаря.Завершена;
	Элементы.СостояниеЗавершенно.Видимость =  Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВопросаРезультата(Результат, ДополнительныеПараметры) экспорт        
    Если Результат<>Неопределено тогда
		Объект.Результат = Результат;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СостояниеВработеСервер(ИзменитьРасписание)
	Если ОтображатьКнопку() Тогда 
		
		Если ИзменитьРасписание Тогда   
			
			Объект.Состояние = Перечисления.cbr_СостояниеЗаписиРабочегоКалендаря.ВРаботе; 
			
			Объект.ДатаОкончания = ТекущаяДатаСеанса() + ( Объект.ДатаОкончания - ОБъект.ДатаНачала ); 
			Объект.ДатаНачала = ТекущаяДатаСеанса();  
			НачальноеЗначениеДатаНачала = Объект.ДатаНачала;
			НачальноеЗначениеДатаОкончания = Объект.ДатаОкончания;
            
		Иначе
			
			Объект.Состояние = Перечисления.cbr_СостояниеЗаписиРабочегоКалендаря.ПоПлану; 
			
		КонецЕсли; 
		        
		Элементы.СостояниеВработе.Видимость =  Ложь;    
		Элементы.СостояниеПоПлану.Видимость =  Ложь;

		Элементы.СостояниеЗавершенно.Видимость =  Истина;

	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ОтображатьКнопку()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_Календарь.Состояние КАК Состояние,
	|	cbr_Календарь.Предмет КАК Предмет
	|ИЗ
	|	Справочник.cbr_Календарь КАК cbr_Календарь
	|ГДЕ
	|	cbr_Календарь.ТипЗаписиКалендаря = &ТипЗаписиКалендаря
	|	И cbr_Календарь.Предмет = &Предмет
	|	И cbr_Календарь.Состояние = &Состояние";

	Запрос.УстановитьПараметр("Предмет", Объект.Предмет);
	Запрос.УстановитьПараметр("Состояние", Перечисления.cbr_СостояниеЗаписиРабочегоКалендаря.ВРаботе);
	Запрос.УстановитьПараметр("ТипЗаписиКалендаря", Перечисления.cbr_ТипЗаписиКалендаря.Задача);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда

		Возврат Истина;

	КонецЕсли;

	Возврат Ложь;

КонецФункции // ОтображатьКнопку()

&НаКлиенте
Процедура ПриВыбореОписания(ВыбранноеЗначение, ИсточникВыбора)  Экспорт  
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ТекстОпиисания = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(ВыбранноеЗначение,"Текст");
		Объект.Описание = ТекстОпиисания;	
	
	КонецЕсли;
   
КонецПроцедуры

#КонецОбласти