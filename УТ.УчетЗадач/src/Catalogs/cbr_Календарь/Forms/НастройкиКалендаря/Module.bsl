#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗагрузитьНастройки();

	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		НастроитьЭлементыФормыДляМобильногоУстройства();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтображатьВремяСПриИзменении(Элемент)

	ОтображатьВремяПоНормализованное = 24 - ОтображатьВремяПо;
	Если ОтображатьВремяС >= ОтображатьВремяПоНормализованное Тогда
		ОтображатьВремяПоНормализованное = ОтображатьВремяС + 1;
		ОтображатьВремяПо = 24 - ОтображатьВремяПоНормализованное;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьВремяПоПриИзменении(Элемент)

	ОтображатьВремяПоНормализованное = 24 - ОтображатьВремяПо;
	Если ОтображатьВремяС >= ОтображатьВремяПоНормализованное Тогда
		ОтображатьВремяС = ОтображатьВремяПоНормализованное - 1;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)

	ГотовоНаСервере();
	Оповестить("Запись_НастройкиКалендаря");
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ГотовоНаСервере()

	СохранитьНастройки();

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()

	cbr_КалендарьСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьВремяС", ОтображатьВремяС);
	cbr_КалендарьСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьВремяПо", ОтображатьВремяПо);		
	cbr_КалендарьСервер.УстановитьПерсональнуюНастройку(
		"ОтображениеВремениЭлементов", ОтображениеВремениЭлементов);
		
	cbr_КалендарьСервер.УстановитьПерсональнуюНастройку(
		"ИспользоватьБыстроеРедактирование", ИспользоватьБыстроеРедактирование);
	cbr_КалендарьСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьПомеченныеНаУдаление", ОтображатьПомеченныеНаУдаление);
	cbr_КалендарьСервер.УстановитьПерсональнуюНастройку(
		"РазмерЯчейкиВремени", РазмерЯчейкиВремени);
	cbr_КалендарьСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьКалендарьВЗаписиКалендаря", ОтображатьКалендарьВЗаписиКалендаря);
	cbr_КалендарьСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьЛегенду", ОтображатьЛегенду);

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройки()

	ОтображатьВремяС = cbr_КалендарьСервер.ПолучитьПерсональнуюНастройку("ОтображатьВремяС");
	ОтображатьВремяПо = cbr_КалендарьСервер.ПолучитьПерсональнуюНастройку("ОтображатьВремяПо");
	ОтображениеВремениЭлементов = cbr_КалендарьСервер.ПолучитьПерсональнуюНастройку("ОтображениеВремениЭлементов");
	ИспользоватьБыстроеРедактирование = cbr_КалендарьСервер.ПолучитьПерсональнуюНастройку(
		"ИспользоватьБыстроеРедактирование");
	ОтображатьПомеченныеНаУдаление = cbr_КалендарьСервер.ПолучитьПерсональнуюНастройку("ОтображатьПомеченныеНаУдаление");
	РазмерЯчейкиВремени = cbr_КалендарьСервер.ПолучитьПерсональнуюНастройку("РазмерЯчейкиВремени");
	
	ОтображатьКалендарьВЗаписиКалендаря = cbr_КалендарьСервер.ПолучитьПерсональнуюНастройку(
		"ОтображатьКалендарьВЗаписиКалендаря");
	ОтображатьЛегенду = cbr_КалендарьСервер.ПолучитьПерсональнуюНастройку("ОтображатьЛегенду");
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормыДляМобильногоУстройства()

	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Верх;
	Элементы.ОтображатьВремяС.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элементы.ОтображатьВремяС.Ширина = 0;
	Элементы.ОтображатьВремяПо.Ширина = 0;

	Элементы.ОтображениеВремениЭлементов.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;

	Элементы.РазмерЯчейкиВремени.Заголовок = СтрШаблон("%1 (%2)", Элементы.РазмерЯчейкиВремени.Заголовок,
		Элементы.ДекорацияМинРазмерЯчейкиВремени.Заголовок);
	Элементы.РазмерЯчейкиВремени.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элементы.ДекорацияМинРазмерЯчейкиВремени.Видимость = Ложь;

	Элементы.ДекорацияМинИнтервалПроверки.Видимость = Ложь;

	Элементы.ДекорацияМинСрокНапоминания.Видимость = Ложь;

	Элементы.ОтображатьКалендарьВЗаписиКалендаря.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ОтображатьЛегенду.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ИспользоватьБыстроеРедактирование.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ОтображатьПомеченныеНаУдаление.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;

	Элементы.СкрытьРаботуИзДома.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;

	Элементы.ИспользоватьБыстроеРедактирование.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элементы.ОтображатьПомеченныеНаУдаление.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	
	Элементы.НадписьСек.Видимость = Ложь;

КонецПроцедуры

#КонецОбласти